//DMMISRI3 JOB MISEIS,EIBMRPT1,COND=(4,LT),CLASS=A,MSGCLASS=X,
//         NOTIFY=&SYSUID
/*JOBPARM S=S1M1
//PRINT7   OUTPUT CLASS=K,
//         NAME='EN. MOHD FADZLI',
//         ROOM='13TH FLOOR',
//         BUILDING='MENARA PUBLIC BANK',
//         DEPT='ISLAMIC BANKING DIVISION',
//         ADDRESS=('NO 146 JALAN AMPANG',
//         '50450, KUALA LUMPUR'),
//         DEST=S1.LOCAL
//*---------------------------------------------------------
//*  DMMISRI3- BREAKDOWN OF ISLAMIC SAVINGS.
//*---------------------------------------------------------
//DMMISRI3 EXEC SAS609
//PGM      DD DSN=SAP.BNM.PROGRAM,DISP=SHR
//BNM      DD DSN=SAP.PIBB.SADATAWH,DISP=SHR
//SASLIST  DD SYSOUT=(,),OUTPUT=(*.PRINT7)
//SYSIN    DD *

OPTIONS NOCENTER NODATE NONUMBER MISSING=0;
*;
%INC PGM(PBBDPFMT,PBMISFMT);
*;
DATA REPTDATE;
  SET BNM.REPTDATE;
  SELECT;
     WHEN( 1<= DAY(REPTDATE) <=8 ) CALL SYMPUT('NOWK', PUT('1', $1.));
     WHEN( 9<= DAY(REPTDATE) <=15) CALL SYMPUT('NOWK', PUT('2', $1.));
     WHEN(16<= DAY(REPTDATE) <=22) CALL SYMPUT('NOWK', PUT('3', $1.));
     OTHERWISE CALL SYMPUT('NOWK', PUT('4', $1.));
  END;
  CALL SYMPUT('REPTYEAR', PUT(REPTDATE,YEAR2.));
  CALL SYMPUT('REPTMON', PUT(MONTH(REPTDATE),Z2.));
  CALL SYMPUT('REPTDAY', PUT(DAY(REPTDATE),Z2.));
  CALL SYMPUT('RDATE', PUT(REPTDATE, DDMMYY8.));
RUN;
*;

TITLE1 'REPORT ID : DMMISRI3';
TITLE2 'PUBLIC ISLAMIC BANK BERHAD (IBU)';
TITLE3 'BREAKDOWN OF ISLAMIC SAVINGS ACCOUNT DEPOSIT';
TITLE4 'REPORT AS AT ' &RDATE;
*;
DATA SAVE;
  SET BNM.ISA&REPTMON&NOWK&REPTYEAR;
  LENGTH DEPRANGE $ 33;
  IF  PRODUCT IN (204,215);
  IF  CURBAL GE 0;
  RANGE    =INPUT(CURBAL, SDRANGE.);
  DEPRANGE =PUT(RANGE, SADPRG.);
*;
PROC SUMMARY DATA=SAVE NWAY;
CLASS  DEPRANGE PRODUCT;
VAR    CURBAL;
OUTPUT OUT=SDRNGE (RENAME=(_FREQ_=NOACCT) DROP=_TYPE_) SUM=CURBAL;
RUN;
*;
PROC TABULATE DATA=SDRNGE NOSEPS MISSING;
  CLASS DEPRANGE PRODUCT;
  VAR NOACCT CURBAL;
  TABLE PRODUCT ALL='BANK TOTAL',
        DEPRANGE='DEPOSIT RANGE' ALL='TOTAL',
        NOACCT=''*SUM='NO OF A/C'*F=COMMA12.
        CURBAL=''*SUM='AMOUNT'*F=DOLLAR20.2
      / RTS=35 BOX=_PAGE_ PRINTMISS CONDENSE;
  FORMAT PRODUCT SDNAME.;
RUN;

