OPTIONS YEARCUTOFF=1950 SORTDEV=3390 NONUMBER NODATE NOCENTER;
*;
DATA REPTDATE;
   SET DEPOSIT.REPTDATE;
   MM=MONTH(REPTDATE);
   MM1=MM-1;
   IF MM1=0 THEN MM1=12;
   CALL SYMPUT('RDATE',PUT(REPTDATE,DDMMYY8.));
   CALL SYMPUT('RYEAR',PUT(REPTDATE,YEAR4.));
   CALL SYMPUT('RMONTH',PUT(REPTDATE,MONTH2.));
   CALL SYMPUT('REPTMON',PUT(MM,Z2.));
   CALL SYMPUT('REPTMON1',PUT(MM1,Z2.));
   CALL SYMPUT('RDAY',PUT(REPTDATE,DAY2.));
RUN;
*;
DATA CURR;
  SET DEPOSIT.CURRENT;
  /* IF  BRANCH=996 THEN BRANCH=168; */
  IF  BRANCH=250 THEN BRANCH=092;
  IF  PRODUCT IN (085,086,090,091,106,170,135,137,138,068,069) OR
      (050<=PRODUCT<=065)              OR
      (100<=PRODUCT<=103)              OR
      (108<=PRODUCT<=125)              OR
      (150<=PRODUCT<=167)              OR
      (174<=PRODUCT<=182)              OR
      (191<=PRODUCT<=198);

  IF  OPENMH=1 AND CLOSEMH=1 THEN DELETE;

  IF  OPENIND='Z' THEN OPENIND='O';
  IF  OPENIND='O' OR
      OPENIND IN ('B','C','P') AND CLOSEMH=1;

  NOACCT=0; CCLOSE=0; BCLOSE=0;
  IF  OPENIND IN ('B','C','P') THEN DO;
      IF OPENIND='C'  THEN CCLOSE=CLOSEMH;
                      ELSE BCLOSE=CLOSEMH;
  END;
  ELSE DO;
      CLOSEMH=0; NOACCT=1;
  END;
*;
DATA CAO; SET CURR; IF OPENMH=1 & CURBAL > 0;
*;
PROC SUMMARY DATA=CAO NWAY;
CLASS BRANCH;
VAR   OPENMH CURBAL;
OUTPUT OUT=MIS.CAO&REPTMON (DROP=_TYPE_ _FREQ_) SUM=;
*;
PROC   SUMMARY DATA=CURR NWAY;
CLASS  BRANCH  PRODUCT;
VAR    OPENMH  CLOSEMH  NOACCT CURBAL BCLOSE CCLOSE;
OUTPUT OUT=CURR (DROP=_FREQ_ _TYPE_) SUM=;
%MACRO PROCESS;
   %IF &REPTMON > 01 %THEN %DO;

       DATA CURRP;
         SET MIS.CURR1&REPTMON1;
         OPENCUX =OPENCUM;
         CLOSECUX=CLOSECUM;
         IF BRANCH=250 THEN BRANCH=092;


       PROC   SUMMARY DATA=CURRP NWAY;
       CLASS  BRANCH  PRODUCT;
       VAR    OPENCUX CLOSECUX;
       OUTPUT OUT=CURRP (DROP=_FREQ_ _TYPE_) SUM=;

       DATA CURR;
         MERGE CURRP CURR; BY BRANCH PRODUCT;
         IF CLOSECUX=.   THEN  CLOSECUX=0;
         IF OPENCUX =.   THEN  OPENCUX=0;
         IF OPENMH  =.   THEN  OPENMH =0;
         IF CLOSEMH =.   THEN  CLOSEMH=0;
         IF CCLOSE  =.   THEN  CCLOSE =0;
         IF BCLOSE  =.   THEN  BCLOSE =0;
         IF NOACCT  =.   THEN  NOACCT =0;
         IF CURBAL  =.   THEN  CURBAL =0;
         IF BCLOSE  =.   THEN  BCLOSE =0;
         IF CCLOSE  =.   THEN  CCLOSE =0;

         OPENCUM =OPENMH  +OPENCUX;
         CLOSECUM=CLOSEMH +CLOSECUX;
         NETCHGMH=OPENMH  -CLOSEMH;
         NETCHGYR=OPENCUM -CLOSECUM;
   %END;
   %ELSE %DO;
       DATA CURR;
         SET CURR;
         OPENCUM =OPENMH;
         CLOSECUM=CLOSEMH;
         NETCHGMH=OPENMH  -CLOSEMH;
         NETCHGYR=OPENCUM -CLOSECUM;
         IF BRANCH=250 THEN BRANCH=092;
   %END;
%MEND;

%PROCESS;

DATA MIS.CURR1&REPTMON;
   SET CURR;
*;
*------------------------------------------------*
*  PRINT REPORT TO TEXT FILE                     *
*------------------------------------------------*;
TITLE1 'PUBLIC BANK BERHAD';
TITLE2 'CURRENT ACCOUNT OPENED/CLOSED FOR THE MONTH AS AT' &RDATE;
PROC TABULATE DATA=CURR FORMAT=COMMA10. MISSING NOSEPS
   FORMCHAR='           ';
   CLASS BRANCH;
   VAR   OPENMH CLOSEMH NOACCT CURBAL CLOSECUM OPENCUM
         NETCHGMH NETCHGYR BCLOSE CCLOSE;
   TABLE BRANCH=' ' ALL='TOTAL',SUM=' '*
        (OPENMH='CURRENT MONTH OPENED'
         OPENCUM='CUMULATIVE OPENED'
         CLOSEMH='CURRENT MONTH CLOSED'
         BCLOSE ='CLOSED BY BANK'
         CCLOSE ='CLOSED BY CUSTOMER'
         CLOSECUM='CUMULATIVE CLOSED'
         NOACCT='NO. OF ACCTS'
         CURBAL='TOTAL(RM) O/S'*F=COMMA17.2
         NETCHGMH='NET CHANGE FOR THE MONTH'
         NETCHGYR='NET CHANGE YEAR TO DATE')
       / BOX='BRANCH' RTS=10;
