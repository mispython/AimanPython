//EIBTNPGS JOB MISEIS,MSGCLASS=A,CLASS=A,NOTIFY=&SYSUID
//*
//* CENSUS @368 IN CRFTABL.
//*
//EIBTNPGS EXEC SAS609
//MNILN    DD DSN=SAP.PBB.MNILN(0),DISP=SHR
//CRFTABL  DD DSN=SAP.PBB.BTRADE.CRFTABL,DISP=SHR
//BTRSA    DD DSN=SAP.PBB.BTRADE.SASDATA,DISP=SHR
//COLL     DD DSN=RBP2.B033.MST00.LCCRISEX,DISP=SHR
//DESC     DD DSN=RBP2.B033.MST00.LCCRISEX.DESC,DISP=SHR
//MICR     DD DSN=SAP.BOPESS.BRANCH.MICR.CODE,DISP=SHR
//NPGS     DD DSN=SAP.PBB.NPGS.SASDATA,DISP=OLD
//PGM      DD DSN=SAP.BNM.PROGRAM,DISP=SHR
//SASLIST  DD SYSOUT=X
//SYSIN    DD *

OPTIONS YEARCUTOFF=1940 SORTDEV=3390;
OPTIONS NONUMBER NODATE NOCENTER;
*;
%INC PGM(PBBDPFMT,PBBELF);
%INC PGM(PBBLNFMT);
*;
DATA REPTDATE (KEEP=REPTDATE);
  SET MNILN.REPTDATE;
  MM = MONTH(REPTDATE);
  DD =   DAY(REPTDATE);
  CALL SYMPUT('REPTMON',PUT(MM,Z2.));
  CALL SYMPUT('REPTDAY',PUT(DD,Z2.));
  CALL SYMPUT('REPTYEAR',PUT(REPTDATE,YEAR4.));
  CALL SYMPUT('REPTDAY',PUT(DAY(REPTDATE),Z2.));
  CALL SYMPUT('RDATE',PUT(REPTDATE,Z5.));
RUN;
*;
DATA CRFT;
  INFILE CRFTABL;
  INPUT  @001  RECTYP1 $1. @;
  IF RECTYP1='1' THEN DELETE;    ELSE
  INPUT  @004 TFID        $8.
         @012 SUBACCT     $5.
         @365 PREIND      $1.  /* IF > 0 THEN DELETE RECORD? */
         @368 CENSUST      1.
         @377 ACCTNO      10.;

  SCH='   ';
  IF  CENSUST=3  THEN SCH='P51'; ELSE
  IF  CENSUST=4  THEN SCH='P72'; ELSE
  IF  CENSUST=5  THEN SCH='P65'; ELSE
  IF  CENSUST=6  THEN SCH='P85'; ELSE
  IF  CENSUST=7  THEN SCH='P53';
  IF SCH NE '   ';
*;
PROC SORT DATA=CRFT OUT=CRFT NODUPKEY; BY ACCTNO CENSUST SUBACCT;
PROC SORT DATA=BTRSA.MAST&REPTDAY&REPTMON OUT=MAST NODUPKEY; BY ACCTNO;
*;
DATA CRFT CRFT1;
  KEEP BRANCH ACCTNO SUBACCT NAME BUSREGN CENSUST TFID SCH;
  MERGE CRFT(IN=A) MAST(IN=B); BY ACCTNO;
  IF A AND B;
  BRANCH=FICODE;
  IF  ACCTNO > 0;
  OUTPUT CRFT;
  SUBACCT='FAC'||SUBSTR(SUBACCT,1,1);
  OUTPUT CRFT1;
PROC SORT DATA=CRFT OUT=CRFT NODUPKEY; BY ACCTNO SUBACCT;
PROC SORT DATA=CRFT1 OUT=CRFT1 NODUPKEY; BY ACCTNO SUBACCT;
*;
PROC SORT DATA=BTRSA.CRED&REPTDAY&REPTMON OUT=CRED; BY ACCTNO SUBACCT;
*;
DATA CRED;
  MERGE CRED(IN=A) CRFT(IN=B); BY ACCTNO SUBACCT;
  IF A AND B;
  IF SUBSTR(SUBACCT,1,3) NE 'FAC';
  IF TRANSREF NE '  ';
  TRANSREX=SUBSTR(TRANSREF,1,7);
*;
PROC SORT DATA=CRED OUT=CRED NODUPKEY; BY ACCTNO TRANSREF;
*;
PROC SUMMARY DATA=CRED NWAY;
CLASS ACCTNO; VAR OUTSTAND;
WHERE SUBSTR(SUBACCT,2,3) NE 'SGL';
OUTPUT OUT=CRED1 (DROP=_TYPE_  _FREQ_) SUM=;
*;
PROC SORT DATA=BTRSA.PROV&REPTDAY&REPTMON OUT=PROV; BY ACCTNO TRANSREX;
WHERE NPLIND NOT IN ('P','F');
PROC SORT DATA=CRED OUT=CRED2; BY ACCTNO TRANSREX;

DATA CRED2;
  MERGE PROV(IN=A) CRED2(IN=B); BY ACCTNO TRANSREX;
  IF A AND B;
  IF SUBSTR(SUBACCT,1,3) IN ('OV ','FAC') THEN DELETE;
* IF OUTSTAND=0.00                        THEN DELETE;
*;
PROC SORT DATA=CRED2; BY ACCTNO MATUREDS;
PROC SORT DATA=CRED2  OUT=CRED2 NODUPKEY; BY ACCTNO;
*;
DATA CRED2;
  KEEP ACCTNO ARREARS MATUREDS NODAYS;
  SET CRED2;
  NODAYS=0; ARREARS=0;
  IF  MATUREDS > 0 AND (&RDATE > MATUREDS)  THEN DO;
      NODAYS=(&RDATE-MATUREDS)+1;
      IF NODAYS > 0      THEN DO;
         ARREARS=INPUT(NODAYS,NDAYS.);
         IF ARREARS=24 THEN ARREARS=ROUND(NODAYS/30);
      END;
  END;
*;
PROC SORT DATA=BTRSA.SUBA&REPTDAY&REPTMON OUT=SUBA; BY ACCTNO SUBACCT;
DATA SUBA;
  MERGE SUBA(IN=A) CRFT1(IN=B); BY ACCTNO SUBACCT;
  IF A AND B;
*;
DATA SUBA1;
  SET SUBA;
  IF  SUBSTR(SUBACCT,1,3)='FAC';
PROC SORT; BY ACCTNO SUBACCT;
PROC SORT DATA=SUBA1 OUT=SUBA1 NODUPKEY; BY ACCTNO SUBACCT;
*;
PROC SUMMARY DATA=SUBA1 NWAY;
CLASS ACCTNO; VAR LIMTCURM;
OUTPUT OUT=SUBA1 (DROP=_TYPE_  _FREQ_) SUM=;
*;
PROC SORT DATA=SUBA OUT=SUBA2 NODUPKEY; BY ACCTNO SUBACCT;
WHERE TRANSREF EQ '  '             AND
      SUBSTR(SUBACCT,1,3) NE 'FAC' AND
      SUBSTR(SUBACCT,2,3) NE 'SGL';
*;
PROC SUMMARY DATA=SUBA2 NWAY;
CLASS ACCTNO; VAR LIMTCURM;
OUTPUT OUT=SUBA2 (DROP=_TYPE_  _FREQ_) SUM=LIMITS;
*;
DATA SUBALMT;
  MERGE SUBA1 SUBA2; BY ACCTNO;
  IF LIMTCURM=.   THEN LIMTCURM=LIMITS;
*;
DATA SUBA;
  SET SUBA;
  IF  TRANSREF NE '   ';
  ISSUEDT=0; MATURED1=0;
  IF CREATDS > 0 THEN DO;
     ISSUEDT=INPUT(PUT(CREATDS,Z6.),YYMMDD6.);
     IF SUBSTR(TRANSREF,1,1)='Y'  THEN MATURED1=ISSUEDT;
     IF MATURED1=0                THEN MATURED1=99999;
  END;
*;
PROC SORT DATA=SUBA; BY ACCTNO ISSUEDT;
PROC SORT DATA=SUBA OUT=SUBA NODUPKEY; BY ACCTNO;
*;
DATA COLL;
   INFILE COLL;
   INPUT @004  CCOLLNO  PD6.
         @146  ACCTNO   PD6.;
PROC SORT; BY CCOLLNO;
*;
DATA DESC;
   INFILE DESC;
   INPUT @001 CCOLLNO   11.
         @051 CINSTCL   $2.
         @055 NATGUAR   $2.
         @211 CENSUS    10.;
   CR='  ';
   IF (51000000<=CENSUS<=51999999)     THEN CR='51'; ELSE
   IF (72000000<=CENSUS<=72999999)     THEN CR='72'; ELSE
   IF (1000000000<=CENSUS<=1099999999) THEN CR='10';
   IF CR NE '  ';
PROC SORT; BY CCOLLNO;
*;
DATA COLL;
  MERGE COLL(IN=A) DESC(IN=B); BY CCOLLNO;
  IF A AND B;
  IF CINSTCL='18' AND NATGUAR='06';
PROC SORT; BY ACCTNO;
*;
DATA MAST;
  MERGE CRFT(IN=A) COLL(IN=B); BY ACCTNO;
  IF A AND B;
PROC SORT; BY BRANCH;
*;
DATA MICR;
  INFILE MICR;
  INPUT @001 BRANCH     3.
        @040 MICRCD    $5.;
PROC SORT; BY BRANCH;
*;
DATA MAST;
  MERGE MAST(IN=A) MICR(IN=B); BY BRANCH;
  IF A;
PROC SORT DATA=MAST OUT=MAST NODUPKEY; BY ACCTNO CENSUS;
*;
DATA NPGS;
   MERGE MAST CRED1 CRED2 SUBA SUBALMT; BY ACCTNO;
   CVAR02='  ';
   IF SCH='P51' AND CR IN ('10','51')  THEN CVAR02='51'; ELSE
   IF SCH='P72' AND CR IN ('10','72')  THEN CVAR02='72'; ELSE
   IF SCH='P85' AND CR='10'            THEN CVAR02='85'; ELSE
   IF SCH='P53' AND CR='10'            THEN CVAR02='53'; ELSE
   IF SCH='P65' AND CR='10'            THEN CVAR02='65';
   IF CVAR02 NE '  ';
*;
DATA NPGS;
  FORMAT CVAR01 CVAR06 10. CVAR03 $15. CVAR04 $50. CVAR14 $4.
         CVAR13 $10. CVAR08 CVAR09 CVAR10 10.2 CVAR11 5.;
  SET NPGS;
  IF  MATURED1 < MATUREDS THEN MATUREDS=MATURED1;
  IF  ARREARS=.           THEN ARREARS =0;
  PRODUCT=0;
  CVAR01=CENSUS;
  CVAR03=BUSREGN;
  CVAR04=NAME;
  CVAR05=ISSUEDT;
  CVAR06=ACCTNO;
  CVAR07='TF';
  CVAR08=LIMTCURM;
  CVAR10=0.00;
  CVAR09=OUTSTAND;
  CVAR11=ARREARS;
  CVAR12='   ';
  CVAR13='          ';

  NDD=&REPTDAY;
  NMM=&REPTMON;
  NYY=&REPTYEAR;
  NORMDT=PUT(NDD,Z2.)||'/'||PUT(NMM,Z2.)||'/'||PUT(NYY,Z4.);

  IF NODAYS > 89 THEN DO;
     MATUREDS=MATUREDS+89; CVAR12='NPL';
     NPLDD=  DAY(MATUREDS);
     NPLMM=MONTH(MATUREDS);
     NPLYY= YEAR(MATUREDS);
     NPLDATE=MDY(NPLMM,NPLDD,NPLYY);
     CVAR13=
     PUT(NPLDD,Z2.)||'/'||PUT(NPLMM,Z2.)||'/'||PUT(NPLYY,Z4.);
  END;
  IF ARREARS GE 3 AND NPLDATE > 0 THEN  CVAR12='NPL';
  CVAR14='0233';
  CVAR15=MICRCD;
  IF OUTSTAND=. THEN DELETE;
*;
*;
PROC SORT; BY CVAR06 CVAR01;
PROC SORT DATA=NPGS.NPLA OUT=NPLA; BY CVAR06 CVAR01;
*;
DATA NPGS;
  MERGE NPGS(IN=A) NPLA; BY CVAR06 CVAR01;
  IF A;
  IF CVAR12='NPL' THEN DO;
     IF STATUS='NPL'          THEN CVAR13=NDATE;
  END;
  IF CVAR12='   ' THEN DO;
     IF STATUS='NPL'          THEN CVAR13=NORMDT;
     IF STATUS='   ' AND NDATE NE '          ' THEN CVAR13=NDATE;
  END;
*;
PROC SORT; BY CVAR01;
DATA NPGS.BTNPGS&REPTMON;
     SET NPGS;
     KEEP CVAR01 CVAR02 CVAR03 CVAR04 CVAR05 CVAR06 CVAR07
          CVAR08 CVAR09 CVAR10 CVAR11 CVAR12 CVAR13 CVAR14
          SCH CR BRANCH CVAR15 CENSUST NATGUAR CINSTCL PRODUCT;
*;
