//EIMNOSTE JOB MSGCLASS=X,MSGLEVEL=(1,1),REGION=8M,
//         USER=OPCC
/*JOBPARM S=S1M2
//*
//***************************************************************
//* LAST MODIFY : ESMR 2023-4385
//***************************************************************
//*
//EIMNOSTE  EXEC SAS609
//MNITB     DD DSN=SAP.PBB.MNITB(0),DISP=SHR
//DPNOST    DD DSN=RBP2.B033.NOSCBNK(0),DISP=SHR
//WKNOST    DD DSN=FDP.APPL.FIDSAS.DATA,DISP=SHR
//NOSTRO    DD DSN=SAP.PBB.NOSTRO(+1),DISP=(NEW,CATLG,DELETE),
//             DCB=(RECFM=FS,LRECL=27648,BLKSIZE=27648),
//             SPACE=(CYL,(200,200),RLSE),UNIT=(SYSDA,5)
//SASLIST   DD DSN=SAP.PBB.EIMNOSTE.TEXT(+1),
//             DISP=(MOD,CATLG,DELETE),
//             DCB=(RECFM=FB,LRECL=256,BLKSIZE=28160),
//             SPACE=(CYL,(5,2)),UNIT=SYSDA
//SFTPFL   DD DSN=&&FTPPUT,DISP=(NEW,PASS),
//            SPACE=(TRK,(1)),UNIT=SYSDA,RECFM=FB,LRECL=80
//SYSIN     DD *

OPTIONS YEARCUTOFF=1950 NOCENTER LS=256;

DATA NOSTRO.REPTDATE(KEEP=REPTDATE);
   SET MNITB.REPTDATE;
   SELECT;
     WHEN(5 <= DAY(REPTDATE) <= 10) CALL SYMPUT('NOWK', PUT('1', $1.));
     WHEN(11 <= DAY(REPTDATE) <= 18) CALL SYMPUT('NOWK', PUT('2', $1.));
     WHEN(19 <= DAY(REPTDATE) <= 25) CALL SYMPUT('NOWK', PUT('3', $1.));
     OTHERWISE CALL SYMPUT('NOWK', PUT('4', $1.));
   END;
   CALL SYMPUT('REPTYEAR', PUT(REPTDATE, YEAR2.));
   CALL SYMPUT('REPTMON', PUT(MONTH(REPTDATE), Z2.));
   CALL SYMPUT('REPTDAY', PUT(DAY(REPTDATE), Z2.));
   CALL SYMPUT('RDATE', PUT(REPTDATE, DDMMYY8.));
   CALL SYMPUT('FILDT',PUT(REPTDATE,DDMMYYN6.));
RUN;


*** WALKER FILE ***;
DATA WALKER(DROP=DD MM YY DD1 MM1 YY1);
  INFILE WKNOST;
  INPUT @1   SBC        8.
        @9   DD         2.
        @12  MM         2.
        @15  YY         2.
        @17  DD1        2.
        @20  MM1        2.
        @23  YY1        2.
        @25  NBR      $24.
        @49  DESC     $60.
        @109 FORCUR    13.0
        @122 SIGN      $1.
        @123 RMCUR     11.0
        @134 AGENTNO   $3.
        @137 CURCODE   $3.
        @140 TRANCODE  $3.
        @143 NAME     $30.
        ;
        FORCUR = FORCUR / 100;
        RMCUR = RMCUR / 100;
        TRXDT = MDY(MM,DD,YY);
        PREFFDT = MDY(MM1,DD1,YY1);
        IF SIGN = 'D' THEN DO;
           FORCUR = FORCUR * (-1);
           RMCUR = RMCUR * (-1);
        END;
RUN;

DATA NOSTRO.WAK&REPTYEAR&REPTMON;
   SET WALKER;
   FORMAT TRDESC $20.;
   SELECT (TRANCODE);
     WHEN('001') TRDESC = 'OUTWARD DD';
     WHEN('003') TRDESC = 'OUTWARD MT';
     WHEN('006') TRDESC = 'OUTWARD TT';
     WHEN('011') TRDESC = 'BANK GUARANTEE';
     WHEN('013') TRDESC = 'ECRF PRE SHIPMENT';
     WHEN('015') TRDESC = 'ECRF POST SHIPMENT';
     WHEN('021') TRDESC = 'FBEP CLEAN';
     WHEN('023') TRDESC = 'FBEP DOCUMENTARY';
     WHEN('025') TRDESC = 'FBEP AP';
     WHEN('031') TRDESC = 'FOBC CLEAN';
     WHEN('033') TRDESC = 'OBC DOCUMENTARY';
     WHEN('041') TRDESC = 'IBC CLEAN';
     WHEN('043') TRDESC = 'IBC DOCUMENTARY';
     WHEN('051') TRDESC = 'BR';
     WHEN('055') TRDESC = 'DPC';
     WHEN('061') TRDESC = 'LC';
     WHEN('063') TRDESC = 'ILC';
     WHEN('081') TRDESC = 'INWARD DD';
     WHEN('083') TRDESC = 'INWARD MT';
     WHEN('085') TRDESC = 'INWARD TT';
     WHEN('091') TRDESC = 'MISCELLANEOUS';
     WHEN('092') TRDESC = 'TREASURY CREDIT';
     WHEN('093') TRDESC = 'TREASURY DEBIT';
     OTHERWISE TRDESC='OTHERS';
   END;
RUN;

*** DEPOSIT FILE ***;
DATA DEPOSIT(DROP=DD MM YY DD1 MM1 YY1 DD2 MM2 YY2);
  INFILE DPNOST LRECL=200;

  INPUT @1   ACCTNO    10.
        @11  AGENTNO   $3.
        @14  NAME     $25.
        @39  TRIND     $1.
        @40  TRTYPE    $2.
        @42  SIGN      $1.
        @43  FORCUR  PD7.2
        @50  RMCUR   PD7.2
        @57  YY        4.
        @61  MM        2.
        @63  DD        2.
        @65  YY1       4.
        @69  MM1       2.
        @71  DD1       2.
        @73  CURCODE  $3.
        @76  BILLIND  $1.
        @77  YY2       4.
        @81  MM2       2.
        @83  DD2       2.
        ;
        STARTDT = MDY(MM,DD,YY);
        ENDDT = MDY(MM1,DD1,YY1);
        TRXDT = MDY(MM2,DD2,YY2);
        IF SIGN = 'D' THEN DO;
           FORCUR = FORCUR * (-1);
           RMCUR = RMCUR * (-1);
        END;
        IF AGENTNO = '' THEN AGENTNO = '000';
   IF ACCTNO NE . ;
RUN;

DATA NOSTRO.DP&REPTYEAR&REPTMON;
   SET DEPOSIT;
   IF NOT (3997200109<=ACCTNO<=3997204029);
   FORMAT TRDESC $20.;
   IF BILLIND IN ('L','X','I','O','G') THEN DO;
      SELECT(BILLIND);
         WHEN('L') TRDESC = 'IMPORT LC';
         WHEN('X') TRDESC = 'EXPORT LC';
         WHEN('I') TRDESC = 'INWARD BILLS COLL';
         WHEN('O') TRDESC = 'OUTWARD BILL COLL';
         WHEN('G') TRDESC = 'BANK GUARANTEE';
         OTHERWISE;
      END;
   END;
   ELSE DO;
      SELECT (TRIND);
         WHEN('O') DO;
            IF TRTYPE EQ 'TT' THEN TRDESC = 'OUTWARD TT';
            ELSE IF TRTYPE EQ 'DD' THEN TRDESC = 'OUTWARD DD';
            ELSE IF TRTYPE EQ 'MT' THEN TRDESC = 'OUTWARD MT';
            ELSE IF TRTYPE EQ 'TF' THEN TRDESC = 'OUTWARD MT';
         END;
         WHEN('R') DO;
            IF TRTYPE EQ 'TT' THEN TRDESC = 'REPLACE TT';
            ELSE IF TRTYPE EQ 'DD' THEN TRDESC = 'REPLACE DD';
            ELSE IF TRTYPE EQ 'MT' THEN TRDESC = 'REPLACE MT';
         END;
         WHEN('I') DO;
            IF TRTYPE EQ 'TT' THEN TRDESC = 'INWARD TT';
            ELSE IF TRTYPE EQ 'DD' THEN TRDESC = 'INWARD DD';
            ELSE IF TRTYPE EQ 'DD' THEN TRDESC = 'INWARD BILL COLL';
         END;
         WHEN('B') DO;
            IF TRTYPE EQ 'TT' THEN TRDESC = 'B BACK TT';
            ELSE IF TRTYPE EQ 'DD' THEN TRDESC = 'B BACK DD';
         END;
         WHEN('C') DO;
            IF TRTYPE EQ 'TT' THEN TRDESC = 'CANCEL TT';
            ELSE IF TRTYPE EQ 'DD' THEN TRDESC = 'CANCEL DD';
         END;
         OTHERWISE TRDESC = 'OTHERS';
      END;
   END;
RUN;

    *** USE DEPOSIT NAME ***;
DATA WKNAME(KEEP=AGENTNO NAME);
   SET NOSTRO.WAK&REPTYEAR&REPTMON;
RUN;
PROC SORT DATA=WKNAME; BY AGENTNO; RUN;

DATA DPNAME(KEEP=AGENTNO NAME);
   SET NOSTRO.DP&REPTYEAR&REPTMON;
RUN;
PROC SORT DATA=DPNAME; BY AGENTNO; RUN;

DATA ALLNAME;
   MERGE WKNAME DPNAME;
   BY AGENTNO;
RUN;
PROC SORT DATA=ALLNAME NODUPKEY; BY AGENTNO; RUN;


    *** COMBINE DEPOSIT & WALKER DATA ***;
DATA ALLREC(DROP=NAME);
   SET NOSTRO.WAK&REPTYEAR&REPTMON
       NOSTRO.DP&REPTYEAR&REPTMON;
RUN;
PROC SORT DATA=ALLREC; BY AGENTNO; RUN;
DATA ALLREC;
   MERGE ALLREC(IN=A) ALLNAME;
   BY AGENTNO;
   IF A;
RUN;

PROC SUMMARY DATA=ALLREC;
   ** WHERE TRXDT = '30SEP05'D;
   VAR FORCUR RMCUR;
   CLASS CURCODE AGENTNO NAME TRDESC SIGN;
   OUTPUT OUT=ALLREC (RENAME=(_FREQ_=NOTRAN))
   SUM=FORCUR RMCUR;
RUN;

PROC SORT DATA=ALLREC; WHERE _TYPE_ = 31;
   BY CURCODE AGENTNO NAME TRDESC SIGN;
RUN;

PROC REPORT DATA=ALLREC NOCENTER SPLIT='*' NOWINDOWS HEADSKIP;
   COLUMN CURCODE AGENTNO NAME TRDESC SIGN NOTRAN FORCUR RMCUR;
   DEFINE CURCODE     /ORDER  WIDTH=8 'CURRENCY*CODE';
   DEFINE AGENTNO     /GROUP  WIDTH=5 'AGENT*NO';
   DEFINE NAME        /DISPLAY WIDTH=30 'NAME';
   DEFINE TRDESC      /DISPLAY WIDTH=20 'TRANSACTION*DESCRIPTION';
   DEFINE SIGN        /DISPLAY WIDTH=9 'DEBIT(D)/*CREDIT(C)';
   DEFINE NOTRAN      /DISPLAY FORMAT=COMMA8. 'NO OF*TRANS*ACTION';
   DEFINE FORCUR      /SUM     FORMAT=COMMA17.2 'FOREIGN*AMOUNT';
   DEFINE RMCUR       /SUM     FORMAT=COMMA17.2 'RM*AMOUNT';

   BREAK AFTER AGENTNO / ;
   COMPUTE AFTER AGENTNO;
      LINE @095 36*'-';
      LINE @095 FORCUR.SUM COMMA17.2
           @114 RMCUR.SUM COMMA17.2;
      LINE @095 36*'-';
      LINE ' ';
   ENDCOMP;

   RBREAK AFTER /;
   COMPUTE AFTER;
      LINE @001 ' ';
      LINE @087 'TOTAL '
           @095 FORCUR.SUM COMMA17.2
           @114 RMCUR.SUM COMMA17.2;
      LINE @095 36*'=';
   ENDCOMP;

   TITLE1 'REPORT ID : EIMNOSTE';
   TITLE2 'REPORT ON FOREIGN EXCHANGE TRANSACTION AS AT ' "&RDATE";
RUN;

DATA _NULL_;
   FILE SFTPFL;
   PUT @1 "PUT //SAP.PBB.EIMNOSTE.TEXT(+1) EIMNOSTE_&FILDT..TXT"
        ;
RUN;

//*---------------------------------------------------------
//* FTP REPORTS TO DATA MENARA FILE SERVER
//*---------------------------------------------------------
//RUNSFTP  EXEC COZBATCH
//CMD.SYSUT1 DD DISP=SHR,DSN=OPER.PBB.PARMLIB(SFTPFIN)
//           DD *
lzopts servercp=$servercp,notrim,overflow=trunc,mode=text
lzopts linerule=$lr
//           DD DISP=SHR,DSN=&&FTPPUT
//           DD *
EOB
/*
