*+--------------------------------------------------------------+
 |  PROGRAM : EIFMNP07                                          |
 |  DATE    : 03.04.98                                          |
 |  MODIFY  : ESMR 2004-720, 2004-579, 2006-1048                |
 |  REPORT  : STATISTICS ON ASSET QUALITY - MOVEMENTS IN NPL    |
 +--------------------------------------------------------------+;
OPTIONS YEARCUTOFF=1950;
*;
%INC PGM(PBBLNFMT);
%INC PGM(PBBELF);
*;
PROC FORMAT;
   VALUE LNTYP 128,130,983             = 'HPD AITAB'
               700,705,993,996,380,381,
               720,725                 = 'HPD CONVENTIONAL'
               200-299                 = 'HOUSING LOANS'
               OTHER   = 'OTHERS';
*;
*------------------------------------------------*
*  MACRO FOR CALCULATING NEXT BLDATE             *
*------------------------------------------------*;
%MACRO DCLVAR;
   RETAIN D1-D12 31 D4 D6 D9 D11 30;
   ARRAY LDAY D1-D12;
%MEND DCLVAR;
*;
%MACRO NXTBLDT;
   DD = DAY(ISSDTE);
   MM = MONTH(BLDATE) + 1;
   YY = YEAR(BLDATE);
   IF MM > 12 THEN DO;
      MM = 1; YY + 1;
   END;
   IF MM = 2 THEN
      IF MOD(YY,4) = 0 THEN D2 = 29;
      ELSE D2 = 28;
   IF DD > LDAY(MM) THEN DD = LDAY(MM);
   BLDATE = MDY(MM,DD,YY);
%MEND NXTBLDT;
*;
*------------------------------------------------*
*  GET REPTDATE                                  *
*------------------------------------------------*;
DATA REPTDATE;
   SET NPL.REPTDATE;
   CALL SYMPUT('RDATE',PUT(REPTDATE,WORDDATX18.));
   CALL SYMPUT('REPTMON',PUT(MONTH(REPTDATE),Z2.));
RUN;
*;
*------------------------------------------------*
*  MERGE WITH WRITTEN OFF ACCOUNT                *
*------------------------------------------------*;
PROC SORT DATA=NPL.LOAN&REPTMON;BY ACCTNO;
PROC SORT DATA=NPL.WAQ;BY ACCTNO;
DATA LOANWOFF;
   MERGE NPL.LOAN&REPTMON NPL.WAQ (IN=AA DROP=NOTENO NTBRCH);
   BY ACCTNO;
   IF AA THEN WRITEOFF = 'Y';ELSE WRITEOFF = 'N';
   IF LOANTYPE IN (983,993) THEN WDOWNIND = 'N';
   IF EARNTERM IN (0,.) THEN EARNTERM = NOTETERM;
*;
*------------------------------------------------*
*  CAL STATISTICS FOR EXISTING NPL ACCOUNTS      *
*------------------------------------------------*;
DATA LOAN1;
   KEEP BRANCH ACCTNO NOTENO NAME DAYS CURBALP CURBAL NETBALP NEWNPL
        ACCRINT RECOVER PL NPLW NPL LOANTYPE LOANTYP OIP ADJUST USER5
        BORSTAT COSTCTR PENDBRH;
   LENGTH LOANTYP $20;
   RETAIN NEWNPL 0 STMTH 1 ENMTH 12 STYR ENYR;
   %DCLVAR
   SET LOANWOFF;
 * SET NPL.LOAN&REPTMON;
   IF _N_ = 1 THEN DO;
      SET REPTDATE;
      STYR = YEAR(REPTDATE);
      ENYR = STYR - 1;
   END;
   IF EXIST = 'Y';
   IF WRITEOFF = 'Y' AND WDOWNIND ^= 'Y' THEN BORSTAT ='W';
   IF CURBAL = . THEN CURBAL = 0;
   ACCRINT = 0; UHC = 0; OI = 0; RECOVER = 0; PL = 0;
   REMMTH1=0;REMMTH2=0;REMMTHS=0;
   * IF LOANTYPE IN (380,381) THEN ADJUST = FEEAMT - FEETOT2;
   * ELSE ADJUST = FEEAMT8 - FEETOT2;
   ADJUST = FEEAMT - FEETOT2;
   IF DAYS <  90 & BORSTAT IN (' ','A','C','S','T','Y') & CURBAL >= 0
      & USER5 NE 'N' OR LOANTYPE IN (983,993) THEN DO;
      PL = NETBALP;
      IF DAYS = 0 AND CURBAL = 0 THEN DO;
         RECOVER = NETBALP;
         PL = 0;
      END;
   END;
   ELSE DO;
      IF LOANTYPE IN (380,381) THEN OI = FEEAMT;
      ELSE OI = FEEAMT;
      ACCRINT = FEEYTD;
      IF BORSTAT = 'F' THEN CURBALP = CURBALP - UHCP;
      IF TERMCHG > 0 OR (USER5 = 'N'
      AND LOANTYPE NOT IN (983,993)) THEN DO;
         REMMTH1 = EARNTERM - ((YEAR(BLDATE) - YEAR(ISSDTE))*12 +
                   MONTH(BLDATE) - MONTH(ISSDTE) + 1);
         REMMTH2 = EARNTERM - ((YEAR(REPTDATE) - YEAR(ISSDTE))*12 +
                   MONTH(REPTDATE) - MONTH(ISSDTE) + 1);
         REMMTHS = EARNTERM - ((STYR - YEAR(ISSDTE))*12 +
                   STMTH - MONTH(ISSDTE) + 1);
         IF REMMTH2 < 0 THEN REMMTH2 = 0;
         REMMTH1 = REMMTH1 - 1;
         IF REMMTHS >= REMMTH2 THEN
            DO REMMTH = REMMTHS TO REMMTH2 BY -1;
               ACCRINT + 2*(REMMTH+1)*TERMCHG/(EARNTERM*(EARNTERM+1));
            END;
         IF REMMTH2 > 0 THEN
            UHC = REMMTH2*(REMMTH2+1)*TERMCHG/(EARNTERM*(EARNTERM+1));
      END;
      IF BORSTAT = 'W' THEN NPLW = NETBALP;
      ELSE RECOVER = CURBALP - CURBAL + FEEPDYTD;
      IF RECOVER < 0 THEN DO;
         CURBALP = CURBALP - RECOVER;
         RECOVER = 0;
      END;
      NPL = CURBAL - UHC + OI;
      IF BORSTAT = 'W' THEN NPL = 0;
   END;
   BRANCH = PUT(NTBRCH,BRCHCD.)||' '||PUT(NTBRCH,Z3.);
   LOANTYP = PUT(LOANTYPE,LNTYP.);
   IF WRITEOFF = 'Y' OR LOANTYPE IN (983,993)THEN DO;
      ACCRINT = WACCRINT;
      NEWNPL = WNEWNPL;
      ADJUST = 0;
      IF WDOWNIND ^= 'Y' THEN DO;
         RECOVER = WRECOVER;
         PL = 0;
         NPL = 0;
         NPLW = SUM(NETBALP,NEWNPL,ACCRINT,(-1)*RECOVER,(-1)*PL);
      END;
      ELSE DO;
         NPLW = WNPLW;
         NPL  = SUM(NETBALP,NEWNPL,ACCRINT,(-1)*RECOVER,
                   (-1)*NPLW,(-1)*PL);
      END;
   END;
*;
*------------------------------------------------*
*  CAL STATISTICS FOR CURRENT NPL ACCOUNTS       *
*------------------------------------------------*;
DATA LOAN2;
   KEEP BRANCH ACCTNO NOTENO NAME DAYS CURBALP CURBAL NETBALP NEWNPL
        ACCRINT RECOVER PL NPLW NPL LOANTYPE LOANTYP OIP ADJUST USER5
        BORSTAT COSTCTR PENDBRH;
   LENGTH LOANTYP $20;
   RETAIN STMTH 1 ENMTH 12 STYR ENYR;
   %DCLVAR
   SET LOANWOFF;
 * SET NPL.LOAN&REPTMON;
   IF _N_ = 1 THEN DO;
      SET REPTDATE;
      STYR = YEAR(REPTDATE);
      ENYR = STYR - 1;
   END;
   REMMTH1=0;REMMTH2=0;
   IF EXIST ^= 'Y';
   UHC = 0; OI = 0;
   ADJUST = 0;
   /* IF LOANTYPE IN (380,381) THEN ADJUST = FEEAMT - FEETOT2;
   ELSE ADJUST = FEEAMT8 - FEETOT2; */
   IF WRITEOFF = 'Y' AND WDOWNIND ^= 'Y' THEN BORSTAT ='W';
   IF BLDATE > 0 & TERMCHG > 0 OR USER5 = 'N' THEN DO;
      REMMTH1 = EARNTERM - ((YEAR(BLDATE) - YEAR(ISSDTE))*12 +
                MONTH(BLDATE) - MONTH(ISSDTE) + 1);
      REMMTH2 = EARNTERM - ((YEAR(REPTDATE) - YEAR(ISSDTE))*12 +
                MONTH(REPTDATE) - MONTH(ISSDTE) + 1);
      IF REMMTH2 < 0 THEN REMMTH2 = 0;
      REMMTH1 = REMMTH1 - 1;
      IF REMMTH2 > 0 THEN
         UHC = REMMTH2*(REMMTH2+1)*TERMCHG/(EARNTERM*(EARNTERM+1));
   END;
   ELSE DO;
      REMMTH2 = EARNTERM - ((YEAR(REPTDATE) - YEAR(ISSDTE))*12 +
                MONTH(REPTDATE) - MONTH(ISSDTE) + 1);
      IF REMMTH2 > 0 THEN
         UHC = REMMTH2*(REMMTH2+1)*TERMCHG/(EARNTERM*(EARNTERM+1));
   END;
   IF LOANTYPE IN (380,381) THEN OI = FEEAMT;
   ELSE OI = FEEAMT;
   NEWNPL = CURBAL - UHC + OI;
   NPL = NEWNPL;
   BRANCH = PUT(NTBRCH,BRCHCD.)||' '||PUT(NTBRCH,Z3.);
   LOANTYP = PUT(LOANTYPE,LNTYP.);
   IF WRITEOFF = 'Y' OR LOANTYPE IN (983,993)THEN DO;
      ACCRINT = WACCRINT;
      NEWNPL = WNEWNPL;
      ADJUST = 0;
      IF WDOWNIND ^= 'Y' THEN DO;
         RECOVER = WRECOVER;
         PL = 0;
         NPL = 0;
         NPLW = SUM(NETBALP,NEWNPL,ACCRINT,(-1)*RECOVER,(-1)*PL);
      END;
      ELSE DO;
         NPLW = WNPLW;
         NPL  = SUM(NETBALP,NEWNPL,ACCRINT,(-1)*RECOVER,
                   (-1)*NPLW,(-1)*PL);
      END;
   END;
*;
*------------------------------------------------*
*  COMBINE EXISTING & CURRENT NPL ACCOUNTS       *
*  CREATE VALUE TO CHECK DISCREPANCIES           *
*------------------------------------------------*;
DATA LOAN NPL.AQ;
   ARRAY VBL NETBALP NEWNPL ACCRINT RECOVER PL NPLW NPL;
   SET LOAN1 LOAN2;
   LENGTH RISK $13;
   IF DAYS > 364 OR BORSTAT = 'W' THEN RISK = 'BAD';
   ELSE IF DAYS > 273 THEN RISK = 'DOUBTFUL';
   ELSE IF DAYS > 182 THEN RISK = 'SUBSTANDARD 2';
   ELSE IF DAYS < 90 AND USER5='N' THEN RISK = 'SUBSTANDARD-1';
   ELSE RISK = 'SUBSTANDARD-1';
   DO OVER VBL;
      IF VBL = . THEN VBL = 0;
   END;
   CHKNPL = NETBALP+NEWNPL+ACCRINT-RECOVER-PL-NPLW;
   WHERE (COSTCTR < 3000 OR COSTCTR > 3999) AND
          COSTCTR NOT IN (4043,4048) AND
          COSTCTR NE .;
*;
*------------------------------------------------*
*  PRODUCE REPORTS                               *
*------------------------------------------------*;
OPTIONS NOCENTER NODATE NONUMBER MISSING=0;

PROC TABULATE DATA=LOAN FORMAT=COMMA17.2 MISSING NOSEPS;
   CLASS LOANTYP RISK BRANCH;
   VAR CURBALP CURBAL NETBALP NEWNPL PL ACCRINT RECOVER NPLW NPL ADJUST;
   TABLE LOANTYP=' ',
         RISK=' '*(BRANCH=' ' ALL='SUB-TOTAL') ALL='TOTAL',
         N='NO OF ACCOUNT'*F=COMMA7.
         SUM=' '*(CURBALP='BAL AS AT PREV YEAR (WITH UHC)'
                  CURBAL='BAL AS AT END OF RPT DATE (WITH UHC)'
                  NETBALP='NET BAL AS AT PREV YEAR (A)'
                  NEWNPL='NEW NPL DURING CURRENT YEAR (B)'
                  ACCRINT='ACCRUED INTEREST (C)'
                  RECOVER='RECOVERIES (D)'
                  PL='NPL CLASSIFIED AS PERFORMING (E)'
                  NPLW='NPL WRITTEN-OFF (F)'
                  ADJUST='ADJUSTMENT'
                  NPL='NPL AS AT END OF RPT DATE (A+B+C-D-E-F)')
         / BOX='RISK        BRANCH' RTS=29;
   TABLE LOANTYP=' ', BRANCH=' ' ALL='TOTAL',
         N='NO OF ACCOUNT'*F=COMMA7.
         SUM=' '*(CURBALP='BAL AS AT PREV YEAR (WITH UHC)'
                  CURBAL='BAL AS AT END OF RPT DATE (WITH UHC)'
                  NETBALP='NET BAL AS AT PREV YEAR (A)'
                  NEWNPL='NEW NPL DURING CURRENT YEAR (B)'
                  ACCRINT='ACCRUED INTEREST (C)'
                  RECOVER='RECOVERIES (D)'
                  PL='NPL CLASSIFIED AS PERFORMING (E)'
                  NPLW='NPL WRITTEN-OFF (F)'
                  ADJUST='ADJUSTMENT'
                  NPL='NPL AS AT END OF RPT DATE (A+B+C-D-E-F)')
         / BOX='BRANCH' RTS=10;
   TITLE1 'PUBLIC BANK - (NPL FROM 3 MONTHS & ABOVE)';
   TITLE2 'STATISTICS ON ASSET QUALITY - MOVEMENTS IN NPL' &RDATE;
*;

PROC SORT DATA=LOAN;
   BY LOANTYP BRANCH RISK DAYS ACCTNO;
*;
PROC PRINT LABEL N;
   FORMAT CURBALP CURBAL NETBALP NEWNPL ACCRINT RECOVER PL NPLW NPL
          COMMA14.2;
   LABEL ACCTNO  = 'MNI ACCOUNT NO'
         DAYS    = 'NO OF DAYS PAST DUE'
         CURBALP = 'BAL AS AT PREV YEAR (WITH UHC)'
         CURBAL  = 'BAL AS AT END OF RPT DATE (WITH UHC)'
         NETBALP = 'NET BAL AS AT PREV YEAR (A)'
         NEWNPL  = 'NEW NPL DURING CURRENT YEAR (B)'
         ACCRINT = 'ACCRUED INTEREST (C)'
         RECOVER = 'RECOVERIES (D)'
         PL      = 'NPL CLASSIFIED AS PERFORMING (E)'
         NPLW    = 'NPL WRITTEN-OFF (F)'
         ADJUST  = 'ADJUSTMENT'
         NPL     = 'NPL AS AT END OF RPT DATE (A+B+C-D-E-F)';
   VAR ACCTNO NAME DAYS CURBALP CURBAL NETBALP NEWNPL ACCRINT RECOVER
       PL NPLW NPL ADJUST;
   BY LOANTYP BRANCH RISK;
   PAGEBY BRANCH;
   SUMBY RISK;
   SUM CURBALP CURBAL NETBALP NEWNPL ACCRINT RECOVER PL NPLW NPL ADJUST;
*;
*------------------------------------------------*
*  PRODUCE DISCREPANCY REPORT                    *
*------------------------------------------------*;
  /*
PROC PRINT LABEL N;
   FORMAT CURBALP CURBAL NETBALP NEWNPL ACCRINT RECOVER PL NPLW NPL
          CHKNPL COMMA14.2;
   LABEL ACCTNO  = 'MNI ACCOUNT NO'
         DAYS    = 'NO OF DAYS PAST DUE'
         CURBALP = 'BAL AS AT PREV YEAR (WITH UHC)'
         CURBAL  = 'BAL AS AT END OF RPT DATE (WITH UHC)'
         NETBALP = 'NET BAL AS AT PREV YEAR (A)'
         NEWNPL  = 'NEW NPL DURING CURRENT YEAR (B)'
         ACCRINT = 'ACCRUED INTEREST (C)'
         RECOVER = 'RECOVERIES (D)'
         PL      = 'NPL CLASSIFIED AS PERFORMING (E)'
         NPLW    = 'NPL WRITTEN-OFF (F)'
         NPL     = 'NPL AS AT END OF RPT DATE (A+B+C-D-E-F)'
         CHKNPL  = '(A+B+C-D-E-F)';
   VAR ACCTNO NAME DAYS CURBALP CURBAL NETBALP NEWNPL ACCRINT RECOVER
       PL NPLW NPL CHKNPL;
   BY LOANTYP BRANCH RISK;
   WHERE ROUND(CHKNPL,0.01) ^= ROUND(NPL,0.01);
   TITLE1 'PUBLIC BANK - (NPL FROM 3 MONTHS & ABOVE)';
   TITLE2 'STATISTICS ON ASSET QUALITY - MOVEMENTS IN NPL' &RDATE;
   TITLE3 '(DISCREPANCY REPORT)';    */

