*;
OPTIONS NODATE NONUMBER NOCENTER MISSING=0;

%INC PGM(PBBDPFMT,PBMISFMT);

DATA REPTDATE;
    REPTDATE=TODAY()-1;
    CALL SYMPUT('REPTMON',PUT(MONTH(REPTDATE),Z2.));
    CALL SYMPUT('REPTYEAR',PUT(REPTDATE,YEAR4.));
    CALL SYMPUT('RDATE',PUT(REPTDATE,DDMMYY8.));
    CALL SYMPUT('ZDATE',PUT(REPTDATE,Z5.));
    CALL SYMPUT('REPTDT6',PUT(REPTDATE,YYMMDDN6.));
RUN;

LIBNAME MIS "SAP.PBB.MIS.D&REPTYEAR" DISP=SHR;
LIBNAME CIS "SAP.PBB.CIDATAWH.DAILY" DISP=SHR;

%MACRO SAVING;
DATA SDMVNT;
  SET MIS.DYMVNT&REPTMON;
  WHERE DEPTYPE EQ 'S' AND REPTDATE EQ &ZDATE;
  MOVEMENT=ROUND(CREDIT-DEBIT,.01);
  PREBAL=CURBAL-MOVEMENT;
  BRCH=PUT(BRANCH,BRCHCD.);
  IF PRODUCT NOT IN (204,207,214,215);
  PRODCD=PUT(PRODUCT,SAPROD.);
  IF PRODCD NE 'N';
RUN;
PROC SORT DATA=SDMVNT;BY ACCTNO;RUN;
*;
PROC SORT; BY ACCTNO;
DATA CNAME;
  SET CIS.CISR1SA&REPTDT6;
  IF  SECCUST='901';
  KEEP ACCTNO CUSTNAME CUSTNO MNIADDL1 MNIADDL2;
PROC SORT DATA=CNAME OUT=CNAME NODUPKEY; BY ACCTNO;
DATA SDMVNT;
  MERGE CNAME SDMVNT(IN=A); BY ACCTNO;
  IF A;
  MOVEMT=ROUND((MOVEMENT*0.001), .1);
  *IF CUSTNAM1=' '  THEN CUSTNAM1=NAME;
*;
PROC SORT DATA=SDMVNT;
BY DESCENDING MOVEMENT ACCTNO;
*;
DATA _NULL_;
  CALL SYMPUT('SDMVNT', N);
  SET SDMVNT NOBS=N;
RUN;

DATA SDMVNT;
   SET SDMVNT;
   RETAIN COUNT 0;
   COUNT=COUNT+1;
RUN;
*;
TITLE1 'REPORT ID: DMMISR22';
TITLE2 "MOVEMENT IN BANK'S SAVING DEPOSITS AS AT" &RDATE;
TITLE3 'DAILY NET INCREASE/DECREASE OF RM 50 THOUSAND & ABOVE PER '
       'ACCOUNT (CONVENTIONAL)';
*;
%IF &SDMVNT EQ 0 %THEN %DO;
 DATA _NULL_;
   FILE PRINT;
   PUT;PUT;
   PUT @5 '****************************************************';
   PUT @5 'NO CUSTOMER WITH MOVEMENT OF 50 THOUSAND AND ABOVE';
   PUT @5 "AT &RDATE";
   PUT @5 '***************************************************';
 RUN;
%END;
%ELSE %DO;
PROC REPORT DATA=SDMVNT NOWD HEADLINE SPLIT='*' LS=256;
  COLUMN COUNT BRANCH ACCTNO BRCH MNIADDL1
         MOVEMT CUSTNAME MNIADDL2 CUSTNO CURBAL PREBAL PRODUCT
         CUSTCODE;
  DEFINE COUNT    / DISPLAY FORMAT=10. 'NO.';
  DEFINE BRANCH   / DISPLAY FORMAT=6. 'BRANCH CODE';
  DEFINE ACCTNO   / DISPLAY FORMAT=10. 'ACCOUNT' 'NO.';
  DEFINE BRCH     / DISPLAY FORMAT=$6.  'BRANCH ABBR ';
  DEFINE MNIADDL1 / FORMAT=$40. 'NAME1 OF CUSTOMER (FR. A/C LVL)';
  DEFINE MOVEMT   / FORMAT=COMMA14.1
                   "DAILY NET INC./(DEC.)" "(RM'000)";
  DEFINE CUSTNAME / FORMAT=$50. 'NAME OF CUSTOMER (FR. CIS LVL)';
  DEFINE MNIADDL2 / FORMAT=$40. 'NAME2 OF CUSTOMER (FR. A/C LVL)';
  DEFINE CUSTNO   / DISPLAY FORMAT=8.  'CUSTOMER CIS NO.';
  DEFINE CURBAL   / FORMAT=COMMA14.2 'CURRENT DAY' 'BALANCE (RM)';
  DEFINE PREBAL    / FORMAT=COMMA14.2 'PREVIOUS DAY' 'BALANCE (RM)';
  DEFINE PRODUCT  / DISPLAY FORMAT=7.   'PRODUCT CODE';
  DEFINE CUSTCODE / DISPLAY FORMAT=8.   'CUSTOMER CODE';

  RBREAK AFTER / PAGE DUL OL SUMMARIZE;

RUN;
%END;
%MEND;
%SAVING;
