OPTIONS MISSING=0 LINESIZE=200 PAGESIZE=100 COMPRESS=YES;
%INC PGM(PBBELF);
DATA REPTDATE;
   SET LOAN.REPTDATE;
   SELECT;
   WHEN(1<= DAY(REPTDATE)<= 8) DO;
        WK = '1';
     END;
     WHEN(9<= DAY(REPTDATE)<= 15) DO;
        WK = '2';
     END;
     WHEN(16<= DAY(REPTDATE)<= 22) DO;
        WK = '3';
     END;
     OTHERWISE DO;
        WK = '4';
     END;
   END;

   YEAREND=MDY(1,1,YEAR(REPTDATE))-1;

   PREVMON=MDY(MONTH(REPTDATE),1,YEAR(REPTDATE))-1;
   CALL SYMPUT('REPTMON',PUT(MONTH(REPTDATE),Z2.));
   CALL SYMPUT('NOWK',PUT(WK,$1.));
   CALL SYMPUT('REPTYEAR',PUT(REPTDATE,YEAR2.));
   CALL SYMPUT('REPTDAY',PUT(DAY(REPTDATE),Z2.));
   CALL SYMPUT('REPTMON1',PUT(MONTH(PREVMON),Z2.));
   CALL SYMPUT('REPTYEAR1',PUT(PREVMON,YEAR2.));
   CALL SYMPUT('REPTDATE',REPTDATE);
   CALL SYMPUT('LMON',PUT(MONTH(YEAREND),Z2.));
   CALL SYMPUT('LYEAR',PUT(YEAREND,YEAR2.));
RUN;

DATA REPTDATE;
   SET REPTDATE;
   DATE=COMPRESS(&REPTDAY||'/'||&REPTMON||'/'||&REPTYEAR);
   CALL SYMPUT('DATE',DATE);
RUN;

DATA HP;
   FORMAT IND $4.;
   SET HP.IHP&REPTMON&NOWK&REPTYEAR;
   IF BALANCE >0;
   IF COSTCTR=4048 THEN BRANCH = 903;
   ELSE IF COSTCTR=4043 THEN BRANCH = 906;
   BRANCHABBR=PUT(BRANCH,BRCHCD.);
   *IF PRODUCT IN (700,705,720,725,380,381) THEN IND='PBB';
   IF PRODUCT IN (128,130,131,132) THEN IND='PIBB';
   IF IND NE ' ';
RUN;

DATA CREDSUB(KEEP=ACCTNUM DAYSARR NOTENO
             RENAME=(ACCTNUM=ACCTNO DAYSARR=DAYARR));
   SET CCRIS.ICREDMSUBAC&REPTMON&REPTYEAR;
   WHERE FACILITY IN ('34331','34332');
RUN;

PROC SORT DATA=CREDSUB;BY ACCTNO NOTENO DESCENDING DAYARR;RUN;
PROC SORT DATA=CREDSUB NODUPKEY;BY ACCTNO NOTENO;RUN;
PROC SORT DATA=HP;BY ACCTNO NOTENO;RUN;

DATA HP;
   MERGE HP(IN=A) CREDSUB(IN=B);BY ACCTNO NOTENO;
   IF A;
RUN;

DATA PIBB(KEEP=ACCTNO NOTENO BRANCH BRANCHABBR PRODUCT
   CATEGORY IND BALANCE AANO PAIDIND MARKETVL);
   FORMAT CATEGORY $25.;
   SET HP;
   AANO = SUBSTR(LEFT(VINNO),1,13);
      IF DAYARR<=30 AND BORSTAT NOT IN ('F','I','R','E','W','Z')
         AND USER5 NOT IN ('N') AND PAIDIND='M'
      THEN DO;
   CATEGORY='CURRENT';
   END;
   IF 31<=DAYARR<=89 AND BORSTAT NOT IN ('F','I','R','E','W','Z')
             AND USER5 NOT IN ('N') AND PAIDIND='M'
      THEN DO;
         CATEGORY='1-2 MTHS';
      END;
   IF BORSTAT NOT IN ('F','I','R','E','W','Z') AND ((USER5 IN ('N')
             AND DAYARR<=182) OR 90<=DAYARR<=182) AND PAIDIND='M'
      THEN DO;
         CATEGORY='3-5 MTHS';
   END;
   IF BORSTAT NOT IN ('F','I','R','E','W','Z') AND ((USER5 IN ('N')
        AND 183<=DAYARR<=364) OR 183<=DAYARR<=364) AND PAIDIND='M'
      THEN DO;
         CATEGORY='6-11 MTHS';
   END;
   IF BORSTAT NOT IN ('F','I','R','E','W','Z') AND ((USER5 IN ('N')
             AND DAYARR>=365) OR DAYARR>=365) AND PAIDIND='M'
      THEN DO;
         CATEGORY='>=12 MTHS';
   END;
   IF BORSTAT='I' AND PAIDIND='M' THEN DO;
         CATEGORY='IRREGULAR';
   END;
   IF BORSTAT='R' AND PAIDIND='M' AND DAYARR<365 THEN DO;
         CATEGORY='REPOSSESSED <12 MTHS';
   END;
   IF BORSTAT='R' AND PAIDIND='M' AND DAYARR>=365 THEN DO;
         CATEGORY='REPOSSESSED >=12 MTHS';
   END;
   IF BORSTAT='F' AND PAIDIND='M' THEN DO;
         CATEGORY='DEFICIT';
   END;
   IF CATEGORY NE ' ';
RUN;

/* PIBB */
PROC SUMMARY DATA=PIBB NWAY;
CLASS CATEGORY;VAR BALANCE;
OUTPUT OUT=NPL.ISUMBAL(KEEP=CATEGORY BALANCE) SUM=;
RUN;

PROC SORT DATA=NPL.ICAP1;BY CATEGORY;RUN;
PROC SORT DATA=NPL.ISUMBAL;BY CATEGORY;RUN;

DATA ICOUNTCAP(KEEP=CATEGORY CARATE PD LGD);
   MERGE NPL.ICAP1 NPL.ISUMBAL;BY CATEGORY;
   CARATE=(CAPROVISION/BALANCE)*100;
   IF CATEGORY IN ('>=12 MTHS','IRREGULAR','DEFICIT',
                   'REPOSSESSED >=12 MTHS') THEN CARATE=100;
RUN;

PROC SORT DATA=PIBB;BY CATEGORY;RUN;
PROC SORT DATA=ICOUNTCAP;BY CATEGORY;RUN;

DATA NPL.ICAP&REPTMON&REPTYEAR;
   FORMAT CAP 20.2;
   MERGE PIBB(IN=A) ICOUNTCAP(IN=B);BY CATEGORY;
   IF A;
   CAP=(BALANCE*CARATE)/100;
RUN;

/* GET OPENING BALANCE */
DATA IOPENBAL(KEEP=ACCTNO NOTENO CAP CATEGORY PRODUCT BRANCH
              BRANCHABBR RENAME=(CAP=OPEN_BALANCE
              CATEGORY=CATEGORY1));
   SET NPL.ICAP&LMON&LYEAR(DROP=CATEGORY1);
   IF CATEGORY NE ' ';
   IF CATEGORY='>=6 MTHS' THEN CATEGORY='6-11 MTHS';
   *IF IND='PIBB' THEN OUTPUT IOPENBAL;
RUN;

PROC SORT DATA=IOPENBAL;BY ACCTNO NOTENO;RUN;
PROC SORT DATA=NPL.ICAP&REPTMON&REPTYEAR;
BY ACCTNO NOTENO;RUN;

DATA INDVIHP;
   MERGE IOPENBAL(IN=B)
         NPL.ICAP&REPTMON&REPTYEAR(IN=A);
         BY ACCTNO NOTENO;
   IF B AND NOT A THEN STATUS='P';
   IF A AND NOT B THEN STATUS='C';
   IF STATUS='P' THEN CATEGORY=CATEGORY1;
   IF PRODUCT IN (983,993,678,679,698,699) THEN CAP=0;
RUN;

DATA NPL.ICAP&REPTMON&REPTYEAR;
   SET INDVIHP;
   IF CAP=. THEN CAP=0;
   IF OPEN_BALANCE=. THEN OPEN_BALANCE=0;
   CHARCAP=CAP-OPEN_BALANCE;

   IF STATUS='P' THEN DO;
      IF CAP<OPEN_BALANCE THEN DO;
         SUSPEND=0;
      WRBACK=CAP-OPEN_BALANCE;
      END;
   END;

   IF STATUS='C' THEN DO;
      SUSPEND=CAP;
      WRBACK=CAP-SUSPEND;
   END;

   IF STATUS NOT IN ('P','C') THEN DO;
      IF CHARCAP<0 THEN DO;
         WRBACK=CHARCAP;
      END;
      ELSE DO;
         SUSPEND=CHARCAP;
      END;
   END;

   IF SUSPEND=. THEN SUSPEND=0;
   IF WRBACK=. THEN WRBACK=0;
   WRBACK=WRBACK*-1;
   NET=SUSPEND-WRBACK;
   WRIOFF_BAL=0; *TO BE HARDCODED*;
   BRANCH1 = BRANCHABBR||' '||PUT(BRANCH,Z3.);
RUN;

 * WRITTEN OFF FIGURE TO BE PROVIDED BY HPCC FOR HARDCODING UNTIL
   YEAR END *;

%MACRO PROCESS;
  %IF "&REPTMON" ="03" OR
      "&REPTMON" ="06" OR
      "&REPTMON" ="09" OR
      "&REPTMON" ="12" %THEN %DO;
  DATA IWOFF(KEEP=ACCTNO NOTENO WRIOFF_BAL);
    INFILE IWMIS;
    INPUT @142 ACCTNO    10.
          @152 NOTENO    5.
          @162 IISWOFF  16.2
          @178 SPWOFF   16.2
          @210 DDWOFF    2.
          @213 MMWOFF    2.
          @216 YYWOFF    4.
          @220 CAPBAL   16.2
          @236 COSTCTR   4.
          ;
    WRIOFF_BAL=CAPBAL;
    WOFFDT = PUT(MMWOFF,Z2.)||'/'||PUT(YYWOFF,Z4.);
    IF ((3000<=COSTCTR<=3999) OR COSTCTR IN (4043,4048));
  RUN;

  DATA HPWO(KEEP=ACCTNO NOTENO CATEGORY);
    SET WOF.IHP&REPTMON1&NOWK&REPTYEAR1;
    FORMAT CATEGORY $25.;
    IF DAYARR<=30 AND BORSTAT NOT IN ('F','I','R','E','W','Z')
       AND USER5 NOT IN ('N') AND PAIDIND='M'
       THEN DO;
       CATEGORY='CURRENT';
    END;
    IF 31<=DAYARR<=89 AND BORSTAT NOT IN ('F','I','R','E','W','Z')
       AND USER5 NOT IN ('N') AND PAIDIND='M'
       THEN DO;
         CATEGORY='1-2 MTHS';
       END;
    IF BORSTAT NOT IN ('F','I','R','E','W','Z') AND ((USER5 IN ('N')
       AND DAYARR<=182) OR 90<=DAYARR<=182) AND PAIDIND='M'
       THEN DO;
         CATEGORY='3-5 MTHS';
    END;
    IF BORSTAT NOT IN ('F','I','R','E','W','Z') AND ((USER5 IN ('N')
       AND 183<=DAYARR<=364) OR 183<=DAYARR>=364) AND PAIDIND='M'
       THEN DO;
         CATEGORY='6-11 MTHS';
    END;
    IF BORSTAT NOT IN ('F','I','R','E','W','Z') AND ((USER5 IN ('N')
       AND DAYARR>=365) OR DAYARR>=365) AND PAIDIND='M'
       THEN DO;
         CATEGORY='>=12 MTHS';
    END;
    IF BORSTAT='I' AND PAIDIND='M' THEN DO;
         CATEGORY='IRREGULAR';
    END;
    IF BORSTAT='R' AND PAIDIND='M' AND DAYARR <365 THEN DO;
       CATEGORY='REPOSSESSED <12 MTHS';
    END;
    IF BORSTAT='R' AND PAIDIND='M' AND DAYARR >=365 THEN DO;
       CATEGORY='REPOSSESSED >=12 MTHS';
    END;
    IF BORSTAT='F' AND PAIDIND='M' THEN DO;
         CATEGORY='DEFICIT';
    END;
  RUN;

  PROC SORT DATA=HPWO;BY ACCTNO NOTENO;RUN;
  PROC SORT DATA=IWOFF;BY ACCTNO NOTENO;RUN;

  DATA IWOFF;
    MERGE IWOFF(IN=A) HPWO(IN=B);BY ACCTNO NOTENO;
    IF A;
  RUN;

  DATA IWOFF;
     SET IWOFF;
     REPTDATE=&REPTDATE;
  RUN;

  PROC APPEND DATA=IWOFF BASE=NPL.WMIS FORCE;RUN;
%END;
%MEND;
%PROCESS;

DATA NPL.WMIS(DROP=CATEGORY RENAME=(CATEGORY1=CATEGORY));
   SET NPL.WMIS;
   FORMAT CATEGORY1 $25.;
   CATEGORY1=CATEGORY;
   IF CATEGORY='REPOSSESSED >=12 MTH' THEN
      CATEGORY1='REPOSSESSED >=12 MTHS';
RUN;

PROC SORT DATA=NPL.WMIS OUT=IWOFF;BY ACCTNO;RUN;
PROC SORT DATA=NPL.ICAP&REPTMON&REPTYEAR;BY ACCTNO;RUN;

DATA IWOFF;
   SET IWOFF;
   IF CATEGORY='>=6 MTHS' THEN CATEGORY='6-11 MTHS';
RUN;

DATA NPL.ICAP&REPTMON&REPTYEAR;
   MERGE NPL.ICAP&REPTMON&REPTYEAR(IN=A)
         IWOFF(IN=B);
   BY ACCTNO;
   IF B THEN WRITEOFF = 'Y';ELSE WRITEOFF = 'N';
   IF A;
RUN;

DATA NPL.ICAP&REPTMON&REPTYEAR;
   SET NPL.ICAP&REPTMON&REPTYEAR;
      IF STATUS='P' AND WRITEOFF = 'Y' THEN DO;
         SUSPEND=WRIOFF_BAL-OPEN_BALANCE;
         IF SUSPEND<0 THEN DO;
            WRBACK=OPEN_BALANCE-WRIOFF_BAL;
            SUSPEND=0;
         END;
         ELSE DO;
            WRBACK=0;
   END;
      END;
   NET=SUSPEND-WRBACK;
RUN;

DATA WOF(KEEP=ACCTNO NOTENO PRODUCT);
   SET HP.IHPWO&REPTMON&NOWK&REPTYEAR;
RUN;

PROC SORT DATA=WOF;BY ACCTNO NOTENO;RUN;
PROC SORT DATA=NPL.ICAP&REPTMON&REPTYEAR;BY ACCTNO NOTENO;RUN;

DATA NPL.ICAP&REPTMON&REPTYEAR;
   MERGE NPL.ICAP&REPTMON&REPTYEAR(IN=A) WOF(IN=B);
   BY ACCTNO NOTENO;IF A;
RUN;

* GENERATE SUMMARY REPORT *;

%LET TBL2=PIBB MOVEMENT OF CAP BY BRANCH AS AT;

PROC TABULATE DATA=NPL.ICAP&REPTMON&REPTYEAR
FORMAT=COMMA20.2
MISSING NOSEPS FORMCHAR(1,2,3,4,5,6,7,8,9,10,11)='|-+++++++++';
CLASS BRANCH1 BRANCHABBR;
VAR BALANCE OPEN_BALANCE SUSPEND WRBACK WRIOFF_BAL CAP NET;
TABLE BRANCH1=' ' ALL='TOTAL',N='NO OF ACCOUNT'*F=COMMA7.
   SUM=' '*(BALANCE='BALANCE'
            OPEN_BALANCE='OPENING BALANCE'
            SUSPEND='CHARGE FOR THE YEAR'
            WRBACK='WRITTEN BACK TO P & L'
            WRIOFF_BAL='WRITTEN-OFF'
            CAP='CLOSING BALANCE'
            NET='NET INCREASE/DECREASE')/ BOX='BRANCH' RTS=15;
   TITLE1 &TBL2 &DATE;
RUN;

