//EIBMLI4I JOB MISEIS,EIBMLIQ4,CLASS=A,MSGCLASS=A,NOTIFY=&SYSUID
//*
//* ADHOC - SAIFUL ADZLIM  ISLAMIC PART 4
//*
//SAS609  EXEC SAS609
//BNMTBL4   DD DSN=SAP.PBB.KAPITI4(0),DISP=SHR
//PGM       DD DSN=SAP.BNM.PROGRAM,DISP=SHR
//SASLIST   DD SYSOUT=X
//SYSIN     DD *

OPTIONS NOCENTER YEARCUTOFF=1950 PS=60 LS=132;
*;
PROC FORMAT;
   VALUE REMFMT
      LOW-0.255 = 'UP TO 1 WK     '
      0.255 -1  = '>1 WK - 1 MTH  '
      1-3     = '>1 MTH - 3 MTHS'
      3-6     = '>3 - 6 MTHS    '
      6-12    = '>6 MTHS - 1 YR '
      OTHER   = '>1 YEAR        ';

   VALUE REMFMTA
      LOW-0.255 = '01'
      0.255-1   = '02'
      1-3       = '03'
      3-6       = '04'
      6-12      = '05'
      OTHER     = '06';
*;
RUN;
*;
 * TO ENSURE CORRECT MTH-END DATE IS TAKEN SHOULD IT BE MTH-END;
%MACRO MTHENDDT;
   IF MTHEND='Y' THEN DO;
      SELECT(MTHIPD);
         WHEN(1,3,5,7,8,10,12) MTHDAY=31;
         WHEN(4,6,9,11) MTHDAY=30;
         OTHERWISE DO;
         IF MOD(YRTRAN,4) = 0 THEN MTHDAY=29;
            ELSE MTHDAY=28;
         END;
      END;
      IF DAYTRAN > MTHDAY THEN DAYTRAN=MTHDAY;
   END;
%MEND MTHENDDT;
*;
%MACRO CALCIPD;
   MTHEND='N';  * CHECK IF TRANSACTION DATE IS ON MONTH-END ;
   NPAY=6;      * PAYMENT FREQUENCY IS FIXED COS UTIPI IS NIL;
   NYEAR=0;
   IF DAY(MATDT+1)=1 THEN MTHEND='Y';

   DAYTRAN=DAY(MATDT);
   YRTRAN =YEAR(MATDT);
   MTHTRAN=MONTH(MATDT);

   TRANDAYS=MATDT-REPTDT;
   NUMOFN=INT(TRANDAYS/(NPAY*30));
   NUMMTH=NUMOFN*NPAY;
   NYEAR=INT(NUMMTH/12);
   NMTH=MOD(NUMMTH,12);
   MTHIPD=(MTHTRAN-NMTH)-NPAY;
   IF MTHIPD <= 0 THEN DO;
      MTHIPD=MTHIPD+12; NYEAR+1;
   END;
   YRTRAN=YRTRAN-NYEAR;
   %MTHENDDT;
   PREINTDT=MDY(MTHIPD,DAYTRAN,YRTRAN);
   IF PREINTDT NE . THEN DO;
      DAYTRAN=DAY(PREINTDT);
      YRTRAN =YEAR(PREINTDT);
      MTHIPD=MONTH(PREINTDT);

      MTHIPD+NPAY;
      IF MTHIPD > 12 THEN DO;
         YRTRAN+1;
         MTHIPD=MOD(MTHIPD,12);
      END;
      %MTHENDDT;
      CURINTDT=MDY(MTHIPD,DAYTRAN,YRTRAN);
   END;
%MEND CALCIPD;
*;
DATA REPTDATE;
   INFILE BNMTBL4  OBS=1;
   INPUT @113 UTRPT $10.;
   REPTDATE=INPUT(UTRPT,DDMMYY10.);
   CALL SYMPUT('RDATE',PUT(REPTDATE,DDMMYY8.));

DATA LIQCLASS;
   INFILE BNMTBL4;
   INPUT @1  UTOSD $10.
         @11 UTSMN $16.
         @27 UTCPR PD6.7
         @33 UTYLD PD4.4
         @37 UTFCV PD8.2
         @45 UTDCV PD8.2
         @53 UTMKV PD8.2
         @61 UTMDT $10.
         @71 UTINT PD8.2
         @79 UTPLS PD8.2
         @87 UTSTY $3.
         @90 UTTTY $1.
         @91 UTITY $1.
         @92 UTIPI $1.
         @93 UTLCD $10.
         @103 UTNCD $10.
         @113 UTRPT $10.
         @123 UTSTS $5.
         @128 UTAMS PD8.2
         @136 UTCLS $1.
         @137 UTPGMID $10.
         @147 UTIDT $10.
         @157 UTRMD $10.
         @167 UTAMP PD8.2
         @175 UTREF $4.
         @175 UTREF1 $1.
         @178 UTREF4 $1.
         @191 UTTYP $3.
         ;

   LABEL UTOSD = 'SETTLEMENT/TRANSACTION DATE'
         UTSMN = 'SECURITY MNEMONIC/STOCK CODE'
         UTCPR = 'DISCOUNT/COUPON RATE'
         UTYLD = 'YIELD'
         UTFCV = 'FACE VALUE'
         UTDCV = 'COST/CAPITAL VALUE'
         UTMKV = 'MARKET VALUE'
         UTMDT = 'MATURITY DATE'
         UTINT = 'INTEREST AMOUNT'
         UTPLS = 'PROFIT & LOSS'
         UTSTY = 'SECURITY TYPE'
         UTTTY = 'TRANSACTION TYPE'
         UTITY = 'INSTRUMENT TYPE'
         UTIPI = 'INTEREST PAYMENT INDICATOR'
         UTLCD = 'LAST INTEREST PAYMENT DATE'
         UTNCD = 'NEXT INTEREST PAYMENT DATE'
         UTRPT = 'REPORTING DATE'
         UTSTS = 'STATUS'
         UTAMS = 'SALES PROCEEDS'
         UTCLS = 'CLASS'
       UTPGMID = 'PROGRAM ID'
         UTIDT = 'ISSUE DATE' /* MM-DD-YYYY */
         UTRMD = 'REPO MAT DATE'
         UTAMP = 'PURCHASE PROCEEDS'
         UTREF = 'PORTFOLIO REF'
         UTTYP = 'PORTFOLIO TYP'
         ;

   IF UTREF1 EQ 'I' AND UTREF4 NE '  ';

DATA LIQCLASS;
   SET LIQCLASS;
       AMOUNT=UTMKV;
       IF AMOUNT=.  THEN AMOUNT=0;
       REPTDATE=INPUT(UTRPT,DDMMYY10.);
       RPYR  = YEAR(REPTDATE);
       RPMTH = MONTH(REPTDATE);
       RPDAY = DAY(REPTDATE);
       IF MOD(RPYR,4) = 0 THEN RD2 = 29;

       CHKDT='04SEP04'D;
       STATUS=SUBSTR(UTSTS,1,2);
       ISSDT=0;
       IF UTIDT NOT = '    ' THEN DO;
          ISSDD=SUBSTR(UTIDT,4,2);
          ISSMM=SUBSTR(UTIDT,1,2);
          ISSYY=SUBSTR(UTIDT,7,4);
          ISSDT=MDY(ISSMM,ISSDD,ISSYY);
       END;
       IF ISSDT LE CHKDT THEN C2='R'; ELSE
       IF ISSDT > CHKDT  THEN C2='Y';
*;
DATA LIQCLAS1;
   SET LIQCLASS;
   SELECT(UTSTY);
     WHEN ('MGS','MTB','BNB','BNN','BMN','BMC',
           'KHA','MGI','ITB') DO;
        CLASS = 'A'; SLIPPAGE = 2;
        IF UTTTY='X'             THEN CLASS='R';
     END;

     WHEN ('IDS','DHB') DO;
        CLASS = 'A'; SLIPPAGE = 3;
        IF UTTTY='X'             THEN CLASS='R';
     END;
     WHEN ('DMB') DO;
        CLASS = 'A'; SLIPPAGE = 4;
        IF UTTTY='X'             THEN CLASS='R';
     END;
     WHEN ('CB2','CF1','CF2','CMB','PNB') DO;
        CLASS = 'B'; SLIPPAGE = 4;
        IF UTSTY='CB2' AND UTTTY='X' AND C2='R'  THEN CLASS='R';
     END;
     WHEN ('CB1','CNT','SMC','SAC') DO;
        CLASS = 'B'; SLIPPAGE=4;
        IF UTTTY='X' AND C2='R'  THEN CLASS='R';
        IF C2='Y' THEN DO; CLASS = 'F'; SLIPPAGE=6; END;
     END;
     WHEN ('SBA') DO;
        IF SUBSTR(UTSTS,1,2) IN ('P1','P2','AA') THEN DO;
           CLASS = 'C'; SLIPPAGE = 4; END;
        ELSE DO;
           CLASS = 'D'; SLIPPAGE = 6; END;
     END;
     OTHERWISE;
   END;
   IF CLASS NE '   ' THEN OUTPUT LIQCLAS1;
*;
DATA LIQCLAS2;
   SET LIQCLASS;
   IF UTSTY IN ('SSD','SDC') AND UTSTS='P1' THEN DO;
      CLASS = 'E'; SLIPPAGE = 6;
      OUTPUT LIQCLAS2;
   END;
   IF UTSTY IN ('SLD','SFD','SZD') AND
      UTSTS IN ('AA','AAA','MARC1','MARC2') THEN DO;
      CLASS = 'E'; SLIPPAGE = 6;
      OUTPUT LIQCLAS2;
   END;
RUN;
*;
DATA LIQCLAS3;
   SET LIQCLAS2;
   IF  UTTTY='R' THEN DO;
       CLASS='X'; OUTPUT;
       CLASS='Y'; OUTPUT;
   END;
*;
DATA LIQCLASS;
  SET LIQCLAS1 LIQCLAS2 LIQCLAS3;
*;

DATA LIQCLASS;
   SET LIQCLASS;
   IF _N_ = 1 THEN DO;
      SET REPTDATE;
      RPYR  = YEAR(REPTDATE);
      RPMTH = MONTH(REPTDATE);
      RPDAY = DAY(REPTDATE);
      IF MOD(RPYR,4) = 0 THEN RD2 = 29;
   END;
   IF CLASS NE ' ' OR CLASS NE . ;
      FORMAT DISCOUNT P 15.6;
      DISTYLD  = UTYLD + SLIPPAGE;
      DISCOUNT = (DISTYLD/100)*UTMKV;
      P = DISCOUNT;
      CPN = UTCPR;
      YLD = DISTYLD;
      RV = 100;
      DISTAMT  = 0;
      REPTDT   = INPUT(UTRPT,DDMMYY10.);
      SETTLEDT = INPUT(UTOSD,DDMMYY10.);
      PREINTDT = INPUT(UTLCD,DDMMYY10.);
      CURINTDT = INPUT(UTNCD,DDMMYY10.);
      MATDT    = INPUT(UTMDT,DDMMYY10.);
      IF UTTTY='X' THEN MATDT=INPUT(UTRMD,DDMMYY10.);
      IF UTTTY='R' THEN DO;
         IF CLASS='X' THEN MATDT=INPUT(UTMDT,DDMMYY10.);
         IF CLASS='Y' THEN MATDT=INPUT(UTRMD,DDMMYY10.);
      END;
      TSM      = MATDT - REPTDT;


      IF CURINTDT=. OR CURINTDT=' ' OR CURINTDT=0 THEN
         TM=MATDT-REPTDT;
      ELSE TM=CURINTDT-REPTDT;

      REMMTH = ROUND((TSM/365)*12,.01);
      IF TSM < 8 THEN REMMTH=0.1;  /* FIXED THIS TO BE MORE ACCURATE */
      IF (UTITY IN ('I','F') AND UTIPI ^= ' ') OR
         (UTSTY IN ('SZD','DHB','DMB','IDS','KHA','MGI')) THEN DO;




         IF UTSTY IN ('DHB','IDS','KHA','DMB','MGI') THEN DO;
            %CALCIPD;   * CALCULATE INTEREST PAYMENT DATES ;
         END;
         *;
         DSC = CURINTDT - REPTDT;
         DCS = REPTDT - PREINTDT;
         DCC = CURINTDT - PREINTDT;

         NDAYS = MATDT - CURINTDT;



         IF UTSTY IN ('SZD','DHB','DMB','IDS','KHA') THEN CPN = 0;
         IF REMMTH < 6 AND UTITY = 'I' THEN DO;
            CPN2 = CPN/100;
            YLD = YLD/100;
            ACCINT = CPN2*(DCS/(2*DCC));
            DISCOUNT = (1+CPN2/2)/(1+YLD*(TSM/(2*DCC)))-ACCINT;
            P = DISCOUNT*100;
         END;
         ELSE DO;  /* APPENDIX 1 FORMULA */
            IF UTSTY NOT IN ('SFD','CF1','CF2','CFB','SSD') THEN DO;
               SELECT(UTIPI);       /* CALCULATE N VALUE  */
                 WHEN('Q') DO; /* QUARTERLY, EVERY 3 MONTHS ONCE */
                   NPAY = 3; END;
                 WHEN('H') DO; /* HALF-YEARLY, EVERY 6 MONTHS ONCE */
                   NPAY = 6; END;
                 WHEN('Y') DO; /* YEARLY, EVERY 12 MONTHS ONCE */
                   NPAY = 12; END;
                 OTHERWISE;
               END;
               *;
               NMTH = ROUND(NDAYS/365*12,.1);
               N = INT(NMTH/NPAY)+1;
               /* CALCULATE PART I VALUE */
               POWERI = N-1+DSC/DCC;   /* CALCULATE FOR PART I */
               I = RV/((1+YLD/200)**POWERI);
               II = 0;

               IF DSC ^= . AND DCC ^= . THEN DO;
                  DO K = 1 TO N;
                     POWERII = K-1+DSC/DCC;
                     II + (CPN/2)/((1 + YLD/200)**POWERII);
                  END;

               /* CALCULATE PART III */
                  III = 100*((CPN/200)*(DCS/DCC));

               /* THE WHOLE FORMULAE */
                  DISCOUNT = I + II - III;
                  P = DISCOUNT;
               END;
            END;
         END;
      END;

      REPTDAYS = REPTDT - PREINTDT;
      ORGTENOR = MATDT - PREINTDT;
      CPN2 = CPN/100;
      YLD2 = YLD/100;
      SELECT(UTSTY);
         WHEN('MGS','CBB','SLD','CB1','CB2','CMB','PNB') DO;
            DISTAMT = UTFCV*((P/100)+(CPN2*REPTDAYS)/(DCC*2));
         END;
         WHEN('CFB','SFD','CF1','CF2') DO;
            DISTAMT = UTFCV*((36500+(CPN*DCC))/(36500+(YLD*TM)));
         END;
     /*  WHEN('SZD','KHA','IDS','DHB','DMB','ISB') DO; */
         WHEN('SZD','KHA','IDS','DHB','DMB') DO;
            DISTAMT = UTFCV*(P/100);
         END;
         WHEN('SSD','SLD') DO;
            DISTAMT = UTFCV*(36500+CPN*ORGTENOR)/(36500+YLD*TSM);
         END;
         OTHERWISE DO;
            DISTAMT = UTFCV*(1-(YLD*TSM)/36500);
         END;
      END;
RUN;

PROC SORT DATA=LIQCLASS OUT=LIQCLASS;
   BY UTSTY CLASS;
RUN;
*;
%INC PGM(MATDTEX);
*;
DATA LIQCLASS;
    SET LIQCLASS;
    MRNGE=PUT(REMMTH,REMFMT.);
*;
TITLE1 'REPORT ID: EIBMLIQ4';
TITLE2 'PUBLIC BANK BERHAD - STATISTICS DEPARTMENT';
TITLE3 'STOCK OF LIQUEFIABLE ASSETS (PART 4)';
TITLE4 'AS AT ' &RDATE;
*;
PROC PRINT DATA=LIQCLASS SPLIT='*';
VAR UTOSD CPN YLD UTMKV UTMDT UTSTY UTFCV PREINTDT CURINTDT
    DISCOUNT DISTAMT UTTTY STATUS UTRMD MRNGE;
SUM DISTAMT;
FORMAT PREINTDT CURINTDT DDMMYY8.;
RUN;
*;
PROC SUMMARY DATA=LIQCLASS NWAY;
CLASS UTSTY CLASS REMMTH UTTTY;
VAR   DISTAMT UTMKV UTAMP;
OUTPUT OUT=LIQASSET (DROP=_TYPE_ _FREQ_)
       SUM=;
RUN;
*;
DATA LIQASSET;
   KEEP MKVNIDX MKVNIDY UTAMP
        CLASS UTSTY AMTSTK AMTREPO MKVBOOK MKVREV TOTDSCT REMMTH;
   SET LIQASSET;
   BY UTSTY CLASS REMMTH;
   IF FIRST.CLASS OR FIRST.REMMTH THEN DO;
      STKNID = 0; REPNID = 0;
      AMTSTK = 0; AMTREPO = 0; AMTREV = 0; TOTDSCT = 0;
   END;
   RETAIN AMTSTK AMTREPO AMTREV TOTDSCT STKNID REPNID;

   SELECT(UTTTY);
      WHEN('S') DO;
         AMTSTK + UTMKV;
      END;
      WHEN('R') DO;
         AMTREPO + UTMKV;
         IF CLASS='X' THEN STKNID=STKNID+UTAMP;
         IF CLASS='Y' THEN REPNID=REPNID+UTAMP;
      END;
      WHEN('X') DO;
         AMTREV + UTMKV;
      END;
      OTHERWISE;
   END;
   IF UTSTY = 'SSD' OR
      UTSTY = 'SLD' THEN DO;
      IF UTTTY='R' THEN TOTDSCT = TOTDSCT - DISTAMT;
      ELSE TOTDSCT + DISTAMT;
   END;
   ELSE TOTDSCT + DISTAMT;

   IF LAST.UTSTY OR LAST.CLASS OR LAST.REMMTH THEN DO;
      MKVBOOK = AMTSTK - AMTREPO;
      MKVREV  = AMTREV;
      MKVNIDX = STKNID;
      MKVNIDY = REPNID;
      OUTPUT;
   END;
RUN;
*;
PROC FORMAT;
   VALUE $CLASSF
     'A' = 'RM MKTBL SECUR/PAPERS ISSUED BY FED GOVT/BNM'
     'B' = 'CAGAMAS BONDS & NOTES'
     'C' = 'BAS ISSUED BY TIER1/AAA-RATED INST.'
     'D' = 'BAS ISSUED BY TIER2 & NON-AAA'
     'E' = 'NIDS ISSUED BY RATING'
     'F' = 'STOCK                '
     'R' = 'REVERSE REPO            '
     'X' = 'NIDS UDR REPO (LIABILITIES)'
     'Y' = 'NIDS UDR REPO (ASSETS)  ';
RUN;
*;
PROC TABULATE DATA=LIQASSET FORMCHAR='           ';
  CLASS CLASS UTSTY;
  VAR   MKVBOOK MKVREV TOTDSCT;
  TABLE CLASS = ' ',UTSTY ALL,
        MKVBOOK = 'MKT VALUE SECUR REPT IN BOOKS'*F=COMMA16.2
        MKVREV  = 'MKT VALUE SECUR RECV UDR REV REPO'*F=COMMA16.2
        TOTDSCT = 'TOTAL VALUE OF SECUR AFTER DISCOUNT'*F=COMMA16.2
        /RTS=50 BOX='CLASS-1 LIQUIFIABLE ASSETS';
  KEYLABEL SUM = ' ' ALL = 'TOTAL';
  FORMAT CLASS $CLASSF.;
  WHERE CLASS IN ('A','B');
RUN;
*;
PROC TABULATE DATA=LIQASSET FORMCHAR='           ';
  CLASS CLASS UTSTY;
  VAR   MKVBOOK MKVREV TOTDSCT;
  TABLE CLASS = ' ',UTSTY ALL,
        MKVBOOK = 'MKT VALUE SECUR REPT IN BOOKS'*F=COMMA16.2
        MKVREV  = 'MKT VALUE SECUR RECV UDR REV REPO'*F=COMMA16.2
        TOTDSCT = 'TOTAL VALUE OF SECUR AFTER DISCOUNT'*F=COMMA16.2
        /RTS=50 BOX='CLASS-1 LIQUIFIABLE ASSETS';
  KEYLABEL SUM = ' ' ALL = 'TOTAL';
  FORMAT CLASS $CLASSF.;
  WHERE CLASS='R';
RUN;
*;
PROC TABULATE DATA=LIQASSET FORMCHAR='           ';
  FORMAT REMMTH REMFMT.;
  CLASS CLASS REMMTH;
  VAR   MKVREV  TOTDSCT;
  TABLE CLASS=' ',REMMTH='REMAINING MATURITY' ALL,
        MKVREV  = 'MKT VALUE SECUR RECV UDR REV REPO'*F=COMMA16.2
        TOTDSCT = 'TOTAL VALUE OF SECUR AFTER DISCOUNT'*F=COMMA16.2
        /RTS=30 BOX='CLASS-1 LIQUID ASSETS BY';
  FORMAT CLASS $CLASSF.;
  KEYLABEL SUM = ' ' ALL = 'TOTAL';
  WHERE CLASS IN ('R');
*;
PROC TABULATE DATA=LIQASSET FORMCHAR='           ';
  CLASS CLASS UTSTY;
  VAR   MKVBOOK MKVREV TOTDSCT;
  TABLE CLASS = ' ',UTSTY ALL,
        MKVBOOK = 'MKT VALUE SECUR REPT IN BOOKS'*F=COMMA16.2
        MKVREV  = 'MKT VALUE SECUR RECV UDR REV REPO'*F=COMMA16.2
        TOTDSCT = 'TOTAL VALUE OF SECUR AFTER DISCOUNT'*F=COMMA16.2
        /RTS=50 BOX='CLASS-2 LIQUIFIABLE ASSETS FOR CLASS F';
  KEYLABEL SUM = ' ' ALL = 'TOTAL';
  FORMAT CLASS $CLASSF.;
  WHERE CLASS='F';
RUN;
*;
PROC TABULATE DATA=LIQASSET FORMCHAR='           ';
  FORMAT REMMTH REMFMT.;
  CLASS  CLASS REMMTH;
  VAR    MKVBOOK MKVREV TOTDSCT;
  TABLE  CLASS=' ',REMMTH='REMAINING MATURITY' ALL,
         MKVBOOK = 'MKT VALUE SECUR REPT IN BOOKS'*F=COMMA16.2
         MKVREV  = 'MKT VALUE SECUR RECV UDR REV REPO'*F=COMMA16.2
         TOTDSCT = 'TOTAL VALUE OF SECUR AFTER DISCOUNT'*F=COMMA16.2
        /RTS=50 BOX='CLASS-2 LIQUIFIABLE ASSETS FOR CLASS F';
  KEYLABEL SUM = ' ' ALL = 'TOTAL';
  FORMAT CLASS $CLASSF.;
  WHERE CLASS='F';
RUN;
*;
PROC TABULATE DATA=LIQASSET FORMCHAR='           ';
  CLASS CLASS UTSTY;
  VAR   MKVBOOK MKVREV TOTDSCT;
  TABLE CLASS=' ',UTSTY ALL,
        MKVBOOK = 'MKT VALUE SECUR REPT IN BOOKS'*F=COMMA16.2
        MKVREV  = 'MKT VALUE SECUR RECV UDR REV REPO'*F=COMMA16.2
        TOTDSCT = 'TOTAL VALUE OF SECUR AFTER DISCOUNT'*F=COMMA16.2
        /RTS=50 BOX='CLASS-2 LIQUID ASSETS & CREDIT LINES';
  FORMAT CLASS $CLASSF.;
  KEYLABEL SUM = ' ' ALL = 'TOTAL';
  WHERE CLASS IN ('C','D','E');
RUN;
*;
PROC TABULATE DATA=LIQASSET FORMCHAR='           ';
  FORMAT REMMTH REMFMT.;
  CLASS CLASS REMMTH;
  VAR   MKVBOOK TOTDSCT;
  TABLE CLASS=' ',REMMTH='REMAINING MATURITY' ALL,
        MKVBOOK = 'MKT VALUE SECUR REPT IN BOOKS'*F=COMMA16.2
        TOTDSCT = 'TOTAL VALUE OF SECUR AFTER DISCOUNT'*F=COMMA16.2
        /RTS=30 BOX='CLASS-2 LIQUID ASSETS BY';
  FORMAT CLASS $CLASSF.;
  KEYLABEL SUM = ' ' ALL = 'TOTAL';
  WHERE CLASS IN ('C','D','E');
*;
PROC TABULATE DATA=LIQASSET FORMCHAR='           ';
  CLASS CLASS UTSTY;
  VAR   MKVNIDY;
  TABLE CLASS=' ',UTSTY ALL,
        MKVNIDY='PURCHASE PROCEEDS FOR NIDS UDR REPO'*F=COMMA16.2
      / RTS=50 BOX='CLASS-2 LIQUID ASSETS';
  FORMAT CLASS $CLASSF.;
  KEYLABEL SUM = ' ' ALL = 'TOTAL';
  WHERE CLASS='Y';
RUN;
*;
PROC TABULATE DATA=LIQASSET FORMCHAR='           ';
  FORMAT REMMTH REMFMT.;
  CLASS  CLASS REMMTH;
  VAR    MKVNIDX;
  TABLE  CLASS=' ',REMMTH='REMAINING MATURITY' ALL,
         MKVNIDX='STOCK MATURITY DATE'*F=COMMA20.2
      /  RTS=30 BOX='CLASS-2 LIQUID ASSETS BY';
  FORMAT CLASS $CLASSF.;
  KEYLABEL SUM = ' ' ALL = 'TOTAL';
  WHERE CLASS='X';
*;
PROC TABULATE DATA=LIQASSET FORMCHAR='           ';
  FORMAT REMMTH REMFMT.;
  CLASS  CLASS REMMTH;
  VAR    MKVNIDY;
  TABLE  CLASS=' ',REMMTH='REMAINING MATURITY' ALL,
         MKVNIDY='REPO MATURITY DATE'*F=COMMA20.2
      /  RTS=30 BOX='CLASS-2 LIQUID ASSETS BY';
  FORMAT CLASS $CLASSF.;
  KEYLABEL SUM = ' ' ALL = 'TOTAL';
  WHERE CLASS='Y';
*;
