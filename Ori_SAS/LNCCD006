*+---------------------------------------------------------------+
 |  PROGRAM  : LNCCD006                                          |
 |  DATE     : 01.01.98                                          |
 |  FUNCTION : TO PROVIDE GP3 REPORTS FOR CCD PFB.               |
 |             (THE MONTH-END VERSION)                           |
 +---------------------------------------------------------------+
 |  DATE MODIFIED : 12-8-2002  (LN6)                             |
 |  CHANGES MADE  : TO INCLUDE ISLAMIC LOANS.                    |
 +---------------------------------------------------------------+;

%INC PGM(PBBLNFMT);

PROC DATASETS LIB=BNM NOLIST;
  DELETE LOANTEMP;
RUN;

%LET LOAN=(KEEP=BRANCH ACCTNO NAME NOTENO PRODUCT BALANCE BORSTAT
                BLDATE ARREAR THISDATE DAYDIFF LOANSTAT BILPAY
                ARREAR2 CHECKDT CENSUS COLLDESC ISSDTE FEEDUE
                OLDNOTEDAYARR);

  /*** TO INCLUDE ISLAMIC MANUPULATION   */

%LET ABBA=(100,101,102,105,106,120,121,127,126,
           112,113,114,116,117,118);

PROC SORT DATA=LOAN.LNNOTE OUT=LNNOTE; BY ACCTNO COMMNO;

PROC SORT DATA=LOAN.LNCOMM OUT=LNCOMM NODUPKEY; BY ACCTNO COMMNO;

DATA LNNOTE;
   MERGE LNNOTE(IN=A) LNCOMM(IN=B); BY ACCTNO COMMNO;
   IF A;
*;
DATA BNM.LOANTEMP &LOAN;
  RETAIN D1-D12 31 D4 D6 D9 D11 30;
  ARRAY LDAY D1-D12;
  /* SET LOAN.LNNOTE (RENAME=(LOANTYPE=PRODUCT PENDBRH=BRANCH)); */
  SET LNNOTE (RENAME=(LOANTYPE=PRODUCT PENDBRH=BRANCH));
  IF REVERSED NE 'Y';
  THISDATE = INPUT("&RDATE", DDMMYY8.);
  ISSDTE = INPUT(SUBSTR(PUT(ISSUEDT,Z11.),1,8),MMDDYY8.);
  /* CHECK = '01JAN98'D;
  IF ISSDTE GE CHECK THEN CHECKDT = 1;
     ELSE CHECKDT = 0;        */
  *+-----------------------------------------------------+
   |  INCLUDE ALL PAIDIND NE 'P' LOANS                   |
   +-----------------------------------------------------+;
  IF NOTENO NE . AND PAIDIND NE 'P' AND PRODUCT NOT IN &ABBA  THEN DO;
    IF BRANCH = 0 THEN BRANCH = NTBRCH;
    SELECT;
      WHEN (INTAMT = 0.01) DO;
        TEMP = -1 * REBATE; REBATE = TEMP;
        TEMP = -1 * INTEARN4; INTEARN4 = TEMP;
        BALANCE = SUM(CURBAL,FEEAMT,REBATE,INTEARN4);
        INTAMT = 0; INTEARN = 0; ACCRUAL = 0;
        INTEARN2 = 0; INTEARN3 = 0;
      END;
      WHEN ((REBATE + INTEARN) NE INTAMT OR
           (INTEARN4 + INTEARN3) NE INTEARN2) DO;
        REPTDATE = INPUT("&RDATE", DDMMYY8.);
        RMM = MONTH(REPTDATE);
        RYY = YEAR(REPTDATE);
        ISSUE = INPUT(SUBSTR(PUT(ISSUEDT, Z11.), 1, 8), MMDDYY8.);
        IMM = MONTH(ISSUE);
        IYY = YEAR(ISSUE);
        REMAIN = NOTETERM-(((RYY*12)+RMM+1)-((IYY*12)+IMM));
        UNEARN1=(REMAIN*(REMAIN+1)*(INTAMT))/(NOTETERM*(NOTETERM+1));
        UNEARN2=(REMAIN*(REMAIN+1)*(INTEARN2))/(NOTETERM*(NOTETERM+1));
        BALANCE=SUM(CURBAL,(-1)*UNEARN1,(-1)*UNEARN2,FEEAMT);
        INTAMT = 0; INTEARN = 0; ACCRUAL = 0; REBATE = 0;
        INTEARN2 = 0; INTEARN3 = 0; INTEARN4 = 0;
      END;
      OTHERWISE DO;
        REBATE=0; INTEARN4=0;
      END;
    END;
    LINK COUNTDAY;
    OUTPUT;
  END;
  IF NOTENO NE . AND PAIDIND NE 'P' AND PRODUCT IN &ABBA THEN DO;
     REBMTD=INTAMT-INTEARN;  /* CALCULATE REBATE AS @MTH-END */
    /* COMPUTE BALANCE FOR ABBA LOANS WHICH REQUIRE CAVAIAMT */
     IF PAIDIND ^='P' THEN DO;
          FEEAMT=FEEAMT+FEEAMT4;
        IF CAVAIAMT NOT IN (0,.)  THEN DO;
           BALANCE=(CURBAL-REBMTD)+FEEAMT-CAVAIAMT;
        END;
        ELSE DO;
           BALANCE=(CURBAL-REBMTD)+FEEAMT;
        END;
     END;
    * IF PRODUCT = 140 THEN BALANCE=(CURBAL-REBMTD)+FEEAMT;
     IF PRODUCT IN (139,117,118,119) THEN
        BALANCE=(CURBAL+ACCRUAL+FEEAMT);
     LINK COUNTDAY;
     OUTPUT;
  END;
  *+-----------------------------------------------------+
   |  INCLUDE OUTSTANDING FEE AMOUNT FOR PAID LOANS      |
   +-----------------------------------------------------+;
  IF PAIDIND = 'P' AND FEEAMT > 0 THEN DO;
    IF BRANCH = 0 THEN BRANCH = NTBRCH;
    BALANCE = FEEAMT;
    LINK COUNTDAY;
    OUTPUT;
  END;
  *+-----------------------------------------------------+
   |  INCLUDE LOANS WHICH ARE SETTLED EARLY, WITHIN      |
   |  THE SAME MONTH                                     |
   +-----------------------------------------------------+;
  REPTDATE = INPUT("&RDATE", DDMMYY8.);
  CURMONTH = MONTH(REPTDATE);
  CURYEAR  = YEAR(REPTDATE);
  LNDATE  = INPUT(SUBSTR(PUT(LASTTRAN,Z11.),1,8),MMDDYY8.);
  LNMONTH = MONTH(LNDATE);
  LNYEAR  = YEAR(LNDATE);
  IF PAIDIND = 'P' AND (LNMONTH = CURMONTH) AND
    (LNYEAR = CURYEAR) AND NTINT = 'A' THEN DO;
    IF BRANCH = 0 THEN BRANCH = NTBRCH;
    IF INTAMT = 0.01 THEN DO;
      BALANCE = 0;
    END;
    ELSE DO;
    BALANCE = ((INTAMT-INTEARN)-REBATE)+((INTEARN2-INTEARN3)-INTEARN4);
      IF BALANCE < 0 THEN NEWBAL = ABS(BALANCE);
        ELSE NEWBAL = 0 - BALANCE;
      BALANCE = NEWBAL;
    END;
    LINK COUNTDAY;
    OUTPUT;
  END;
RETURN;
COUNTDAY:
  IF BLDATE > 0 THEN DO;
     ISSDTE = INPUT(SUBSTR(PUT(ISSUEDT,Z11.),1,8),MMDDYY8.);
     IF BILPAY > 0 THEN DO;
        IF BILTOT = . THEN BILTOT = 0;
        IF BILPRIN = . THEN BILPRIN = 0;
        IF BILTOT / BILPAY <= 0.01 THEN DO;
           DD = DAY(ISSDTE);
           MM = MONTH(BLDATE) + 1;
           YY = YEAR(BLDATE);
           IF MM > 12 THEN DO;
              MM = 1; YY + 1;
           END;
           IF MM = 2 THEN
              IF MOD(YY,4) = 0 THEN D2 = 29;
              ELSE D2 = 28;
           IF DD > LDAY(MM) THEN DD = LDAY(MM);
           BLDATE = MDY(MM,DD,YY);
        END;
     END;
  END;
  IF BLDATE > 0 THEN DAYDIFF = THISDATE - BLDATE;
  /* IF 200<= PRODUCT<= 299 AND BORSTAT EQ 'A' THEN DO;
        IF  '20OCT00'D <= THISDATE <= '30JUN01'D THEN
          DAYDIFF = DAYDIFF - (THISDATE - '20OCT00'D);
        IF THISDATE > '30JUN01'D THEN
           DAYDIFF = DAYDIFF - ('30JUN01'D-'20OCT00'D);
     END;   */
  IF OLDNOTEDAYARR > 0 AND 98000<=NOTENO<=98999 THEN DO;
     IF DAYDIFF < 0 THEN DAYDIFF = 0;
     DAYDIFF = SUM(DAYDIFF,OLDNOTEDAYARR);
  END;
  SELECT;
    * WHEN (BORSTAT = 'F')  ARREAR2=14;
    WHEN (BLDATE  <=  0)   ARREAR2=1;
    WHEN (DAYDIFF <  31)  ARREAR2=1;
    WHEN (DAYDIFF >  30   AND DAYDIFF < 60)   ARREAR2=2;
    WHEN (DAYDIFF >  59   AND DAYDIFF < 90)   ARREAR2=3;
    WHEN (DAYDIFF >  89   AND DAYDIFF < 122)  ARREAR2=4;
    WHEN (DAYDIFF > 121   AND DAYDIFF < 152)  ARREAR2=5;
    WHEN (DAYDIFF > 151   AND DAYDIFF < 183)  ARREAR2=6;
    WHEN (DAYDIFF > 182   AND DAYDIFF < 214)  ARREAR2=7;
    WHEN (DAYDIFF > 213   AND DAYDIFF < 244)  ARREAR2=8;
    WHEN (DAYDIFF > 243   AND DAYDIFF < 274)  ARREAR2=9;
    WHEN (DAYDIFF > 273   AND DAYDIFF < 365)  ARREAR2=10;
    WHEN (DAYDIFF > 364   AND DAYDIFF < 548)  ARREAR2=11;
    WHEN (DAYDIFF > 547   AND DAYDIFF < 730)  ARREAR2=12;
    WHEN (DAYDIFF > 729   AND DAYDIFF < 1095) ARREAR2=13;
    WHEN (DAYDIFF > 1094) ARREAR2=14;
  END;
  SELECT;
    * WHEN (BORSTAT = 'F')  ARREAR=17;
    WHEN (BLDATE <=  0)   ARREAR=1;
    WHEN (DAYDIFF <  31)  ARREAR=1;
    WHEN (DAYDIFF >  30   AND DAYDIFF < 60)   ARREAR=2;
    WHEN (DAYDIFF >  59   AND DAYDIFF < 90)   ARREAR=3;
    WHEN (DAYDIFF >  89   AND DAYDIFF < 122)  ARREAR=4;
    WHEN (DAYDIFF > 121   AND DAYDIFF < 152)  ARREAR=5;
    WHEN (DAYDIFF > 151   AND DAYDIFF < 183)  ARREAR=6;
    WHEN (DAYDIFF > 182   AND DAYDIFF < 214)  ARREAR=7;
    WHEN (DAYDIFF > 213   AND DAYDIFF < 244)  ARREAR=8;
    WHEN (DAYDIFF > 243   AND DAYDIFF < 274)  ARREAR=9;
    WHEN (DAYDIFF > 273   AND DAYDIFF < 304)  ARREAR=10;
    WHEN (DAYDIFF > 303   AND DAYDIFF < 334)  ARREAR=11;
    WHEN (DAYDIFF > 333   AND DAYDIFF < 365)  ARREAR=12;
    WHEN (DAYDIFF > 364   AND DAYDIFF < 548)  ARREAR=13;
    WHEN (DAYDIFF > 547   AND DAYDIFF < 730)  ARREAR=14;
    WHEN (DAYDIFF > 729   AND DAYDIFF < 1095) ARREAR=15;
    WHEN (DAYDIFF > 1094) ARREAR=16;
  END;
RETURN;
RUN;

PROC SORT DATA = BNM.LOANTEMP OUT = BNM.LOANTEMP;
  BY BRANCH ARREAR;
RUN;

