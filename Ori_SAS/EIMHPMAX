*+--------------------------------------------------------------+
 | OBJECTIVE     : MAX & MIN LENDING RATE BY SECTOR FOR HP (D)  |
 |                 DISBURSED FOR THE MONTH                      |
 +--------------------------------------------------------------+;
TITLE;
OPTIONS YEARCUTOFF=1950 NOCENTER NODATE NONUMBER;
%INC PGM(PBBLNFMT);

DATA REPTDATE;
   SET NOTE.REPTDATE;
   SELECT(DAY(REPTDATE));
      WHEN (8)  CALL SYMPUT('NOWK',PUT('1',$1.));
      WHEN (15) CALL SYMPUT('NOWK',PUT('2',$1.));
      WHEN (22) CALL SYMPUT('NOWK',PUT('3',$1.));
      OTHERWISE CALL SYMPUT('NOWK',PUT('4',$1.));
   END;
   MM = MONTH(REPTDATE);
   MM1 = MM - 1;
   IF MM1 = 0 THEN MM1 = 12;
   SDATE = MDY(MM,1,YEAR(REPTDATE));
   QTRVALUE=MOD(MM,3);
   CALL SYMPUT('REPTMON',PUT(MM,Z2.));
   CALL SYMPUT('REPTMON1',PUT(MM1,Z2.));
   CALL SYMPUT('RDATE',REPTDATE);
   CALL SYMPUT('RYEAR',PUT(YEAR(REPTDATE),Z4.));
   CALL SYMPUT('SDATE',SDATE);
   CALL SYMPUT('WMONTH',TRIM(LEFT(UPCASE(PUT(REPTDATE,MONNAME9.)))));
   CALL SYMPUT('QTRVALUE',QTRVALUE);
RUN;
*;
*+------------------------------------------+
 +  HP ACCOUNT                              +
 +------------------------------------------+;
DATA LN(KEEP=ACCTNO NOTENO NAME SECTORCD CUSTCD BALANCE BRANCH);
     SET LOAN.LOAN&REPTMON&NOWK;
     IF (&SDATE <= ISSDTE <= &RDATE) AND
        PRODUCT IN &HPD;
RUN;
PROC SORT; BY ACCTNO NOTENO;

DATA LNNOTE(KEEP=ACCTNO NOTENO NTAPR LOANTYPE VINNO);
     SET NOTE.LNNOTE;
     ISSDTE = INPUT(SUBSTR(PUT(ISSUEDT,Z11.),1,8),MMDDYY8.);
     IF (&SDATE <= ISSDTE <= &RDATE) AND
        LOANTYPE IN (128,130,380,381,700,705,131,132,720,725);
RUN;
PROC SORT; BY ACCTNO NOTENO;

DATA LOAN1;
     MERGE LNNOTE LN(IN=A); BY ACCTNO NOTENO;
     IF A;
     SECTCD = PUT(SECTORCD,$SECTA.);
     IF SECTCD = ' ' THEN SECTCD = PUT(SECTORCD,$SECTB.);
     CUSTDESC=PUT(CUSTCD,$BUSIND.);
RUN;
PROC SORT; BY SECTCD CUSTDESC;

 * **************************** *
 * FOR HPD ONLY                 *
 * **************************** *;
DATA HPD;
     SET LOAN1;
     IF LOANTYPE IN (131,132,720,725,380,381,700,705) AND NTAPR > 0;
RUN;

PROC SUMMARY DATA=HPD NWAY;
     CLASS SECTCD CUSTDESC NTAPR;
     VAR BALANCE;
     OUTPUT OUT=HPD1 (RENAME=(_FREQ_=NOACCT) DROP=_TYPE_)
                    SUM=BALANCE;
RUN;

 * **************************** *
 * FOR SMI ONLY                 *
 * **************************** *;
DATA HPD2;
     SET LOAN1;
     IF CUSTCD IN ('66','67','68','69') AND
        LOANTYPE IN (131,132,720,725,380,381,700,705) AND
        NTAPR > 0;
     CUSTDESC = 'SMI';
RUN;
PROC SORT; BY SECTCD CUSTDESC;

PROC SUMMARY DATA=HPD2 NWAY;
     CLASS SECTCD CUSTDESC NTAPR;
     VAR BALANCE;
     OUTPUT OUT=HPD2 (RENAME=(_FREQ_=NOACCT) DROP=_TYPE_)
                    SUM=BALANCE;
RUN;

DATA RATELOW(RENAME=(NTAPR=LOWRATE BALANCE=LOWBAL))
     RATEHIGH(RENAME=(NTAPR=HIRATE BALANCE=HIBAL));
     SET HPD1 HPD2;
     BY SECTCD CUSTDESC;
     IF (FIRST.SECTCD OR FIRST.CUSTDESC) THEN OUTPUT RATELOW;
     IF (LAST.SECTCD OR LAST.CUSTDESC) THEN OUTPUT RATEHIGH;
RUN;
PROC SORT DATA=RATELOW; BY SECTCD CUSTDESC;
PROC SORT DATA=RATEHIGH; BY SECTCD CUSTDESC;

DATA HPD3;
     MERGE RATELOW RATEHIGH;
     BY SECTCD CUSTDESC;
RUN;
PROC SORT; BY CUSTDESC;

DATA IND(RENAME=(LOWRATE=ILOWRATE LOWBAL=ILOWBAL
                 HIRATE=IHIRATE HIBAL=IHIBAL))
     BUS(RENAME=(LOWRATE=BLOWRATE LOWBAL=BLOWBAL
                 HIRATE=BHIRATE HIBAL=BHIBAL))
     SMI(RENAME=(LOWRATE=SLOWRATE LOWBAL=SLOWBAL
                 HIRATE=SHIRATE HIBAL=SHIBAL));
     SET HPD3;
     IF CUSTDESC = 'IND' THEN OUTPUT IND;
     IF CUSTDESC = 'BUS' THEN OUTPUT BUS;
     IF CUSTDESC = 'SMI' THEN OUTPUT SMI;
RUN;
PROC SORT DATA=IND; BY SECTCD;
PROC SORT DATA=BUS; BY SECTCD;
PROC SORT DATA=SMI; BY SECTCD;

DATA HPD3;
     MERGE IND BUS SMI; BY SECTCD;
     SECTDESC=PUT(SECTCD,SECDES.);
RUN;

DATA _NULL_;
   SET HPD3 END=EOF; BY SECTCD;
   FILE PRINT HEADER=NEWPAGE;

   TIHIBAL  + IHIBAL;
   TILOWBAL + ILOWBAL;
   TBHIBAL  + BHIBAL;
   TBLOWBAL + BLOWBAL;
   TSHIBAL  + SHIBAL;
   TSLOWBAL + SLOWBAL;

   PUT @001 SECTCD
       @006 SECTDESC  $17.
       @022 IHIBAL    12.2
       @034 IHIRATE    6.2
       @040 ILOWBAL   12.2
       @052 ILOWRATE   6.2
       @058 BHIBAL    12.2
       @070 BHIRATE    6.2
       @076 BLOWBAL   12.2
       @088 BLOWRATE   6.2
       @094 SHIBAL    12.2
       @106 SHIRATE    6.2
       @112 SLOWBAL   12.2
       @124 SLOWRATE   6.2;
   IF EOF  THEN DO;
      PUT @001 132*'-'
         /@001 'GRAND TOTAL : '
          @022 TIHIBAL    12.2
          @040 TILOWBAL   12.2
          @058 TBHIBAL    12.2
          @076 TBLOWBAL   12.2
          @094 TSHIBAL    12.2
          @112 TSLOWBAL   12.2;
      PUT @001 132*'=';
      PUT @001 ' ';
   END;
RETURN;

NEWPAGE :
   PAGECNT+1;
   PUT /@001 'PUBLIC BANK BERHAD - FINANCE DIVISION (ATTN:HISHAM)'
        @118 'PAGE NO : ' PAGECNT
       /@001 'TITLE   : MAXIMUM & MINIMUM LENDING RATE BY SECTORS FOR'
        @053 'HP(D) FOR THE MONTH OF : '"&WMONTH" " &RYEAR"
       /@001 'RPT ID  : EIMHPMAX';
   PUT /@001 ' ';
   PUT /@024 '     INDIVIDUAL CUSTOMERS(A)'
        @060 '   BUSINESS RELATED SECTORS(B)'
        @096 '         SMI CUSTOMER (C)'
       /@027 '----------------------------------'
        @063 '----------------------------------'
        @099 '----------------------------------'
       /@036 'MAX'
        @054 'MIN'
        @072 'MAX'
        @090 'MIN'
        @108 'MAX'
        @126 'MIN'
       /@001 'SECTOR'
        @034 'LENDING'
        @052 'LENDING'
        @070 'LENDING'
        @088 'LENDING'
        @106 'LENDING'
        @123 'LENDING'
       /@001 'CODE'
        @006 'SECTOR DESC'
        @024 'AMOUNT(RM)'
        @035 'RATE%'
        @042 'AMOUNT(RM)'
        @053 'RATE%'
        @060 'AMOUNT(RM)'
        @071 'RATE%'
        @078 'AMOUNT(RM)'
        @089 'RATE%'
        @096 'AMOUNT(RM)'
        @107 'RATE%'
        @114 'AMOUNT(RM)'
        @125 'RATE%';
    PUT @001 132*'-';
RETURN;

PROC SORT DATA=HPD3 OUT=HPD3; BY SECTCD;
PROC SORT DATA=HPD OUT=HPD; BY SECTCD;
DATA HPD4;
   MERGE HPD3 HPD;
   BY SECTCD;
   IF NTAPR = IHIRATE THEN OUTPUT;
   IF NTAPR = ILOWRATE THEN OUTPUT;
   IF NTAPR = BHIRATE THEN OUTPUT;
   IF NTAPR = BLOWRATE THEN OUTPUT;
   IF NTAPR = SHIRATE THEN OUTPUT;
   IF NTAPR = SLOWRATE THEN OUTPUT;
RUN;

PROC SORT DATA=HPD4 NODUPKEY; BY ACCTNO NOTENO; RUN;
PROC SORT DATA=HPD4; BY SECTCD NTAPR; RUN;
PROC PRINT DATA=HPD4;
   VAR SECTCD NTAPR ACCTNO NOTENO NAME CUSTCD BRANCH VINNO BALANCE;
   TITLE1  'DETAIL - MAXIMUM & MINIMUM LENDING RATE BY SECTORS FOR '
           'HP(D) FOR THE MONTH OF : '"&WMONTH" " &RYEAR";
RUN;

