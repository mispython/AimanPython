*;
OPTIONS NODATE NONUMBER NOCENTER MISSING=0;

%INC PGM(PBBDPFMT,PBMISFMT);

DATA REPTDATE;
    SET DEPOSIT.REPTDATE;
    CALL SYMPUT('REPTMON',PUT(MONTH(REPTDATE),Z2.));
    CALL SYMPUT('REPTYEAR',PUT(REPTDATE,YEAR4.));
    CALL SYMPUT('RDATE',PUT(REPTDATE,DDMMYY8.));
    CALL SYMPUT('ZDATE',PUT(REPTDATE,Z5.));
RUN;
*;
LIBNAME CISS "SAP.PBB.CRM.CISBEXT" DISP=SHR;
*;
DATA PREV;
  KEEP ACCTNO PREBAL;
  SET DEP0.SAVING;
  PREBAL=CURBAL;
PROC SORT; BY ACCTNO;
*;
DATA CRMOVE;
  SET DEPOSIT.SAVING;
*;
DATA CRMOVE;
  MERGE CRMOVE(IN=A) PREV; BY ACCTNO;
  IF A;
  BRCH=PUT(BRANCH,BRCHCD.);
  IF PREBAL=. THEN PREBAL=0.00;
  MOVEMENT=CURBAL-PREBAL;
  MOVEX   =ABS(MOVEMENT);
  IF MOVEX GE 50000.00;
*;
PROC SORT; BY ACCTNO;
*;
DATA CISN;
  SET CISS.DEPOSIT;
  KEEP ACCTNO CUSTNAM1;
  IF SECCUST='901';
PROC SORT DATA=CISN OUT=CISN NODUPKEY; BY ACCTNO;
*;
DATA CRMOVE;
  MERGE CISN CRMOVE(IN=A); BY ACCTNO;
  IF A;
  IF CUSTNAM1=' ' THEN CUSTNAM1=NAME;
*;
PROC SORT DATA=CRMOVE; BY DESCENDING MOVEMENT ACCTNO;
*;
%MACRO SAVING;
OPTIONS NODATE NONUMBER NOCENTER MISSING=0;
DATA _NULL_;
  CALL SYMPUT('CRMOVE',P);
  SET CRMOVE NOBS=P;
RUN;

TITLE1 'REPORT ID : DMMISR12 (ISLAMIC)';
TITLE2 'SALES ADMINISTRATION & SUPPORT';
TITLE3 'PUBLIC ISLAMIC BANK BERHAD';
TITLE4 "MOVEMENT IN BANK'S SAVING DEPOSITS AS AT " &RDATE;
TITLE5 'NET INCREASE/DECREASE OF RM 50 THOUSAND & ABOVE PER CUSTOMER';
TITLE6;
%IF &CRMOVE EQ 0 %THEN %DO;
 DATA _NULL_;
 FILE PRINT;
 PUT;PUT;
 PUT @5 '************************************************************';
 PUT @5 'NO CUSTOMER WITH CREDIT MOVEMENT OF 50 THOUSAND AND ABOVE   ';
 PUT @5 "AT &RDATE";
 PUT @5 '************************************************************';
 RUN;
%END;
%ELSE %DO;
PROC REPORT DATA=CRMOVE NOWD HEADLINE SPLIT='*';
  COLUMN BRANCH BRCH CUSTNAM1 ACCTNO PREBAL CURBAL MOVEMENT MOVEMT;
  DEFINE BRANCH  /         FORMAT=6. 'BRANCH';
  DEFINE BRCH    /         FORMAT=$6.  '/CODE ';
  DEFINE CUSTNAM1 / FORMAT=$40. 'NAME OF CUSTOMER';
  DEFINE ACCTNO  / DISPLAY FORMAT=20. 'ACCOUNT NUMBER';
  DEFINE PREBAL  / DISPLAY FORMAT=COMMA15.2 'PREVIOUS BALANCE';
  DEFINE CURBAL  / DISPLAY FORMAT=COMMA15.2 'CURRENT BALANCE';
  DEFINE MOVEMENT/ NOPRINT;
  DEFINE MOVEMT  / COMPUTED FORMAT=10.2
                   'INCREASE/*DECREASE*(RM THOUSAND)';
  COMPUTE MOVEMT;
    MOVEMT=MOVEMENT.SUM*0.001;
  ENDCOMP;
  RBREAK AFTER / PAGE DUL OL SUMMARIZE;
RUN;
%END;
%MEND SAVING;
%SAVING;
