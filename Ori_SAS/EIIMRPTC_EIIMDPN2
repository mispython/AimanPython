OPTIONS YEARCUTOFF=1950 SORTDEV=3390 NONUMBER NODATE NOCENTER;
*;
%INC PGM(PBBDPFMT,PBBELF);
*;
DATA REPTDATE;
   SET DEPOSIT.REPTDATE;
   MM=MONTH(REPTDATE);
   MM1=MM-1;
   IF MM1=0 THEN MM1=12;
   CALL SYMPUT('RDATE',PUT(REPTDATE,DDMMYY8.));
   CALL SYMPUT('RYEAR',PUT(REPTDATE,YEAR4.));
   CALL SYMPUT('RMONTH',PUT(REPTDATE,MONTH2.));
   CALL SYMPUT('REPTMON',PUT(MM,Z2.));
   CALL SYMPUT('REPTMON1',PUT(MM1,Z2.));
   CALL SYMPUT('RDAY',PUT(REPTDATE,DAY2.));
RUN;
*;
DATA CURR;
  SET DEPOSIT.CURRENT;
  IF  BRANCH=996 THEN BRANCH=168;
  IF  BRANCH=250 THEN BRANCH=092;
  IF  PRODUCT IN (21,32,33,070,071,073,090:097,137,138,74,23,24,25,
                  46,47,48,49,75,76,45,13,14,15,16,17,18,19,7,8,22,
                  5,6,81,20) OR
      (050<=PRODUCT<=067)              OR
      (100<=PRODUCT<=106)              OR
      (108<=PRODUCT<=125)              OR
      (150<=PRODUCT<=170)              OR
      (174<=PRODUCT<=188)              OR
      (191<=PRODUCT<=198);

  IF  OPENIND='Z' THEN DO;
      OPENIND='O'; CLOSEMH=0;
  END;
  IF  OPENIND='O' OR
      OPENIND IN ('B','C','P') AND CLOSEMH=1;

  NOACCT=0; CCLOSE=0; BCLOSE=0;
  IF  OPENIND IN ('B','C','P') THEN DO;
      IF OPENIND='C'  THEN CCLOSE=CLOSEMH;
                      ELSE BCLOSE=CLOSEMH;
  END;
  ELSE DO;
      NOACCT=1;
  END;
*;
DATA MIS.CURRC&REPTMON;
  KEEP BRANCH ACCTNO OPENDT CLOSEDT OPENIND CURBAL CUSTCODE
       MTDAVBAL YTDAVBAL CUSTFISS DOBMNI PRODUCT ACCPROF APPRLIMT
       CENSUST CHGIND COSTCTR DATE_LST_DEP DEPTYPE DNBFI_ORI DNBFISME
       FLATRATE INTPD INTPLAN INTRSTPD INTYTD L_DEP LASTTRAN LIMIT1
       LIMIT2 LIMIT3 LIMIT4 LIMIT5 MAXPROF ODINTACC ODINTCHR ODPLAN
       ORGCODE ORGTYPE PURPOSE RATE1 RATE2 RATE3 RATE4 RATE5 RETURNS_Y
       RISKCODE SECOND SECTOR SERVICE STATE USER2 USER3 USER5
       DSR REPAY_TYPE_CD MTD_REPAID_AMT INDUSTRIAL_SECTOR_CD
       WRITE_DOWN_BAL MTD_DISBURSED_AMT MTD_REPAY_TYPE10_AMT
       MTD_REPAY_TYPE20_AMT MTD_REPAY_TYPE30_AMT MODIFIED_FACILITY_IND
       STMT_CYCLE NXT_STMT_CYCLE_DT INTPLAN_IBCA;
  SET CURR;
  IF  OPENIND IN ('B','C','P');
  IF  CLOSEMH=1;
  YTDAVBAL=YTDAVAMT;
   SELECT(PRODUCT);
      WHEN(104) CUSTFISS='02';
      WHEN(105) CUSTFISS='81';
      OTHERWISE CUSTFISS=PUT(CUSTCODE, DDCUSTCD.);
   END;
   DNBFI_ORI=DNBFISME;
   IF CUSTFISS NOT IN (04,05,06,30,31,32,33,34,35,37,38,39,40,45)
      THEN DNBFISME = '0';
   IF LASTTRAN NOT IN (0,.) THEN
      LASTTRAN = INPUT(SUBSTR(PUT(LASTTRAN,Z9.),1,6),MMDDYY6.);
      IF BDATE NOT IN (0,.) THEN
         BIRTHS = INPUT(SUBSTR(PUT(BDATE,Z11.),1,8),MMDDYY8.);
      ELSE BIRTHS = 0;
      DOBMNI= BIRTHS;
*;
PROC   SUMMARY DATA=CURR NWAY;
CLASS  BRANCH  PRODUCT;
VAR    OPENMH  CLOSEMH  NOACCT CURBAL BCLOSE CCLOSE;
OUTPUT OUT=CURR (DROP=_FREQ_ _TYPE_) SUM=;
*;
%MACRO PROCESS;
   %IF &REPTMON > 01 %THEN %DO;

       DATA CURRP;
         SET MIS.CURRF&REPTMON1;
         OPENCUX =OPENCUM;
         CLOSECUX=CLOSECUM;
         IF BRANCH=250 THEN BRANCH=092;


       PROC   SUMMARY DATA=CURRP NWAY;
       CLASS  BRANCH  PRODUCT;
       VAR    OPENCUX CLOSECUX;
       OUTPUT OUT=CURRP (DROP=_FREQ_ _TYPE_) SUM=;

       DATA CURR;
         MERGE CURRP CURR; BY BRANCH PRODUCT;
         IF CLOSECUX=.   THEN  CLOSECUX=0;
         IF OPENCUX =.   THEN  OPENCUX=0;
         IF OPENMH  =.   THEN  OPENMH =0;
         IF CLOSEMH =.   THEN  CLOSEMH=0;
         IF CCLOSE  =.   THEN  CCLOSE =0;
         IF BCLOSE  =.   THEN  BCLOSE =0;
         IF NOACCT  =.   THEN  NOACCT =0;
         IF CURBAL  =.   THEN  CURBAL =0;
         IF BCLOSE  =.   THEN  BCLOSE =0;
         IF CCLOSE  =.   THEN  CCLOSE =0;

         OPENCUM =OPENMH  +OPENCUX;
         CLOSECUM=CLOSEMH +CLOSECUX;
         NETCHGMH=OPENMH  -CLOSEMH;
         NETCHGYR=OPENCUM -CLOSECUM;
   %END;
   %ELSE %DO;
       DATA CURR;
         SET CURR;
         OPENCUM =OPENMH;
         CLOSECUM=CLOSEMH;
         NETCHGMH=OPENMH  -CLOSEMH;
         NETCHGYR=OPENCUM -CLOSECUM;
         IF BRANCH=250 THEN BRANCH=092;
   %END;
%MEND;

%PROCESS;

DATA MIS.CURRF&REPTMON;
   SET CURR;
*;
*------------------------------------------------*
*  PRINT REPORT TO TEXT FILE                     *
*------------------------------------------------*;
TITLE1 'PUBLIC ISLAMIC BANK BERHAD';
TITLE2 'CURRENT ACCOUNT OPENED/CLOSED FOR THE MONTH AS AT' &RDATE;
PROC TABULATE DATA=CURR FORMAT=COMMA10. MISSING NOSEPS
   FORMCHAR='           ';
   CLASS BRANCH;
   VAR   OPENMH CLOSEMH NOACCT CURBAL CLOSECUM OPENCUM
         NETCHGMH NETCHGYR BCLOSE CCLOSE;
   TABLE BRANCH='  ' ALL='TOTAL',
         SUM=' '*(OPENMH='CURRENT MONTH OPENED'
         OPENCUM='CUMULATIVE OPENED'
         CLOSEMH='CURRENT MONTH CLOSED'
         BCLOSE ='CLOSED BY BANK'
         CCLOSE ='CLOSED BY CUSTOMER'
         CLOSECUM='CUMULATIVE CLOSED'
         NOACCT='NO. OF ACCTS'
         CURBAL='TOTAL(RM) O/S'*F=COMMA17.2
         NETCHGMH='NET CHANGE FOR THE MONTH'
         NETCHGYR='NET CHANGE YEAR TO DATE')
   /     BOX='BRANCH' RTS=10;
 *;
