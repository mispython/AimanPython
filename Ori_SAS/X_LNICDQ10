*+---------------------------------------------------------------+
 |  PROGRAM  : LNICDQ10                                          |
 |  DATE     : 31.07.01                                          |
 |  FUNCTION : DETAILS FOR NPL ACCOUNTS FOR CCD PFB.             |
 |             (A MONTHLY REPORT)                                |
 |  MODIFY   : 22 FEB 2006 ESMR 2005-610                         |
 +---------------------------------------------------------------+;
%INC PGM(PBBLNFMT);

TITLE;
OPTIONS NONUMBER NODATE;

DATA _NULL_;
  SET BNM.REPTDATE;
  IF MONTH(REPTDATE) EQ 1 THEN DO;
     PMTH = 12;
     PYEAR = YEAR(REPTDATE) - 1;
  END;
  ELSE DO;
     PMTH = MONTH(REPTDATE) - 1;
     PYEAR = YEAR(REPTDATE);
  END;
  PDATE = MDY(PMTH,1,PYEAR);
  CALL SYMPUT('PREPTDTE',PDATE);
  CALL SYMPUT('RDATE', PUT(REPTDATE, DDMMYY8.));
  CALL SYMPUT('REPTYEAR', PUT(REPTDATE, YEAR4.));
  CALL SYMPUT('REPTMON', PUT(MONTH(REPTDATE), Z2.));
  CALL SYMPUT('REPTDAY', PUT(DAY(REPTDATE), Z2.));
RUN;

PROC SORT DATA=BNM.LOANTEMP OUT=LNTEMP;
  WHERE BALANCE > 0 AND BORSTAT NE 'Z' AND
        PRODUCT IN &HPD;
  BY BRANCH;
RUN;

DATA BRHDATA;
   INFILE BRHFILE LRECL=80;
   INPUT @2 BRANCH  3.
         @6 BRHCODE $3.;
RUN;
PROC SORT DATA=BRHDATA; BY BRANCH; RUN;
DATA LNTEMP;
  MERGE LNTEMP(IN=PRESENT) BRHDATA;
  BY BRANCH;
  IF PRESENT=1 THEN OUTPUT LNTEMP;
RUN;

*+---------------------------------------------------------------+
 |  ONLY FOR HP PRODUCT,                                         |
 |  PLUS IT IS NOT UNDER CAC BRANCHES....                        |
 +---------------------------------------------------------------+;
DATA LOAN;
  SET LNTEMP;
  IF ARREAR2 GE 3  OR BORSTAT = 'R' OR BORSTAT = 'I' OR
     BORSTAT = 'T'  OR BORSTAT = 'F' OR BORSTAT = 'Y'
     THEN OUTPUT;
  IF ISSDTE GE &PREPTDTE AND DAYDIFF >= 8 THEN OUTPUT;
RUN;

DATA LOAN1;
  FORMAT  TYPE  $53.;
  SET LOAN;
  IF BORSTAT = 'F' THEN ARREAR2 = 15;
  ARREARS=PUT(ARREAR2, ARRCLASS.);
  BLDATE2 =PUT(BLDATE,MMDDYY8.);
  IF PRODUCT IN (380,381,700,705,720,725) THEN DO;
     CAT  = 'A';
     TYPE = 'HP DIRECT(CONV) ';
     OUTPUT;
  END;
  IF PRODUCT IN (380,381) THEN DO;
     CAT  = 'B';
     TYPE = 'HP (380,381) ';
     OUTPUT;
  END;
  IF PRODUCT IN (128,130,131,132) THEN DO;
     CAT  = 'C';
     TYPE = 'AITAB ';
     OUTPUT;
  END;
RUN;

PROC SORT DATA=LOAN1; BY BRANCH;

DATA LOAN1;
  MERGE LOAN1(IN=PRESENT) BRHDATA;
  BY BRANCH;
  IF PRESENT=1 THEN OUTPUT LOAN1;
RUN;

PROC SORT DATA=LOAN1 OUT=LOAN1;
  BY CAT BRANCH ARREAR2 DAYDIFF;
RUN;

DATA _NULL_;
  SET LOAN1 END=LAST;
  BY CAT BRANCH ARREAR2 DAYDIFF;
  FILE OUTFILE;

  IF _N_=1 THEN DO;
     TOTAL=0;
  END;

  IF FIRST.CAT    THEN DO;
     PAGECNT=0;
     BRHARR=0;
     BRHAMT=0;
     BRHCAP=0;
     BRHCAPB=0;
     LINK NEWPAGE;
  END;

  IF FIRST.BRANCH THEN DO;
     PAGECNT=0;
     BRHAMT=0;
     BRHCAPB=0;
     IF NOT FIRST.CAT THEN DO;
        LINK NEWPAGE;
     END;
  END;

  IF FIRST.ARREAR2 THEN DO;
     BRHARR=0;
     BRHCAP=0;
  END;

  PUT  @1   BRHCODE
       @5   ACCTNO              @16  NAME
       @41  NOTENO              @54  PRODUCT
       @59  BORSTAT             @68  ISSDTE   DDMMYY8.
       @79  DAYDIFF             @84  ARREARS
       @98  BALANCE  COMMA17.2  @116 NOISTLPD COMMA7.
       @127 DELQCD   $5. ;
  PUT  @5   LASTRAN  DDMMYY8.   @16  MATURDT  DDMMYY8.
       @29  LSTTRNAM COMMA17.2  @46  PAYAMT   COMMA11.2
       @59  COLLDESC;
  PUT  @35   CAP COMMA16.2;
  PUT  @1   ' ';
  LINECNT + 4;
  BRHARR + BALANCE;
  BRHAMT + BALANCE;
  BRHCAP + CAP;
  BRHCAPB + CAP;
  TOTALCAP + CAP;
  TOTAL + BALANCE;

  IF LINECNT > 56 THEN DO;
     LINK NEWPAGE;
  END;

  IF LAST.ARREAR2 THEN DO;
     PUT  @41  '----------------------------------------'
          @81  '----------------------------------------'
          @121 '----------';
     PUT  @5   'SUBTOTAL'           @35 BRHCAP COMMA16.2
                                   @100 BRHARR COMMA17.2;
     PUT  @41  '----------------------------------------'
          @81  '----------------------------------------'
          @121 '----------';
     PUT;
     LINECNT + 4;
  END;

  IF LINECNT > 56 THEN DO;
     LINK NEWPAGE;
  END;

  IF LAST.BRANCH THEN DO;
     PUT  @41  '----------------------------------------'
          @81  '----------------------------------------'
          @121 '----------';
     PUT  @5   'BRANCH TOTAL'      @35 BRHCAPB COMMA16.2
                                   @100 BRHAMT COMMA17.2;
     PUT  @41  '----------------------------------------'
          @81  '----------------------------------------'
          @121 '----------';
     PUT;
  END;

  IF LAST.CAT   THEN DO;
     PUT  @41  '----------------------------------------'
          @81  '----------------------------------------'
          @121 '----------';
     PUT  @5   'GRAND TOTAL'     @35 TOTALCAP COMMA16.2
                                   @100 TOTAL COMMA17.2;
     PUT  @41  '----------------------------------------'
          @81  '----------------------------------------'
          @121 '----------';
     PUT;
     TOTAL = 0;
  END;
  RETURN;

  NEWPAGE:
    PUT _PAGE_;
    PAGECNT+1;
    LINECNT=6;
    PUT @1   'PROGRAM-ID : LNICDQ10 - BRANCH : ' BRANCH  3.
        @43  'P U B L I C   I S L A M I C   B A N K   B E R H A D'
        @118 'PAGE NO.: ' PAGECNT;
    PUT @39  TYPE '2 MTHS & ABOVE AND NEW A/C WITH ARREAR '
             'AS AT ' "&RDATE";
    PUT @1   ' ';
    PUT @1   'BRH'
        @5   'ACCTNO'           @16 'NAME'
        @40  'NOTENO'           @50 'PRODUCT'
        @59  'BORSTAT'          @68 'ISSUE DT'
        @78  'DAYS'             @84 'ARREARS'
        @108 'BALANCE'          @116 'NO ISTL'
        @127 'DELQ';
    PUT @1   ' '
        @5   'LST TR DT'        @16  'MAT. DATE'
        @36  'LST TR AMT'       @49  'ISTL AMT'
        @69  'COLLATERAL DESCRIPTION'
        @116 '   PAID'          @127 'CODE';
    PUT @35   'CAPCLOSE';
    PUT @1   '----------------------------------------'
        @41  '----------------------------------------'
        @81  '----------------------------------------'
        @121 '----------';
  RETURN;
RUN;

PROC DATASETS LIB=WORK NOLIST; DELETE LOAN1; RUN;
