//EIBMP574 JOB MSGCLASS=X,MSGLEVEL=(1,1),REGION=8M
/*JOBPARM S=S1M2
//**********************************************************
//* ESMR 2012-1093 (CWK)                                   *
//* ESMR DESC: INCORPORATE CONVENTIONAL RETAIL PRODUCT     *
//*            CODES: 607 INTO MEF REPORT                  *
//**********************************************************
//DELETE  EXEC PGM=IEFBR14
//DEL01    DD DSN=SAP.PBB.P574.REPT,
//            DISP=(MOD,DELETE,DELETE),
//            SPACE=(CYL,(0))
//*
//EIBMP574 EXEC SAS609
//SAS      DD DSN=SAP.PBB.SASDATA,DISP=SHR
//LOAN     DD DSN=SAP.PBB.MNILN(0),DISP=SHR
//PRVLN    DD DSN=SAP.PBB.MNILN(-4),DISP=SHR
//DISPAY   DD DSN=SAP.PBB.DISPAY,DISP=SHR
//CCRIS    DD DSN=SAP.PBB.CCRIS.CREDSUB,DISP=SHR
//PGM      DD DSN=SAP.BNM.PROGRAM,DISP=SHR
//SASLIST  DD DSN=SAP.PBB.P574.REPT,
//            DISP=(NEW,CATLG,DELETE),
//            SPACE=(TRK,(10,10),RLSE),
//            UNIT=(SYSDA,3),
//            DCB=(LRECL=256,RECFM=FBA,BLKSIZE=0,BUFNO=30)
//SYSIN    DD *

OPTIONS YEARCUTOFF=1950 NOCENTER NODATE NONUMBER MISSING=0;

DATA _NULL_;
   SET LOAN.REPTDATE;
   MM1 = MONTH(REPTDATE)-1;
   IF MM1 = 0 THEN MM1 = 12;
   CALL SYMPUT('NOWK',PUT('4',$1.));
   CALL SYMPUT('REPTYEAR',PUT(REPTDATE,YEAR2.));
   CALL SYMPUT('REPTMON',PUT(MONTH(REPTDATE),Z2.));
   CALL SYMPUT('PREVMON',PUT(MM1,Z2.));
   CALL SYMPUT('RDATE', PUT(REPTDATE, DDMMYY8.));
RUN;

 *** PREVIOUS MTH ***;
DATA PRVMTH;
   SET SAS.LOAN&PREVMON&NOWK;
   IF  PRODUCT=574 OR
       PRODUCT=607 AND CENSUS IN (574.00,574.01,574.02);
RUN;
 *** CURRENT MTH ***;
DATA CURMTH;
   SET SAS.LOAN&REPTMON&NOWK;
   IF  PAIDIND NOT IN ('P','C') AND BALANCE NE 0;
   IF  PRODUCT=574 OR
       PRODUCT=607 AND CENSUS IN (574.00,574.01,574.02);
   NOACC = 1;
RUN;
*;
PROC SORT DATA=CURMTH; BY ACCTNO NOTENO;
PROC SORT DATA=PRVMTH; BY ACCTNO NOTENO;
*;
PROC SORT DATA=DISPAY.DISPAYMTH&REPTMON
          OUT=DISPAY(KEEP=ACCTNO NOTENO DISBURSE REPAID CENSUS);
   BY ACCTNO NOTENO;
   WHERE DISBURSE > 0 OR REPAID > 0;
RUN;
DATA LOAN;
   MERGE PRVMTH(IN=A RENAME=(BALANCE=LASTBAL))
         CURMTH(IN=B);
   BY ACCTNO NOTENO;
RUN;

DATA LOAN;
   MERGE LOAN(IN=A) DISPAY;
   BY ACCTNO NOTENO;
   IF A;
   IF CENSUS IN (574.00,574.01,574.02);
RUN;

PROC SUMMARY DATA=LOAN NWAY;
   VAR BALANCE NOACC DISBURSE REPAID;
   OUTPUT OUT=LN1 (DROP=_FREQ_ _TYPE_) SUM=;
RUN;

PROC PRINT DATA=LN1 SPLIT='*';
   VAR BALANCE NOACC DISBURSE REPAID;
   SUM BALANCE NOACC DISBURSE REPAID;
   LABEL BALANCE='O/S BALANCE'
         NOACC='NO OF*ACCOUNT'
         DISBURSE='DISBURSEMENT*AMOUNT'
         REPAID='REPAYMENT*AMOUNT';
   FORMAT BALANCE DISBURSE REPAID COMMA15.2
          NOACC COMMA10.;
   TITLE  'PUBLIC BANK BERHAD';
   TITLE1 'PERFORMANCE REPORT ON PRODUCT 574 AS AT ' "&RDATE";
   TITLE2 'REPORT ID : EIBMP574';
RUN;

PROC SORT DATA=CURMTH; BY ACCTNO NOTENO BRANCH; RUN;
PROC SORT DATA=CCRIS.CREDMSUBAC&REPTMON&REPTYEAR
           OUT=CREDMSUB(KEEP=ACCTNUM NOTENO BRANCH DAYSARR MTHARR
                        RENAME=(ACCTNUM=ACCTNO DAYSARR=DAYARR));
   BY ACCTNUM NOTENO BRANCH;
RUN;

PROC SORT DATA=CCRIS.PROVSUBAC&REPTMON&REPTYEAR
           OUT=PROVSUBAC(KEEP=ACCTNUM NOTENO BRANCH
                        RENAME=(ACCTNUM=ACCTNO));
           WHERE IMPAIRED_LOAN='Y';
   BY ACCTNUM NOTENO BRANCH;
RUN;

DATA LN2;
   MERGE CURMTH CREDMSUB PROVSUBAC(IN=A);
   BY ACCTNO NOTENO BRANCH;
   IF A;
   IF BALANCE NOT IN (.,0);
RUN;

PROC PRINT DATA=LN2 SPLIT='*';
   VAR ACCTNO NOTENO LOANSTAT BALANCE DAYARR MTHARR;
   SUM BALANCE;
   LABEL ACCTNO='ACCOUNT*NO'
         NOTENO='NOTE*NO'
         LOANSTAT='LOAN*STATUS'
         BALANCE='O/S BALANCE'
         DAYARR='DAYS IN*ARREARS'
         MTHARR='MTHS IN*ARREARS';
   FORMAT BALANCE COMMA15.2;
   TITLE  'PUBLIC BANK BERHAD';
   TITLE1 'PRODUCT 574 (IMPAIRED LOANS) AS AT ' "&RDATE";
   TITLE2 'REPORT ID : EIBMP574';
RUN;

//
