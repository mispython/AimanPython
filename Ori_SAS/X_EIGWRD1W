OPTIONS NOCENTER YEARCUTOFF=1950;

DATA _NULL_;
   DTE = TODAY();
   DD = DAY(DTE); MM = MONTH(DTE); YY = YEAR(DTE);
   SELECT;
      WHEN (DD <  8) REPTDATE = MDY(MM,1,YY) - 1;
      WHEN (DD < 15) REPTDATE = MDY(MM,8,YY);
      WHEN (DD < 22) REPTDATE = MDY(MM,15,YY);
      OTHERWISE REPTDATE = MDY(MM,22,YY);
   END;
   CALL SYMPUT('NDATE',REPTDATE);
RUN;

DATA ALWZ.REPTDATE
     ALW.REPTDATE;
   INFILE WALALW OBS=1;
   INPUT @2 WALKDATE $8.;
   REPTDATE=INPUT(WALKDATE,DDMMYY8.);
   CALL SYMPUT('RDATE',REPTDATE);
RUN;

%MACRO CONVERT;
DATA R1GL4000 (KEEP=ACCT_NO ACKIND GLAMT)
     R1GL4100 (KEEP=ACCT_NO EFFDATE JEAMT ACKIND)
     R1KEY913 (KEEP=ACCT_NO)
     R1R115   (KEEP=SET_ID USERCD)
     R1R913   (KEEP=ACCT_NO SET_ID);
   RETAIN SETID;
   INFILE WALALW;
   INPUT @2 ID $1. @;
   IF ID = '1' THEN INPUT @3 SETID $8.;
   ELSE IF ID='+' THEN SELECT (SETID);
      WHEN ('R1GL4000') DO;
         INPUT @3 ACCT_NO $22. @29 ACKIND $1. @31 GLAMT COMMA21.2;
         OUTPUT R1GL4000;
      END;
      WHEN ('R1GL4100') DO;
         INPUT @3 ACCT_NO $22. @30 EFFDATE DDMMYY8.
         @47 JEAMT COMMA21.2   @75 ACKIND $1.;
         OUTPUT R1GL4100;
      END;
      WHEN ('R1KEY913') DO;
         INPUT @3 ACCT_NO $22.;
         OUTPUT R1KEY913;
      END;
      WHEN ('R1R115') DO;
         INPUT @3 SET_ID $12.
               @17 USERCD $4.;
         OUTPUT R1R115;
      END;
      WHEN ('R1R913') DO;
         INPUT @3 ACCT_NO $22. @28 SET_ID $12.;
         OUTPUT R1R913;
      END;
      OTHERWISE;
   END;
RUN;
*;
TITLE 'R1GL4000';
PROC SORT DATA=R1GL4000 OUT=ALW.R1GL4000;
   BY ACCT_NO ACKIND GLAMT; RUN;
*;
TITLE 'R1GL4100';
PROC SORT DATA=R1GL4100 OUT=ALW.R1GL4100;
   BY ACCT_NO EFFDATE JEAMT ACKIND; RUN;
*;
TITLE 'R1KEY913';
PROC SORT DATA=R1KEY913 OUT=ALW.R1KEY913;
   BY ACCT_NO; RUN;
*;
TITLE 'R1R115';
PROC SORT DATA=R1R115 OUT=ALW.R1R115;
   BY SET_ID; RUN;
*;
TITLE 'R1R913';
PROC SORT DATA=R1R913 OUT=ALW.R1R913;
   BY ACCT_NO SET_ID; RUN;
*;
PROC DATASETS LIB=WORK KILL NOLIST; RUN;
*;
%MEND CONVERT;

%MACRO PROCESS;
   %IF "&NDATE" = "&RDATE" %THEN %CONVERT;
   %ELSE %PUT WALKER SOURCE IS NOT AS AT CURRENT REPORTING DATE;
%MEND PROCESS;
%PROCESS;
