/** PROGRAM : WISDPBBE                            **/
/** DATE    : 14/05/97                            **/
/** PURPOSE : TO EXTRACT THE INFORMATION FROM     **/
/**           RDAL1 EXTRACTION TO BE USED FOR     **/
/**           MIS REPORTING                       **/

%INC PGM(WISDPBBF);

PROC SORT DATA=SETITEMS OUT=ITEMS(KEEP=SET_ID) NODUPKEYS;
BY SET_ID;
RUN;

PROC SQL;
  CREATE TABLE GL AS
  SELECT T1.BRNO,
         T1.SET_ID, T1.GLAMT,
         SUBSTR(T1.ACCT_NO, 8, 5) AS ACK_NO FROM
  BNM.ALWGL&REPTMON&NOWK T1, ITEMS T2
  WHERE T1.SET_ID = T2.SET_ID;
QUIT;

PROC SQL;
  CREATE TABLE JE AS
  SELECT T1.BRNO,
         T1.SET_ID, T1.JEAMT,
         SUBSTR(T1.ACCT_NO, 8, 5) AS ACK_NO,
         T1.EFFDATE FROM
  BNM.ALWJE&REPTMON&NOWK T1, ITEMS T2
  WHERE T1.SET_ID = T2.SET_ID;
QUIT;

PROC SUMMARY DATA=JE NWAY;
CLASS BRNO SET_ID ACK_NO EFFDATE;
VAR JEAMT;
OUTPUT OUT=JE (DROP=_TYPE_ _FREQ_)
       SUM=JEAMT;
RUN;

DATA JE (DROP=EFFDATE);
  SET JE;
  BY BRNO SET_ID ACK_NO;
  IF FIRST.ACK_NO THEN DO;
     JEVALUE=0;
     REPTDATE=INPUT('01'||"&REPTMON"||"&REPTYEAR", DDMMYY8.);
  END;
  DO WHILE(REPTDATE LT EFFDATE);
     OUTPUT;
     REPTDATE+1;
  END;
  JEVALUE+JEAMT;
  OUTPUT;
  REPTDATE+1;
  IF LAST.ACK_NO AND REPTDATE LE INPUT("&RDATE", DDMMYY8.) THEN DO;
     DO WHILE(REPTDATE LE INPUT("&RDATE", DDMMYY8.));
        OUTPUT;
        REPTDATE+1;
     END;
  END;
RUN;

PROC SUMMARY DATA=GL NWAY;
CLASS BRNO SET_ID ACK_NO;
VAR GLAMT;
OUTPUT OUT=GL (DROP=_TYPE_ _FREQ_)
       SUM=GLAMT;
RUN;

DATA MIS.WISDAY&REPTMON
  (DROP=GLAMT JEVALUE JEAMT);
  MERGE GL (IN=A)
        JE (IN=B);
  BY BRNO SET_ID ACK_NO;
  IF A AND NOT B THEN DO;
     DO REPTDATE=INPUT('01'||"&REPTMON"||"&REPTYEAR", DDMMYY8.)
        TO INPUT("&RDATE", DDMMYY8.);
        AMOUNT=GLAMT;
        OUTPUT;
     END;
  END;
  ELSE DO;
  AMOUNT=SUM(GLAMT, JEVALUE);
  OUTPUT;
  END;
RUN;
