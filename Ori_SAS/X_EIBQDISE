//EIBQDISE JOB MISEIS,EIBMTH1A,MSGCLASS=A,CLASS=A,NOTIFY=&SYSUID
//*
//EIBQDISE  EXEC SAS609,REGION=8M,WORK='250000,200000'
//BNM1      DD DSN=SAP.PBB.DEPOSIT(0),DISP=SHR
//BNM       DD DSN=SAP.PBB.DEPOSIT.DIS(0),DISP=OLD
//FD        DD DSN=SAP.PBB.MNIFD(0),DISP=SHR
//DEPOSIT   DD DSN=SAP.PBB.MNITB(0),DISP=SHR
//PGM       DD DSN=SAP.BNM.PROGRAM,DISP=SHR
//SYSIN     DD *

OPTIONS SORTDEV=3390 YEARCUTOFF=1950 LS=132 PS=60 NOCENTER;

DATA REPTDATE (KEEP=REPTDATE);
  REPTDATE=INPUT('01'||PUT(MONTH(TODAY()), Z2.)||
                 PUT(YEAR(TODAY()), 4.), DDMMYY8.)-1;
  SELECT(DAY(REPTDATE));
    WHEN (8)  DO; SDD = 1;  WK = '1'; WK1 = '4'; END;
    WHEN(15)  DO; SDD = 9;  WK = '2'; WK1 = '1'; END;
    WHEN(22)  DO; SDD = 16; WK = '3'; WK1 = '2'; END;
    OTHERWISE DO; SDD = 23; WK = '4'; WK1 = '3';
                            WK2= '2'; WK3 = '1'; END;
  END;
  MM = MONTH(REPTDATE);
  IF WK = '1' THEN DO;
     MM1 = MM - 1;
     IF MM1 = 0 THEN MM1 = 12;
  END;
  ELSE MM1 = MM;
  SDATE = MDY(MM,SDD,YEAR(REPTDATE));
  CALL SYMPUT('NOWK',PUT(WK,$1.));
  CALL SYMPUT('NOWK1',PUT(WK1,$1.));
  CALL SYMPUT('NOWK2',PUT(WK2,$1.));
  CALL SYMPUT('NOWK3',PUT(WK3,$1.));
  CALL SYMPUT('REPTMON',PUT(MM,Z2.));
  CALL SYMPUT('REPTMON1',PUT(MM1,Z2.));
  CALL SYMPUT('REPTYEAR',PUT(REPTDATE,YEAR4.));
  CALL SYMPUT('REPTDAY',PUT(DAY(REPTDATE),Z2.));
  CALL SYMPUT('RDATE',PUT(REPTDATE,DDMMYY8.));
  CALL SYMPUT('SDATE',PUT(SDATE,DDMMYY8.));
RUN;

%INC PGM(PBBDPFMT);

 /***********************************************************/
 /** MANIPULATE THE EXTRACTED THE SAVING AND CURRENT       **/
 /** ACCOUNTS DATABASE                                     **/
 /***********************************************************/

%LET SAVG1=(KEEP=BRANCH DEPTYPE PRODUCT OPENIND CUSTCODE INTPAYBL
                 RACE CURBAL OPENMH CLOSEMH BDATE NAME ACCTNO LASTTRAN
                 ACCYTD LEDGBAL SCHIND BANKNO OPENDT COSTCTR CHQFLOAT);

%LET SAVG2=(KEEP=BRANCH DEPTYPE PRODUCT PRODCD CUSTCD STATECD AGE
                 RACE INTPAYBL CURBAL OPENMH CLOSEMH RANGE BDATE NAME
                 ACCTNO LASTTRAN ACCYTD AMTIND LEDGBAL SCHIND BANKNO
                 OPENDATE COSTCTR CHQFLOAT);

%LET CURN1=(KEEP=BRANCH DEPTYPE PRODUCT OPENIND CUSTCODE INTPAYBL
                 RACE CURBAL OPENMH CLOSEMH AVGAMT PURPOSE NAME ACCTNO
                 LASTTRAN ACCYTD LEDGBAL ODINTACC COSTCTR SECTOR
                 CHQFLOAT FORATE CURCODE);

%LET CURN2=(KEEP=BRANCH DEPTYPE PRODUCT PRODCD CUSTCD STATECD CUSTNO
                 RACE INTPAYBL CURBAL OPENMH CLOSEMH RANGE AVGAMT
                 AVGRNGE PURPOSE SABAL CABAL AGE NAME ACCTNO AMTIND
                 LASTTRAN ACCYTD LEDGBAL ODINTACC COSTCTR SECTOR
                 CHQFLOAT FORATE CURCODE);

%LET AGELIMIT = 12;
%LET MAXAGE   = 18;
%LET AGEBELOW = 11;

OPTIONS YEARCUTOFF=1930;

DATA UMA;
   SET BNM.UMA;
   IF BNKIND='PBB';
RUN;

DATA BNM.SAVG&REPTMON&NOWK &SAVG2;
  LENGTH CUSTCD $2. PRODCD $5. STATECD $1. AMTIND $1.;
  SET DEPOSIT.SAVING &SAVG1 UMA &SAVG1;
  IF OPENIND NOT IN ('B','C','P');
  /****************************************************/
  /** APPLY THE FORMAT                               **/
  /****************************************************/
  CUSTCD=PUT(CUSTCODE, SACUSTCD.);
  STATECD=PUT(BRANCH, STATECD.);
  PRODCD=PUT(PRODUCT, SAPROD.);
  AMTIND=PUT(PRODUCT, SADENOM.);
  RANGE=INPUT(CURBAL, SDRANGE.);
  RACE=PUT(RACE, $RACE.);

  IF OPENDT NE 0 THEN
     OPENDATE=INPUT(SUBSTR(PUT(OPENDT,Z11.),1,8), MMDDYY8.);
  ELSE OPENDATE=0;

  /******************************************/
  /*  CALCULATION OF AGE RANGE              */
  /******************************************/
  AGE = 0;
  IF (BDATE NE 0) AND (BDATE NE .) THEN
    DO;
     BDATE  =INPUT(SUBSTR(PUT(BDATE, Z11.),1, 8), MMDDYY8.);
     BDAY   =DAY(BDATE);
     BMONTH =MONTH(BDATE);
     BYEAR  =YEAR(BDATE);

     AGE=(&REPTYEAR - BYEAR);
     SELECT;
      WHEN (AGE = &AGELIMIT)
        DO;
         IF (BMONTH = &REPTMON) AND
            (BDAY > &REPTDAY) THEN AGE=&AGEBELOW;
         ELSE IF BMONTH > &REPTMON THEN AGE=&AGEBELOW;
        END;
      WHEN (AGE = &MAXAGE)
        DO;
         IF (BMONTH = &REPTMON) AND
            (BDAY > &REPTDAY) THEN AGE=&AGELIMIT;
         ELSE IF BMONTH > &REPTMON THEN AGE=&AGELIMIT;
        END;
      WHEN (AGE > &MAXAGE) AGE = &MAXAGE;
      WHEN (AGE < &AGELIMIT) AGE = &AGEBELOW;
      OTHERWISE AGE = &AGELIMIT;
     END;
    END;
RUN;

DATA CURRENT;
   SET DEPOSIT.CURRENT &CURN1;
RUN;
PROC SORT DATA=CURRENT;BY ACCTNO DESCENDING CURBAL;RUN;
PROC SORT DATA=CURRENT NODUPKEY;BY ACCTNO;RUN;

DATA BNM.CURN&REPTMON&NOWK &CURN2
     BNM.FCY&REPTMON&NOWK &CURN2;
  LENGTH CUSTCD $2. PRODCD $5. STATECD $1. AMTIND $1.;
  SET CURRENT;
  IF OPENIND NOT IN ('B','C','P');
  IF CURCODE NE 'MYR' THEN DO;
      INTPAYBL = ROUND(INTPAYBL * FORATE,.01);
  END;
  STATECD=PUT(BRANCH, STATECD.);
  PRODCD=PUT(PRODUCT, CAPROD.);
  AMTIND=PUT(PRODUCT, CADENOM.);
  RACE=PUT(RACE, $RACE.);
  RANGE=INPUT(CURBAL, DDRANGE.);
  AVGRNGE=INPUT(AVGAMT, DDRANGE.);
  CABAL = 0; SABAL = 0;
  /****************************************************/
  /** IF VOSTRO ACCOUNT THEN THE COUNTER PARTY CODE  **/
  /** WILL BE TAKEN AS EITHER CB '02' OR '81' BASED  **/
  /** ON THE PRODUCT CODE :                          **/
  /**              104 = VOSTRO LOCAL                **/
  /**              105 = VOSTRO FOREIGN              **/
  /****************************************************/
  SELECT(PRODUCT);
    WHEN(104) CUSTCD='02';
    WHEN(105) CUSTCD='81';
    OTHERWISE CUSTCD=PUT(CUSTCODE, DDCUSTCD.);
  END;

  /*****************************************************/
  /** IF ACE PRODUCTS THEN CHECK BALANCE              **/
  /**   ELSE TREAT LIKE OTHER CURRENT ACCOUNT PRODUCT **/
  /*****************************************************/
  IF PRODUCT IN &ACE THEN DO;
        * INTPAYBL = 0;
        PRODCD=PUT(PRODUCT, CAPROD.);
        AMTIND=PUT(PRODUCT, CADENOM.);
        RANGE=INPUT(CURBAL, DDRANGE.);
        AVGRNGE=INPUT(AVGAMT, DDRANGE.);
        CHQFLOAT = 0;
        OUTPUT BNM.CURN&REPTMON&NOWK;
  END;
  ELSE IF 400 <= PRODUCT <= 444 THEN DO;
     IF CUSTCD IN ('77','78','95') THEN DO;
        IF SECTOR IN (4,5) THEN SECTOR = 1;
        ELSE IF SECTOR NOT IN (1,2,3,4,5) THEN SECTOR = 1;
     END;
     ELSE DO;
        IF SECTOR IN (1,2,3) THEN SECTOR = 4;
        ELSE IF SECTOR NOT IN (1,2,3,4,5) THEN SECTOR = 4;
     END;
     OUTPUT BNM.FCY&REPTMON&NOWK;
  END;
  ELSE DO;
     OUTPUT BNM.CURN&REPTMON&NOWK;
  END;
RUN;
  /*
DATA CISDP;
   INFILE CISDP MISSOVER;
   INPUT @1 ACCTNO 11. @13 CUSTNO 11.;
RUN;
PROC SORT DATA=CISDP OUT=CISDP(KEEP=ACCTNO);
   BY ACCTNO;
*;
DATA BNM.FCY&REPTMON&NOWK;
   MERGE BNM.FCY&REPTMON&NOWK(IN=A) CISDP;
   BY ACCTNO;
   IF A;
*; */
PROC SORT DATA=BNM.FCY&REPTMON&NOWK;
   BY ACCTNO;
*;
PROC APPEND DATA=BNM.FCY&REPTMON&NOWK
            BASE=BNM.CURN&REPTMON&NOWK; RUN;

 /***********************************************************/
 /** SUMMARIZE THE DATASET AT BRANCH LEVEL - FOR RDAL      **/
 /***********************************************************/
PROC DATASETS LIB=BNM NOLIST; DELETE DEPT&REPTMON&NOWK; RUN;
PROC SUMMARY DATA=BNM.SAVG&REPTMON&NOWK NWAY;
CLASS BRANCH STATECD PRODCD CUSTCD AMTIND;
VAR CURBAL INTPAYBL;
OUTPUT OUT=DEPT
       SUM=CURBAL INTPAYBL;
RUN;

PROC APPEND DATA=DEPT BASE=BNM.DEPT&REPTMON&NOWK; RUN;
PROC DATASETS LIB=WORK NOLIST; DELETE DEPT; RUN;

PROC SUMMARY DATA=BNM.CURN&REPTMON&NOWK NWAY MISSING;
CLASS BRANCH STATECD PRODCD CUSTCD SECTOR AMTIND;
VAR CURBAL INTPAYBL;
OUTPUT OUT=DEPT
       SUM=CURBAL INTPAYBL;
RUN;

PROC APPEND DATA=DEPT BASE=BNM.DEPT&REPTMON&NOWK FORCE; RUN;
PROC DATASETS LIB=WORK NOLIST; DELETE DEPT; RUN;

DATA SAVE293 &SAVG2;
  LENGTH CUSTCD $2. PRODCD $5. STATECD $1. AMTIND $1.;
  SET DEPOSIT.SAVING &SAVG1 UMA &SAVG1;
  IF OPENIND NOT IN ('B','C','P');
  /****************************************************/
  /** APPLY THE FORMAT                               **/
  /****************************************************/
  CUSTCD =PUT(CUSTCODE, SACUSTCD.);
  STATECD=PUT(BRANCH, STATECD.);
  PRODCD =PUT(PRODUCT, SAPROD.);
  AMTIND =PUT(PRODUCT, SADENOM.);
  IF PRODCD='N' THEN DELETE;
  OPENMH=1;
  OUTPUT SAVE293;
*;
PROC SUMMARY DATA=SAVE293 NWAY;
CLASS BRANCH STATECD PRODCD CUSTCD AMTIND;
VAR OPENMH;
OUTPUT OUT=SAVE293 SUM=;
*;
DATA CURR293 &CURN2;
  LENGTH CUSTCD $2. PRODCD $5. STATECD $1. AMTIND $1.;
  SET DEPOSIT.CURRENT &CURN1;
  IF  OPENIND NOT IN ('B','C','P');
  IF  BRANCH > 900 THEN DELETE;
  STATECD=PUT(BRANCH, STATECD.);
  PRODCD =PUT(PRODUCT, CAPROD.);
  AMTIND =PUT(PRODUCT, CADENOM.);
  CUSTCD ='00';
  IF PRODCD='N' THEN DELETE;
  /*  IF PRODUCT IN (160,161,162,163,164,165,182) THEN DO; */
  IF PRODUCT IN (160,161,162,163,164,165,166,182) THEN DO;
     PRODCD='42310'; AMTIND='I';
  END;
  ELSE DO;
     PRODCD='42110'; AMTIND='D';
  END;
  OPENMH=1;
  OUTPUT CURR293;
*;
PROC SUMMARY DATA=CURR293 NWAY;
CLASS BRANCH STATECD PRODCD CUSTCD AMTIND;
VAR OPENMH;
OUTPUT OUT=CURR293 SUM=;
*;
DATA FD;
   SET FD.FD;
   IF ACCTTYPE IN (397,398) THEN DELETE;
RUN;

DATA BNM.FDMTHLY
     (KEEP=BRANCH ACCTNO STATE CUSTCODE OPENIND CURBAL TERM NAME AMTIND
           ORGDATE MATDATE RATE RENEWAL INTPLAN INTPAY INTDATE BIC
           LASTACTV LSTMATDT PURPOSE FORATE ACCTTYPE);
     LENGTH STATE $1. CUSTCODE $2. BIC $5.;
     LSTMATDT=0;
     SET FD;
     IF CURCODE NE 'MYR' THEN DO;
      INTPAY = ROUND(INTPAY * FORATE,.01);
     END;
     STATE = PUT(BRANCH, STATECD.);
     BIC = PUT(INTPLAN, FDPROD.);
     IF LMATDATE ^= 0 THEN
     LSTMATDT=INPUT(SUBSTR(PUT(LMATDATE,Z11.),1,8),MMDDYY8.);
     AMTIND = PUT(INTPLAN, FDDENOM.);
     IF BIC IN ('42130','42630') THEN CUSTCODE = PUT(CUSTCD, FDCUSTCD.);
        ELSE CUSTCODE = PUT(CUSTCD, IFDCUSCD.);
     IF BIC = '42630' THEN DO;
        IF CUSTCODE IN ('77','78','95') THEN DO;
           IF PURPOSE NOT IN ('1','2','3') THEN PURPOSE=1;
        END;
        ELSE DO;
           IF PURPOSE NOT IN ('4','5') THEN PURPOSE=4;
        END;
     END;
     IF ACCTTYPE IN (315,394) THEN BIC='42132'; ELSE
     IF ACCTTYPE IN (397,398) THEN BIC='42199';
     IF OPENIND = 'D' OR OPENIND = 'O' THEN OUTPUT;
RUN;
