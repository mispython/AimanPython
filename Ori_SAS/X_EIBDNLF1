//EIBDNLF1 JOB MIS,'MIS',COND=(4,LT),CLASS=A,
//         MSGCLASS=X,NOTIFY=&SYSUID
/*JOBPARM S=S1M1
//*
//EIBDNLF1 EXEC SAS609,REGION=6M,WORK='120000,8000'
//LOAN     DD DSN=SAP.PBB.MNILN.DAILY(0),DISP=SHR
//BNM1     DD DSN=SAP.PBB.SASDATA.DAILY(0),DISP=SHR
//WALK     DD DSN=SAP.FDP.APPL.DAILY.NLF(0),DISP=SHR
//BNM      DD DSN=SAP.PBB.NLF.LN.DAILY,DISP=OLD
//SASLIST  DD DSN=SAP.PBB.NLFTABLE.TEXT(+1),
//         DISP=(NEW,CATLG,DELETE),
//         DCB=(RECFM=FB,LRECL=300,BLKSIZE=0,DSORG=PS),
//         SPACE=(CYL,(3,2),RLSE),UNIT=SYSDA
//SYSIN    DD *

OPTIONS SORTDEV=3390 YEARCUTOFF=1950 NOCENTER;
*------------------------------------------------*
*  GET REPTDATE                                  *
*------------------------------------------------*;
DATA REPTDATE (KEEP=REPTDATE);
  SET LOAN.REPTDATE;
   SELECT(DAY(REPTDATE));
      WHEN (8)  CALL SYMPUT('NOWK',PUT('1',$1.));
      WHEN (15) CALL SYMPUT('NOWK',PUT('2',$1.));
      WHEN (22) CALL SYMPUT('NOWK',PUT('3',$1.));
      OTHERWISE CALL SYMPUT('NOWK',PUT('4',$1.));
   END;
  LAST = PUT(DAY(INPUT('01'||PUT(MONTH(TODAY()), Z2.)||
                 PUT(YEAR(TODAY()), 4.), DDMMYY8.)-1),Z2.);
  REPTDAY = PUT(DAY(REPTDATE), Z2.);
  IF REPTDAY IN ('08','15','22') THEN INSERT = 'Y';
  ELSE IF REPTDAY = LAST & DAY(TODAY()) < 8 THEN INSERT = 'Y';

  CALL SYMPUT('INSERT',INSERT);
  CALL SYMPUT('RDATE',PUT(REPTDATE,DDMMYY8.));
  CALL SYMPUT('RDAT1',PUT(REPTDATE,Z5.));
  CALL SYMPUT('REPTMON',PUT(MONTH(REPTDATE),Z2.));
  CALL SYMPUT('REPTDAY',PUT(DAY(REPTDATE),Z2.));
RUN;

PROC FORMAT;
   VALUE REMFMT
      LOW-0.1 = '01'   /*  UP TO 1 WK       */
      0.1-1   = '02'   /*  >1 WK - 1 MTH    */
      1-3     = '03'   /*  >1 MTH - 3 MTHS  */
      3-6     = '04'   /*  >3 - 6 MTHS      */
      6-12    = '05'   /*  >6 MTHS - 1 YR   */
      OTHER   = '06';  /*  > 1 YEAR         */

   VALUE REMFMTB
      LOW-0.255 = 'UP TO 1 WK'
      0.255-1   = '>1 WK - 1 MTH'
      1-3       = '>1 MTH - 3 MTHS'
      3-6       = '>3 - 6 MTHS'
      6-12      = '>6 MTHS - 1 YR'
      OTHER     = '> 1 YEAR';
RUN;

***************************************************
* MACRO TO CALCULATE OD,PBCARD,SMC PRODUCTS       *
***************************************************;
%MACRO CALCULATE;
PROC SORT DATA=BNM.STORE_&PROD OUT=STORE;
  BY DESCENDING REPTDATE;RUN;

DATA WEEK48;
   SET STORE;
   COUNT+1;
   IF COUNT <49;
RUN;
PROC PRINT DATA=WEEK48;TITLE '48 WEEKS TABLE -' &PROD &RDATE;RUN;

PROC SORT DATA=WEEK48 OUT=CUR;BY DESCENDING REPTDATE;RUN;
DATA CUR(KEEP=AMOUNT);
    SET CUR(OBS=1);
    CALL SYMPUT('CURBAL',AMOUNT);
RUN;

PROC SORT DATA=WEEK48 OUT=MIN;BY AMOUNT;RUN;
DATA MIN(KEEP=AMOUNT);
    SET MIN(OBS=1);
    CALL SYMPUT('MINBAL',AMOUNT);
RUN;

PROC PRINT DATA=CUR;TITLE 'CURRENT VALUE -' &PROD &RDATE;RUN;
PROC PRINT DATA=MIN;TITLE 'MINIMUM VALUE -' &PROD &RDATE;RUN;

DATA TABLE;
   SET BNM.TABLE;
   ***CURBAL= &CURBAL/1000;
   ***MINBAL= &MINBAL/1000;
   CURBAL= &CURBAL;
   MINBAL= &MINBAL;
   IF REMMTH < 12 THEN AMOUNT=(CURBAL-MINBAL)/5;
   ELSE AMOUNT=MINBAL;
RUN;
***PROC PRINT DATA=TABLE;RUN;

PROC TABULATE DATA=TABLE MISSING NOSEPS;
   FORMAT REMMTH REMFMTB.;
   CLASS REMMTH;
   VAR AMOUNT;
   TABLE (REMMTH=' ' ALL='TOTAL')*
         (SUM=' '*AMOUNT=' '*F=COMMA20.)
         / BOX='CORE (NON-TRADING) BANKING ACTIVITIES' RTS=45 CONDENSE;
   TITLE4 'BREAKDOWN BY PURE CONTRACTUAL MATURITY PROFILE' &RDATE;
RUN;
%MEND CALCULATE;

***************************************************
* MACRO TO APPEND THE LATEST RECORD TO THE BASE   *
***************************************************;
%MACRO MISAPPD;
%IF "&INSERT" EQ "Y" %THEN
    %DO;
      DATA &PROD;
         SET NEWREC;
      RUN;
      DATA BNM.BASE_&PROD;
         SET BNM.BASE_&PROD;
         IF REPTDATE EQ "&RDAT1" THEN DELETE;
      RUN;
      PROC SORT; BY REPTDATE;

      PROC APPEND DATA=&PROD BASE=BNM.BASE_&PROD; RUN;
      DATA BNM.STORE_&PROD;
         SET BNM.BASE_&PROD;
         WHERE REPTDATE <= &RDAT1;
      RUN;
      ***PROC PRINT;
    %END;
%ELSE %DO;
      DATA &PROD;
         SET NEWREC;
      RUN;

      DATA BNM.STORE_&PROD;
         SET BNM.BASE_&PROD &PROD;
         WHERE REPTDATE <= &RDAT1;
      PROC SORT; BY REPTDATE;
      ***PROC PRINT;
      RUN;
%END;
%CALCULATE;
%MEND MISAPPD;
RUN;

*----------------------------------------------*
* PBCARD                                       *
*----------------------------------------------*;
DATA NEWREC(KEEP=REPTDATE AMOUNT);
 INFILE WALK FIRSTOBS=1;
 INPUT @2   DESC          $19.
       @21  DD              2.
       @24  MM              2.
       @27  YY              2.
       @42  AMOUNT    COMMA20.2
       ;
 REPTDATE = MDY(MM,DD,YY);
 IF DESC = '34200';
 IF REPTDATE= &RDAT1 THEN OUTPUT NEWREC;
 CALL SYMPUT('CARDAMT',AMOUNT);
RUN;

DATA _NULL_;
   PROD = 'CARD';
   CALL SYMPUT('PROD',PROD);
RUN;
%MISAPPD;

DATA PBCARD(KEEP=BNMCODE AMOUNT);
   SET TABLE;
   BNMCODE = '9321508'||PUT(REMMTH,REMFMT.)||'0000Y';
RUN;
PROC PRINT DATA=PBCARD;TITLE 'PBCARD -' &RDATE;RUN;

DATA PBCARD2(KEEP=BNMCODE AMOUNT);
   SET BNM.TABLE;
   IF BASKET = 1 THEN DO;
   BNMCODE = '9521508'||PUT(REMMTH,REMFMT.)||'0000Y';
   AMOUNT = &CARDAMT * 0.15;
   END;
   IF BASKET = 2 THEN DO;
   BNMCODE = '9521508'||PUT(REMMTH,REMFMT.)||'0000Y';
   AMOUNT = &CARDAMT * 0.69;
   END;
   IF BASKET = 3 THEN DO;
   BNMCODE = '9521508'||PUT(REMMTH,REMFMT.)||'0000Y';
   AMOUNT = &CARDAMT * 0.01;
   END;
   IF BASKET = 4 THEN DO;
   BNMCODE = '9521508'||PUT(REMMTH,REMFMT.)||'0000Y';
   AMOUNT = &CARDAMT * 0.03;
   END;
   IF BASKET = 5 THEN DO;
   BNMCODE = '9521508'||PUT(REMMTH,REMFMT.)||'0000Y';
   AMOUNT = &CARDAMT * 0.07;
   END;
   IF BASKET = 6 THEN DO;
   BNMCODE = '9521508'||PUT(REMMTH,REMFMT.)||'0000Y';
   AMOUNT = &CARDAMT * 0.05;
   END;
RUN;
***PROC PRINT DATA=PBCARD2;TITLE 'PBCARD2 -' &RDATE;RUN;

*----------------------------------------------*
* OVERDRAFT - CORP                             *
*----------------------------------------------*;
DATA NOTEX;
   SET BNM.NOTE;
   BNMCODE1 = SUBSTR(BNMCODE,1,7);
RUN;

PROC SUMMARY DATA=NOTEX NWAY;
   CLASS BNMCODE1;
   VAR AMOUNT;
   WHERE BNMCODE1 = '9521309';
   OUTPUT OUT=NEWREC (DROP=_TYPE_ _FREQ_) SUM=;

DATA NEWREC(KEEP=REPTDATE AMOUNT);
   SET NEWREC;
   REPTDATE=&RDAT1;
RUN;

DATA _NULL_;
   PROD = 'ODCORP';
   CALL SYMPUT('PROD',PROD);
RUN;
%MISAPPD;

DATA ODCORP(KEEP=BNMCODE AMOUNT);
   SET TABLE;
   BNMCODE = '9321309'||PUT(REMMTH,REMFMT.)||'0000Y';
RUN;
PROC PRINT DATA=ODCORP;TITLE 'ODCORP -' &RDATE;RUN;

*----------------------------------------------*
* OVERDRAFT - IND                              *
*----------------------------------------------*;
PROC SUMMARY DATA=NOTEX NWAY;
   CLASS BNMCODE1;
   VAR AMOUNT;
   WHERE BNMCODE1 = '9521308';
   OUTPUT OUT=NEWREC (DROP=_TYPE_ _FREQ_) SUM=;

DATA NEWREC(KEEP=REPTDATE AMOUNT);
   SET NEWREC;
   REPTDATE=&RDAT1;
RUN;

DATA _NULL_;
   PROD = 'ODIND';
   CALL SYMPUT('PROD',PROD);
RUN;
%MISAPPD;

DATA ODIND(KEEP=BNMCODE AMOUNT);
   SET TABLE;
   BNMCODE = '9321308'||PUT(REMMTH,REMFMT.)||'0000Y';
RUN;
PROC PRINT DATA=ODIND;TITLE 'ODIND -' &RDATE;RUN;

*----------------------------------------------*
* SMC - CORP                                   *
*----------------------------------------------*;
DATA SMC (KEEP=BNMCODE AMOUNT);
   SET BNM1.LOAN&REPTMON&REPTDAY;
   IF PAIDIND NOT IN ('P','C');
   IF SUBSTR(PRODCD,1,2) = '34' OR PRODUCT IN (225,226);
   IF CUSTCD IN ('77','78','95','96') THEN CUST = '08';
   ELSE CUST = '09';
   IF ACCTYPE = 'OD' THEN DO;
      IF PRODUCT IN (151,152,181) THEN DELETE;
      REMMTH = 0.1;
      AMOUNT = BALANCE;
      IF PRODCD='34240' THEN
         BNMCODE = '95219'||CUST||PUT(REMMTH,REMFMT.)||'0000Y';
      OUTPUT;
   END;
RUN;

PROC SUMMARY DATA=SMC NWAY;
   CLASS BNMCODE;
   VAR AMOUNT;
   WHERE SUBSTR(BNMCODE,1,7) = '9521909';
   OUTPUT OUT=NEWREC (DROP=_TYPE_ _FREQ_) SUM=;

DATA NEWREC(KEEP=REPTDATE AMOUNT);
   SET NEWREC;
   REPTDATE=&RDAT1;
RUN;

DATA _NULL_;
   PROD = 'SMCCORP';
   CALL SYMPUT('PROD',PROD);
RUN;
%MISAPPD;

DATA SMCCORP(KEEP=BNMCODE AMOUNT);
   SET TABLE;
   BNMCODE = '9321909'||PUT(REMMTH,REMFMT.)||'0000Y';
RUN;
PROC PRINT DATA=SMCCORP;TITLE 'SMCCORP -' &RDATE;RUN;

*----------------------------------------------*
* SMC - IND                                    *
*----------------------------------------------*;
PROC SUMMARY DATA=SMC NWAY;
   CLASS BNMCODE;
   VAR AMOUNT;
   WHERE SUBSTR(BNMCODE,1,7) = '9521908';
   OUTPUT OUT=NEWREC (DROP=_TYPE_ _FREQ_) SUM=;

DATA NEWREC(KEEP=REPTDATE AMOUNT);
   SET NEWREC;
   REPTDATE=&RDAT1;
RUN;

DATA _NULL_;
   PROD = 'SMCIND';
   CALL SYMPUT('PROD',PROD);
RUN;
%MISAPPD;

DATA SMCIND(KEEP=BNMCODE AMOUNT);
   SET TABLE;
   BNMCODE = '9321908'||PUT(REMMTH,REMFMT.)||'0000Y';
RUN;
PROC PRINT DATA=SMCIND;TITLE 'SMCIND -' &RDATE;RUN;

DATA BNM.CALC;
   SET PBCARD PBCARD2 ODCORP ODIND SMCCORP SMCIND;
RUN;
