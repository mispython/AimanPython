/** PROGRAM : KALWPBBS                       **/
/** DATE    : 18/11/2002                     **/
/** REPORT  : RDIR PART I  (KAPITI ITEMS )   **/
/**         : QUOTED RATES ON NIDS, OWN BAS  **/
/**         : AND REPOS (BNM TABLES 1,3      **/

%LET AMTIND='D';

PROC FORMAT;
  VALUE NSRSFDORGMT
   LOW - 1   = '12'   /* ORG. MAT. <= 1 MTH      */
     1 - 3   = '13'   /* ORG. MAT. >1 - 3 MTHS   */
     3 - 6   = '15'   /* ORG. MAT. >3 - 6 MTHS   */
     6 - 9   = '16'   /* ORG. MAT. >6 - 9 MTHS   */
     9 - 12  = '17'   /* ORG. MAT. >9 - 12 MTHS  */
    12 - 15  = '21'   /* ORG. MAT. >12 - 15 MTHS */
    15 - 18  = '22'   /* ORG. MAT. >15 - 18 MTHS */
    18 - 24  = '23'   /* ORG. MAT. >18 - 24 MTHS */
    24 - 36  = '24'   /* ORG. MAT. >24 - 36 MTHS */
    36 - 48  = '25'   /* ORG. MAT. >36 - 48 MTHS */
    48 - 60  = '26'   /* ORG. MAT. >48 - 60 MTHS */
    60 - HIGH = '30'; /* ORG. MAT. > 60 MTHS     */
    ;

  VALUE $NIDSDESC
  '13' = 'ORG. MAT. >1 - 3 MTHS'
  '15' = 'ORG. MAT. >3 - 6 MTHS'
  '16' = 'ORG. MAT. >6 - 9 MTHS'
  '17' = 'ORG. MAT. >9 - 12 MTHS'
  ;
  VALUE $BASDESC
  '12' = 'ORG. MAT. <= 1 MTH'
  '15' = 'ORG. MAT. >1 - 3 MTHS'
  '16' = 'ORG. MAT. >3 - 6 MTHS'
  ;

  VALUE $SBBADESC
  '11' = 'OVERNIGHT'
  '18' = '> OVERNIGHT TO 1 WK'
  '19' = '> 1 TO 2 WKS'
  '27' = '> 2 TO 3 WKS'
  '28' = '> 3 TO 4 WKS'
  ;
*;
*------------------------------------------------*
*  MACRO TO DECLARE VARIABLES                    *
*------------------------------------------------*;
%MACRO DCLVAR;
   RETAIN D1-D12 31 D4 D6 D9 D11 30
          RD1-RD12 MD1-MD12 31 RD2 MD2 28 RD4 RD6 RD9 RD11
          MD4 MD6 MD9 MD11 30 RPYR RPMTH RPDAY 0;
   ARRAY LDAY D1-D12;
   ARRAY RPDAYS RD1-RD12;
   ARRAY MDDAYS MD1-MD12;
%MEND DCLVAR;
*;
*------------------------------------------------*
*  MACRO TO CALCULATE REMAIN MONTH               *
*------------------------------------------------*;
%MACRO REMMTH;
   MDYR  = YEAR(MATDTE);
   MDMTH = MONTH(MATDTE);
   MDDAY = DAY(MATDTE);
   REMY = MDYR - RPYR;
   REMM = MDMTH - RPMTH;
   REMD = MDDAY - RPDAY;
   REMMTH = REMY*12 + REMM + REMD/RPDAYS(RPMTH);
%MEND REMMTH;
   /* QUOTED RATES ON REPOS */
PROC SORT DATA=BNMK.KWTBL&REPTMON&NOWK OUT=K1TBL NODUPKEY;
  BY GWDLR;
RUN;
*;
PROC SORT DATA=BNMKD.UTRP&REPTMON&NOWK OUT=UTRP NODUPKEY;BY DEALREF;RUN;
*;
DATA K1TBL;
  MERGE K1TBL(IN=A) UTRP(KEEP=DEALREF FACEVALU RENAME=(DEALREF=GWDLR));
  BY GWDLR;
  IF A;
RUN;
*;
DATA K1TBLQ (KEEP=BNMCODE AMOUNT BNMC WAMT AMTIND);
  LENGTH BNMCODE $14. BNMC $2. AMTIND $1.;
  SET K1TBL (RENAME=(FACEVALU=AMOUNT));
  IF  GWSDT > &PDATE;
  AMTIND=&AMTIND;
  DAYS=(GWMDT-GWSDT);
  IF SUBSTR(GWDLP,2,2) IN ('MI','MT','MA','MQ') AND DAYS > 0;
     WAMT=(AMOUNT*GWRATC)/100;
     IF      DAYS=1    THEN BNMC='11'; ELSE
     IF (2<= DAYS<=7 ) THEN BNMC='18'; ELSE
     IF (7<  DAYS<=14) THEN BNMC='19'; ELSE
     IF (14< DAYS<=21) THEN BNMC='27'; ELSE
     IF (21< DAYS<=28) THEN BNMC='28';
     IF WAMT NE . AND BNMC IN ('11','18','19','27','28')
        THEN DO;
        BNMCODE='8430800'||BNMC||'0000Y';
        OUTPUT K1TBLQ;
     END;
*;
   /* QUOTED RATES ON BA (BANKERS ACCEPTANCE) AND NIDS */
PROC SORT DATA=BNMKD.K3TBL&REPTMON&NOWK OUT=K3TBL;
  BY UTDLR EXTDATE;
RUN;
PROC SORT DATA=K3TBL NODUPKEY;BY UTDLR;RUN;
DATA K3TBLA (KEEP=BNMCODE AMOUNT OMTH WAMT AMTIND)
     K3TBLB (KEEP=BNMCODE AMOUNT OMTH WAMT AMTIND)
     BNMK.K3TBLA (KEEP=BNMCODE AMOUNT OMTH WAMT AMTIND
                       ORIYY ORIMM ORIDD)
     BNMK.K3TBLB (KEEP=BNMCODE AMOUNT OMTH WAMT AMTIND
                  ORIYY ORIMM ORIDD);
  LENGTH BNMCODE $14. OMTH $2. AMTIND $1.;
  %DCLVAR
  SET K3TBL;
  IF  ISSDT > &PDATE;
  IF  UTDLP NE 'MOS';
  MATDTE = INPUT(UTMDT,DDMMYY10.);
  ISSDTE = INPUT(UTOSD,DDMMYY10.);
  RPYR  = YEAR(ISSDTE);
  RPMTH = MONTH(ISSDTE);
  RPDAY = DAY(ISSDTE);
  AMTIND=&AMTIND;
  IF UTSTY='ILD' THEN UTQDS=UTCPR;
  IF UTSTY IN ('IFD','ILD','ISD','IZD','PBA');
     AMOUNT=UTFCV;
     WAMT=(UTQDS*AMOUNT)/100;
     SELECT (UTSTY);
     WHEN  ('IFD','ILD','ISD','IZD') DO;
     %REMMTH
     OMTH=PUT(REMMTH,NSRSFDORGMT.);
        IF WAMT NE . AND OMTH IN ('13','15','16','17')
           THEN DO;
           BNMCODE='8430600'||OMTH||'0000Y';
           OUTPUT K3TBLA BNMK.K3TBLA;
        END;
     END;
     WHEN  ('PBA') DO;
     MATDD = DAY(MATDTE);
     MATMM = MONTH(MATDTE);
     MATYY = YEAR(MATDTE);
     ORIDD = MATDD - RPDAY;
     ORIMM = MATMM - RPMTH;
     ORIYY = MATYY - RPYR;
      IF ORIYY >= 1 THEN ORIMM = ORIMM + 12;
        IF (ORIMM = 1 AND ORIDD LE 0) OR (ORIMM = 0 AND ORIDD > 0)
                               THEN OMTH='12'; ELSE
        IF (ORIMM = 2 AND ORIDD LE 0) OR (ORIMM = 1 AND ORIDD > 0)
                               THEN OMTH='15'; ELSE
        IF (ORIMM = 3 AND ORIDD LE 0) OR (ORIMM = 2 AND ORIDD > 0)
                               THEN OMTH='15'; ELSE
        IF (ORIMM = 4 AND ORIDD LE 0) OR (ORIMM = 3 AND ORIDD > 0)
                               THEN OMTH='16'; ELSE
        IF (ORIMM = 5 AND ORIDD LE 0) OR (ORIMM = 4 AND ORIDD > 0)
                               THEN OMTH='16'; ELSE
        IF (ORIMM = 6 AND ORIDD LE 0) OR (ORIMM = 5 AND ORIDD > 0)
                               THEN OMTH='16';
        IF WAMT NE . AND  OMTH IN ('12','15','16')
           THEN DO;
           BNMCODE='8430700'||OMTH||'0000Y';
           OUTPUT K3TBLB BNMK.K3TBLB;
        END;
     END;
     OTHERWISE;
     END;
RUN;
*;
DATA K3TBL3;
  SET K3TBLA K3TBLB K1TBLQ;
RUN;

PROC SUMMARY DATA=K3TBL3 NWAY;
   CLASS BNMCODE AMTIND;
   VAR   AMOUNT WAMT;
   OUTPUT OUT=K3TBL3 SUM=BALANCE WAMT;
RUN;
*;
DATA K3TBL3 (KEEP=BNMCODE AMOUNT AMTIND);
   SET K3TBL3;
   AMOUNT=(WAMT/BALANCE)*100;
*;
PROC SORT DATA=K3TBL3; BY BNMCODE;
PROC SORT DATA=BNMK.K3TBE&REPTMON1&NOWK1 OUT=K3TBLE; BY BNMCODE;
*;
DATA K3TBL3;
  MERGE K3TBLE K3TBL3; BY BNMCODE;
  AMOUNT=ROUND(AMOUNT,0.0001);
  FORMAT AMOUNT 7.4;
  IF SUBSTR(BNMCODE,1,5) = '84306' THEN DO;
     GROUP = 'QUOTED RATES ON NIDS/INIDS ISSUED';
     DESCRIPTION  = PUT(SUBSTR(BNMCODE,8,2),$NIDSDESC.);
  END;
  ELSE IF SUBSTR(BNMCODE,1,5) = '84307' THEN DO;
     GROUP = 'QUOTED RATES ON OWN BAS/IBAS';
     DESCRIPTION  = PUT(SUBSTR(BNMCODE,8,2),$BASDESC.);
  END;
  ELSE IF SUBSTR(BNMCODE,1,5) = '84308' THEN DO;
     GROUP = 'QUOTED RATES ON REPOS/SBBA ORIGINAL';
     DESCRIPTION  = PUT(SUBSTR(BNMCODE,8,2),$SBBADESC.);
  END;
*;
OPTIONS NOCENTER NONUMBER NODATE;
TITLE1 &SDESC;
TITLE2 'REPORT ON DOMESTIC INTEREST RATE - PART I (KAPITI)';
TITLE3 'REPORTING DATE :' &RDATE;
TITLE4;
*;
*;
PROC PRINTTO PRINT=NSRSTXT NEW;RUN;
PROC PRINT DATA=K3TBL3;BY GROUP;
  VAR BNMCODE DESCRIPTION AMTIND AMOUNT;
RUN;
DATA BNMK.K3TBE&REPTMON&NOWK;
  SET K3TBL3;
