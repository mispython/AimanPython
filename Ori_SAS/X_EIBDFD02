*-------------------------------------------------*
*   DAILY FD MOVEMENT                             *
*   - PLACEMENT/WITHDRAWAL OF RM100K & ABOVE'     *
*   PREVIOUSLY KNOWN AS 'FDDYMOVE'                *
*                                                 *
*-------------------------------------------------*;
OPTIONS YEARCUTOFF=1990 NOCENTER NODATE NONUMBER;
*;
%INC PGM(PBBDPFMT);
*;
DATA REPTDATE;
  INFILE DPFD4010 LRECL=1692 OBS=1;
  INPUT @106 TBDATE PD6.;
  REPTDATE=INPUT(SUBSTR(PUT(TBDATE, Z11.),1,8), MMDDYY8.);
  REPTDATX=REPTDATE+1;
  CALL SYMPUT('REPTYEAR', PUT(REPTDATE, YEAR4.));
  CALL SYMPUT('RDATE',PUT(REPTDATE,DDMMYY8.));
  CALL SYMPUT('XDATE',PUT(REPTDATX,Z5.));
RUN;

LIBNAME MIS "SAP.PBB.MIS.D&REPTYEAR" DISP=OLD;

DATA TEMP.FD4010
(KEEP=REPTDATE BRANCH NAMETRAN ACCTNO CDNO OPENIND TRANCD
      TRANAMT TRANDTE PENALTY)
     TEMP.FD4001 (KEEP=REPTDATE BRANCH NAME ACCTNO CDNO RATE ACCTTYPE
                          TERM ORGDATE MATDATE INTPLAN RENEW MATID);
  DROP LMDATES LMMM LMDD LMYY;
  INFILE DPFD4010 LRECL=1692;
  INPUT @3   BANKNO   PD2.
        @24  REPTNO   PD3.
        @27  FMTCODE  PD2. @;
  IF _N_ = 1 THEN SET REPTDATE;
  IF REPTNO = 4010 AND FMTCODE = 1 THEN
    DO;
     INPUT @106 BRANCH  PD4.
           @110 ACCTNO  PD6.
           @117 CDNO    PD6.
           @123 OPENIND $1.
           @124 TRANCD  PD2.
           @126 TRANAMT PD7.2
           @133 TRANDTE PD5.
           @146 PENALTY PD6.2
           @152 NAMETRAN $20.;
 * IF TRANCD IN (761,763,770,970,971,972) THEN OUTPUT TEMP.FD4010 ;
     IF TRANCD IN (297,770,970,971,972) THEN OUTPUT TEMP.FD4010 ;
     /* CODE 770 - PLACEMENT FOR THE DAY (NEW CD)
             297 - FORCE PLACEMENT FOR NEGOTIATED RATE
        970,971,972 - WITHDRAWAL FOR THE DAY
          970       - INTEREST
          971       - PRINCIPAL
          972       - PENALTY       */
    END;
  ELSE IF REPTNO = 4001 AND FMTCODE = 1 THEN
    DO;
     INPUT @106 BRANCH  PD4.
           @110 ACCTNO  PD6.
           @134 NAME    $15.
           @149 CDNO    PD6.
           @174 ORGDT   PD5.
           @179 MATDT   PD5.
           @184 MATDT2  PD5.
           @189 RATE    PD3.3
           @203 ACCTTYPE PD2.
           @205 TERM    PD2.
           @207 MATID   $1.
           @208 INTPLAN PD2.
           @211 RENEW   $1.
           @392 LMATDATE PD6.;

      IF LMATDATE > 0 THEN DO;
         LMDATES=INPUT(SUBSTR(PUT(LMATDATE,Z11.),1,8),MMDDYY8.);
         LMMM=MONTH(LMDATES);
         LMDD=DAY  (LMDATES);
         LMYY=YEAR (LMDATES);
         IF RENEW  ='N' AND (LMDATES=&XDATE) THEN
            MATDT=PUT(LMYY,Z4.)||PUT(LMMM,Z2.)||PUT(LMDD,Z2.);
      END;
           ORGDATE=INPUT(SUBSTR(PUT(ORGDT,Z9.),1,6),MMDDYY6.);
           MATDATE=INPUT(SUBSTR(PUT(MATDT,8.),1,8),YYMMDD8.);
     OUTPUT TEMP.FD4001 ;
   END;
RUN;

PROC SORT DATA=TEMP.FD4010;
 BY ACCTNO CDNO;
RUN;

PROC SORT DATA=TEMP.FD4001;
 BY ACCTNO CDNO;
RUN;

DATA TEMP.FD4001;
  MERGE TEMP.FD4010  (IN=A) TEMP.FD4001;
  BY ACCTNO CDNO;
  IF A THEN OUTPUT;
RUN;

DATA BRHDATA;
  INFILE BRHFILE LRECL=80;
  INPUT @2 BRANCH  3.
        @6 BRHCODE $3.;
RUN;
PROC SORT; BY BRANCH;

PROC SORT DATA=TEMP.FD4001;
 BY BRANCH;
RUN;

DATA TEMP.FD4001;
  MERGE TEMP.FD4001 (IN=A) BRHDATA;
  BY BRANCH;
  IF A THEN OUTPUT ;
RUN;
*;
PROC SORT DATA=TEMP.FD4001 (RENAME=(BRANCH=BRCH));
 BY ACCTNO CDNO;
RUN;

 /* DAILY FD REPORT PRINTING */

 /* CALCULATION OF WITHDRAWAL AMOUNT & SEGREGATION OF WITHDRAWAL
     & PLACEMENT  */
DATA MIS.WITHDRAW (DROP=BALANCE2 TERM2)
     MIS.PLACEMNT (DROP=TERM BALANCE AMT1 AMT2 AMT3 TOT1);
 DROP OPENIND TRANDTE NAMETRAN;
 SET TEMP.FD4001;
 BY ACCTNO CDNO;
 TERMALL = TERM;
 IF TRANCD IN (970,971,972) THEN
   DO;
    TRANTYPE = 'W';
    IF FIRST.CDNO OR FIRST.ACCTNO THEN
      DO;
       BALANCE = 0; AMT1 = 0; AMT2 = 0; AMT3 = 0; TOT1=0;
      END;
    RETAIN BALANCE TOT1 AMT1-AMT3;

    IF TRANCD = 972 THEN
      DO;
       AMT1 = AMT1+TRANAMT;
       AMT3 = AMT3+PENALTY;
       TOT1 = ROUND(TOT1+TRANAMT,.01);
      END;
    IF TRANCD IN (970,971) THEN AMT2 = ROUND(AMT2+TRANAMT,.01);
    IF TRANCD = 971 THEN TOT1 = TOT1+TRANAMT;
    IF LAST.CDNO OR LAST.ACCTNO THEN
      DO;
       BALANCE = ROUND(((AMT1 + AMT2) - AMT3),.01);
       OUTPUT MIS.WITHDRAW;
      END;
   END;
 ELSE IF TRANCD IN (770,297) THEN
  DO;
   TRANTYPE = 'D';
   BALANCE2= ROUND(TRANAMT,.01);
   TERM2   = TERM;
   OUTPUT MIS.PLACEMNT;
  END;
RUN;

PROC SORT DATA=MIS.WITHDRAW (RENAME=(TOT1=BAL));
 BY ACCTNO BAL TERMALL;
RUN;

DATA WITHDRAW;
 SET MIS.WITHDRAW;
 BY ACCTNO BAL TERMALL;
 IF FIRST.ACCTNO OR FIRST.BAL OR FIRST.TERMALL THEN N=0;
 N+1;
RUN;

PROC SORT DATA=MIS.PLACEMNT (RENAME=(BALANCE2=BAL));
 BY ACCTNO BAL TERMALL;
RUN;

DATA PLACEMNT;
 SET MIS.PLACEMNT;
 BY ACCTNO BAL TERMALL;
 IF FIRST.ACCTNO OR FIRST.BAL OR FIRST.TERMALL THEN N=0;
 N+1;
RUN;

DATA RENEWAL1
     WITHDRAW (DROP=TERM2 RENAME=BAL=BALANCE1) /*RETAINING PRINCIPAL*/
     PLACEMNT (DROP=TERM BALANCE AMT1 AMT2 AMT3);
  MERGE WITHDRAW (IN=W ) /* PRINCIPAL */
        PLACEMNT (IN=P);
  BY ACCTNO BAL TERMALL N;

  IF NOT P THEN OUTPUT WITHDRAW;
  ELSE IF NOT W THEN OUTPUT PLACEMNT;
  ELSE OUTPUT RENEWAL1;

PROC SORT DATA=WITHDRAW (RENAME=(AMT2=BAL));
 BY ACCTNO BAL TERMALL N; RUN;

DATA WITHDRAW;
 SET WITHDRAW;
 BY ACCTNO BAL TERMALL;
 IF FIRST.ACCTNO OR FIRST.BAL OR FIRST.TERMALL THEN N=0;
 N+1;
RUN;

DATA RENEWAL2
     WITHDRAW (DROP=TERM2 BAL)
     PLACEMNT (DROP=TERM BALANCE AMT1 AMT3 RENAME=(BAL=BALANCE2));
  MERGE WITHDRAW (IN=W) /* PRINCIPAL+I */
        PLACEMNT (IN=P);
  BY ACCTNO BAL TERMALL N;
  IF NOT P THEN OUTPUT WITHDRAW;
  ELSE IF NOT W THEN OUTPUT PLACEMNT;
  ELSE OUTPUT RENEWAL2;

DATA FDW;   /* OBTAIN ACCOUNTS WITH WITHDRAWAL OF RM100K & ABOVE */
 KEEP ACCTNO;
 SET WITHDRAW;
 BY ACCTNO;
 IF FIRST.ACCTNO THEN TOTAL = 0;
 RETAIN TOTAL;
 TOTAL + BALANCE1;
 IF LAST.ACCTNO;
   IF TOTAL >= 100000 THEN OUTPUT;
RUN;

DATA FDP;     /* OBTAIN ACCOUNTS WITH PLACEMENT OF RM100K & ABOVE  */
 KEEP ACCTNO;
 SET PLACEMNT;
 BY ACCTNO;
 IF FIRST.ACCTNO THEN TOTAL = 0;
 RETAIN TOTAL;
 TOTAL + BALANCE2;
 IF LAST.ACCTNO;
   IF TOTAL >= 100000 THEN OUTPUT;
RUN;

DATA MERGEFDW;
  KEEP REPTDATE BRCH BRHCODE ACCTNO NAME TRANTYPE TERM BALANCE1 INTPLAN
  ACCTTYPE;
  MERGE WITHDRAW (IN=A) FDW (IN=B);
  BY ACCTNO;

  IF B THEN
   DO;
    TRANTYPE = 'W';
    OUTPUT;
   END;
RUN;

PROC SORT DATA=MERGEFDW;
 BY TRANTYPE TERM;
RUN;

DATA MERGEFDP;
  KEEP REPTDATE BRCH BRHCODE ACCTNO NAME TRANTYPE TERM2 BALANCE2 INTPLAN
  ACCTTYPE;
  MERGE PLACEMNT
        FDP (IN=B);
  BY ACCTNO;
  IF B THEN
   DO;
    TRANTYPE = 'D';
    OUTPUT;
   END;
RUN;
PROC SORT DATA=MERGEFDP (RENAME=(TERM2=TERM));
 BY  TRANTYPE TERM;
RUN;

DATA BOTH;
  SET MERGEFDW  MERGEFDP;
  BY TRANTYPE TERM;
  FDI=PUT(INTPLAN,FDDENOM.);
  IF FDI='I'    THEN PLAN=1;
                ELSE PLAN=2;
  BRANCH = LEFT(BRCH||'/'||BRHCODE);
  IF TRANTYPE = 'W' THEN BALANCE1 = 0-BALANCE1;
  OUTPUT BOTH;
RUN;

PROC SORT DATA=BOTH;
 BY BRCH ACCTNO TERM;
RUN;
*;
PROC FORMAT;
  VALUE PLANFMT 1 = 'AL-MUDHARABAH'
                2 = 'CONVENTIONAL';
*;
OPTION MISSING = ' ';
PROC PRINT DATA=BOTH LABEL;
 WHERE PLAN = 1 AND ACCTTYPE NOT IN (396,394,393);
 VAR NAME TERM BALANCE1 BALANCE2;
 ID BRANCH;
 BY PLAN BRCH ACCTNO;
 SUM BALANCE1 BALANCE2;
 LABEL BALANCE1  = '(WITHDRAWAL)'
       BALANCE2 = 'PLACEMENT'
       ACCTNO   = 'ACCOUNT NO';
 TITLE1 'REPORT ID : EIBDFD02';
 TITLE2 'SALES ADMINISTRATION & SUPPORT';
 TITLE3 'PUBLIC BANK BERHAD';
 TITLE4 'DAILY FD MOVEMENT - PLACEMENT/WITHDRAWAL OF RM100K & ABOVE';
 TITLE5 'DATE : ' &RDATE;
 FORMAT PLAN PLANFMT. BALANCE1 NEGPAREN18.2 BALANCE2 COMMA18.2;
RUN;
*;
PROC PRINT DATA=BOTH LABEL;
 WHERE PLAN = 2 AND ACCTTYPE NOT IN (395,394,393);
 VAR NAME TERM BALANCE1 BALANCE2;
 ID BRANCH;
 BY PLAN BRCH ACCTNO;
 SUM BALANCE1 BALANCE2;
 LABEL BALANCE1  = '(WITHDRAWAL)'
       BALANCE2 = 'PLACEMENT'
       ACCTNO   = 'ACCOUNT NO';
 TITLE1 'REPORT ID : EIBDFD02';
 TITLE2 'SALES ADMINISTRATION & SUPPORT';
 TITLE3 'PUBLIC BANK BERHAD';
 TITLE4 'DAILY FD MOVEMENT - PLACEMENT/WITHDRAWAL OF RM100K & ABOVE';
 TITLE5 'DATE : ' &RDATE;
 FORMAT PLAN PLANFMT. BALANCE1 NEGPAREN18.2 BALANCE2 COMMA18.2;
RUN;
/*
//
