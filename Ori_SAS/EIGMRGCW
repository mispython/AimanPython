OPTIONS NOCENTER YEARCUTOFF=1950;

DATA _NULL_;
   REPTDATE=INPUT('01'||PUT(MONTH(TODAY()), Z2.)||
            PUT(YEAR(TODAY()), 4.), DDMMYY8.)-1;
   CALL SYMPUT('NDATE',REPTDATE);
RUN;

DATA GAY.REPTDATE;
   INFILE WALGAY OBS=1;
   INPUT @2 WALKDATE $8.;
   REPTDATE=INPUT(WALKDATE,DDMMYY8.);
   CALL SYMPUT('RDATE',REPTDATE);
RUN;

%MACRO CONVERT;
DATA RGGL4000 (KEEP=ACCT_NO ACKIND GLAMT)
     RGGL4100 (KEEP=ACCT_NO EFFDATE JEAMT ACKIND)
     RGKEY913 (KEEP=ACCT_NO)
     RGR115   (KEEP=SET_ID USERCD)
     RGR901   (KEEP=SEG_VAL SEG_DESC)
     RGR913   (KEEP=ACCT_NO SET_ID);
   RETAIN SETID;
   INFILE WALGAY;
   /* TEXT FILE AT COLUMN 2 = '1' SIGN DENOTES START OF A REPT MEMBER */
   /* TEXT FILE AT COLUMN 2 = '+' SIGN DENOTES RECORDS IN THE MEMBER  */
   INPUT @2 ID  $1. @;
   IF ID  = '1' THEN INPUT @3 SETID $8.;
   ELSE IF ID='+' THEN SELECT (SETID);
      WHEN ('RGGL4000') DO;
         INPUT @3 ACCT_NO $22. @29 ACKIND $1. @31 GLAMT COMMA21.2;
         OUTPUT RGGL4000;
      END;
      WHEN ('RGGL4100') DO;
         INPUT @3 ACCT_NO $22. @30 EFFDATE DDMMYY8.
         @48 JEAMT COMMA21.2   @75 ACKIND $1.;
         OUTPUT RGGL4100;
      END;
      WHEN ('RGKEY913') DO;
         INPUT @3 ACCT_NO $22.;
         OUTPUT RGKEY913;
      END;
      WHEN ('RGR115') DO;
         INPUT @3 SET_ID $12.
               @17 USERCD $4.;
         OUTPUT RGR115;
      END;
      WHEN ('RGR901') DO;
         INPUT @3 SEG_VAL $5. @11 SEG_DESC $40.;
         OUTPUT RGR901;
      END;
      WHEN ('RGR913') DO;
         INPUT @3 ACCT_NO $22. @28 SET_ID $12.;
         OUTPUT RGR913;
      END;
      OTHERWISE;
   END;
*;
TITLE 'RGGL4000';
PROC SORT DATA=RGGL4000 OUT=GAY.RGGL4000;
   BY ACCT_NO ACKIND GLAMT; RUN;
*;
TITLE 'RGGL4100';
PROC SORT DATA=RGGL4100 OUT=GAY.RGGL4100;
   BY ACCT_NO EFFDATE JEAMT ACKIND; RUN;
*;
TITLE 'RGR115';
PROC SORT DATA=RGR115 OUT=GAY.RGR115;
   BY SET_ID USERCD; RUN;
*;
TITLE 'RGR901';
PROC SORT DATA=RGR901 OUT=GAY.RGR901;
   BY SEG_VAL SEG_DESC; RUN;
*;
TITLE 'RGKEY913';
PROC SORT DATA=RGKEY913 OUT=GAY.RGKEY913;
   BY ACCT_NO; RUN;
*;
TITLE 'RGR913';
PROC SORT DATA=RGR913 OUT=GAY.RGR913;
   BY ACCT_NO SET_ID; RUN;
*;
PROC DATASETS LIB=WORK KILL NOLIST; RUN;
*;
%MEND CONVERT;

%MACRO PROCESS;
   %IF "&NDATE" = "&RDATE" %THEN %CONVERT;
   %ELSE %PUT WALKER SOURCE IS NOT AS AT CURRENT REPORTING DATE;
%MEND PROCESS;
%PROCESS;
