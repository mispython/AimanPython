//EIIMTOP5 JOB MIS,MISEIS,COND=(4,LT),CLASS=A,MSGCLASS=X,
//         NOTIFY=&SYSUID,USER=OPCC
/*JOBPARM S=S1M2
//PRINT1   OUTPUT CLASS=R,
//         NAME='MS CHAN KIM LIAN ',
//         ROOM='26TH FLOOR FINANCIAL ACCOUNTING',
//         BUILDING='MENARA PBB',
//         ADDRESS=('FINANCE DIVISION','MENARA PUBLIC BANK',
//         '146 JALAN AMPANG','50450 KUALA LUMPUR'),
//         DEST=S1.RMT5
//*
//DELETE   EXEC PGM=IEFBR14
//DD0      DD DISP=(MOD,DELETE,DELETE),
//            SPACE=(TRK,(1,10)),
//            DSN=SAP.PIBB.SUBTOP50.TEXT
//DD1      DD DISP=(MOD,DELETE,DELETE),
//            SPACE=(TRK,(1,10)),
//            DSN=SAP.PIBB.INDTOP50.TEXT
//DD2      DD DISP=(MOD,DELETE,DELETE),
//            SPACE=(TRK,(1,10)),
//            DSN=SAP.PIBB.CORTOP50.TEXT
//*
//EIIMTOP5 EXEC SAS609
//PGM      DD DSN=SAP.BNM.PROGRAM,DISP=SHR
//DEPOSIT  DD DSN=SAP.PIBB.MNITB(0),DISP=SHR
//FD       DD DSN=SAP.PIBB.MNIFD(0),DISP=SHR
//LOAN     DD DSN=SAP.PIBB.MNILN(0),DISP=SHR
//CISCA    DD DSN=SAP.PBB.CISBEXT.DP,DISP=SHR
//CISFD    DD DSN=SAP.PBB.CRM.CISBEXT,DISP=SHR
//BNM      DD DSN=&&TEMP,DISP=(NEW,DELETE,DELETE),
//            DCB=(RECFM=FS,LRECL=27648,BLKSIZE=27648),
//            SPACE=(CYL,(600,300)),UNIT=(SYSDA,5)
//BNM1     DD DSN=SAP.PBB.SASDATA,DISP=SHR
//FD11TEXT  DD DSN=SAP.PIBB.INDTOP50.TEXT,
//            DISP=(NEW,CATLG,DELETE),
//            DCB=(RECFM=FB,LRECL=320,BLKSIZE=0),
//            SPACE=(CYL,(50,50),RLSE),UNIT=(SYSDA,5)
//FD12TEXT  DD DSN=SAP.PIBB.CORTOP50.TEXT,
//            DISP=(NEW,CATLG,DELETE),
//            DCB=(RECFM=FB,LRECL=320,BLKSIZE=0),
//            SPACE=(CYL,(50,50),RLSE),UNIT=(SYSDA,5)
//FDSTEXT  DD DSN=SAP.PIBB.SUBTOP50.TEXT,
//            DISP=(NEW,CATLG,DELETE),
//            DCB=(RECFM=FB,LRECL=320,BLKSIZE=0),
//            SPACE=(CYL,(50,50),RLSE),UNIT=(SYSDA,5)
//SASLIST  DD SYSOUT=*
//SYSIN     DD *

OPTIONS SORTDEV=3390 YEARCUTOFF=1950;
*;
DATA BNM.REPTDATE;
   SET DEPOSIT.REPTDATE;
   SELECT(DAY(REPTDATE));
      WHEN (8)  CALL SYMPUT('NOWK',PUT('1',$1.));
      WHEN (15) CALL SYMPUT('NOWK',PUT('2',$1.));
      WHEN (22) CALL SYMPUT('NOWK',PUT('3',$1.));
      OTHERWISE CALL SYMPUT('NOWK',PUT('4',$1.));
   END;
   CALL SYMPUT('REPTYEAR',PUT(REPTDATE,YEAR4.));
   CALL SYMPUT('REPTMON',PUT(MONTH(REPTDATE),Z2.));
   CALL SYMPUT('REPTDAY',PUT(DAY(REPTDATE),Z2.));
   CALL SYMPUT('RDATE',PUT(REPTDATE,DDMMYY8.));
RUN;
*;
DATA CISCA CISFD;
   KEEP CUSTNO ACCTNO CUSTNAME ICNO NEWIC OLDIC INDORG;
   SET CISCA.DEPOSIT
       CISFD.DEPOSIT;
   IF NEWIC NE '' THEN ICNO = NEWIC;
                  ELSE ICNO = OLDIC;
   IF (3000000000<=ACCTNO<=3999999999) THEN OUTPUT CISCA;
   IF (1000000000<=ACCTNO<=1999999999) THEN OUTPUT CISFD;
   IF (7000000000<=ACCTNO<=7999999999) THEN OUTPUT CISFD;
RUN;

DATA CA FD;
   SET DEPOSIT.CURRENT
       DEPOSIT.FD;
   IF CURBAL > 0;
   IF (3000000000<=ACCTNO<=3999999999) THEN OUTPUT CA;
   IF (1000000000<=ACCTNO<=1999999999) THEN OUTPUT FD;
   IF (7000000000<=ACCTNO<=7999999999) THEN OUTPUT FD;
RUN;
*;
PROC SORT DATA=CISCA; BY ACCTNO; RUN;
PROC SORT DATA=CA;    BY ACCTNO; RUN;
DATA CAIND
     CAORG;
   MERGE CA(IN=A) CISCA; BY ACCTNO;
   IF A AND PURPOSE NE '2' AND
      PRODUCT NOT IN (400,401,402,403,404,405,406,407,408,409,410);
   CABAL = CURBAL;
   IF CUSTCODE IN (77,78,95,96) THEN OUTPUT CAIND; ELSE
   IF INDORG = 'O'              THEN OUTPUT CAORG;
RUN;

PROC SORT DATA=CISFD; BY ACCTNO; RUN;
PROC SORT DATA=FD; BY ACCTNO; RUN;
DATA FDIND
     FDORG;
   MERGE CISFD FD(IN=A); BY ACCTNO;
   IF A AND PURPOSE NE '2' AND
      PRODUCT NOT IN (350,351,352,353,354,355,356,357);
   FDBAL = CURBAL;
   IF CUSTCODE IN (77,78,95,96) THEN OUTPUT FDIND; ELSE
   IF INDORG = 'O'              THEN OUTPUT FDORG;
RUN;

OPTIONS NOCENTER NODATE NONUMBER MISSING=0;
TITLE1 'PUBLIC ISLAMIC BANK BERHAD (EIIMBTOP5)';
TITLE2 'NEW LIQUIDITY FRAMEWORK AS AT' &RDATE;
*;
%MACRO PRNREC;
   PROC SORT DATA=DATA1; WHERE ICNO NE ''; BY ICNO CUSTNAME; RUN;
   PROC SUMMARY DATA=DATA1;
      BY ICNO CUSTNAME;
      VAR CURBAL FDBAL CABAL;
      OUTPUT OUT=DATA2 (DROP=_TYPE_ _FREQ_) SUM=;
   RUN;

   PROC SORT DATA=DATA2; BY DESCENDING CURBAL; RUN;
   DATA DATA2;
      SET DATA2(OBS=50);
   RUN;

   PROC PRINT DATA=DATA2 SPLIT='*';
      FORMAT  CURBAL FDBAL CABAL COMMA16.2;
      LABEL CUSTNAME   = 'DEPOSITOR'
            CURBAL     = 'TOTAL BALANCE'
            FDBAL      = 'FD BALANCE'
            CABAL      = 'CA BALANCE';
      VAR CUSTNAME CURBAL FDBAL CABAL;
   RUN;

   PROC SORT DATA=DATA2 OUT=DATA2(KEEP=ICNO CUSTNAME CUSTNO);
      BY ICNO CUSTNAME; RUN;
   DATA DATA3;
      MERGE DATA1(IN=A) DATA2(IN=B); BY ICNO CUSTNAME;
      IF A AND B;
   RUN;
   PROC SORT DATA=DATA3; BY ICNO CUSTNAME; RUN;
   PROC PRINT DATA=DATA3 SPLIT='*';
      FORMAT  CURBAL COMMA16.2;
      BY ICNO CUSTNAME;
      LABEL BRANCH     = 'BRANCH*CODE'
            ACCTNO     = 'MNI NO'
            CUSTNAME   = 'DEPOSITOR'
            CUSTNO     = 'CIS NO'
            NEWIC      = 'NEW IC'
            OLDIC      = 'OLD IC'
            CURBAL     = 'CURRENT*BALANCE'
            PRODUCT    = 'PRODUCT'
            COSTCTR    = 'COST*CENTRE';
      VAR BRANCH ACCTNO CUSTNAME CUSTNO NEWIC OLDIC CURBAL
          PRODUCT COSTCTR;
      SUM CURBAL;
   RUN;
%MEND;

*** FD+CA INDIVIDUAL CUSTOMERS ***;
DATA DATA1;
   SET FDIND CAIND;
   IF ICNO='  ' THEN ICNO='XX';
RUN;
PROC PRINTTO PRINT=FD11TEXT NEW;
TITLE 'TOP 50 LARGEST FD+CA INDIVIDUAL CUSTOMERS AS AT ' "&RDATE";
%PRNREC;

*** FD+CA CORPORATE CUSTOMERS ***;
DATA DATA1;
   SET FDORG CAORG;
   IF ICNO='  ' THEN ICNO='XX';
RUN;
PROC PRINTTO PRINT=FD12TEXT NEW;
TITLE 'TOP 50 LARGEST FD+CA CORPORATE CUSTOMERS AS AT ' "&RDATE";
%PRNREC;
*;
DATA DATA1;
   SET FDORG CAORG;
   IF  CUSTNO IN (53227,169990,170108,3562038,3721354);
RUN;
PROC SORT; BY CUSTNO ACCTNO;
PROC PRINTTO PRINT=FDSTEXT NEW;
TITLE 'PB SUBSIDIARIES UNDER TOP 50 CORP DEPOSITORS @ ' "&RDATE";
PROC PRINT DATA=DATA1 SPLIT='*';
     FORMAT  CURBAL COMMA16.2;
     BY CUSTNO;
     LABEL BRANCH     = 'BRANCH*CODE'
           ACCTNO     = 'MNI NO'
           CUSTNAME   = 'DEPOSITOR'
           CUSTNO     = 'CIS NO'
           CUSTCODE   = 'CUSTCD'
           CURBAL     = 'CURRENT*BALANCE'
           PRODUCT    = 'PRODUCT'
           COSTCTR    = 'COST*CENTRE';
      VAR BRANCH ACCTNO CUSTNAME CUSTNO CUSTCODE CURBAL
          PRODUCT COSTCTR;
      SUM CURBAL;
RUN;
