OPTIONS NOCENTER YEARCUTOFF=1950 LS=132 MISSING=0;

%INC PGM(PBBLNFMT);
  /* ************************************************ */
  /* PBB - ACCUMULATE YTD AVERAGE BALANCE (DAILY)     */
  /* ************************************************ */

PROC FORMAT;
   VALUE SLNPRDF
      110-116                      = 'ISLAMIC HOUSING'
      120,127,129,137,138,193      = 'ISLAMIC TERM'
      135,136                      = 'ISLAMIC PERSONAL'
      194,195,196                  = 'ISLAMIC CON. DURABLE'
      170                          = 'ISLAMIC FUND FOR FOOD'
      OTHER                        = 'OTHERS';

DATA REPTDATE (KEEP=REPTDATE);
  SET LOAN.REPTDATE;
  SELECT(DAY(REPTDATE));
    WHEN (8)  DO; SDD = 1;  WK = '1'; WK1 = '4'; END;
    WHEN(15)  DO; SDD = 9;  WK = '2'; WK1 = '1'; END;
    WHEN(22)  DO; SDD = 16; WK = '3'; WK1 = '2'; END;
    OTHERWISE DO; SDD = 23; WK = '4'; WK1 = '3'; END;
  END;
  MM = MONTH(REPTDATE);
  IF WK = '1' THEN DO;
     MM1 = MM - 1;
     IF MM1 = 0 THEN MM1 = 12;
  END;
  ELSE MM1 = MM;
  SDATE = MDY(MM,SDD,YEAR(REPTDATE));
  CALL SYMPUT('NOWK',PUT(WK,$1.));
  CALL SYMPUT('NOWK1',PUT(WK1,$1.));
  CALL SYMPUT('REPTMON',PUT(MM,Z2.));
  CALL SYMPUT('REPTMON1',PUT(MM1,Z2.));
  CALL SYMPUT('REPTYEAR',PUT(REPTDATE,YEAR4.));
  CALL SYMPUT('REPTDAY',PUT(DAY(REPTDATE),Z2.));
  CALL SYMPUT('REPTDATE', REPTDATE);
  CALL SYMPUT('RDATE',PUT(REPTDATE,DDMMYY8.));
RUN;
DATA IIS;
   SET NPL.TOTIIS&REPTMON;
   PRODTYPE = PUT(LOANTYPE,SLNPRDF.);
   IF PRODTYPE = 'OTHERS' THEN DELETE;
RUN;
DATA LOAN;
   SET SASDATA.LOAN&REPTMON&NOWK;
   IF PRODCD NE 'N' AND AMTIND = 'I' AND ISSDTE < '04SEP04'D AND
   ACCTNO < 8000000000;
   IF ACCTYPE EQ 'LN' THEN PRODTYPE = PUT(PRODUCT,SLNPRDF.);
   IF PRODCD = '34180' & AMTIND='I' THEN DO;
      PRODTYPE = 'ISLAMIC OD';
      CURBAL = (-1)*CURBAL;
   END;
   IF PRODUCT = 193 THEN CURBAL = SUM(CURBAL,ACCRUAL);
   IF PRODTYPE = '  ' THEN PRODTYPE = PRODUCT;
   IF RISKRTE IN (1,2,3,4) THEN NPLBAL = BALANCE;

RUN;
PROC SORT DATA=IIS OUT=IIS(KEEP=ACCTNO NOTENO TOTIIS PRODTYPE);
   BY ACCTNO NOTENO;
RUN;
PROC SORT DATA=LOAN OUT=LOAN(KEEP=ACCTNO NOTENO CURBAL PRODTYPE NPLBAL);
   BY ACCTNO NOTENO;
RUN;
DATA LOAN;
   MERGE LOAN(IN=A) IIS;
   BY ACCTNO NOTENO;
   IF A;
RUN;
TITLE1 'TOTAL INTEREST IN SUSPENSE TAGGED WITH LOANS AS AT ' &RDATE;
PROC TABULATE DATA=LOAN MISSING NOSEPS;
   CLASS PRODTYPE;
   VAR CURBAL TOTIIS NPLBAL;
   TABLE (PRODTYPE=' '),
         SUM=' '*(CURBAL='BALANCE'*F=COMMA17.2
          TOTIIS='IIS'*F=COMMA17.2
          NPLBAL='NPL'*F=COMMA17.2)
         / BOX='PRODUCT TYPE' RTS=25;
RUN;
DATA ISLM&REPTMON;
   SET CRM.ISLM&REPTMON;
   PRODTYPE = PUT(LOANTYPE,SLNPRDF.);
   IF PRODTYPE = 'OTHERS' THEN DELETE;
   IF '03SEP04'D < REPTDATE <= &REPTDATE;
RUN;
PROC SORT DATA=ISLM&REPTMON NODUPKEYS; BY LOANTYPE REPTDATE;
PROC SORT DATA=ISLM&REPTMON; BY PRODTYPE REPTDATE;
*;
PROC SUMMARY DATA=ISLM&REPTMON;
   BY PRODTYPE REPTDATE;
   VAR CURBAL YIELD;
OUTPUT OUT=ISLM1&REPTMON SUM= ;

DATA ISLM1&REPTMON;
   SET ISLM1&REPTMON;
   RATE = YIELD / CURBAL;
   DAY = DAY(REPTDATE);
RUN;
TITLE1 'PBB ISLAMIC LOANS PRIOR TO PRIVATISATION AS AT ' &RDATE;
PROC TABULATE DATA=ISLM1&REPTMON MISSING NOSEPS;
   * FORMAT REPTDATE DDMMYY8.;
   CLASS PRODTYPE DAY;
   VAR CURBAL RATE;
   TABLE (PRODTYPE=' '),
         (DAY=' '),
         SUM=' '*(CURBAL='O/S BALANCE (RM)'*F=COMMA13.2
          RATE*F=4.2)
         / BOX='DAY' RTS=10;
RUN;
PROC SUMMARY DATA=ISLM&REPTMON;
   BY PRODTYPE;
   VAR CURBAL YIELD;
OUTPUT OUT=ISLM1&REPTMON SUM= ;
DATA ISLM1&REPTMON;
   SET ISLM1&REPTMON;
   RATE = YIELD / CURBAL;
   DAY = DAY(&REPTDATE);
   AVGAMT = CURBAL / DAY;
RUN;
PROC PRINT DATA=ISLM1&REPTMON LABEL;
VAR PRODTYPE AVGAMT RATE;
LABEL PRODTYPE = 'PRODUCT TYPE'
        AVGAMT = 'AVERAGE BALANCE FOR THE MONTH'
         RATE  = 'WEIGHTED AVERAGE RATE';
RUN;
