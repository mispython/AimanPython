OPTIONS MISSING=0 LINESIZE=200 PAGESIZE=100;
%INC PGM(PBBELF);
DATA REPTDATE;
   SET LOAN.REPTDATE;
   SELECT;
   WHEN(1<= DAY(REPTDATE)<= 8) DO;
        WK = '1';
     END;
     WHEN(9<= DAY(REPTDATE)<= 15) DO;
        WK = '2';
     END;
     WHEN(16<= DAY(REPTDATE)<= 22) DO;
        WK = '3';
     END;
     OTHERWISE DO;
        WK = '4';
     END;
   END;

   YEAREND=MDY(1,1,YEAR(REPTDATE))-1;

   PREVMON=MDY(MONTH(REPTDATE),1,YEAR(REPTDATE))-1;
   CALL SYMPUT('REPTMON',PUT(MONTH(REPTDATE),Z2.));
   CALL SYMPUT('NOWK',PUT(WK,$1.));
   CALL SYMPUT('REPTYEAR',PUT(REPTDATE,YEAR2.));
   CALL SYMPUT('REPTDAY',PUT(DAY(REPTDATE),Z2.));
   CALL SYMPUT('REPTMON1',PUT(MONTH(PREVMON),Z2.));
   CALL SYMPUT('REPTYEAR1',PUT(PREVMON,YEAR2.));
   CALL SYMPUT('REPTDATE',REPTDATE);
   CALL SYMPUT('LMON',PUT(MONTH(YEAREND),Z2.));
   CALL SYMPUT('LYEAR',PUT(YEAREND,YEAR2.));
RUN;

DATA REPTDATE;
   SET REPTDATE;
   DATE=COMPRESS(&REPTDAY||'/'||&REPTMON||'/'||&REPTYEAR);
   CALL SYMPUT('DATE',DATE);
RUN;

DATA CREDSUB(KEEP=ACCTNUM DAYSARR NOTENO
             RENAME=(ACCTNUM=ACCTNO DAYSARR=DAYARR));
   SET CCRIS.CREDMSUBAC&REPTMON&REPTYEAR;
   WHERE FACILITY IN ('34331','34332','34371');
RUN;

DATA HP;
   FORMAT IND $4.;
   SET LN.LN&REPTMON&NOWK&REPTYEAR;
   IF BALANCE >0;
   IF COSTCTR = 8044 THEN BRANCH=902;
   ELSE IF COSTCTR = 8048 THEN BRANCH = 903;
   IND='PBB';
   BRANCHABBR=PUT(BRANCH,BRCHCD.);
   IF PRODUCT IN (15,20,71,72) AND (DAYARR >= 90
   OR BORSTAT IN ('F','I','R') OR USER5='N') THEN OUTPUT;
RUN;

PROC SORT DATA=CREDSUB;BY ACCTNO NOTENO DESCENDING DAYARR;RUN;
PROC SORT DATA=CREDSUB NODUPKEY;BY ACCTNO NOTENO;RUN;
PROC SORT DATA=HP;BY ACCTNO NOTENO;RUN;

DATA HP;
   MERGE HP(IN=A) CREDSUB(IN=B);BY ACCTNO NOTENO;
   IF A;
RUN;

DATA PBB(KEEP=ACCTNO NOTENO BRANCH BRANCHABBR PRODUCT
   CATEGORY IND BALANCE AANO PAIDIND MARKETVL);
   FORMAT CATEGORY $25.;
   SET HP;
   AANO = SUBSTR(LEFT(VINNO),1,13);
      IF DAYARR<=30 AND BORSTAT NOT IN ('F','I','R','E','W','Z')
         AND USER5 NOT IN ('N') AND PAIDIND='M'
      THEN DO;
   CATEGORY='CURRENT';
   END;
   IF 31<=DAYARR<=89 AND BORSTAT NOT IN ('F','I','R','E','W','Z')
             AND USER5 NOT IN ('N') AND PAIDIND='M'
      THEN DO;
         CATEGORY='1-2 MTHS';
      END;
   IF BORSTAT NOT IN ('F','I','R','E','W','Z') AND ((USER5 IN ('N')
             AND DAYARR<=182) OR 90<=DAYARR<=182) AND PAIDIND='M'
      THEN DO;
         CATEGORY='3-5 MTHS';
   END;
   IF BORSTAT NOT IN ('F','I','R','E','W','Z') AND ((USER5 IN ('N')
         AND 183<=DAYARR<=364) OR 183<=DAYARR<=364) AND PAIDIND='M'
      THEN DO;
         CATEGORY='6-11 MTHS';
   END;
   IF BORSTAT NOT IN ('F','I','R','E','W','Z') AND ((USER5 IN ('N')
         AND DAYARR>=365) OR DAYARR>=365) AND PAIDIND='M'
      THEN DO;
         CATEGORY='>=12 MTHS';
   END;
   IF BORSTAT='I' AND PAIDIND='M' THEN DO;
         CATEGORY='IRREGULAR';
   END;
   IF BORSTAT='R' AND PAIDIND='M' AND DAYARR<365 THEN DO;
         CATEGORY='REPOSSESSED <12 MTHS';
   END;
   IF BORSTAT='R' AND PAIDIND='M' AND DAYARR>=365 THEN DO;
         CATEGORY='REPOSSESSED >=12 MTHS';
   END;
   IF BORSTAT='F' AND PAIDIND='M' THEN DO;
         CATEGORY='DEFICIT';
   END;
   IF CATEGORY NE ' ';
RUN;

/* PBB */
PROC SUMMARY DATA=PBB NWAY;
CLASS CATEGORY;VAR BALANCE;
OUTPUT OUT=NPL.SUMBAL_STAFF(KEEP=CATEGORY BALANCE) SUM=;
RUN;

PROC SORT DATA=NPL.CAP1_STAFF;BY CATEGORY;RUN;
PROC SORT DATA=NPL.SUMBAL_STAFF;BY CATEGORY;RUN;

DATA COUNTCAP(KEEP=CATEGORY CARATE PD LGD);
   MERGE NPL.CAP1_STAFF NPL.SUMBAL_STAFF;BY CATEGORY;
   CARATE=(CAPROVISION/BALANCE)*100;
   IF CATEGORY IN ('>=12 MTHS','IRREGULAR','DEFICIT',
                   'REPOSSESSED >=12 MTHS') THEN CARATE=100;
RUN;

PROC SORT DATA=PBB;BY CATEGORY;RUN;
PROC SORT DATA=COUNTCAP;BY CATEGORY;RUN;

DATA NPL.CAP_STAFF&REPTMON&REPTYEAR;
   FORMAT CAP 20.2;
   MERGE PBB(IN=A) COUNTCAP(IN=B);BY CATEGORY;
   IF A;
   CAP=(BALANCE*CARATE)/100;
RUN;

/* GET OPENING BALANCE */
DATA OPENBAL(KEEP=ACCTNO NOTENO CAP CATEGORY PRODUCT BRANCH
              BRANCHABBR RENAME=(CAP=OPEN_BALANCE
              CATEGORY=CATEGORY1));
   SET NPL.CAP_STAFF&LMON&LYEAR(DROP=CATEGORY1);
   IF CATEGORY NE ' ';
   IF CATEGORY='>=6 MTHS' THEN CATEGORY='6-11 MTHS';
   *IF IND='PBB' THEN OUTPUT OPENBAL;
RUN;

PROC SORT DATA=OPENBAL;BY ACCTNO NOTENO;RUN;
PROC SORT DATA=NPL.CAP_STAFF&REPTMON&REPTYEAR;
BY ACCTNO NOTENO;RUN;

DATA INDVHP;
   MERGE OPENBAL(IN=B)
         NPL.CAP_STAFF&REPTMON&REPTYEAR(IN=A);
         BY ACCTNO NOTENO;
   IF B AND NOT A THEN STATUS='P';
   IF A AND NOT B THEN STATUS='C';
   IF STATUS='P' THEN CATEGORY=CATEGORY1;
   IF PRODUCT IN (983,993) THEN CAP=0;
RUN;

DATA NPL.CAP_STAFF&REPTMON&REPTYEAR;
   SET INDVHP;
   IF CAP=. THEN CAP=0;
   IF OPEN_BALANCE=. THEN OPEN_BALANCE=0;
   CHARCAP=CAP-OPEN_BALANCE;

   IF STATUS='P' THEN DO;
      IF CAP<OPEN_BALANCE THEN DO;
         SUSPEND=0;
      WRBACK=CAP-OPEN_BALANCE;
   END;
   END;

   IF STATUS='C' THEN DO;
      SUSPEND=CAP;
   WRBACK=CAP-SUSPEND;
   END;

   IF STATUS NOT IN ('P','C') THEN DO;
      IF CHARCAP<0 THEN DO;
      WRBACK=CHARCAP;
   END;
   ELSE DO;
      SUSPEND=CHARCAP;
   END;
   END;

   IF SUSPEND=. THEN SUSPEND=0;
   IF WRBACK=. THEN WRBACK=0;
   WRBACK=WRBACK*-1;
   NET=SUSPEND-WRBACK;
   WRIOFF_BAL=0;
   BRANCH1 = BRANCHABBR||' '||PUT(BRANCH,Z3.);

RUN;

PROC SORT DATA=NPL.WMIS OUT=WOFF;BY ACCTNO;RUN;
PROC SORT DATA=NPL.CAP_STAFF&REPTMON&REPTYEAR;BY ACCTNO;RUN;

DATA WOFF;
   SET WOFF;
   IF CATEGORY='>=6 MTHS' THEN CATEGORY='6-11 MTHS';
RUN;

DATA NPL.CAP_STAFF&REPTMON&REPTYEAR;
   MERGE NPL.CAP_STAFF&REPTMON&REPTYEAR(IN=A)
         WOFF(IN=B);
   BY ACCTNO;
   IF B THEN WRITEOFF = 'Y';ELSE WRITEOFF = 'N';
   IF A;
RUN;

DATA NPL.CAP_STAFF&REPTMON&REPTYEAR;
   SET NPL.CAP_STAFF&REPTMON&REPTYEAR;
      IF STATUS='P' AND WRITEOFF = 'Y' THEN DO;
         SUSPEND=WRIOFF_BAL-OPEN_BALANCE;
         IF SUSPEND<0 THEN DO;
            WRBACK=OPEN_BALANCE-WRIOFF_BAL;
            SUSPEND=0;
         END;
         ELSE DO;
            WRBACK=0;
   END;
      END;
   NET=SUSPEND-WRBACK;
RUN;

DATA WOF(KEEP=ACCTNO NOTENO PRODUCT);
   SET HP.HPWO&REPTMON&NOWK&REPTYEAR;
RUN;

PROC SORT DATA=WOF;BY ACCTNO NOTENO;RUN;
PROC SORT DATA=NPL.CAP_STAFF&REPTMON&REPTYEAR;BY ACCTNO NOTENO;RUN;

DATA NPL.CAP_STAFF&REPTMON&REPTYEAR;
   MERGE NPL.CAP_STAFF&REPTMON&REPTYEAR(IN=A) WOF(IN=B);
   BY ACCTNO NOTENO;IF A;
RUN;

* GENERATE SUMMARY REPORT *;

%LET TBL1=PBB(STAFF) MOVEMENT OF CAP BY BRANCH AS AT;

PROC TABULATE DATA=NPL.CAP_STAFF&REPTMON&REPTYEAR
FORMAT=COMMA20.2
MISSING NOSEPS FORMCHAR(1,2,3,4,5,6,7,8,9,10,11)='|-+++++++++';
CLASS BRANCH1 BRANCHABBR;
VAR BALANCE OPEN_BALANCE SUSPEND WRBACK WRIOFF_BAL CAP NET;
TABLE BRANCH1=' ' ALL='TOTAL',N='NO OF ACCOUNT'*F=COMMA7.
   SUM=' '*(BALANCE='BALANCE'
            OPEN_BALANCE='OPENING BALANCE'
            SUSPEND='CHARGE FOR THE YEAR'
            WRBACK='WRITTEN BACK TO P & L'
            WRIOFF_BAL='WRITTEN-OFF'
            CAP='CLOSING BALANCE'
            NET='NET INCREASE/DECREASE')/ BOX='BRANCH' RTS=15;
   TITLE1 &TBL1 &DATE;
RUN;

