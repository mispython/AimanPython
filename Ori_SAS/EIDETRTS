//EIDETRTS JOB MSGCLASS=X,MSGLEVEL=(1,1),NOTIFY=&SYSUID                 JOB25178
/*JOBPARM S=S1M1
//******************************************************************
//* EXTRACT REMITTANCE TRANSACTION IFS FOR DETICA
//******************************************************************
//DELETE   EXEC PGM=IEFBR14
//DEL01    DD DSN=SAP.AML.DETICA.REMTRAN.RENTAS.TEXT.BKP,
//            DISP=(MOD,DELETE,DELETE),
//            UNIT=SYSDA,SPACE=(TRK,(0))
//EIDETRTS EXEC SAS609
//LN       DD DSN=SAP.PBB.MNILN(0),DISP=SHR
//ILN      DD DSN=SAP.PIBB.MNILN(0),DISP=SHR
//DP       DD DSN=SAP.PBB.MNITB(0),DISP=SHR
//IDP      DD DSN=SAP.PIBB.MNITB(0),DISP=SHR
//BT       DD DSN=SAP.PBB.BTRADE.SASDATA,DISP=SHR
//IBT      DD DSN=SAP.PIBB.BTRADE.SASDATA,DISP=SHR
//CIS      DD DSN=RBP2.B033.CIS.CUST.DAILY,DISP=SHR
//PGM      DD DSN=SAP.BNM.PROGRAM,DISP=SHR
//RENTAS   DD DSN=SAP.PBB.RENTASWH,DISP=SHR
//TRAN     DD DSN=SAP.AML.DETICA.REMTRAN.RENTAS,DISP=OLD
//RENTRAN  DD DSN=SAP.AML.DETICA.REMTRAN.RENTAS.TEXT,DISP=OLD
//SYSIN    DD *

%INC PGM(PBBELF);

 *2017-2058;
%LET LIST = ('PUBLIC BANK BHD COLOMBO BRANCH',
             'PUBLIC BANK VIETNAM LIMITED',
             'CAMBODIAN PUBLIC BANK PLC',
             'PUBLIC BANK VIENTIANE BR',
             'PB CARD SERVICES AC 1',
             'FIN DIV-BC NORM AC',
             'IBG COLLECTION ACCOUNT',
             'PUBLIC BANK (L) LTD',
             'PUBLIC MUTUAL BERHAD',
             'PB TRUSTEE SERVICES BERHAD',
             'PUBLIC BANK (HONG KONG) LIMITED',
             'AMANAHRAYA TRUSTEES BERHAD',
             'PUBLIC BANK BHD FOR COLLECTION A/C',
             'AKAUNTAN NEGARA MALAYSIA',
             'PUBLIC BANK',
             'PUBLIC BANK BERHAD',
             'PUBLIC BANK BHD',
             'PBB',
             'PUBLIC ISLAMIC BANK',
             'PUBLIC ISLAMIC BANK BERHAD',
             'PUBLIC ISLAMIC BANK BHD',
             'PIBB');

DATA REPTDATE;
   SET DP.REPTDATE;
   SELECT(DAY(REPTDATE));
     WHEN (8)  DO; WK = '1'; WK1 = '4'; END;
     WHEN(15)  DO; WK = '2'; WK1 = '1'; END;
     WHEN(22)  DO; WK = '3'; WK1 = '2'; END;
     OTHERWISE DO; WK = '4'; WK1 = '3'; END;
   END;
   MM=MONTH(REPTDATE);
   CALL SYMPUT('REPTYEAR', PUT(REPTDATE, YEAR2.));
   CALL SYMPUT('REPTMON', PUT(MM, Z2.));
   CALL SYMPUT('REPTDAY', PUT(DAY(REPTDATE), Z2.));
   CALL SYMPUT('MM', PUT(MM, Z2.));
   CALL SYMPUT('NOWK',PUT(WK,$1.));
   CALL SYMPUT('RDATE', PUT(REPTDATE, YYMMDDN8.));
   CALL SYMPUT('YYYY',PUT(REPTDATE,YEAR4.));
RUN;

DATA INWARD OUTWARD BT;
   SET RENTAS.RENTAS&REPTMON&NOWK&REPTYEAR;
   FORMAT SOURCE_TXN_UNIQUE_ID $32.;
   FORMAT RUN_TIMESTAMP $14.;
   FORMAT LOCAL_TIMESTAMP $14.;
   FORMAT TRANS_REF_DESC $255.;
   FORMAT ORIGINATOR_NAME $1000.;
   FORMAT BENEFICIARY_NAME $1000.;
   FORMAT ORG_UNIT_CODE $10.;
   RUN_TIMESTAMP = COMPRESS("&RDATE"||'000000');
   SOURCE_TXN_UNIQUE_ID = COMPRESS('RM'||BANKNO||ISTTYPE||
                                   VALUEDTE||TRANSREF||UMRNO);
   BRANCH_ID = PUT(BRANCHABB,$BRCHRVR.);
   CURCODE = 'MYR';
   CURBASE = 'MYR';
   ORIGINATION_DATE = VALUEDTE;
   YYYY = SUBSTR(TTIMESTAMP,1,4);
   MM   = SUBSTR(TTIMESTAMP,6,2);
   DD   = SUBSTR(TTIMESTAMP,9,2);
   HOUR = SUBSTR(TTIMESTAMP,12,2);
   MIN  = SUBSTR(TTIMESTAMP,15,2);
   SEC  = SUBSTR(TTIMESTAMP,18,2);
   LOCAL_TIMESTAMP = COMPRESS(YYYY||MM||DD||HOUR||MIN||SEC);
   YYYY = SUBSTR(PTIMESTAMP,1,4);
   MM   = SUBSTR(PTIMESTAMP,6,2);
   DD   = SUBSTR(PTIMESTAMP,9,2);
   IF YYYY = '0101' THEN POSTING_DATE = SUBSTR(LOCAL_TIMESTAMP,1,8);
   ELSE POSTING_DATE = COMPRESS(YYYY||MM||DD);
   TXN_AMOUNT_ORIG = AMOUNT;
   TRANS_REF_DESC = CATX(' ',PAYMODE,TRANSREF);
   MENTION = PAYMODE;
   TXN_STATUS_CODE = STATUS;
   CHANNEL = 999;
   ORIGINATOR_NAME = CATX(',',APPLNAME,APPLNAME2);
   BENEFICIARY_NAME = CATX(',',BENENAME,BENENAME2);
   TELLER_ID = USERID;
   ORIGINATOR_ID = APPLID;
   BENEFICIARY_ID = TRACKCODE;
   REMITTANCE_REF_NO = TRANSREF;
   INCOMING_OUTGOING_TRANS = STATUS;
   EMPLOYEE_ID = 88888;

   *2017-2058;
   IF UPCASE(APPLNAME) IN &LIST
      OR UPCASE(BENENAME) IN &LIST THEN DELETE;

   IF BRANCHABB IN ('701','702','IKB','IPJ') THEN
      ORG_UNIT_CODE = 'PIBBTRSRY';
   ELSE ORG_UNIT_CODE = 'PBBTRSRY';
   IF ISTTYPE = 'RI' AND STATUS = 'TI' THEN OUTPUT INWARD;
   ELSE IF ISTTYPE = 'RO' AND STATUS = 'TO' THEN OUTPUT OUTWARD;
   ELSE IF ISTTYPE = 'BT' AND STATUS = 'TO' THEN OUTPUT BT;
RUN;

* OBTAIN ACCOUNT NUMBER FROM BENE / APPL ACCTNO;
DATA INWARD1 LOANS_INWARD INWARD2;
   SET INWARD;
   FORMAT BENE_BRANCH SENDER_BRANCH $4.;
   TXN_CODE='RMT001';
   CRDR = 'C';
   BENEFICIARY_BANK = BRANCHABB;
   INCOMING_OUTGOING_FLG = 'I';
   BENE_BRANCH = BRANCHABB;
   SENDER_BRANCH = '0';
   COMPRESS_BENE=COMPRESS(BENEACCTNO,",:.()-/ ",'A');
   COMPRESS_BENE = IFC(COMPRESS_BENE=:'0',
        SUBSTR(COMPRESS_BENE,VERIFY(COMPRESS_BENE,'0')),COMPRESS_BENE);
   BENE_AC_LEN=LENGTH(COMPRESS_BENE);
   TEMP_BENE_AC=SUBSTR(COMPRESS_BENE,1,10);
   IF TEMP_BENE_AC = COMPRESS(ACCTNO) THEN DO;
      FIRST_DIGIT=SUBSTR(TEMP_BENE_AC,1,1);
      IF FIRST_DIGIT IN ('2','8') THEN TEMP_ACCTCODE = 'LN';
      ELSE TEMP_ACCTCODE = 'DP';
      ACCOUNT_SOURCE_UNIQUE_ID=COMPRESS(TEMP_ACCTCODE||TEMP_BENE_AC);
      OUTPUT INWARD1;
   END;
   ELSE IF TEMP_BENE_AC ^= COMPRESS(ACCTNO) THEN DO;
      IF BENE_AC_LEN = 19 OR (BENE_AC_LEN = 15 AND
         SUBSTR(BENEACCTNO,1,1) = '2') THEN DO;
      *LOANS WITH NOTENO AND TRAILING 0000 OR 0001;
         IF SUBSTR(COMPRESS_BENE,5,1) IN ('2','8') THEN DO;
            ACCOUNT_SOURCE_UNIQUE_ID=COMPRESS('LN'||
                                     SUBSTR(COMPRESS_BENE,5,15));
         OUTPUT LOANS_INWARD;
         END;
         ELSE OUTPUT INWARD2;
      END;
      ELSE OUTPUT INWARD2; * REQUIRE FURTHER CHECKING;
   END;
RUN;

DATA INWARD2;
   SET INWARD2;
   IF TEMP_BENE_AC NOT IN ('0','') THEN DO;
      FIRST_DIGIT=SUBSTR(TEMP_BENE_AC,1,1);
      IF FIRST_DIGIT IN ('2','8') THEN TEMP_ACCTCODE = 'LN';
      ELSE TEMP_ACCTCODE = 'DP';
      ACCOUNT_SOURCE_UNIQUE_ID=COMPRESS(TEMP_ACCTCODE||TEMP_BENE_AC);
   END;
RUN;

DATA OUTWARD;
   SET OUTWARD;
   FORMAT BENE_BRANCH SENDER_BRANCH $4.;
   TXN_CODE ='RMT002';
   CRDR = 'D';
   ORIGINATOR_BANK = BRANCHABB;
   INCOMING_OUTGOING_FLG = 'O';
   BENE_BRANCH = '0';
   SENDER_BRANCH = BRANCHABB;
   COMPRESS_APPL=COMPRESS(APPLACCTNO,",:.()-/",'A');
   COMPRESS_APPL = IFC(COMPRESS_APPL=:'0',
   SUBSTR(COMPRESS_APPL,VERIFY(COMPRESS_APPL,'0')),COMPRESS_APPL);
   APPL_AC_LEN=LENGTH(COMPRESS_APPL);
   IF APPL_AC_LEN = 10 THEN DO;
     IF SUBSTR(APPLACCTNO,1,1) IN ('1','3','4','6') THEN
        TEMP_ACCTCODE='DP';
     ELSE TEMP_ACCTCODE='LN';
     ACCOUNT_SOURCE_UNIQUE_ID = COMPRESS(TEMP_ACCTCODE||APPLACCTNO);
    END;
    * ELSE USE HARDCODED ACCOUNT NO;
   RUN;
RUN;

DATA BT;
   SET BT;
   FORMAT BENE_BRANCH SENDER_BRANCH $4.;
   FORMAT CUSTOMER_SOURCE_UNIQUE_ID $20.;
   TXN_CODE = 'RMT002';
   CRDR = 'D';
   ORIGINATOR_BANK = BRANCHABB;
   INCOMING_OUTGOING_FLG = 'O';
   BENE_BRANCH = '0';
   SENDER_BRANCH = BRANCHABB;
   COMPRESS_APPL=COMPRESS(APPLACCTNO,",:.()-/",'A');
   COMPRESS_APPL = IFC(COMPRESS_APPL=:'0',
   SUBSTR(COMPRESS_APPL,VERIFY(COMPRESS_APPL,'0')),COMPRESS_APPL);
   APPL_AC_LEN=LENGTH(COMPRESS_APPL);
   /*
   IF APPL_AC_LEN = 10 AND SUBSTR(APPLACCTNO,1,1) ^= '0' THEN DO;
      IF SUBSTR(APPLACCTNO,1,1) = '2' THEN TEMP_ACCTCODE='LN';
      ACCOUNT_SOURCE_UNIQUE_ID = COMPRESS(TEMP_ACCTCODE||APPLACCTNO);
   END;
   */
   LENGTH ACCTNOX $10.;
   IF SUBSTR(APPLACCTNO,1,2) = '25' OR SUBSTR(APPLACCTNO,1,3) = '285'
      THEN ACCTNOX = APPLACCTNO;

   ACCOUNT_SOURCE_UNIQUE_ID = 'RMT00001A';
   CUSTOMER_SOURCE_UNIQUE_ID = 'RMT00001C';
   * ELSE USE HARDCODED ACCOUNT NO;
RUN;
PROC SORT DATA=BT;BY ACCTNOX;RUN;
DATA BTMAST;
  SET BT.MAST&REPTDAY&REPTMON   BT.MAST2&REPTDAY&REPTMON
      IBT.IMAST&REPTDAY&REPTMON IBT.IMAST2&REPTDAY&REPTMON;
  IF SUBSTR(ACCTNOX,1,2) = '25' OR SUBSTR(ACCTNOX,1,3) = '285';
  ACCTBRCH = FICODE;
  KEEP ACCTNOX ACCTBRCH;
RUN;
PROC SORT DATA=BTMAST NODUPKEY;BY ACCTNOX;RUN;

DATA BT;
  MERGE BT(IN=A) BTMAST(IN=B);
  BY ACCTNOX;
  IF A;
  IF A AND B THEN DO;
     BRANCH_ID = ACCTBRCH;
     SENDER_BRANCH = PUT(ACCTBRCH,BRCHCD.);
  END;
RUN;

DATA LOAN;
   SET LN.LNNOTE(KEEP=ACCTNO NOTENO COSTCTR LOANTYPE NTBRCH)
       ILN.LNNOTE(KEEP=ACCTNO NOTENO COSTCTR LOANTYPE NTBRCH);
    /*
   IF (3000<=COSTCTR<=3999) THEN ORG_UNIT_CODE = 'PIBBLN';
   ELSE                          ORG_UNIT_CODE = 'PBBLN ';
   */
   MNI_ACCTCODE = 'LN';
   PRODUCT=LOANTYPE;
   PROD = COMPRESS('LN'||PUT(LOANTYPE,Z3.));
   DROP LOANTYPE;
   ACCTBRCH = NTBRCH;
RUN;
PROC SORT DATA=LOAN OUT=LOAN_ACCT NODUPKEY;BY ACCTNO NOTENO;RUN;
PROC SORT DATA=LOAN(DROP=NOTENO) OUT=CTR NODUPKEY;BY ACCTNO;RUN;

DATA DEPO_ACCT;
   SET DP.CURRENT(IN=A) IDP.CURRENT (IN=B)
       DP.SAVING        IDP.SAVING
       DP.FD            IDP.FD
       DP.UMA           IDP.UMA
       DP.VOSTRO;
   /*
   IF (3000<=COSTCTR<=3999) THEN ORG_UNIT_CODE = 'PIBBDP';
   ELSE                          ORG_UNIT_CODE = 'PBBDP ';
   */
   MNI_ACCTCODE='DP';
   PROD = COMPRESS('DP'||PUT(PRODUCT,Z3.));
   ACCTBRCH = BRANCH;
RUN;

DATA ACCT;
   SET DEPO_ACCT LOAN_ACCT CTR;
   FORMAT ACCOUNT_SOURCE_UNIQUE_ID $17.;
   IF NOTENO ^= . THEN
      ACCOUNT_SOURCE_UNIQUE_ID  = COMPRESS(MNI_ACCTCODE||ACCTNO||
                                           PUT(NOTENO,Z5.));
   ELSE ACCOUNT_SOURCE_UNIQUE_ID  = COMPRESS(MNI_ACCTCODE||ACCTNO);
   BANK_ACC_IND = 'Y';
   KEEP ACCTNO NOTENO MNI_ACCTCODE BANK_ACC_IND ORG_UNIT_CODE
        ACCOUNT_SOURCE_UNIQUE_ID PROD ACCTBRCH;
RUN;

DATA INWARD_OUTWARD TRAN.INOUT;
   SET INWARD1 LOANS_INWARD INWARD2 OUTWARD;
RUN;

PROC SQL;
  CREATE TABLE INWARD_OUTWARD AS
  SELECT T1.*,
         T2.BANK_ACC_IND,
         T2.MNI_ACCTCODE,
         T2.PROD,
         T2.ACCTBRCH
  FROM INWARD_OUTWARD T1 LEFT JOIN ACCT T2 ON
  T1.ACCOUNT_SOURCE_UNIQUE_ID = T2.ACCOUNT_SOURCE_UNIQUE_ID;
QUIT;

DATA INWARD_OUTWARD;
  SET INWARD_OUTWARD;
  IF (BRANCHABB = 'EBK' AND PAYMODE = 'DR A/C') OR
     (BRANCHABB = 'CPC' AND PAYMODE IN ('AUTO CR','INWARD'))
     THEN DO;
     IF ACCTBRCH ^= . THEN DO;
        BRANCH_ID = ACCTBRCH;
        IF ISTTYPE='RI' THEN BENE_BRANCH=PUT(ACCTBRCH,BRCHCD.);
        ELSE IF ISTTYPE='RO' THEN SENDER_BRANCH=PUT(ACCTBRCH,BRCHCD.);
     END;
  END;
RUN;

DATA TRAN.INWARD_OUTWARD;
  SET INWARD_OUTWARD(IN=A) BT(IN=B);
  FORMAT TEMP_ACCT_SOURCE $12.;
  FORMAT CUSTOMER_SOURCE_UNIQUE_ID $20.;
  IF A THEN DO;
     IF BANK_ACC_IND ^= 'Y' THEN DO;
        *ORG_UNIT_CODE = 'PBB';
        IF INCOMING_OUTGOING_FLG = 'O' THEN DO;
           ACCOUNT_SOURCE_UNIQUE_ID = 'RMT00001A';
           CUSTOMER_SOURCE_UNIQUE_ID = 'RMT00001C';
           PROD = 'RT108';
        END;
        ELSE DO;
           ACCOUNT_SOURCE_UNIQUE_ID = 'RMT00002A';
           CUSTOMER_SOURCE_UNIQUE_ID = 'RMT00002C';
           PROD = 'RT109';
        END;
     END;
  END;
  IF B THEN PROD = 'RT108';
  TEMP_ACCT_SOURCE = SUBSTR(ACCOUNT_SOURCE_UNIQUE_ID,1,12);
RUN;

DATA CIS;
   SET CIS.CUSTDLY(KEEP=ACCTCODE ACCTNO CUSTNO ALIAS PRISEC);
   WHERE PRISEC=901 AND ACCTCODE IN ('DP','LN');
   FORMAT ACCOUNT_SOURCE_UNIQUE_ID $12.;
   FORMAT CIS $20.;
   ACCOUNT_SOURCE_UNIQUE_ID = COMPRESS(ACCTCODE||ACCTNO);
   CIS = COMPRESS('CIS'||CUSTNO);
RUN;
PROC SORT DATA=CIS NODUPKEY;BY ACCTNO;RUN;
PROC SQL;
   CREATE TABLE TRAN.RENTAS_CIS AS
   SELECT T1.*,
          T2.CIS,
          T2.ALIAS,
          T2.ACCOUNT_SOURCE_UNIQUE_ID AS CIS_ACCTNO
   FROM TRAN.INWARD_OUTWARD T1 LEFT JOIN CIS T2 ON
   T1.TEMP_ACCT_SOURCE = T2.ACCOUNT_SOURCE_UNIQUE_ID;
QUIT;

DATA TRAN.RENTAS_CIS;
   SET TRAN.RENTAS_CIS;
   IF CIS ^= ' ' THEN CUSTOMER_SOURCE_UNIQUE_ID = CIS;
   ELSE DO;
      IF INCOMING_OUTGOING_FLG = 'O' THEN DO;
         CUSTOMER_SOURCE_UNIQUE_ID = 'RMT00001C';
         ACCOUNT_SOURCE_UNIQUE_ID = 'RMT00001A';
         *ORG_UNIT_CODE = 'PBB';
         PROD = 'RT108';
      END;
      ELSE DO;
         CUSTOMER_SOURCE_UNIQUE_ID = 'RMT00002C';
         ACCOUNT_SOURCE_UNIQUE_ID = 'RMT00002A';
         *ORG_UNIT_CODE = 'PBB';
         PROD = 'RT109';
      END;
   END;
RUN;

DATA OUT;
   SET TRAN.RENTAS_CIS;
   IF INCOMING_OUTGOING_FLG = 'I' AND BENEFICIARY_ID = '' THEN DO;
      IF ALIAS ^= '' THEN BENEFICIARY_ID = ALIAS;
   END;
   ELSE IF INCOMING_OUTGOING_FLG = 'O' AND ORIGINATOR_ID = '' THEN DO;
      IF ALIAS ^= '' THEN ORIGINATOR_ID = ALIAS;
   END;
   IF BENEFICIARY_ID = '' THEN BENEFICIARY_ID = BENEFICIARY_NAME;
   IF ORIGINATOR_ID  = '' THEN ORIGINATOR_ID  = ORIGINATOR_NAME;
   IF ORIGINATOR_ID = '' OR BENEFICIARY_ID = '' THEN DELETE;

   * SUPPRESS DOUBLE ALIAS IN ORIGINATOR_ID;
   DOUBLE_ALIAS=INDEX(ORIGINATOR_ID,'@@');
   DO WHILE (DOUBLE_ALIAS > 0);
      ID_PART1 = SUBSTR(ORIGINATOR_ID,1,DOUBLE_ALIAS-1);
      ID_PART2 = SUBSTR(ORIGINATOR_ID,DOUBLE_ALIAS + 2, 40);

      ORIGINATOR_ID = CATX(' ',ID_PART1,ID_PART2);
      DOUBLE_ALIAS=INDEX(ORIGINATOR_ID,'@@');
   END;

   * SUPPRESS DOUBLE ALIAS IN BENEFICAIRY_ID;
   DOUBLE_ALIAS=INDEX(BENEFICIARY_ID,'@@');
   DO WHILE (DOUBLE_ALIAS > 0);
      ID_PART1 = SUBSTR(BENEFICIARY_ID,1,DOUBLE_ALIAS-1);
      ID_PART2 = SUBSTR(BENEFICIARY_ID,DOUBLE_ALIAS + 2, 40);

      BENEFICIARY_ID = CATX(' ',ID_PART1,ID_PART2);
      DOUBLE_ALIAS=INDEX(BENEFICIARY_ID,'@@');
   END;

   * 2022-1211 REMOVE BANK TRANSACTIONS;
   IF CATS(REPEAT('0',20-LENGTH(ORIGINATOR_ID)-1),ORIGINATOR_ID)
      IN ('0000000000000006463H','0000000000000014328V')
      THEN DELETE;
RUN;

DATA _NULL_;
   SET OUT;
   FILE RENTRAN;
   DELIM = '1D'X;
   RETAIN COUNT 1;
   SOURCE_TXN_UNIQUE_ID=COMPRESS('RTS'||"&RDATE"||PUT(COUNT,Z10.));
   PUT RUN_TIMESTAMP              +(-1)DELIM+(-1) /* 1.  */
       SOURCE_TXN_UNIQUE_ID       +(-1)DELIM+(-1) /* 2.  */
       SOURCE_TXN_UNIQUE_ID       +(-1)DELIM+(-1) /* 3.  */
       ACCOUNT_SOURCE_UNIQUE_ID   +(-1)DELIM+(-1) /* 4.  */
       ACCOUNT_SOURCE_UNIQUE_ID   +(-1)DELIM+(-1) /* 5.  */
       CUSTOMER_SOURCE_UNIQUE_ID  +(-1)DELIM+(-1) /* 6.  */
       CUSTOMER_SOURCE_UNIQUE_ID  +(-1)DELIM+(-1) /* 7.  */
       BRANCH_ID                  +(-1)DELIM+(-1) /* 8.  */
       TXN_CODE                   +(-1)DELIM+(-1) /* 9.  */
       ' '                        +(-1)DELIM+(-1) /* 10. */
       CURCODE                    +(-1)DELIM+(-1) /* 11. */
       CURBASE                    +(-1)DELIM+(-1) /* 12. */
       ORIGINATION_DATE           +(-1)DELIM+(-1) /* 13. */
       POSTING_DATE               +(-1)DELIM+(-1) /* 14. */
       ' '                        +(-1)DELIM+(-1) /* 15. */
       ' '                        +(-1)DELIM+(-1) /* 16. */
       LOCAL_TIMESTAMP            +(-1)DELIM+(-1) /* 17. */
       PROD                       +(-1)DELIM+(-1) /* 18. */
       ' '                        +(-1)DELIM+(-1) /* 19. */
       ' '                        +(-1)DELIM+(-1) /* 20. */
       AMOUNT                     +(-1)DELIM+(-1) /* 21. */
       AMOUNT                     +(-1)DELIM+(-1) /* 22. */
       CRDR                       +(-1)DELIM+(-1) /* 23. */
       MENTION                    +(-1)DELIM+(-1) /* 24. */
       ' '                        +(-1)DELIM+(-1) /* 25. */
       ' '                        +(-1)DELIM+(-1) /* 26. */
       ' '                        +(-1)DELIM+(-1) /* 27. */
       ' '                        +(-1)DELIM+(-1) /* 28. */
       ' '                        +(-1)DELIM+(-1) /* 29. */
       ' '                        +(-1)DELIM+(-1) /* 30. */
       ' '                        +(-1)DELIM+(-1) /* 31. */
       CHANNEL                    +(-1)DELIM+(-1) /* 32. */
       ' '                        +(-1)DELIM+(-1) /* 33. */
       ' '                        +(-1)DELIM+(-1) /* 34. */
       ' '                        +(-1)DELIM+(-1) /* 35. */
       ORG_UNIT_CODE              +(-1)DELIM+(-1) /* 36. */
       ' '                        +(-1)DELIM+(-1) /* 37. */
       ' '                        +(-1)DELIM+(-1) /* 38. */
       ' '                        +(-1)DELIM+(-1) /* 39. */
       ' '                        +(-1)DELIM+(-1) /* 40. */
       EMPLOYEE_ID                +(-1)DELIM+(-1) /* 41. */
       ' '                        +(-1)DELIM+(-1) /* 42. */
       ' '                        +(-1)DELIM+(-1) /* 43. */
       ' '                        +(-1)DELIM+(-1) /* 44. */
       ' '                        +(-1)DELIM+(-1) /* 45. */
       ' '                        +(-1)DELIM+(-1) /* 46. */
       ' '                        +(-1)DELIM+(-1) /* 47. */
       ' '                        +(-1)DELIM+(-1) /* 48. */
       ' '                        +(-1)DELIM+(-1) /* 49. */
       ' '                        +(-1)DELIM+(-1) /* 50. */
       ' '                        +(-1)DELIM+(-1) /* 51. */
       ' '                        +(-1)DELIM+(-1) /* 52. */
       ' '                        +(-1)DELIM+(-1) /* 53. */
       ' '                        +(-1)DELIM+(-1) /* 54. */
       ' '                        +(-1)DELIM+(-1) /* 55. */
       ' '                        +(-1)DELIM+(-1) /* 56. */
       ' '                        +(-1)DELIM+(-1) /* 57. */
       ' '                        +(-1)DELIM+(-1) /* 58. */
       ' '                        +(-1)DELIM+(-1) /* 59. */
       ORIGINATOR_NAME            +(-1)DELIM+(-1) /* 60. */
       BENEFICIARY_NAME           +(-1)DELIM+(-1) /* 61. */
       ORIGINATOR_BANK            +(-1)DELIM+(-1) /* 62. */
       BENEFICIARY_BANK           +(-1)DELIM+(-1) /* 63. */
       USERID                     +(-1)DELIM+(-1) /* 64. */
       ' '                        +(-1)DELIM+(-1) /* 65. */
       ' '                        +(-1)DELIM+(-1) /* 66. */
       ' '                        +(-1)DELIM+(-1) /* 67. */
       ' '                        +(-1)DELIM+(-1) /* 68. */
       ' '                        +(-1)DELIM+(-1) /* 69. */
       BENEFICIARY_ID             +(-1)DELIM+(-1) /* 70. */
       ORIGINATOR_ID              +(-1)DELIM+(-1) /* 71. */
       TRANSREF                   +(-1)DELIM+(-1) /* 72. */
       INCOMING_OUTGOING_FLG      +(-1)DELIM+(-1) /* 73. */
       SENDER_BRANCH              +(-1)DELIM+(-1) /* 74. */
       BENE_BRANCH                +(-1)DELIM+(-1) /* 75. */
       ;
       COUNT + 1;
RUN;
//*-------------------------------
//*--> BACKUP INTERFACE FILE
//*-------------------------------
//COPYFILE EXEC PGM=ICEGENER
//SYSPRINT DD SYSOUT=*
//SYSUT1   DD DISP=SHR,DSN=SAP.AML.DETICA.REMTRAN.RENTAS.TEXT
//SYSUT2   DD DSN=SAP.AML.DETICA.REMTRAN.RENTAS.TEXT.BKP,
//            DISP=(NEW,CATLG,DELETE),
//            UNIT=SYSDA,LIKE=SAP.AML.DETICA.REMTRAN.RENTAS.TEXT
//SYSIN    DD DUMMY
