//EIEMCRLS JOB MSGCLASS=X,MSGLEVEL=(1,1),REGION=64M,NOTIFY=&SYSUID      JOB77707
/*JOBPARM S=S1M1
//*-------------------------------------------------------------------
//* PROGRAM  : EIEMCRLS
//* ESMR     : 2019-1884 (AAB)
//* DESC     : AUTO MAIL TO PBB/PIBB'S 2 1/2 YEARS GOOD REPAYMENT
//*            BORROWERS/CUSTOMERS WITH PAID DOWN AMOUNT > RM100K @
//*            1/2 YEARLY BASIS FOR REINSTATEMENT OF LOANS/FINANCING
//* FREQ     : 31/03 AND 30/09
//* ESMR     : 2021-1836 (TBC)
//* DESC     : SPLIT BY MAILCODE AND EMAILADD
//*-------------------------------------------------------------------
//DELETE  EXEC PGM=IEFBR14
//DEL01   DD DSN=SAP.EMC.LNRIHLCP.TEXT,
//           DISP=(MOD,DELETE,DELETE),
//           SPACE=(CYL,(0))
//DEL02   DD DSN=SAP.EMC.LNRIHLCP.SUMM.TEXT,
//           DISP=(MOD,DELETE,DELETE),
//           SPACE=(CYL,(0))
//DEL03   DD DSN=SAP.EMC.LNRIHLIP.TEXT,
//           DISP=(MOD,DELETE,DELETE),
//           SPACE=(CYL,(0))
//DEL04   DD DSN=SAP.EMC.LNRIHLIP.SUMM.TEXT,
//           DISP=(MOD,DELETE,DELETE),
//           SPACE=(CYL,(0))
//DEL05   DD DSN=SAP.EMC.REPORT.TEXT,
//           DISP=(MOD,DELETE,DELETE),
//           SPACE=(CYL,(0))
//DEL06   DD DSN=SAP.EMC.LNRIHLCE.TEXT,
//           DISP=(MOD,DELETE,DELETE),
//           SPACE=(CYL,(0))
//DEL07   DD DSN=SAP.EMC.LNRIHLIE.TEXT,
//           DISP=(MOD,DELETE,DELETE),
//           SPACE=(CYL,(0))
//DEL08   DD DSN=SAP.EMC.LNRIHLCE.SUMM.TEXT,
//           DISP=(MOD,DELETE,DELETE),
//           SPACE=(CYL,(0))
//DEL09   DD DSN=SAP.EMC.LNRIHLIE.SUMM.TEXT,
//           DISP=(MOD,DELETE,DELETE),
//           SPACE=(CYL,(0))
//DEL10   DD DSN=SAP.EMC.LNRIHLCE.IDX.TEXT,
//           DISP=(MOD,DELETE,DELETE),
//           SPACE=(CYL,(0))
//DEL11   DD DSN=SAP.EMC.LNRIHLIE.IDX.TEXT,
//           DISP=(MOD,DELETE,DELETE),
//           SPACE=(CYL,(0))
//*-------------------------------------------------------------------
//EIEMCRLS EXEC SAS609
//PROMOTE  DD DSN=SAP.PBB.PROMOTE,DISP=SHR
//LN       DD DSN=SAP.PBB.LNNAME,DISP=SHR
//LNI      DD DSN=SAP.PIBB.LNNAME,DISP=SHR
//PGM      DD DSN=RBP2.IB330P.SASSRCE,DISP=SHR
//EMCPBB   DD DSN=SAP.EMC.LNRIHLCP.TEXT,
//            DISP=(NEW,CATLG,DELETE),
//            DCB=(RECFM=FB,LRECL=270,BLKSIZE=0),
//            SPACE=(TRK,(10,10)),UNIT=SYSDA
//EMCPBBS  DD DSN=SAP.EMC.LNRIHLCP.SUMM.TEXT,
//            DISP=(NEW,CATLG,DELETE),
//            DCB=(RECFM=FB,LRECL=100,BLKSIZE=10000),
//            SPACE=(TRK,(10,10)),UNIT=SYSDA
//EMCPIB   DD DSN=SAP.EMC.LNRIHLIP.TEXT,
//            DISP=(NEW,CATLG,DELETE),
//            DCB=(RECFM=FB,LRECL=270,BLKSIZE=0),
//            SPACE=(TRK,(10,10)),UNIT=SYSDA
//EMCPIBS  DD DSN=SAP.EMC.LNRIHLIP.SUMM.TEXT,
//            DISP=(NEW,CATLG,DELETE),
//            DCB=(RECFM=FB,LRECL=100,BLKSIZE=10000),
//            SPACE=(TRK,(10,10)),UNIT=SYSDA
//SASLIST  DD DSN=SAP.EMC.REPORT.TEXT,
//            DISP=(NEW,CATLG,DELETE),
//            DCB=(RECFM=FB,LRECL=133,BLKSIZE=0),
//            SPACE=(CYL,(100,100),RLSE),UNIT=SYSDA
//EMLPBB   DD DSN=SAP.EMC.LNRIHLCE.TEXT,
//            DISP=(NEW,CATLG,DELETE),
//            DCB=(RECFM=FB,LRECL=300,BLKSIZE=0),
//            SPACE=(TRK,(10,10)),UNIT=SYSDA
//EMLPIB   DD DSN=SAP.EMC.LNRIHLIE.TEXT,
//            DISP=(NEW,CATLG,DELETE),
//            DCB=(RECFM=FB,LRECL=300,BLKSIZE=0),
//            SPACE=(TRK,(10,10)),UNIT=SYSDA
//EMLPBBS  DD DSN=SAP.EMC.LNRIHLCE.SUMM.TEXT,
//            DISP=(NEW,CATLG,DELETE),
//            DCB=(RECFM=FB,LRECL=300,BLKSIZE=0),
//            SPACE=(TRK,(10,10)),UNIT=SYSDA
//EMLPIBS  DD DSN=SAP.EMC.LNRIHLIE.SUMM.TEXT,
//            DISP=(NEW,CATLG,DELETE),
//            DCB=(RECFM=FB,LRECL=300,BLKSIZE=0),
//            SPACE=(TRK,(10,10)),UNIT=SYSDA
//EMXPBB   DD DSN=SAP.EMC.LNRIHLCE.IDX.TEXT,
//            DISP=(NEW,CATLG,DELETE),
//            DCB=(RECFM=FB,LRECL=250,BLKSIZE=25000),
//            SPACE=(TRK,(10,10)),UNIT=SYSDA
//EMXPIB   DD DSN=SAP.EMC.LNRIHLIE.IDX.TEXT,
//            DISP=(NEW,CATLG,DELETE),
//            DCB=(RECFM=FB,LRECL=250,BLKSIZE=25000),
//            SPACE=(TRK,(10,10)),UNIT=SYSDA
//SORTWK01 DD UNIT=SYSDA,SPACE=(CYL,(1000))
//SORTWK02 DD UNIT=SYSDA,SPACE=(CYL,(1000))
//SORTWK03 DD UNIT=SYSDA,SPACE=(CYL,(1000))
//SYSIN    DD *

OPTIONS SORTDEV=3390 YEARCUTOFF=1950 NOCENTER NODATE MISSING=' ';

%INC PGM(LNENCRID);

DATA REPTDATE;
   REPTDATE = TODAY()-DAY(TODAY());
   CALL SYMPUT('REPTDT',PUT(REPTDATE,DDMMYYN6.));
   CALL SYMPUT('INDXDT',PUT(REPTDATE,YYMMDDN8.));
   CALL SYMPUT('RDATE',PUT(REPTDATE,DDMMYY8.));
   CALL SYMPUT('REPTMON',PUT(MONTH(REPTDATE),Z2.));
   CALL SYMPUT('REPTYR',PUT(REPTDATE,YEAR4.));
   CALL SYMPUT('MTHNAM',UPCASE(PUT(REPTDATE,MONNAME3.)));
RUN;

PROC SORT DATA=PROMOTE.LOAN&REPTMON OUT=RLSLIST;
   BY GUAREND DESCENDING REPAID;
   WHERE REPAID > 100000;
RUN;
PROC SORT DATA=RLSLIST NODUPKEY; BY GUAREND; RUN;
PROC SORT DATA=RLSLIST; BY ACCTNO; RUN;

PROC SORT DATA=LN.LNNAME OUT=PBBNAME; BY ACCTNO; RUN;
DATA PBBNAME;
   MERGE PBBNAME(IN=A) RLSLIST(IN=B);
   BY ACCTNO;
   IF A AND B;
RUN;

*SMR2021-1836;
DATA PBBNAME MAILPBB;
   SET PBBNAME;
   ID = COMPRESS(NEWIC);
   %ENCR_ID;
   IF MAILCODE IN (' ','13','14') AND EMAILADD NE ''
               THEN OUTPUT MAILPBB;
   ELSE OUTPUT PBBNAME;
RUN;

DATA _NULL_;
   SET PBBNAME END=LAST;
   ROWCNT+1;
   FILE EMCPBB;
   PUT @001 'B'
       @002 "&REPTDT"
       @008 NAMELN1     $40.
       @048 NAMELN2     $40.
       @088 NAMELN3     $40.
       @128 NAMELN4     $40.
       @168 NAMELN5     $40.
       @208 BRANCH       Z7.
       @215 ACCTNO      Z11.
       @226 MASK_IDS    $24.
       ;
   IF LAST THEN DO;
      FILE EMCPBBS;
      PUT @001 'LNRIHLCP'
          @031 "&REPTDT"
          @038 ROWCNT   Z16.
          ;
   END;
RUN;

*SMR2021-1836;
DATA MAILPBB;
   SET MAILPBB;
   ROWCNT+1;
   VAR_ID = COMPRESS("PBB_EMAIL_STMT_RIL_C"||
                     PUT(ROWCNT,Z10.)||"_&INDXDT");
   STATE_DTE = COMPRESS("&MTHNAM"||"&REPTYR");
RUN;

DATA _NULL_;
   SET MAILPBB END=LAST;
   FILE EMLPBB;
   PUT @001 'B'
       @002 "&REPTDT"
       @008 NAMELN1     $40.
       @048 NAMELN2     $40.
       @088 NAMELN3     $40.
       @128 NAMELN4     $40.
       @168 NAMELN5     $40.
       @208 BRANCH       Z7.
       @215 ACCTNO      Z11.
       @227 VAR_ID      $40.
       @268 MASK_IDS    $24.
       ;
   IF LAST THEN DO;
      FILE EMLPBBS;
      PUT @001 'LNRIHLCE'
          @031 "&REPTDT"
          @038 ROWCNT   Z16.
      ;
   END;
RUN;

*PBB INDEX FILE;
DATA _NULL_;
   SET MAILPBB;
   FILE EMXPBB;
   PUT @001 VAR_ID      $40.
       @041 EMAILADD    $60.
       @101 STATE_DTE
       @108 NAMELN1     $40.
       @148 NEWIC       $17.
       ;
RUN;

PROC SORT DATA=LNI.LNNAME OUT=PIBNAME; BY ACCTNO; RUN;
DATA PIBNAME;
   MERGE PIBNAME(IN=A) RLSLIST(IN=B);
   BY ACCTNO;
   IF A AND B;
RUN;

*SMR2021-1836;
DATA PIBNAME MAILPIB;
   SET PIBNAME;
   ID = COMPRESS(NEWIC);
   %ENCR_ID;
   IF MAILCODE IN (' ','13','14') AND EMAILADD NE ''
               THEN OUTPUT MAILPIB;
   ELSE OUTPUT PIBNAME;
RUN;

DATA _NULL_;
   SET PIBNAME END=LAST;
   ROWCNT+1;
   FILE EMCPIB;
   PUT @001 'B'
       @002 "&REPTDT"
       @008 NAMELN1     $40.
       @048 NAMELN2     $40.
       @088 NAMELN3     $40.
       @128 NAMELN4     $40.
       @168 NAMELN5     $40.
       @208 BRANCH       Z7.
       @215 ACCTNO      Z11.
       @226 MASK_IDS    $24.
       ;
   IF LAST THEN DO;
      FILE EMCPIBS;
      PUT @001 'LNRIHLIP'
          @031 "&REPTDT"
          @038 ROWCNT   Z16.
          ;
   END;
RUN;

*SMR2021-1836;
DATA MAILPIB;
   SET MAILPIB;
   ROWCNT+1;
   VAR_ID = COMPRESS("PIB_EMAIL_STMT_RIL_C"||
                     PUT(ROWCNT,Z10.)||"_&INDXDT");
   STATE_DTE = COMPRESS("&MTHNAM"||"&REPTYR");
RUN;

DATA _NULL_;
   SET MAILPIB END=LAST;
   FILE EMLPIB;
   PUT @001 'B'
       @002 "&REPTDT"
       @008 NAMELN1     $40.
       @048 NAMELN2     $40.
       @088 NAMELN3     $40.
       @128 NAMELN4     $40.
       @168 NAMELN5     $40.
       @208 BRANCH       Z7.
       @215 ACCTNO      Z11.
       @227 VAR_ID      $40.
       @268 MASK_IDS    $24.
       ;
   IF LAST THEN DO;
      FILE EMLPIBS;
      PUT @001 'LNRIHLIPE'
          @031 "&REPTDT"
          @038 ROWCNT   Z16.
          ;
   END;
RUN;

*PIBB INDEX FILE;
DATA _NULL_;
   SET MAILPIB;
   FILE EMXPIB;
   PUT @001 VAR_ID      $40.
       @041 EMAILADD    $60.
       @101 STATE_DTE
       @108 NAMELN1     $40.
       @148 NEWIC       $17.
       ;
RUN;


DATA RLSLIST;
   SET PBBNAME PIBNAME;
   NOEMC = 1;
RUN;
PROC SORT DATA=RLSLIST; BY BRANCH ACCTNO NOTENO; RUN;
PROC REPORT DATA=RLSLIST NOCENTER SPLIT='*' NOWINDOWS HEADSKIP;
   TITLE  "REPORT ID : EIQPROM2";
   TITLE2 "AUTOMAILING LISTING FOR REINSTATEMENT OF LOAN AS AT &RDATE";
   COLUMN BRANCH BRCH ACCTNO NOTENO PRODUCT NAMELN1 MAILCODE NOEMC;
   DEFINE BRANCH   /GROUP   WIDTH=7 FORMAT=3. 'BRANCH*CODE';
   DEFINE BRCH     /DISPLAY WIDTH=6 FORMAT=$3. 'BRANCH';
   DEFINE ACCTNO   /DISPLAY FORMAT=10. 'A/C NO';
   DEFINE NOTENO   /DISPLAY FORMAT=5. 'NOTE*NO';
   DEFINE PRODUCT  /DISPLAY WIDTH=8 'PRODUCT*CODE';
   DEFINE NAMELN1  /DISPLAY WIDTH=40 'NAME OF BORROWER/CUSTOMER';
   DEFINE MAILCODE /DISPLAY WIDTH=2 'MAIL CODE';
   DEFINE NOEMC    /SUM NOPRINT;
   BREAK AFTER BRANCH /SKIP;
   COMPUTE AFTER BRANCH;
      LINE @004 123*'-';
      LINE @004 'NO OF BORROWER/CUSTOMER :'
           @029 NOEMC.SUM COMMA8.;
   ENDCOMP;
RUN;
