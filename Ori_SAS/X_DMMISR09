OPTIONS YEARCUTOFF=1950 NOCENTER NODATE MISSING=0;

%INC PGM(PBMISFMT);

DATA REPTDATE;
  INFILE DPTRBL LRECL=779 OBS=1;
  INPUT @106 TBDATE PD6.;
  REPTDATE=INPUT(SUBSTR(PUT(TBDATE, Z11.), 1, 8), MMDDYY8.);
  REPTDAT1=REPTDATE-1;
  CALL SYMPUT('RDATEA', PUT(REPTDATE,DDMMYY8.));
  CALL SYMPUT('RDATE1', PUT(REPTDATE,Z5.));
  CALL SYMPUT('RDATEB', PUT(REPTDAT1,DDMMYY8.));
  CALL SYMPUT('RDATE2', PUT(REPTDAT1,Z5.));
RUN;
*;
DATA PREV;
  KEEP ACCTNO BRANCH OLDBAL NAME;
  SET MIS.DDMV;
  IF  REPTDATE=&RDATE2;
  OLDBAL=CURBAL;
*;
PROC SORT; BY ACCTNO;
*;
DATA CURR;
  KEEP ACCTNO BRANCH NEWBAL NAME;
  SET MIS.DDMV;
  IF REPTDATE=&RDATE1;
  NEWBAL=CURBAL;
*;
PROC SORT; BY ACCTNO;
*;

DATA DDMV;
  MERGE PREV
        CURR;
  BY ACCTNO;
  IF OLDBAL=. THEN OLDBAL=0;
  IF NEWBAL=. THEN NEWBAL=0;
  IF ABS(SUM(NEWBAL,(-1)*OLDBAL)) GE 1000000;
RUN;
*;
DATA CRMOVE ODMOVE;
  LENGTH BRCH $6.;
  SET DDMV;
  BRCH=PUT(BRANCH, BRCHCD.);
  IF OLDBAL < 0 AND NEWBAL >= 0 THEN DO;
     MOVEMENT = ROUND((0 + OLDBAL)*0.000001,.01);
     OUTPUT ODMOVE;
     MOVEMENT = ROUND((0 + NEWBAL)*0.000001,.01);
     IF NEWBAL > 0  THEN OUTPUT CRMOVE;
   END;
   ELSE
   IF (OLDBAL >= 0 AND NEWBAL <=0) THEN DO;
      MOVEMENT = ROUND((0 - OLDBAL)*0.000001,.01);
      IF OLDBAL > 0 THEN OUTPUT CRMOVE;
      MOVEMENT = ROUND((0 - NEWBAL)*0.000001,.01);
      OUTPUT ODMOVE;
   END;
   ELSE
   IF (OLDBAL < 0 AND NEWBAL <=0) THEN DO;
      MOVEMENT = ROUND((OLDBAL-NEWBAL)*0.000001,.01);
      OUTPUT ODMOVE;
   END;
   ELSE DO;
      MOVEMENT = ROUND((NEWBAL - OLDBAL)*0.000001,.01);
      OUTPUT CRMOVE;
     END;
RUN;
*;
TITLE3 'DEMAND DEPOSITS CREDIT MOVEMENT OF 1 MILLION AND ABOVE';
TITLE4 'IN THEIR CURRENT ACCOUNTS ' &RDATEB ' TO ' &RDATEA;
TITLE5;

PROC REPORT DATA=CRMOVE NOWD HEADLINE SPLIT='*';
  COLUMN BRCH ACCTNO NAME OLDBAL NEWBAL MOVEMENT;
  DEFINE BRCH     / DISPLAY FORMAT=$6.  'BRANCH';
  DEFINE ACCTNO   / DISPLAY FORMAT=20.  'ACCOUNT NUMBER';
  DEFINE NAME     / DISPLAY FORMAT=$25. 'NAME OF CUSTOMER';
  DEFINE OLDBAL   / ANALYSIS SUM FORMAT=COMMA16.2
                                        'YESTERDAYS BALANCE';
  DEFINE NEWBAL   / ANALYSIS SUM FORMAT=COMMA16.2
                                        'TODAYS     BALANCE';
  DEFINE MOVEMENT / ANALYSIS FORMAT=10.1
                            'INCREASE/*DECREASE*(RM MILLION)';
  RBREAK AFTER / PAGE DUL OL SUMMARIZE;
RUN;

TITLE3 'DEMAND DEPOSITS OD MOVEMENT OF 1 MILLION AND ABOVE';
TITLE4 'IN THEIR CURRENT ACCOUNTS ' &RDATEB ' TO ' &RDATEA;
TITLE5;

PROC REPORT DATA=ODMOVE NOWD HEADLINE SPLIT='*';
  COLUMN BRCH ACCTNO NAME OLDBAL NEWBAL MOVEMENT;
  DEFINE BRCH    / DISPLAY FORMAT=$6.  'BRANCH';
  DEFINE ACCTNO  / DISPLAY FORMAT=20.  'ACCOUNT NUMBER';
  DEFINE NAME    / DISPLAY FORMAT=$25. 'NAME OF CUSTOMER';
  DEFINE OLDBAL  / ANALYSIS SUM FORMAT=COMMA16.2
                            'YESTERDAYS BALANCE';
  DEFINE NEWBAL  / ANALYSIS SUM FORMAT=COMMA16.2
                            'TODAYS     BALANCE';
  DEFINE MOVEMENT / ANALYSIS FORMAT=10.1
                    'INCREASE/*DECREASE*(RM MILLION)';
  RBREAK AFTER / PAGE DUL OL SUMMARIZE;
RUN;
