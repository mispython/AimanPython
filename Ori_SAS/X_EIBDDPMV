******************************************************************
* REPORT OWNER : PRODUCT DEVELOPMENT DIVISION, PBB               *
* DAILY SAVINGS & ACE DEPOSITS MOVEMENTS BY RANGE                *
* DATASETS DYDPS & DYACE GENERATED FROM PROGRAM : DALDPBBE       *
******************************************************************;
OPTIONS YEARCUTOFF=1930 NOCENTER;
*;
PROC FORMAT;
   VALUE MVTFDESA
      5000 = 'RM 5,000 & BELOW'
     10000 = '> RM 5,000 TO RM10,000'
     30000 = '> RM10,000 TO RM30,000'
     50000 = '> RM30,000 TO RM50,000'
     75000 = '> RM50,000 TO RM75,000'
     80000 = 'ABOVE RM75,000';

   VALUE MVTFDESB
      5000 = 'RM 5,000 & BELOW'
     10000 = '> RM 5,000 TO RM10,000'
     30000 = '> RM10,000 TO RM30,000'
     50000 = '> RM30,000 TO RM50,000'
     75000 = '> RM50,000 TO RM75,000'
    100000 = '> RM75,000 TO RM100,000'
    200000 = 'ABOVE RM100,000';
RUN;
*;
DATA REPTDATE;
  INFILE DPTRBL LRECL=779 OBS=1;
  INPUT @106 TBDATE PD6.;
  REPTDATE=INPUT(SUBSTR(PUT(TBDATE, Z11.), 1, 8), MMDDYY8.);
  SELECT;
     WHEN(1 <= DAY(REPTDATE) <= 8) CALL SYMPUT('NOWK', PUT('1', $1.));
     WHEN(9 <= DAY(REPTDATE) <= 15) CALL SYMPUT('NOWK', PUT('2', $1.));
     WHEN(16 <= DAY(REPTDATE) <= 22) CALL SYMPUT('NOWK', PUT('3', $1.));
     OTHERWISE CALL SYMPUT('NOWK', PUT('4', $1.));
  END;
  CALL SYMPUT('REPTYEAR', PUT(REPTDATE, YEAR4.));
  CALL SYMPUT('REPTMON', PUT(MONTH(REPTDATE), Z2.));
  CALL SYMPUT('REPTDAY', PUT(DAY(REPTDATE), Z2.));
  CALL SYMPUT('RDATE', PUT(REPTDATE, DDMMYY8.));
RUN;
*;
LIBNAME MIS "SAP.PBB.MIS.D&REPTYEAR" DISP=SHR;
*;
TITLE1 "REPORT ID : EIBDDPMV";
TITLE2 "DAILY SUMMARY REPORT ON SAVINGS DEPOSITS CUSTOMERS' MOVEMENTS";
TITLE3 'AS AT ' &RDATE ' EXCLUDE ACE & AL-WADIAH';
*;
PROC TABULATE DATA=MIS.DYDPS&REPTMON;
   CLASS MVRANGE;
   VAR   MOVEMENT;
   TABLE MVRANGE=' ' ALL='TOTAL',
         MOVEMENT="TOTAL MOVEMENTS"*SUM=' '*F=COMMA18.2
         / RTS=30 BOX='NET INCREASE/(DECREASE)';
   FORMAT MVRANGE MVTFDESA.;
   WHERE REPTDATE=INPUT("&RDATE",DDMMYY8.);
RUN;
*;
TITLE2 "DAILY SUMMARY REPORT ON ACE DEPOSITS CUSTOMERS' MOVEMENTS";
TITLE3 'AS AT ' &RDATE;
PROC TABULATE DATA=MIS.DYACE&REPTMON;
   CLASS MVRANGE;
   VAR   MOVEMENT;
   TABLE MVRANGE=' ' ALL='TOTAL',
         MOVEMENT="TOTAL MOVEMENTS"*SUM=' '*F=COMMA18.2
         / RTS=30 BOX='NET INCREASE/(DECREASE)';
   FORMAT MVRANGE MVTFDESB.;
   WHERE REPTDATE=INPUT("&RDATE",DDMMYY8.);
RUN;
