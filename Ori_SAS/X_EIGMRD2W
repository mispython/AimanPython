OPTIONS NOCENTER YEARCUTOFF=1950;

DATA _NULL_;
   REPTDATE=INPUT('01'||PUT(MONTH(TODAY()), Z2.)||
            PUT(YEAR(TODAY()), 4.), DDMMYY8.)-1;
   CALL SYMPUT('NDATE',REPTDATE);
RUN;

DATA ALM.REPTDATE;
   INFILE WALALM OBS=1;
   INPUT @2 WALKDATE $8.;
   REPTDATE=INPUT(WALKDATE,DDMMYY8.);
   CALL SYMPUT('RDATE',REPTDATE);
RUN;

%MACRO CONVERT;
DATA R2GL4000 (KEEP=ACCT_NO ACKIND GLAMT)
     R2KEY913 (KEEP=ACCT_NO)
     R2R115   (KEEP=SET_ID USERCD)
     R2R913   (KEEP=ACCT_NO SET_ID);
   RETAIN SETID;
   INFILE WALALM;
   INPUT @2 ID $1. @;
   IF ID = '1' THEN INPUT @3 SETID $8.;
   ELSE IF ID='+' THEN SELECT (SETID);
      WHEN ('R2GL4000') DO;
         INPUT @3 ACCT_NO $22. @29 ACKIND $1. @30 GLAMT COMMA21.2;
         OUTPUT R2GL4000;
      END;
      WHEN ('R2KEY913') DO;
         INPUT @3 ACCT_NO $22.;
         OUTPUT R2KEY913;
      END;
      WHEN ('R2R115') DO;
         INPUT @3 SET_ID $12.
               @17 USERCD $4.;
         OUTPUT R2R115;
      END;
      WHEN ('R2R913') DO;
         INPUT @3 ACCT_NO $22. @28 SET_ID $12.;
         OUTPUT R2R913;
      END;
      OTHERWISE;
   END;
*;
TITLE 'R2GL4000';
PROC SORT DATA=R2GL4000 OUT=ALM.R2GL4000;
   BY ACCT_NO ACKIND GLAMT; RUN;
*;
TITLE 'R2KEY913';
PROC SORT DATA=R2KEY913 OUT=ALM.R2KEY913;
   BY ACCT_NO; RUN;
*;
TITLE 'R2R115';
PROC SORT DATA=R2R115 OUT=ALM.R2R115;
   BY SET_ID; RUN;
*;
TITLE 'R2R913';
PROC SORT DATA=R2R913 OUT=ALM.R2R913;
   BY ACCT_NO SET_ID; RUN;
*;
PROC DATASETS LIB=WORK KILL NOLIST; RUN;
*;
%MEND CONVERT;

%MACRO PROCESS;
   %IF "&NDATE" = "&RDATE" %THEN %CONVERT;
   %ELSE %PUT WALKER SOURCE IS NOT AS AT CURRENT REPORTING DATE;
%MEND PROCESS;
%PROCESS;
