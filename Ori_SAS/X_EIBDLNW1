//EIBDLNW1 JOB MSGCLASS=X,MSGLEVEL=(1,1),REGION=128M                    JOB50635
/*JOBPARM S=S1M1
//**********************************************************************
//* DAILY EXTRACTION OF LOANS INFORMATION FOR DATA WAREHOUSE
//* ESMR : 06-1428,
//* ESMR : 07-1195(RHA) - CHANGE 'PAYEFDT' FROM CHAR TO NUMERIC(SASDATE)
//*        07-1413
//* ESMR : 12-180 (MFM) - CREATE HPWO DATASET & EXCLUDE WO PRODUCT
//*                       FROM LN & HP.
//* ESMR : 14-2876(TBC) - TAG VB IN LOANS
//* ESMR : 15-2489(TBC) - TAG RSN IN LAONS
//* ESMR : 16-1430(TBC)
//* ESMR : 16-1845(NSA)
//* ESMR : 17-1823(IFA) - INCLUDE LOAN PRODUCT(392) IN HP DATASET
//**********************************************************************
//DELETE  EXEC PGM=IEFBR14
//DEL02    DD DSN=SAP.PBB.LNDATAWH.DAILY.DLNFTP,
//            DISP=(MOD,DELETE,DELETE),
//            SPACE=(CYL,(0))
//DEL25    DD DSN=SAP.PBB.LNDATAWH.DAILY.HP,
//            DISP=(MOD,DELETE,DELETE),
//            SPACE=(CYL,(0))
//DEL26    DD DSN=SAP.PBB.LNDATAWH.DAILY.HPDFP,
//            DISP=(MOD,DELETE,DELETE),
//            SPACE=(CYL,(0))
//DEL31    DD DSN=SAP.PBB.LNDATAWH.INVLOC,
//            DISP=(MOD,DELETE,DELETE),
//            SPACE=(CYL,(0))
//DEL32    DD DSN=SAP.PBB.LNDATAWH.INVLOC.INVLOCFP,
//            DISP=(MOD,DELETE,DELETE),
//            SPACE=(CYL,(0))
//*
//CREATE  EXEC PGM=IEFBR14
//CRT01    DD DSN=SAP.PBB.LNDATAWH.DAILY.DLNFTP,
//            DISP=(NEW,CATLG,DELETE),
//            DCB=(RECFM=FB,LRECL=80,BLKSIZE=24000),
//            SPACE=(CYL,(300,200),RLSE),UNIT=(SYSDA,8)
//CRT13    DD DSN=SAP.PBB.LNDATAWH.DAILY.HPDFP,
//            DISP=(NEW,CATLG,DELETE),
//            DCB=(RECFM=FB,LRECL=80,BLKSIZE=24000),
//            SPACE=(CYL,(200,100),RLSE),UNIT=(SYSDA,5)
//CRT16    DD DSN=SAP.PBB.LNDATAWH.INVLOC.INVLOCFP,
//            DISP=(NEW,CATLG,DELETE),
//            DCB=(RECFM=FB,LRECL=80,BLKSIZE=24000),
//            SPACE=(CYL,(200,100),RLSE),UNIT=(SYSDA,5)
//*
//EIBDLNW1 EXEC SAS609
//PGM      DD DSN=SAP.BNM.PROGRAM,DISP=SHR
//PAYFI    DD DSN=RBP2.SAS.B033.PAYSFILE,DISP=SHR
//LNFILE   DD DSN=SAP.PBB.LNMIG,DISP=SHR
//BNM      DD DSN=SAP.PBB.SASDATA.DAILY(0),DISP=SHR
//BNM1     DD DSN=SAP.PBB.MNILN.DAILY(0),DISP=SHR
//MNITB    DD DSN=SAP.PBB.MNITB.DAILY(0),DISP=SHR
//ODGP3    DD DSN=SAP.PBB.MNILIMT.DAILY(0),DISP=SHR
//CISL     DD DSN=SAP.PBB.CISBEXT.LN.DAILY,DISP=SHR
//MISMLN   DD DSN=SAP.PBB.MLN.SASDATA,DISP=SHR
//REFNOTE  DD DSN=RBP2.B033.LN.MIGR.CCRISFL(0),DISP=SHR
//LOAN     DD DSN=SAP.PBB.LNDATAWH.DAILY(+1),
//            DISP=(NEW,CATLG,DELETE),
//            DCB=(RECFM=FS,LRECL=27648,BLKSIZE=27648),
//            SPACE=(CYL,(300,200),RLSE),UNIT=(SYSDA,8)
//HP       DD DSN=SAP.PBB.LNDATAWH.DAILY.HP,
//            DISP=(NEW,CATLG,DELETE),
//            DCB=(RECFM=FS,LRECL=27648,BLKSIZE=27648),
//            SPACE=(CYL,(200,100),RLSE),UNIT=(SYSDA,5)
//INV      DD DSN=SAP.PBB.LNDATAWH.INVLOC,
//            DISP=(NEW,CATLG,DELETE),
//            DCB=(RECFM=FS,LRECL=27648,BLKSIZE=27648),
//            SPACE=(CYL,(200,100),RLSE),UNIT=(SYSDA,5)
//SASLIST  DD SYSOUT=X
//SORTWK01 DD UNIT=SYSDA,SPACE=(CYL,(500))
//SORTWK02 DD UNIT=SYSDA,SPACE=(CYL,(500))
//SORTWK03 DD UNIT=SYSDA,SPACE=(CYL,(500))
//SORTWK04 DD UNIT=SYSDA,SPACE=(CYL,(500))
//SORTWK05 DD UNIT=SYSDA,SPACE=(CYL,(500))
//SORTWK06 DD UNIT=SYSDA,SPACE=(CYL,(500))
//SORTWK07 DD UNIT=SYSDA,SPACE=(CYL,(500))
//SYSIN    DD *

*+--------------------------------------------------------------+
 |  PROGRAM : LN DAILY DATA WAREHOUSE EXTRACTION PROGRAM        |
 +--------------------------------------------------------------+;
OPTIONS YEARCUTOFF=1950 NOCENTER NODATE NONUMBER MISSING=0;

%INC PGM(PBBLNFMT,PBBELF);
*------------------------------------------------*
*  GET REPTDATE                                  *
*------------------------------------------------*;

DATA LOAN.REPTDATE (KEEP=REPTDATE);
  SET BNM1.REPTDATE;
  CALL SYMPUT('REPTYEAR', PUT(REPTDATE, YEAR2.));
  CALL SYMPUT('REPTMON', PUT(MONTH(REPTDATE), Z2.));
  CALL SYMPUT('REPTDAY', PUT(DAY(REPTDATE), Z2.));
  CALL SYMPUT('RDATE', PUT(REPTDATE, DDMMYY8.));
  CALL SYMPUT('REPTDATE',REPTDATE);
  LASTDATE=INTNX('MONTH',REPTDATE,0,'E');
  CALL SYMPUT('LASTDT',LASTDATE);
RUN;

%LET DLN =(KEEP=ACCTNO BRANCH NAME PRODUCT ACCTYIND BORSTAT
                SECTFISS STATECD CUSTCODE NOTENO SECURE INTRATE
                NOTETERM ISSDTE APPRLIMT SPREAD LOANSTAT UNDRAWN
                RISKRTE APPVALUE MARKETVL BLDATE BILPAY BILLTYPE
                REMAINMH COSTCTR BALMNI PRODCD AMTIND CLOSEDTE
                PAYTYPE  PAYAMT  PAYFREQ REMAINMT EXPRDATE
                RESTBALC APPRLMTACCT FLAG3 PAYIND NXTBIL FORATE
                INTEARN APPRLIM2 CUSTFISS FISSPURP FCYBAL CCY
                BAL_AFT_EIR EIR_ADJ DNBFISME DNBFI_ORI VB
                ACCRUAL BALANCE FEEAMT CURBAL ABM_HL IA_LRU
                UNEARNED1 UNEARNED2 UNEARNED CCRIS_INSTLAMT
                ASCORE_PERM ASCORE_LTST CANO INTSTDTE
                FDACCTNO FDCERTNO EARNTERM POINTAMT ASCORE_COMM
                INDUSTRIAL_SECTOR_CD);
%LET LOAN=(KEEP=ACCTNO NOTENO BRANCH COSTCTR PRODUCT CUSTFISS
                SECTFISS FISSPURP BALANCE CURBAL INTEARN INTAMT
                FEEAMT INTEARN2 INTEARN3 INTEARN4 REBATE ACCRUAL
                INTRATE PAIDIND EXPRDATE CLOSEDTE LNTYPE AMTIND
                ODINTACC BALMNI NEWBAL COSTFUND PAYEFDT PAYAMT
                APPRLIM2 NETPROC NOTETERM SPREAD ISSDTE LASTTRAN
                LSTTRNCD LSTTRNAM CAGATAG FLAG3 PAYIND NXTBIL CFINDEX
                BLDATE BORSTAT DAYARR MTHARR ASSMDATE FORATE CCY
                FCYBAL CJFEE RETAILID APPVALUE DEALERNO MODELDES
                SCORE2 USER5 MAKE MODEL COLLYEAR CASHPRICE SIACCTNO
                CENSUS0 CENSUS1 CENSUS3 CENSUS4 CENSUS5 ORGISSDTE
                NTINDEX TOT_MIGR OLDNOTEDAYARR OLDNOTEBLDATE LOANSTAT
                DNBFISME DNBFI_ORI SCORE2CT NUMCPNS RRCOUNTDTE
                REACCRUAL USMARGIN APPRLIMT PAYFREQ EXPRDATE
                F5ACCONV VB RSN CP BAL_AFT_EIR COLLAGE COMMNO
                CORPCODE CRISPURP CUSTCODE DELQCD DLIVRYDT EIR_ADJ
                INTINYTD LNUSER2 NPLCRR NTAPR NTINT ORGBAL ORIGRATE
                PAYTYPE PURPOSE RESTIND RISKRTE SCORE1 SECTOR
                SECURE SITYPE STATECD U2RACECO VINNO YTDEARNS INTBASIS
                RESTBALC NXDUEDT REBIND ABM_HL IA_LRU
                MNIAPLMT MNIAPDTE ASCORE_PERM ASCORE_LTST
                CANO INTSTDTE MTDAVBAL_MIS COMMNO_OLD ESCROWRBAL
                NACOSPADT PTMNATE FDB MOSTDTE MOENDDTE
                MO_TAG MO_MAIN_DT MO_INSTL_ARR
                CPNSTDTE DAYARR_MO MTHARR_MO LN_UTILISE_LOCAT_CD
                UNEARNED1 UNEARNED2 UNEARNED CCRIS_INSTLAMT
                FDACCTNO FDCERTNO EARNTERM POINTAMT
                EARLY_SETTLE_FEE_CHARGE_FLG LOCK_IN_END_DT
                TIMES_RENEWED NURS_TAG NUR_STARTDT NURS_TAGDT
                NURS_ENDDT NURS_COUNTER WRIOFF_DT WRIOFF_AMT
                DSR REPAY_SOURCE REPAY_TYPE_CD MTD_REPAID_AMT
                VALUATION_DT DISPOSED_AMT INDUSTRIAL_SECTOR_CD
                CUM_WRIOFF RECOVER_COST ASCORE_COMM F1RELMOD
                FULLREL_DT UNDRAWN POSTNTRN INSOLVENCY_IND
                PROMPT_PAY_TRACKER REFINANC_LN OLD_FI OLD_MACC_NO
                OLD_SUBACC_NO
                STAFF_FREE_INT_IND STAFF_FREE_INT_LOAN_AMT
                OMNIBUS_FACILITY_IND MARKED_PAYMENT_AMT
                MARKED_PAYMENT_IND RISK_GRADE_CLASS
                REPO_ORDER_ISSUE_DT NUM_REPO_ORDER_ISSUE
                COURT_ORDER_APPLY_DT COURT_ORDER_OBTAIN_DT
                REPAY_PROPOSAL_CD REFNOTENO
                DIGITAL_RR_STATUS_CD DIGITAL_RR_STATUS_DT
                LTST_MGB_SCORE AKPK_STATUS TFA_NURS_TAG
                TFA_NURS_TAG_DT TFA_NURS_START_DT
                TFA_NURS_END_DT TFA_NURS_COUNTER
                TFA_DIG_STATUS_CD TFA_DIG_STATUS_DT
                LMOSTDATE LMOENDDATE REPAY_PROPOSAL_DT
                MANUAL_RR_TAG MANUAL_RR_DT AUTO_REPRICE_INSTL_AMT
                BULLET_REPAY_IND BALLOON_REPAY_IND
                PROP_DEVELOP_FIN_IND COM_FEE_NOTICE_IND
                DIA_PAST01_MTH DIA_PAST02_MTH DIA_PAST03_MTH
                DIA_PAST04_MTH DIA_PAST05_MTH DIA_PAST06_MTH
                DIA_PAST07_MTH DIA_PAST08_MTH DIA_PAST09_MTH
                DIA_PAST10_MTH DIA_PAST11_MTH DIA_PAST12_MTH
                DIA_PAST13_MTH DIA_PAST14_MTH DIA_PAST15_MTH
                DIA_PAST16_MTH DIA_PAST17_MTH DIA_PAST18_MTH
                DIA_PAST19_MTH DIA_PAST20_MTH DIA_PAST21_MTH
                DIA_PAST22_MTH DIA_PAST23_MTH DIA_PAST24_MTH
                AUTO_EXT_TAG AUTO_EXT_TAG_DT ORIG_RESTIND
                RESTIND_END_DT NUM_MORA AUTO_REPRICE_DIFF_INSTL_AMT
                AKPK_RA_TAG AKPK_RA_TAG_DT AKPK_RA_DIG_STATUS_CD
                AKPK_RA_DIG_STATUS_DT AKPK_RA_START_DT AKPK_RA_END_DT
                AKPK_RA_ORIG_SPREAD TRA_EFF_DT AKPK_RA_DLY_INT_ACCRUAL
                AKPK_RA_MTD_INT_ACCRUAL AKPK_RA_MTH_INT_WAIVER_AMT
                AKPK_RA_CUMM_INT_WAIVER_AMT AKPK_RA_MTH_INT_CAP_AMT
                AKPK_RA_CUMM_INT_CAP_AMT AKPK_RA_ORIG_CEILING_RT
                SCHBIL_INT_DT SCHBIL_INSTL_DT LASTBIL_INT_DT
                LASTBIL_INSTL_DT NUM_PAY_BIL_INT NUM_PAY_BIL_INSTL
                MORA_BENCHMARK_AMT TRA_RR_IND TRA_RR_ACCEPT_DT
                NUM_RR DAYARR_MORA HI_TAG HI_TAG_DT HI_DIG_STATUS_CD
                HI_DIG_STATUS_DT REPO_ORDER_EXPIRY_DT FLOOD_MO_TAG
                FLOOD_MO_DT IMPAIRED_HP_TAG);
%LET HPWO=(KEEP=ACCTNO NOTENO PAYEFDT PRODUCT BRANCH APPVALUE
                NOTETERM FLAG3 COSTCTR INTRATE SPREAD REBATE
                INTEARN ACCRUAL BORSTAT PAYAMT PAYIND FEEAMT
                BLDATE EXPRDATE ISSDTE FISSPURP CURBAL INTAMT
                BALANCE CUSTFISS SECTFISS ASSMDATE LSTTRNCD NXTBIL
                PAIDIND NETPROC DEALERNO INTEARN2 INTEARN3 INTEARN4
                LSTTRNAM SCORE2 COLLYEAR MODELDES SIACCTNO CASHPRICE
                MAKE MODEL DAYARR MTHARR USER5 OLDNOTEDAYARR
                OLDNOTEBLDATE LOANSTAT VB CP ASCORE_PERM ASCORE_LTST
                ESCROWRBAL NACOSPADT PTMNATE CAGATAG ASCORE_COMM
                VALUATION_DT DISPOSED_AMT LASTTRAN DLIVRYDT
                REPAY_PROPOSAL_CD REPO_ORDER_ISSUE_DT
                NUM_REPO_ORDER_ISSUE COURT_ORDER_APPLY_DT
                COURT_ORDER_OBTAIN_DT DNBFISME CENSUS1 CENSUS4
                DIGITAL_RR_STATUS_CD DIGITAL_RR_STATUS_DT
                POSTNTRN INSOLVENCY_IND MOSTDTE MOENDDTE
                MO_TAG MO_INSTL_ARR AKPK_STATUS
                REPAY_PROPOSAL_DT MANUAL_RR_TAG MANUAL_RR_DT NUM_RR
                REPO_ORDER_EXPIRY_DT);
DATA INV.PBB_INVALID_LOC(KEEP=ACCTNO NOTENO BRANCH STATE);
   SET BNM.LOAN&REPTMON&REPTDAY;
   IF INVALID_LOC = 'Y';
RUN;
DATA LOAN &DLN;
   SET BNM.LOAN&REPTMON&REPTDAY(DROP=STATE)
       BNM.LNWOD&REPTMON&REPTDAY
       BNM.LNWOF&REPTMON&REPTDAY;
   CUSTFISS=CUSTCD;
   SECTFISS=SECTORCD;
RUN;

PROC SORT DATA=BNM1.LNNOTE OUT=LNNOTE; BY ACCTNO NOTENO; RUN;
PROC SORT DATA=LOAN; BY ACCTNO NOTENO; RUN;

%MACRO GET_GP3;
   %IF "&LASTDT" EQ "&REPTDATE" %THEN %DO;
       DATA GP3;
          SET ODGP3.GP3;
          RISKRTE = INPUT(RISKCODE,1.);
       RUN;
       PROC SORT; BY ACCTNO; RUN;
       DATA LOAN;
          MERGE LOAN(IN=A)
                GP3(KEEP=ACCTNO RISKRTE);
          BY ACCTNO;
          IF A;
       RUN;
   %END;
%MEND GET_GP3;
%GET_GP3;
*;
 PROC SORT DATA=MNITB.CURRENT OUT=CURRENT;
 WHERE CURBAL < 0; BY ACCTNO;
 RUN;
 PROC SORT DATA=ODGP3.OVERDFT
 OUT=OVERDFT(RENAME=APPRLIMT=APPRLIM2) NODUPKEY;
 BY ACCTNO;
 RUN;
       DATA OVERDFT(KEEP=ACCTNO APPRLIM2 ASCORE_PERM ASCORE_LTST
                         ASCORE_COMM INDUSTRIAL_SECTOR_CD DAYARR);
          MERGE CURRENT (IN=A)
                OVERDFT;
          BY ACCTNO;
          IF A;
          IF (EXODDATE NE 0 OR TEMPODDT NE 0) AND (CURBAL LE 0) THEN DO;
             EXODDT = INPUT(SUBSTR(PUT(EXODDATE,Z11.),1,8),MMDDYY8.);
             TEMPDT = INPUT(SUBSTR(PUT(TEMPODDT,Z11.),1,8),MMDDYY8.);
             IF EXODDT NOT IN (.,0) AND TEMPDT NOT IN (.,0) THEN DO;
                IF EXODDT < TEMPDT THEN ODDATE = EXODDT;
                ELSE                    ODDATE = TEMPDT;
             END;
             ELSE DO;
                IF EXODDATE = 0    THEN ODDATE = TEMPDT;
                ELSE                    ODDATE = EXODDT;
             END;
          END;
          DAYARR = &REPTDATE - ODDATE + 1;
       RUN;
       DATA LOAN;
          MERGE LOAN(IN=A)
                OVERDFT;
          BY ACCTNO;
          IF A;
          IF APPRLIM2 EQ . THEN APPRLIM2 = 0;
       RUN;
*;
DATA LOAN;
  MERGE LOAN(IN=A) MISMLN.LNVG&REPTMON;
  BY ACCTNO NOTENO;
  IF A;
RUN;
DATA REFNOTE;
   INFILE REFNOTE;
   INPUT @001 ACCTNO     11.
         @012 OLDNOTE     5.
         @017 NOTENO      5.
         ;
RUN;
PROC SORT DATA=REFNOTE NODUPKEY;BY ACCTNO NOTENO;RUN;

DATA LOAN(DROP=OLDNOTE);
  MERGE LNNOTE(IN=B) LOAN(IN=A) REFNOTE;
  BY ACCTNO NOTENO;
  CAGATAG=PZIPCODE;
  SCORE2CT=CONTRTYPE;
  F1RELMOD=FLAG1;
  F5ACCONV=FLAG5;
  LNUSER2=USER5;
  CANO = ESCRACCT;
  ESCROWRBAL = ECSRRSRV;
  LN_UTILISE_LOCAT_CD = STATE;
  IF DAYARR_MO GT 0      THEN DO;
     ARREARS=INPUT(DAYARR_MO,NDAYS.);
     IF  ARREARS=24   THEN ARREARS=INT((DAYARR_MO/365)*12);
     MTHARR_MO=ARREARS;
  END;
  IF OLDNOTE NOT IN (.,0) THEN REFNOTENO = OLDNOTE;
  ELSE                         REFNOTENO = BONUSANO;
  IF A THEN OUTPUT;
RUN;

PROC SORT DATA=LOAN;BY ACCTNO NOTENO;RUN;
PROC SORT DATA=LNFILE.TOTPAY OUT=TOTPAY(DROP=DATE);
  BY ACCTNO NOTENO;RUN;

DATA LOAN;
   MERGE LOAN(IN=A) TOTPAY(IN=B);BY ACCTNO NOTENO;
   IF A;
RUN;

*--------------------------------------------------*
*   EXTRACT SECTOR FIELD FOR OD                    *
*--------------------------------------------------*;
DATA OVDFT(KEEP=ACCTNO ODINTACC SECTOR);
  SET MNITB.CURRENT(RENAME=(SECTOR=SECT));
  IF OPENIND NOT IN ('B','C','P') AND CURBAL LT 0;
  SECTOR=PUT(SECT,$4.);
RUN;
PROC SORT DATA=OVDFT; BY ACCTNO;
PROC SORT DATA=LOAN OUT=LOAN; BY ACCTNO;

DATA LOAN;
  MERGE LOAN(IN=A) OVDFT;
  BY ACCTNO; IF A;
RUN;

DATA LOAN(DROP=LASTTRAN);
  SET LOAN;
  IF PRODUCT LT 800 OR PRODUCT GT 899 THEN DO;
  IF BRANCH NE 998 AND PRODCD IN ('34180','34240') THEN LNTYPE = 'OD';
  IF BRANCH NE 998 AND PRODUCT NOT IN (500,520,199,983,993,996)
     AND PRODCD NOT IN ('34180','34240') THEN DO;
     IF PRODCD = '34120' THEN LNTYPE = 'HL';
     ELSE IF PRODCD = '54120' THEN LNTYPE = 'HL';
     ELSE IF PRODCD = '34111' THEN LNTYPE = 'HP';
     ELSE IF PRODCD IN ('34190') THEN LNTYPE = 'RC';
     ELSE IF PRODCD IN ('34170') THEN LNTYPE = 'ST';
     ELSE IF PRODCD NOT IN ('M','N') THEN LNTYPE = 'FL';
  END;
  END;
  ELSE DO;
     IF PRODCD='34690'          THEN LNTYPE='FR';
     IF PRODCD='34600' THEN DO;
        IF PRODUCT IN (800,801,805,807,809,811,813) THEN LNTYPE='FT';
        IF PRODUCT IN (804)     THEN LNTYPE='FS';
        IF PRODUCT NOT IN (800,801,802,803,804,805,807,809,811,813)
                                THEN LNTYPE='FN';
     END;
  END;
  IF PAYEFFDT NOT IN (.,0) THEN
  PAYEFDTO =(INPUT(SUBSTR(PUT(PAYEFFDT,Z11.),10,2),$2.)||'/'||
            INPUT(SUBSTR(PUT(PAYEFFDT,Z11.),8,2),$2.)||'/'||
            INPUT(SUBSTR(PUT(PAYEFFDT,Z11.),3,2),$2.));
  NEWBAL  =BALANCE-(FEEAMT+ACCRUAL);
  IF ISSUEDT NOT IN (.,0) THEN
     ISSDTE =INPUT(SUBSTR(PUT(ISSUEDT,Z11.),1,8),MMDDYY8.);
  IF FRELEAS NOT IN (.,0) THEN
     ORGISSDTE =INPUT(SUBSTR(PUT(FRELEAS,Z11.),1,8),MMDDYY8.);
  IF LASTTRAN NOT IN (.,0) THEN
     LASTRAN = INPUT(SUBSTR(PUT(LASTTRAN,Z11.),1,8),MMDDYY8.);
  IF ASSMDATE NE . THEN
     ASSMDATE = INPUT(SUBSTR(PUT(ASSMDATE,Z11.),1,8),MMDDYY8.);
  IF FCLOSUREDT NOT IN (.,0) THEN
     RRCOUNTDTE = INPUT(SUBSTR(PUT(FCLOSUREDT,Z11.),1,8),MMDDYY8.);
  IF INTSTDTE NOT IN (.,0) THEN DO;
     DD=SUBSTR(INTSTDTE,11,2);
     MM=SUBSTR(INTSTDTE,9,2);
     YY=SUBSTR(INTSTDTE,2,4);
     INTSTDTE=MDY(MM,DD,YY);
  END;
  IF CPNSTDTE NOT IN (.,0) THEN
     CPNSTDTE = INPUT(SUBSTR(PUT(CPNSTDTE,Z11.),1,8),MMDDYY8.);
  IF VALUEDTE NOT IN (.,0) THEN
     VALUATION_DT = INPUT(VALUEDTE,DDMMYY8.);

  FULLREL_DT = ORGISSDTE;
RUN;

DATA LOAN;
   SET LOAN;
   PAYD=SUBSTR(PAYEFDTO,1,2);
   PAYM=SUBSTR(PAYEFDTO,4,2);
   PAYY=SUBSTR(PAYEFDTO,7,2);

   IF PAYM EQ 2 AND PAYD > 29 THEN DO;
      PAYD=28;
      IF MOD(PAYY,4)=0 THEN PAYD=29;
   END;

   IF PAYM NE 2 AND PAYD > 31 THEN DO;
      IF PAYM IN (01,03,05,07,08,10,12)  THEN PAYD=31; ELSE
      IF PAYM IN (04,06,09,11)           THEN PAYD=30; ELSE
   END;
   PAYEFDT=MDY(PAYM,PAYD,PAYY);
RUN;

DATA PAYFI;
  FORMAT PAYMM PAYDD 2. PAYCY 4.;
  KEEP ACCTNO NOTENO PAYEFDT;
  INFILE PAYFI;
  INPUT  @001 ACCTNO   PD6.
         @007 NOTENO   PD3.
         @011 PAYEFDX  PD6.;
  PAYCY=SUBSTR(PUT(PAYEFDX,Z11.),1,4);
  PAYMM=SUBSTR(PUT(PAYEFDX,Z11.),8,2);
  PAYDD=SUBSTR(PUT(PAYEFDX,Z11.),10,2);

  IF PAYMM EQ 2 AND PAYDD > 29 THEN DO;
     PAYDD=28;
     IF MOD(PAYCY,4)=0 THEN PAYDD=29;
  END;

  IF PAYMM NE 2 AND PAYDD > 31 THEN DO;
     IF PAYMM IN (01,03,05,07,08,10,12)  THEN PAYDD=31; ELSE
     IF PAYMM IN (04,06,09,11)           THEN PAYDD=30; ELSE
  END;
  PAYEFDT=MDY(PAYMM,PAYDD,PAYCY);
*;
PROC SORT DATA=PAYFI; BY ACCTNO NOTENO DESCENDING PAYEFDT;
PROC SORT DATA=PAYFI NODUPKEY; BY ACCTNO NOTENO;
PROC SORT DATA=LOAN OUT=LN; BY ACCTNO NOTENO;
*;
DATA LOAN;
  MERGE PAYFI LN(IN=A);  BY ACCTNO NOTENO;
  IF A;
*;
DATA LOAN2;
  SET LOAN;
  IF LNTYPE = 'FL' AND PRODUCT IN (4,5,6,7,70,100,101) THEN DO;
     *** STAFF HL ***;
     LNTYPE = 'HL';
  END;
  LASTTRAN=LASTRAN;
RUN;
*;
DATA CIS;
   KEEP ACCTNO U2RACECO;
   SET CISL.LOAN;
   IF SECCUST='901';
   U2RACECO=RACE;
RUN;
*;
PROC SORT DATA=CIS OUT=CIS NODUPKEY; BY ACCTNO;
*;
DATA LOAN2;
  MERGE CIS LOAN2(IN=A); BY ACCTNO;
  IF A;
RUN;
*;
PROC SORT DATA=LOAN2; BY ACCTNO NOTENO;RUN;

DATA LOAN.LN&REPTYEAR&REPTMON&REPTDAY &LOAN;
   SET LOAN2;
   FORMAT NUMCPNS Z4.;
   MAKE = SUBSTR(COLLDESC,1,16);
   MODEL = SUBSTR(COLLDESC,16,21);
   CENSUS0 = CENSUS;
   CENSUS1 = SUBSTR(PUT(CENSUS,7.2),1,2);
   CENSUS3 = SUBSTR(PUT(CENSUS,7.2),3,1);
   CENSUS4 = SUBSTR(PUT(CENSUS,7.2),4,1);
   CENSUS5 = SUBSTR(PUT(CENSUS,7.2),6,2);
   THISDATE = INPUT("&RDATE", DDMMYY8.);
   IF BLDATE > 0 THEN DAYARR = THISDATE - BLDATE;
   IF OLDNOTEDAYARR > 0 AND 98000<=NOTENO<=98999 THEN DO;
      IF DAYARR < 0 THEN DAYARR = 0;
      DAYARR = SUM(DAYARR,OLDNOTEDAYARR);
   END;
   IF DAYARR_MO NE . THEN DAYARR = DAYARR_MO;
   IF COLLYEAR > 0 THEN COLLAGE=ROUND((YEAR(THISDATE)-COLLYEAR)+1,0.1);
   SELECT;
      WHEN (DAYARR>729) MTHARR = INT((DAYARR/365)*12);
      WHEN (DAYARR>698) MTHARR = 23;
      WHEN (DAYARR>668) MTHARR = 22;
      WHEN (DAYARR>638) MTHARR = 21;
      WHEN (DAYARR>608) MTHARR = 20;
      WHEN (DAYARR>577) MTHARR = 19;
      WHEN (DAYARR>547) MTHARR = 18;
      WHEN (DAYARR>516) MTHARR = 17;
      WHEN (DAYARR>486) MTHARR = 16;
      WHEN (DAYARR>456) MTHARR = 15;
      WHEN (DAYARR>424) MTHARR = 14;
      WHEN (DAYARR>394) MTHARR = 13;
      WHEN (DAYARR>364) MTHARR = 12;
      WHEN (DAYARR>333) MTHARR = 11;
      WHEN (DAYARR>303) MTHARR = 10;
      WHEN (DAYARR>273) MTHARR = 9;
      WHEN (DAYARR>243) MTHARR = 8;
      WHEN (DAYARR>213) MTHARR = 7;
      WHEN (DAYARR>182) MTHARR = 6;
      WHEN (DAYARR>151) MTHARR = 5;
      WHEN (DAYARR>121) MTHARR = 4;
      WHEN (DAYARR>89)  MTHARR = 3;
      WHEN (DAYARR>59)  MTHARR = 2;
      WHEN (DAYARR>30)  MTHARR = 1;
      OTHERWISE       MTHARR = 0;
   END;
   IF BRANCH NE 998 AND BALANCE NE 0 AND LNTYPE NE ' ' THEN DO;
      IF (3000000000<=ACCTNO<=3999999999) THEN DO;
         IF PRODUCT IN (31,50,51,52,53,54,55,56,57,58,59,60,
                        61,62,63,64,65,70,71) THEN DO;
            CORPROD = 'Y';
            RETAILID = 'C';
         END;
         ELSE RETAILID = 'R';
      END;
      ELSE IF (800<=PRODUCT<=899) OR
              (630<=PRODUCT<=649) OR
            PRODUCT IN (180,181,182,183,184,193,900,901,902,903,
                        904,905,906,907,908,909,910,914,915,916,
                        917,918,919,920,925,950,951,911,912,638,
                        639,188,189,190,688,689
                        ) THEN DO;
            CORPROD = 'Y';
            RETAILID = 'C';
      END;
      ELSE RETAILID = 'R';
   END;
   ELSE RETAILID = ' ';
RUN;

DATA LOAN.LN&REPTYEAR&REPTMON&REPTDAY (DROP=DAYARR_MO MTHARR_MO)
     LN&REPTYEAR&REPTMON&REPTDAY (DROP=DAYARR_MO MTHARR_MO)
     HP.HP&REPTYEAR&REPTMON&REPTDAY (DROP=F5ACCONV CCRIS_INSTLAMT
                                          MO_MAIN_DT)
     HP.HPWO&REPTYEAR&REPTMON&REPTDAY &HPWO;
   SET LOAN.LN&REPTYEAR&REPTMON&REPTDAY;
  IF PRODUCT IN &HP OR PRODUCT = 392 THEN DO;
     IF PRODUCT IN (678,679,698,699,983,993,996) THEN
        OUTPUT HP.HPWO&REPTYEAR&REPTMON&REPTDAY;
     ELSE
        OUTPUT HP.HP&REPTYEAR&REPTMON&REPTDAY;
  END;
  /* EXCLUDE WRITTEN-OFF/DOWN PRODUCT */
  IF PRODUCT NOT IN (678,679,698,699,983,993,996) THEN
     OUTPUT LOAN.LN&REPTYEAR&REPTMON&REPTDAY;
  OUTPUT LN&REPTYEAR&REPTMON&REPTDAY;
RUN;


FILENAME TRANFILE 'SAP.PBB.LNDATAWH.DAILY.DLNFTP' DISP=OLD;
PROC CPORT LIBRARY=WORK FILE=TRANFILE;
SELECT LN&REPTYEAR&REPTMON&REPTDAY;
RUN;
FILENAME TRANFILE 'SAP.PBB.LNDATAWH.DAILY.HPDFP' DISP=OLD;
PROC CPORT LIBRARY=HP FILE=TRANFILE;
RUN;
FILENAME TRANFILE 'SAP.PBB.LNDATAWH.INVLOC.INVLOCFP' DISP=OLD;
PROC CPORT LIBRARY=INV FILE=TRANFILE;
RUN;

PROC PRINT DATA=LOAN.REPTDATE;
PROC PRINT DATA=LOAN.LN&REPTYEAR&REPTMON&REPTDAY   (OBS=50);

