OPTIONS NOCENTER YEARCUTOFF=1950;
%INC PGM(PBBLNFMT);

%MACRO WEEKLY;
   %INC PGM(PBBWRDLF);
%MEND WEEKLY;

%MACRO MONTHLY;
   %INC PGM(PBBMRDLF);
%MEND MONTHLY;

%MACRO MRGBIC;
   DATA PBBRDAL1;
      SET PBBRDAL;
      IF SUBSTR(ITCODE,2,1)='0' THEN AMTIND=' ';
      ELSE AMTIND='D';
      AMOUNT=0;
   RUN;

   PROC SORT DATA=PBBRDAL1 OUT=PBBRDAL1;
   BY ITCODE AMTIND;
   RUN;

   PROC SORT DATA=BNM.ALW&REPTMON&NOWK OUT=ALW;
   BY ITCODE AMTIND;

   DATA RDAL;
      MERGE ALW       (RENAME=(AMOUNT=AMT1) IN=A)
            PBBRDAL1  (RENAME=(AMOUNT=AMT2) IN=B);
      BY ITCODE AMTIND;
      IF A AND B THEN DO;
        AMOUNT=AMT1;
      END;
      IF NOT A AND B THEN DO;
        AMOUNT=AMT2;
      END;
      IF A AND NOT B THEN DO;
        AMOUNT=AMT1;
      END;
      /* REMOVE UNWANTED ITEMS */
      IF NOT ('30221' <= SUBSTR(ITCODE,1,5) <= '30228') &
         NOT ('30231' <= SUBSTR(ITCODE,1,5) <= '30238') &
         NOT ('30091' <= SUBSTR(ITCODE,1,5) <= '30098') &
         NOT ('40151' <= SUBSTR(ITCODE,1,5) <= '40158') &
         SUBSTR(ITCODE,1,5) NOT IN ('NSSTS');
   RUN;
%MEND MRGBIC;

/************************************************************/
/** GET_BICS : GET THE COMPLETE SET OF BICS                **/
/************************************************************/

%MACRO GET_BICS;
   %IF "&NOWK" EQ "4" %THEN
        %MONTHLY;
   %ELSE
        %WEEKLY;
   %MRGBIC;
%MEND GET_BICS;

%GET_BICS;
*;
DATA CAG;
    SET LOAN.LNNOTE;
    IF  LOANTYPE IN (124,145);
    PRODCD='34120';
    AMTIND='I';
    IF  PZIPCODE IN (2002,2013,3039,3047,800003098,800003114,
                     800004016,800004022,800004029,800040050,
                     800040053,800050024,800060024,800060045,
                     800060081,80060085);
    ITCODE='7511100000000Y';
RUN;
*;
PROC SUMMARY DATA=CAG NWAY;
CLASS ITCODE AMTIND;
VAR   BALANCE;
OUTPUT OUT=CAG (DROP=_FREQ_ _TYPE_) SUM=AMOUNT;
RUN;
*;
DATA RDAL;
   SET RDAL CAG;
   IF  SUBSTR(ITCODE,1,3) IN ('331','421','426','431')
       THEN DELETE;
*;
PROC SORT DATA=RDAL;
   BY ITCODE AMTIND;

DATA AL OB SP;
   SET RDAL;
   IF AMTIND ^= ' ' THEN DO;
      IF SUBSTR(ITCODE,1,3) IN ('307') THEN OUTPUT SP;
      ELSE IF SUBSTR(ITCODE,1,5) IN ('40190') THEN OUTPUT SP;
      ELSE IF SUBSTR(ITCODE,1,4) = 'SSTS' THEN DO;
         ITCODE = '4017000000000Y';
         OUTPUT SP;
      END;
      ELSE IF SUBSTR(ITCODE,1,1) ^= '5' THEN DO;
         IF SUBSTR(ITCODE,1,3) IN ('685','785') THEN OUTPUT SP;
         ELSE OUTPUT AL;
      END;
      ELSE OUTPUT OB; END;
   ELSE IF SUBSTR(ITCODE,2,1)='0' THEN OUTPUT SP;
RUN;

DATA _NULL_;
   SET AL;
   BY ITCODE AMTIND;

   PHEAD='RDAL'||"&REPTDAY"||"&REPTMON"||"&REPTYEAR";

   FILE RDAL;
   IF _N_=1 THEN DO;
      PUT @1 PHEAD;
      PUT @1 'AL';
      AMOUNTD=0; AMOUNTI=0;
   END;

      RETAIN AMOUNTD AMOUNTI;

      AMOUNT=ROUND(AMOUNT/1000);
      IF AMTIND='D' THEN AMOUNTD+AMOUNT;
      ELSE IF AMTIND='I' THEN AMOUNTI+AMOUNT;

      IF LAST.ITCODE THEN DO;
         AMOUNTD=AMOUNTD+AMOUNTI;
         PUT @1 ITCODE +(-1) ';' AMOUNTD +(-1) ';' AMOUNTI;
         AMOUNTD=0; AMOUNTI=0;
      END;
RUN;

DATA _NULL_;
   SET OB;
   BY ITCODE AMTIND;

   FILE RDAL MOD;
   IF _N_=1 THEN DO;
      PUT @1 'OB';
      AMOUNTD=0; AMOUNTI=0;
   END;

   RETAIN AMOUNTD AMOUNTI;
   IF AMTIND='D' THEN AMOUNTD+ROUND(AMOUNT/1000);
   ELSE IF AMTIND='I' THEN AMOUNTI+ROUND(AMOUNT/1000);

   IF LAST.ITCODE THEN DO;
      AMOUNTD=AMOUNTD+AMOUNTI;
      PUT @1 ITCODE +(-1) ';' AMOUNTD +(-1) ';' AMOUNTI;
      AMOUNTD=0; AMOUNTI=0;
   END;
*;
PROC SORT DATA=SP OUT=SP;
   BY ITCODE;
*;
DATA _NULL_;
   SET SP;
   BY ITCODE;

   FILE RDAL MOD;
   IF _N_=1 THEN DO;
      PUT @1 'SP';
      AMOUNTD=0;
   END;

   AMOUNTD+AMOUNT;
   IF LAST.ITCODE THEN DO;
      AMOUNTD=ROUND(AMOUNTD/1000);
      PUT @1 ITCODE +(-1) ';' AMOUNTD +(-1);
      AMOUNTD=0;
   END;
RUN;
