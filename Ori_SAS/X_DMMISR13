*;
OPTIONS NODATE NONUMBER NOCENTER MISSING=0;

%INC PGM(PBMISFMT);

DATA REPTDATE;
    SET DEPOSIT.REPTDATE;
    REPTDAT1=REPTDATE-1;
    CALL SYMPUT('REPTMON',PUT(MONTH(REPTDATE),Z2.));
    CALL SYMPUT('REPTYEAR',PUT(REPTDATE,YEAR4.));
    CALL SYMPUT('RDATE',PUT(REPTDATE,DDMMYY8.));
    CALL SYMPUT('ZDATE',PUT(REPTDATE,Z5.));
    CALL SYMPUT('PDATE',PUT(REPTDAT1,Z5.));
RUN;

LIBNAME MIS  "SAP.PBB.MIS.D&REPTYEAR" DISP=SHR;
LIBNAME MISI "SAP.PIBB.MIS.D&REPTYEAR" DISP=SHR;

DATA SAVPRE (RENAME=(BALANCE=PREBAL))
     SAVTOD
     DDPRE  (RENAME=(BALANCE=PREBAL))
     DDTOD;
  **SET MIS.DYBRDP&REPTMON;
  SET MIS.DYDPBS&REPTMON;
  IF DPTYPE='S' THEN DO;
     IF REPTDATE=&PDATE THEN OUTPUT SAVPRE;
     ELSE IF REPTDATE=&ZDATE THEN OUTPUT SAVTOD; END;
  ELSE IF DPTYPE IN ('D','N') THEN DO;
     IF REPTDATE=&PDATE THEN OUTPUT DDPRE;
     ELSE IF REPTDATE=&ZDATE THEN OUTPUT DDTOD; END;
RUN;
*;
PROC SORT DATA=SAVPRE; BY BRANCH;
PROC SORT DATA=SAVTOD; BY BRANCH;
PROC SORT DATA=DDPRE; BY BRANCH;
PROC SORT DATA=DDTOD; BY BRANCH;
*;
DATA SAVMVNT;
   MERGE SAVPRE SAVTOD;
   BY BRANCH;
   BRCH=PUT(BRANCH,BRCHCD.);
*;
DATA DDMVNT;
   MERGE DDPRE DDTOD;
   BY BRANCH;
   BRCH=PUT(BRANCH,BRCHCD.);
*;
PROC PRINTTO PRINT=PMMISR13 NEW;
*;
TITLE1 'REPORT ID : DMMISR13';
TITLE2 'PUBLIC BANK BERHAD';
TITLE3 'PRODUCT DEVELOPMENT & MARKETING';
TITLE4 "DAILY MOVEMENT OF SAVING DEPOSITS BY BRANCH AS AT " &RDATE;
TITLE5;
*;
PROC REPORT DATA=SAVMVNT NOWD HEADLINE SPLIT='*';
  COLUMN REPTDATE BRANCH BRCH PREBAL BALANCE MOVEMENT;
  DEFINE REPTDATE  / DISPLAY FORMAT=DDMMYY8. 'DATE' NOPRINT;
  DEFINE BRANCH    / DISPLAY  'BRANCH' FORMAT=6.;
  DEFINE BRCH      / DISPLAY  '(ABBR)' FORMAT=$6.;
  DEFINE PREBAL    / ANALYSIS SUM FORMAT=COMMA18.2;
  DEFINE BALANCE   / ANALYSIS SUM FORMAT=COMMA18.2;
  DEFINE MOVEMENT  / COMPUTED FORMAT=COMMA20.2
                   'NET INCREASE/*DECREASE (A-B)';
  COMPUTE MOVEMENT;
     MOVEMENT=SUM(BALANCE.SUM,(-1)*PREBAL.SUM);
  ENDCOMP;
  RBREAK AFTER / PAGE DUL OL SUMMARIZE;
RUN;
*;
PROC PRINTTO PRINT=PMMISR23 NEW;
*;
TITLE4 "DAILY MOVEMENT OF DEMAND DEPOSITS BY BRANCH AS AT " &RDATE;
TITLE5;
*;
PROC REPORT DATA=DDMVNT NOWD HEADLINE SPLIT='*';
  COLUMN REPTDATE BRANCH BRCH PREBAL BALANCE MOVEMENT;
  DEFINE REPTDATE/ DISPLAY FORMAT=DDMMYY8. 'DATE' NOPRINT;
  DEFINE BRANCH / DISPLAY  'BRANCH' FORMAT=6.;
  DEFINE BRCH   / DISPLAY  '(ABBR)' FORMAT=$6.;
  DEFINE PREBAL  / ANALYSIS SUM FORMAT=COMMA18.2;
  DEFINE BALANCE / ANALYSIS SUM FORMAT=COMMA18.2;
  DEFINE MOVEMENT/ COMPUTED FORMAT=COMMA20.2
                   'NET INCREASE/*DECREASE (A-B)';
  COMPUTE MOVEMENT;
     MOVEMENT=SUM(BALANCE.SUM,(-1)*PREBAL.SUM);
  ENDCOMP;
  RBREAK AFTER / PAGE DUL OL SUMMARIZE;
RUN;
