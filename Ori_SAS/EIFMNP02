*+--------------------------------------------------------------+
 |  PROGRAM : EIFMNP02                                          |
 |  DATE    : 29.01.99                                          |
 |  MODIFY  : ESMR 2004-720, 2004-579, 2006-1048                |
 |  FUNCTION: PREPARE NPL ACCOUNTS FOR NEW AND OLD GUIDELINES   |
 |  BORSTAT CODES DESCRIPTION :                                 |
 |  F - DEFICIT                                                 |
 |  I - IRREGULAR                                               |
 |  R - REPOSSES                                                |
 |  T - INSURANCE TOTAL LOST CLAIM WHERE PAYMENT HAS NOT BEEN   |
 |      RECEIVED. WILL CHANGE TO F WHENEVER PAYMENT RECEIVED BUT|
 |      NOT ENOUGH TO COVER THE BALANCE.                        |
 |  S - RESTRUCTURE                                             |
 |  W - CURRENT YEAR WRITTEN OFF                                |
 |  Z - PRIOR YEAR WRITTEN OFF                                  |
 |  Y - RECOVERY APPEAR DOUBTFUL                                |
 |  A - ??? MAYBE NPL TURN PERFORMING LN (REINSTATE)            |
 +--------------------------------------------------------------+;
OPTIONS YEARCUTOFF=1950;
*;
DATA NPL6.REPTDATE;
   SET LOAN.REPTDATE;
   CALL SYMPUT('REPTMON',PUT(MONTH(REPTDATE),Z2.));
RUN;
*;
*------------------------------------------------*
*  MACRO FOR CALCULATING NEXT BLDATE             *
*------------------------------------------------*;
%MACRO DCLVAR;
   RETAIN D1-D12 31 D4 D6 D9 D11 30;
   ARRAY LDAY D1-D12;
%MEND DCLVAR;
*;
%MACRO NXTBLDT;
   DD = DAY(ISSDTE);
   MM = MONTH(BLDATE) + 1;
   YY = YEAR(BLDATE);
   IF MM > 12 THEN DO;
      MM = 1; YY + 1;
   END;
   IF MM = 2 THEN
      IF MOD(YY,4) = 0 THEN D2 = 29;
      ELSE D2 = 28;
   IF DD > LDAY(MM) THEN DD = LDAY(MM);
   BLDATE = MDY(MM,DD,YY);
%MEND NXTBLDT;
*;
*------------------------------------------------*
*  MACRO FOR CALCULATING OVERDUE INTEREST        *
*------------------------------------------------*;
%MACRO OVINT;
   IF LOANTYPE = 705 THEN DO;
      IF NOTETERM > 12 THEN TERM = 12;
      ELSE TERM = NOTETERM;
      TRATE = NOTETERM*INTRATE;
      APR = TRATE*(300*TERM+TRATE)/
            ((NOTETERM*TRATE)+(150*TERM*(NOTETERM+1)))*12/TERM;
      RATE = (APR+1)/100;
   END;
   ELSE RATE = 8/100;
   BILAMT = BILPAY;
   BILAMTL = ORGBAL-BILPAY*(NOTETERM-1);
   DO REMMTH = REMMTH1 TO REMMTH2 BY -1;
      IF REMMTH = 0 THEN AMT = BILAMTL;
      ELSE AMT = BILAMT;
      %NXTBLDT
      OI + AMT*RATE*(REPTDATE-BLDATE)/365;
   END;
%MEND OVINT;
*;
PROC SORT DATA=LOAN.LNNOTE OUT=LOAN;
   BY ACCTNO NOTENO;
   WHERE REVERSED^='Y' & NOTENO^=. & PAIDIND^='P' & NTBRCH^=. &
         LOANTYPE IN (128,130,380,381,700,705,131,132,720,725);
*;
RUN;

PROC SORT DATA=LOAN.LNNOTE OUT=LNNOTE(DROP=NFEEAMT10 NFEEAMT11
   NFEEAMT12); BY ACCTNO DESCENDING NOTENO;
   WHERE REVERSED^='Y' & NOTENO^=. & NTBRCH^= . &
         LOANTYPE IN (128,130,700,705,983,993,996,380,381,131,132,
                      720,725);
RUN;
PROC SORT DATA=LNNOTE NODUPKEY;
   BY ACCTNO;
RUN;

PROC SORT DATA=LOAN.LNNOTE OUT=LOANNO(KEEP=ACCTNO NOTENO);
   BY ACCTNO DESCENDING NOTENO;
   WHERE REVERSED^='Y' & NOTENO^=. & PAIDIND^='P' & NTBRCH^=. &
         LOANTYPE IN (128,130,380,381,700,705,983,993,996,131,132,
                      720,725);
RUN;
PROC SORT DATA=LOANNO NODUPKEYS; BY ACCTNO; RUN;

DATA LNNOTE;
   MERGE LNNOTE(IN=A) LOANNO;
   BY ACCTNO;
   IF A;
RUN;

PROC SORT DATA=NPL6.WIIS; BY ACCTNO;
DATA NPL6.WIIS;
   MERGE NPL6.WIIS (IN=A) LNNOTE;
   BY ACCTNO;
   IF A;
RUN;
PROC SORT DATA=NPL6.WAQ; BY ACCTNO;
DATA NPL6.WAQ;
   MERGE NPL6.WAQ (IN=A) LNNOTE;
   BY ACCTNO;
   IF A;
RUN;
PROC SORT DATA=NPL6.WSP2; BY ACCTNO;
DATA NPL6.WSP2;
   MERGE NPL6.WSP2 (IN=A) LNNOTE;
   BY ACCTNO;
   IF A;
RUN;
DATA NPLOBAL6;
   MERGE NPL6.NPLOBAL (IN=A KEEP=ACCTNO NOTENO NTBRCH NAME LOANTYPE
         LOANSTAT IISP OIP SPP1 SPP2 CURBALP NETBALP EXIST UHCP)
         LNNOTE (DROP=LOANSTAT)
         NPL6.WIIS (IN=B KEEP=ACCTNO);
   BY ACCTNO;
   IF B THEN BORSTAT = 'W';
   IF A OR B THEN OUTPUT;
RUN;

*------------------------------------------------*
*  NEW GUIDELINE (NPL FROM 6 MONTHS)             *
*------------------------------------------------*;
DATA NPL6.LOAN&REPTMON
     NPL6.PLOAN&REPTMON;
   KEEP LOANTYPE NTBRCH ACCTNO NOTENO NAME BORSTAT LOANSTAT NETPROC
        ORGBAL CURBAL NOTETERM ISSDTE BLDATE BILPAY INTRATE APPVALUE
        MARKETVL DAYS TERMCHG IISP OIP SPP1 SPP2 CURBALP NETBALP USER5
        FEEAMT FEEAMT3 FEEAMT4 EXIST FEEYTD FEEPDYTD UHCP CENSUS7
        VINNO FEETOT2 FEEAMT8 PENDBRH COSTCTR EARNTERM FEEAMTA FEEAMT5
        OLDNOTEDAYARR ACCRUAL;
   LENGTH LOANTYPE NTBRCH NOTETERM ISSDTE BLDATE DAYS 3 LOANSTAT 2;
   %DCLVAR
   MERGE NPLOBAL6 (IN=AA) LOAN (IN=BB DROP=LOANSTAT);
   BY ACCTNO;
   IF _N_ = 1 THEN SET LOAN.REPTDATE;
   IF BORSTAT ^= 'Z';
   ISSDTE = INPUT(SUBSTR(PUT(ISSUEDT,Z11.),1,8),MMDDYY8.);
   CENSUS7 = SUBSTR(PUT(CENSUS,8.2),7,1);
   IF ACCTNO = 8095419311 THEN BLDATE = '21JAN1997'D;
   IF BLDATE > 0 THEN DO;
      IF BILPAY > 0 THEN DO;
         IF BILTOT = . THEN BILTOT = 0;
         IF BILTOT / BILPAY <= 0.01 THEN DO;
            %NXTBLDT
         END;
      END;
      DAYS = REPTDATE - BLDATE;
      IF OLDNOTEDAYARR > 0 AND 98000<=NOTENO<=98999 THEN DO;
         IF DAYS < 0 THEN DAYS = 0;
         DAYS = SUM(DAYS,OLDNOTEDAYARR);
      END;
   END;
   IF INTAMT > 0.1 THEN TERMCHG = INTAMT + INTEARN2;
   ELSE TERMCHG = 0;
   IF BORSTAT NOT IN ('F','I','R','Y','W','Z','A') &
      NOTENO IN (98010,98011,99010,99011) THEN BORSTAT = 'S';
*;
   IF AA THEN DO;
      IF ^BB THEN DO;
         LOANSTAT = .;
         DAYS = 0;
         BORSTAT = ' ';
      END;
    **  TEMPORARY FOR FEB99 RUN  **;
      ELSE IF DAYS <= 0 THEN LOANSTAT = 1;
      IF LOANSTAT = 1 AND BORSTAT = ' ' THEN BORSTAT = 'A';
    ****;
    * ELSE IF BORSTAT = 'A' THEN LOANSTAT = 1;
    * IF LOANSTAT = 1 AND BORSTAT = ' ' THEN BORSTAT = 'A';
    OUTPUT NPL6.LOAN&REPTMON;
   END;
   ELSE IF DAYS > 89 OR BORSTAT NOT IN (' ','A','C','S','T')
     OR USER5 = 'N' THEN DO;
        IF BORSTAT = 'Y' AND DAYS <= 182 THEN
           OUTPUT NPL6.PLOAN&REPTMON;
        ELSE OUTPUT NPL6.LOAN&REPTMON;
   END;
   ELSE OUTPUT NPL6.PLOAN&REPTMON;
RUN;


