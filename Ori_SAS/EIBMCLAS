* ******************************************************
* MONTHLY REPORT ON MORE PLAN LOANS - PURPOSE OTHERS
* ******************************************************;

OPTIONS YEARCUTOFF=1950 LS=132 PS=60 NOCENTER;
%INC PGM(PBBLNFMT);

DATA REPTDATE (KEEP=REPTDATE);
   SET LOAN.REPTDATE;
   SELECT;
     WHEN(1 <= DAY(REPTDATE) <= 8) CALL SYMPUT('NOWK', PUT('1', $1.));
     WHEN(9 <= DAY(REPTDATE) <= 15) CALL SYMPUT('NOWK', PUT('2', $1.));
     WHEN(16 <= DAY(REPTDATE) <= 22) CALL SYMPUT('NOWK', PUT('3', $1.));
     OTHERWISE CALL SYMPUT('NOWK', PUT('4', $1.));
   END;
   CALL SYMPUT('REPTMON', PUT(MONTH(REPTDATE), Z2.));
   CALL SYMPUT('REPTDT',PUT(REPTDATE,DDMMYY8.));
RUN;

DATA LNNOTE;
     SET LOAN.LNNOTE;
     IF LOANTYPE IN &MOREPLAN AND
        SECTOR IN ('0311','0312','0313','0314','0315','0316');
RUN;

PROC SORT DATA=LNNOTE OUT=LNNOTE; BY ACCTNO NOTENO; RUN;
PROC SORT DATA=BNM.LOAN&REPTMON&NOWK OUT=LOAN;
     WHERE PRODUCT IN &MOREPLAN;
     BY ACCTNO NOTENO; RUN;

DATA LOAN;
     MERGE LNNOTE(IN=AA) LOAN;
     BY ACCTNO NOTENO;
     IF AA;
     RECLASS = BALANCE * APPORMT;
     CALL SYMPUT('LNTYPE', 'MORE PLAN LOANS');
RUN;

%MACRO PRNRPT;
   PROC SUMMARY DATA=LOAN NWAY;
     CLASS BRANCH;
     VAR BALANCE RECLASS;
     OUTPUT OUT=CLASS (RENAME=(_FREQ_=NOACCT)) SUM=;

   PROC REPORT DATA=CLASS NOCENTER
     HEADLINE HEADSKIP NOWD SPLIT='*' MISSING;
     COLUMN BRANCH BALANCE RECLASS;
     DEFINE BRANCH    / ORDER FORMAT=6.  'BRANCH';
     DEFINE BALANCE   / SUM FORMAT=COMMA15.2
                        'BALANCE';
     DEFINE RECLASS   / SUM FORMAT=COMMA15.2
                        'AMOUNT TO*RECLASS TO* 100% RISK';
     RBREAK AFTER  / DUL DOL SUMMARIZE;
     COMPUTE AFTER BRANCH;
     ENDCOMP;
     TITLE1 'REPORT ID : EIBMCLAS';
     TITLE2 'MONTHLY REPORT ON '&LNTYPE '(PURPOSE OTHER) - '&REPTDT;
   RUN;
%MEND;

PROC SORT DATA=LOAN OUT=LOAN; BY BRANCH; RUN;
%PRNRPT;

DATA LOAN;
     SET LOAN;
     IF PRODUCT IN &MOREISLM;
     CALL SYMPUT('LNTYPE', 'ISLAMIC MORE PLAN LOANS');
RUN;
PROC SORT DATA=LOAN OUT=LOAN; BY BRANCH; RUN;
%PRNRPT;
