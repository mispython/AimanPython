*+--------------------------------------------------------------+
 |  PROGRAM : EIBMRM04                                          |
 |  DATE    : 14.07.99                                          |
 |  REPORT  : LOANS & ADVANCES, BY TIME TO MATURITY FOR ALCO    |
 |            (WEIGHTED AVERAGE YIELD BY MATURITY PROFILE)      |
 +--------------------------------------------------------------+;
OPTIONS YEARCUTOFF=1950 NOCENTER NODATE NONUMBER MISSING=0;
*;
PROC FORMAT;
   VALUE LNPRDF
      4,5,15,20,25-32              = 'STAFF'
      110-115                      = 'HL(SPTF)'
      200,201,204,205,209-212,214,
      215,219,220                  = 'HL(CONV)'
      309,310,905                  = 'BRIDGING'
      325,330,355                  = 'PERSONAL'
      350,910,925                  = 'RC'
      914,915,919                  = 'SYND'
      33,116,180,227,228,230-234,300,
      301,304,305,320,335,500,504,
      505,510,515-525,555,556,560,
      561,564,565,570,573,900,901  = 'OTHER TL'
      OTHER                        = 'OTHERS';
*;
   VALUE ODPRDF
      160,162                      = 'OD(SPTF)'
      OTHER                        = 'OD(CONV)';
*;
   INVALUE SLNPRDF
      4,5,15,20,25-32              = 2   /* STAFF LN */
      110-115                      = 2   /* HSE (SPTF) */
      33,116,500,504,505,510,
      515-525,555,556,560,561,564,
      565,570                      = 2   /* OTHER TL */
      200,201,204,205,209,210-212,
      214,215,219,220              = 3   /* HSE (CONV) */
      227,228,230-234,300,301,304,
      305,320,335,900,901          = 3   /* OTHER TL */
      309,310,905                  = 3   /* BRIDGING */
      325,330,355                  = 3   /* PERSONAL */
      914,915,919                  = 3   /* SYNDICATE */
      350,910,925                  = 3   /* RC       */
      OTHER                        = 1;
*;
   INVALUE SODPRDF
      160,162                         = 2
      OTHER                           = 3;
*;
   VALUE SUBTYPF
      1   = 'PRINCIPAL'
      2   = 'PRIN(FIXED)'
      3   = 'PRIN(FLOAT)'
      7   = 'ACCRUED INT'
      8   = 'FEE AMOUNT'
      9   = 'NPL';
*;
   VALUE REMFMT
      LOW-1   = 'UP TO 1 MTH'
      1-2     = '>1  MTH - 2 MTHS'
      2-3     = '>2  MTHS - 3 MTHS'
      3-4     = '>3  MTHS - 4 MTHS'
      4-5     = '>4  MTHS - 5 MTHS'
      5-6     = '>5  MTHS - 6 MTHS'
      6-7     = '>6  MTHS - 7 MTHS'
      7-8     = '>7  MTHS - 8 MTHS'
      8-9     = '>8  MTHS - 9 MTHS'
      9-10    = '>9  MTHS - 10 MTHS'
      10-11   = '>10 MTHS - 11 MTHS'
      11-12   = '>11 MTHS - 12 MTHS'
      12-24   = '>1  YR - 2 YRS'
      24-36   = '>2  YRS - 3 YRS'
      36-48   = '>3  YRS - 4 YRS'
      48-60   = '>4  YRS - 5 YRS'
      OTHER   = '>5  YEAR';
*;
*------------------------------------------------*
*  MACRO TO DECLARE VARIABLES                    *
*------------------------------------------------*;
%MACRO DCLVAR;
   RETAIN D1-D12 31 D4 D6 D9 D11 30
          RD1-RD12 MD1-MD12 31 RD2 MD2 28 RD4 RD6 RD9 RD11
          MD4 MD6 MD9 MD11 30 RPYR RPMTH RPDAY;
   ARRAY LDAY D1-D12;
   ARRAY RPDAYS RD1-RD12;
   ARRAY MDDAYS MD1-MD12;
%MEND DCLVAR;
*;
*------------------------------------------------*
*  MACRO TO CALCULATE NEXT BLDATE                *
*------------------------------------------------*;
%MACRO NXTBLDT;
   IF PAYFREQ = '6' THEN DO;
      DD = DAY(BLDATE) + 14;
      MM = MONTH(BLDATE);
      YY = YEAR(BLDATE);
      IF MM = 2 THEN
         IF MOD(YY,4) = 0 THEN D2 = 29;
         ELSE D2 = 28;
      IF DD > LDAY(MM) THEN DO;
         DD = DD - LDAY(MM);
         MM + 1;
         IF MM > 12 THEN DO;
            MM = MM - 12; YY + 1;
         END;
      END;
   END;
   ELSE DO;
      DD = DAY(ISSDTE);
      MM = MONTH(BLDATE) + FREQ;
      YY = YEAR(BLDATE);
      IF MM > 12 THEN DO;
         MM = MM - 12; YY + 1;
      END;
   END;
   IF MM = 2 THEN
      IF MOD(YY,4) = 0 THEN D2 = 29;
      ELSE D2 = 28;
   IF DD > LDAY(MM) THEN DD = LDAY(MM);
   BLDATE = MDY(MM,DD,YY);
%MEND NXTBLDT;
*;
*------------------------------------------------*
*  MACRO TO CALCULATE REMAIN MONTH               *
*------------------------------------------------*;
%MACRO REMMTH;
   MDYR  = YEAR(MATDT);
   MDMTH = MONTH(MATDT);
   MDDAY = DAY(MATDT);
   IF MDMTH = 2 THEN
      IF MOD(MDYR,4) = 0 THEN MD2 = 29;
      ELSE MD2 = 28;
   IF MDDAY > RPDAYS(RPMTH) THEN MDDAY = RPDAYS(RPMTH);
   REMY = MDYR - RPYR;
   REMM = MDMTH - RPMTH;
   REMD = MDDAY - RPDAY;
   REMMTH = REMY*12 + REMM + REMD/RPDAYS(RPMTH);
%MEND REMMTH;
*;
*------------------------------------------------*
*  GET REPTDATE                                  *
*------------------------------------------------*;
DATA REPTDATE;
   SET BNM.REPTDATE;
   SELECT(DAY(REPTDATE));
      WHEN (8)  CALL SYMPUT('NOWK',PUT('1',$1.));
      WHEN (15) CALL SYMPUT('NOWK',PUT('2',$1.));
      WHEN (22) CALL SYMPUT('NOWK',PUT('3',$1.));
      OTHERWISE CALL SYMPUT('NOWK',PUT('4',$1.));
   END;
   CALL SYMPUT('REPTYEAR',PUT(REPTDATE,YEAR4.));
   CALL SYMPUT('REPTMON',PUT(MONTH(REPTDATE),Z2.));
   CALL SYMPUT('REPTDAY',PUT(DAY(REPTDATE),Z2.));
   CALL SYMPUT('RDATE',PUT(REPTDATE,DDMMYY8.));
RUN;
*;
*------------------------------------------------*
*  LOANS - FL/HL USE REPAYMENT DATE              *
*        - OD/RC USE EXPIRY DATE                 *
*------------------------------------------------*;
DATA NOTE (KEEP=PRODTYP SUBTYP REMMTH AMOUNT YIELD);
   LENGTH SUBTYP 2;
   %DCLVAR
     SET BNM.LOAN&REPTMON&NOWK;
   IF _N_ = 1 THEN DO;
      SET REPTDATE;
      RPYR  = YEAR(REPTDATE);
      RPMTH = MONTH(REPTDATE);
      RPDAY = DAY(REPTDATE);
      IF MOD(RPYR,4) = 0 THEN RD2 = 29;
   END;
   IF SUBSTR(PRODCD,1,2) = '34' OR PRODUCT IN (225,226);
   IF BALANCE ^= 0;
*;
   *------------------------------------------------*
   *  SPLIT AMOUNT INTO PRINCIPAL, ACCRUAL & FEES   *
   *------------------------------------------------*;
   IF ACCTYPE = 'OD' THEN DO;
      CURBAL = BALANCE;
    * ACRINT = INTPAYBL;
      ACRINT = 0;
      FEEAMT = 0;
      PRODTYP = PUT(PRODUCT,ODPRDF.);
   END;
   ELSE DO;
      ACRINT = 0;
      IF BALANCE < 0 THEN ACRINT = BALANCE;
      ELSE IF BALANCE ^= FEEAMT THEN
         ACRINT = BALANCE - CURBAL - FEEAMT;
      PRODTYP = PUT(PRODUCT,LNPRDF.);
   END;
*;
   *------------------------------------------------*
   *  OUTPUT ACCRUED INT, FEE AMOUNT FOR ALL LOANS  *
   *------------------------------------------------*;
   REMMTH = 0.1;
   SUBTYP = 7; AMOUNT = ACRINT; OUTPUT;
   SUBTYP = 8; AMOUNT = FEEAMT; OUTPUT;
*;
   *------------------------------------------------*
   *  IF NON PERFORMING LOANS                       *
   *------------------------------------------------*;
  /*  IF BIGX ^= 1 THEN BLDATE = .;  */
   IF BLDATE > 0 THEN DAYS = REPTDATE - BLDATE;
   IF DAYS > 89 OR LOANSTAT = 3 OR ODSTAT IN ('RI','NI') THEN DO;
      REMMTH = 0.1; SUBTYP = 9;
      AMOUNT = CURBAL;
      YIELD = AMOUNT * INTRATE;
      OUTPUT;
      RETURN;
   END;
*;
   *------------------------------------------------*
   *  IF PERFORMING LOANS                           *
   *------------------------------------------------*;
   IF ACCTYPE = 'OD' THEN DO;
      REMMTH = 0.1;
      AMOUNT = CURBAL;
      YIELD = AMOUNT * INTRATE;
      SUBTYP = INPUT(PUT(PRODUCT,3.),SODPRDF.);
      OUTPUT;
      RETURN;
   END;
*;
 * IF ACCTYPE = 'LN';
   SUBTYP = INPUT(PUT(PRODUCT,3.),SLNPRDF.);
   /*  CONVERT PAYFREQ CODE TO MONTHS  */
   SELECT (PAYFREQ);
      WHEN ('1') FREQ = 1;
      WHEN ('2') FREQ = 3;
      WHEN ('3') FREQ = 6;
      WHEN ('4') FREQ = 12;
      OTHERWISE;
   END;
   IF PAYFREQ IN ('5','9',' ') OR PRODUCT IN (350,910,925) THEN
      BLDATE = EXPRDATE;
   ELSE IF BLDATE <= 0 THEN DO;
      BLDATE = ISSDTE;
      DO WHILE (BLDATE <= REPTDATE);
         %NXTBLDT
      END;
   END;
   IF BILPAY <= 0 THEN BILPAY = PAYAMT;
   IF BLDATE > EXPRDATE | CURBAL <= BILPAY THEN BLDATE = EXPRDATE;
   DO WHILE (BLDATE <= EXPRDATE);
      MATDT = BLDATE;
      %REMMTH
      IF REMMTH > 60 OR BLDATE = EXPRDATE THEN LEAVE;
      AMOUNT = BILPAY;
      YIELD = AMOUNT * INTRATE;
      OUTPUT;
      CURBAL = CURBAL - BILPAY;
      %NXTBLDT
      IF BLDATE > EXPRDATE | CURBAL <= BILPAY THEN BLDATE = EXPRDATE;
   END;
   AMOUNT = CURBAL;
   YIELD = AMOUNT * INTRATE;
   OUTPUT;
*;
*------------------------------------------------*
*  SUMMARISE FOR CALCULATION OF WT AV YIELD      *
*------------------------------------------------*;
PROC SUMMARY DATA=NOTE NWAY;
   FORMAT REMMTH REMFMT.;
   CLASS PRODTYP SUBTYP REMMTH;
   VAR AMOUNT YIELD;
   OUTPUT OUT=NOTE (DROP=_TYPE_ _FREQ_) SUM=;
*;
DATA NOTE;
   SET NOTE;
   IF SUBTYP NOT IN (7,8) THEN
      WAYLD = YIELD / AMOUNT;
   AMOUNT = ROUND(AMOUNT/1000);
*;
*------------------------------------------------*
*  PRODUCE REPORTS                               *
*------------------------------------------------*;
TITLE1 'PUBLIC BANK BERHAD';
TITLE2 'TIME TO MATURITY AS AT' &RDATE;
TITLE3 'RISK MANAGEMENT REPORT : EIBMRM04';
TITLE4 'RM DENOMINATION';
*;
PROC TABULATE DATA=NOTE MISSING NOSEPS;
   FORMAT SUBTYP SUBTYPF.;
   CLASS PRODTYP SUBTYP REMMTH;
   VAR AMOUNT WAYLD;
   TABLE PRODTYP=' '*SUBTYP=' ' ALL='TOTAL',
         (ALL='TOTAL' REMMTH=' ')*SUM=' '*
         (AMOUNT='BALANCE O/S (RM''000)'*F=COMMA12.
          WAYLD='W.A. YIELD'*F=6.2)
         / BOX='LOANS AND ADVANCES' RTS=24 CONDENSE;
/*
//
