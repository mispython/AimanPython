*+--------------------------------------------------------------+
 |  PROGRAM : EIFMNP22                                          |
 |  DATE    : 01.11.99                                          |
 |  FUNCTION: REPTS TO FLAT FILE FOR DOWNLOADING BY ACCOUNTS,PFB|
 |  REPORTS : OUTSTANDING BALANCE FOR PC/FEE RECEIVABLE         |
 |            1. BY RISK AND BRANCH                             |
 |            2. ALL HP LOANS                                   |
 +--------------------------------------------------------------+;
OPTIONS YEARCUTOFF=1950 NOCENTER NODATE NONUMBER MISSING=0;
*;
%INC PGM(PFBLNFMT,PFBELF);
*;
PROC FORMAT;
   VALUE LNTYP 110,115,983             = 'HPD AITAB'
               700,705,993             = 'HPD CONVENTIONAL'
               200-299                 = 'HOUSING LOANS'
               300-499,504-550,900-980 = 'FIXED LOANS'
               OTHER   = 'OTHERS';
*;
DATA REPTDATE;
   SET NPL.REPTDATE;
   CALL SYMPUT('RDATE',PUT(REPTDATE,WORDDATX18.));
   CALL SYMPUT('REPTMON',PUT(MONTH(REPTDATE),Z2.));
RUN;
*;
DATA LNNOTE;
   SET LOAN.LNNOTE;
   WHERE REVERSED^='Y' & NOTENO^=. & NTBRCH^=. &
         LOANTYPE IN (110,115,700,705);
*;
DATA FEPLAN;
   INFILE FEEFILE;
   INPUT @001 ACCTNO    PD6.
         @007 NOTENO    PD3.
         @010 LOANTYPE  PD2.
         @022 FEEPLN    $2.
         @035 FEEAMT4   PD8.2
         @075 FEEAMT    PD8.2;

      IF LOANTYPE IN (110,115,700,705) AND FEEPLN IN ('DC');
*;
PROC SORT DATA=FEPLAN;
BY ACCTNO NOTENO;
*;
PROC SUMMARY DATA=FEPLAN;
BY ACCTNO NOTENO;
VAR   FEEAMT FEEAMT4; OUTPUT OUT=FEEPO (DROP=_TYPE_ _FREQ_)
                             SUM=FEEAMT3A FEEAMT4A;
DATA LNNOTE;
   MERGE LNNOTE(IN=A) FEEPO; BY ACCTNO NOTENO;
   IF A;
   FEEAMT3 = SUM(FEEAMT3,FEEAMT3A);
   FEEAMT4 = SUM(FEEAMT4,FEEAMT4A);
*;
DATA LOAN;
   KEEP LOANTYP RISK BRANCH FEEAMT3 FEEAMT4;
   LENGTH RISK $13 LOANTYP $20;
   SET NPL.LOAN&REPTMON;
   IF LOANTYPE IN (110,115,700,705);
   IF DAYS > 364 THEN RISK = 'BAD';
   ELSE IF DAYS > 273 THEN RISK = 'DOUBTFUL';
   ELSE IF DAYS > 182 THEN RISK = 'SUBSTANDARD 2';
   ELSE RISK = 'SUBSTANDARD-1';
   BRANCH = PUT(NTBRCH,BRCHCD.)||' '||PUT(NTBRCH,Z3.);
   LOANTYP = PUT(LOANTYPE,LNTYP.);
*;
*------------------------------------------------*
*  PRODUCE REPORTS                               *
*------------------------------------------------*;
PROC PRINTTO PRINT=OUTFL NEW;
TITLE1 'PUBLIC BANK BERHAD';
TITLE2 'OUTSTANDING BALANCE FOR PC/FEE RECEIVABLE AS AT' &RDATE;
*;
PROC TABULATE MISSING NOSEPS;
   TITLE3 '(NPL FROM 6 MONTHS & ABOVE)';
   CLASS LOANTYP RISK BRANCH;
   VAR FEEAMT3 FEEAMT4;
   TABLE LOANTYP=' ',
         RISK=' '*(BRANCH=' ' ALL='SUB-TOTAL'),
         ALL='TOTAL FEE'*(
         SUM=' '*FEEAMT3='PC/FEE REC'*F=COMMA20.2
         SUM=' '*FEEAMT4='CURR FEES'*F=COMMA20.2)
         / BOX='RISK        BRANCH' RTS=29;
*;
PROC TABULATE DATA=LNNOTE MISSING NOSEPS;
   VAR FEEAMT3 FEEAMT4;
   TABLE SUM=' '*FEEAMT3='PC/FEE RECEIVABLE'*F=COMMA20.2
         SUM=' '*FEEAMT4='AMT ACCESS CURR'*F=COMMA20.2;
   WHERE REVERSED^='Y' & NOTENO^=. & NTBRCH^=. &
         LOANTYPE IN (110,115,700,705,709,710,750,752,760,770,775,799);
   TITLE3 '(ALL HP LOANS)';

/*
