*;
OPTIONS NODATE NONUMBER NOCENTER MISSING=0;

%INC PGM(PBBDPFMT,PBMISFMT);

DATA REPTDATE;
    REPTDATE=TODAY()-1;
    CALL SYMPUT('REPTMON',PUT(MONTH(REPTDATE),Z2.));
    CALL SYMPUT('REPTYEAR',PUT(REPTDATE,YEAR4.));
    CALL SYMPUT('RDATE',PUT(REPTDATE,DDMMYY8.));
    CALL SYMPUT('ZDATE',PUT(REPTDATE,Z5.));
RUN;

LIBNAME MIS "SAP.PBB.MIS.D&REPTYEAR" DISP=SHR;

%MACRO SAVING;
DATA SDMVNT;
  SET MIS.DYMVNT&REPTMON;
  WHERE DEPTYPE EQ 'S' AND REPTDATE EQ &ZDATE;
  MOVEMENT=ROUND(CREDIT-DEBIT,.01);
  BRCH=PUT(BRANCH,BRCHCD.);
  SATYPE='CONV';
  IF PRODUCT IN (204,207,214,215) THEN SATYPE='ISLM';
RUN;
*;
PROC SORT DATA=SDMVNT;
BY DESCENDING MOVEMENT ACCTNO SATYPE;
*;
DATA _NULL_;
  CALL SYMPUT('SDMVNT', N);
  SET SDMVNT NOBS=N;
RUN;
*;
TITLE1 'REPORT ID: DMMISR02';
TITLE2 'PUBLIC BANK BERHAD ';
TITLE3 'PRODUCT DEVELOPMENT & MARKETING';
TITLE4 "MOVEMENT IN BANK'S SAVING DEPOSITS AS AT " &RDATE;
TITLE5 'NET INCREASE/DECREASE OF RM 50 THOUSAND & ABOVE PER CUSTOMER';
*;
%IF &SDMVNT EQ 0 %THEN %DO;
 DATA _NULL_;
   FILE PRINT;
   PUT;PUT;
   PUT @5 '****************************************************';
   PUT @5 'NO CUSTOMER WITH MOVEMENT OF 50 THOUSAND AND ABOVE';
   PUT @5 "AT &RDATE";
   PUT @5 '***************************************************';
 RUN;
%END;
%ELSE %DO;
PROC REPORT DATA=SDMVNT NOWD HEADLINE SPLIT='*';
  COLUMN BRANCH BRCH NAME ACCTNO MOVEMENT MOVEMT;

  DEFINE BRANCH  / DISPLAY FORMAT=6. 'BRANCH';
  DEFINE BRCH    / DISPLAY FORMAT=$6.  '/CODE ';
  DEFINE NAME    / FORMAT=$25. 'NAME OF CUSTOMER';
  DEFINE ACCTNO  / DISPLAY FORMAT=20. 'ACCOUNT NUMBER';
  DEFINE MOVEMENT/ NOPRINT;
  DEFINE MOVEMT  / COMPUTED FORMAT=14.1
                   'INCREASE/*DECREASE*(RM THOUSAND)';
  WHERE SATYPE='CONV';
  COMPUTE MOVEMT;
    MOVEMT=MOVEMENT.SUM*0.001;
  ENDCOMP;

  RBREAK AFTER / PAGE DUL OL SUMMARIZE;
  TITLE6 '(CONVENTIONAL)';
RUN;
*;
PROC REPORT DATA=SDMVNT NOWD HEADLINE SPLIT='*';
  COLUMN BRANCH BRCH NAME ACCTNO MOVEMENT MOVEMT;

  DEFINE BRANCH  /         FORMAT=6. 'BRANCH';
  DEFINE BRCH    /         FORMAT=$6.  '/CODE ';
  DEFINE NAME    / FORMAT=$25. 'NAME OF CUSTOMER';
  DEFINE ACCTNO  / DISPLAY FORMAT=20. 'ACCOUNT NUMBER';
  DEFINE MOVEMENT/ NOPRINT;
  DEFINE MOVEMT  / COMPUTED FORMAT=14.1
                   'INCREASE/*DECREASE*(RM THOUSAND)';
  WHERE SATYPE='ISLM';
  COMPUTE MOVEMT;
    MOVEMT=MOVEMENT.SUM*0.001;
  ENDCOMP;
  RBREAK AFTER / PAGE DUL OL SUMMARIZE;
  TITLE6 '(ISLAMIC)';
RUN;
%END;
%MEND SAVING;
%SAVING;
/*******************************************/
/* REPORT 2 DEMAND DEPOSITS                */
/* SEGREGATION OF SPTF & NON-SPTF          */
/* - FILTERING ACE PRODUCTS                */
/* - DATASETS FOR TODAY'S AND PREVIOUS'S   */
/*******************************************/
*;
DATA CRMOVE;
  SET MIS.DYMVNT&REPTMON;
  WHERE DEPTYPE IN ('D','N') AND REPTDATE EQ &ZDATE
  AND ABS(CREDIT-DEBIT) GE 1000000
  AND PRODUCT NOT IN (150,151,152,181);

  BRCH=PUT(BRANCH, BRCHCD.);
  DDTYPE=PUT(PRODUCT, CAPROD.);
  IF DDTYPE NOT EQ 'OTHER';
     MOVEMENT = CREDIT-DEBIT;
     PREBAL   = CURBAL-MOVEMENT;
     IF CURBAL >= 0;
        IF PREBAL < 0 THEN MOVEMENT = 0+CURBAL;
        ELSE MOVEMENT = CURBAL - PREBAL;
        IF ABS(MOVEMENT) >= 1000000 THEN OUTPUT CRMOVE;

RUN;
*;
DATA CRMVSPTF CRMOVE;
   SET CRMOVE;
   IF (60<=PRODUCT<=64) OR (160<=PRODUCT<=165)
                     THEN OUTPUT CRMVSPTF;
                     ELSE OUTPUT CRMOVE;
RUN;
*;
PROC SORT DATA=CRMVSPTF;
 BY DESCENDING MOVEMENT ACCTNO;
*;
PROC SORT DATA=CRMOVE;
 BY DESCENDING MOVEMENT ACCTNO;
*;
%MACRO CURRENT;
OPTIONS NODATE NONUMBER NOCENTER MISSING=0;
DATA _NULL_;
  CALL SYMPUT('CRMOVE', N);
  CALL SYMPUT('CRMVSPTF',P);
  SET CRMOVE   NOBS=N;
  SET CRMVSPTF NOBS=P;
RUN;

TITLE4 "CREDIT MOVEMENT IN BANK'S DEMAND DEPOSITS AS AT " &RDATE;
TITLE5 'NET INCREASE/DECREASE OF RM 1 MILLION & ABOVE PER CUSTOMER';
TITLE6;

%IF &CRMOVE EQ 0 %THEN %DO;
 DATA _NULL_;
   FILE PRINT;
   PUT;PUT;
   PUT @5 '*******************************************************';
   PUT @5 'NO CUSTOMER WITH CREDIT MOVEMENT OF 1 MILLION AND ABOVE';
   PUT @5 "AT &RDATE";
   PUT @5 '*******************************************************';
 RUN;
%END;
%ELSE %DO;
PROC REPORT DATA=CRMOVE NOWD HEADLINE SPLIT='*';
  COLUMN BRANCH BRCH NAME ACCTNO PREBAL CURBAL MOVEMENT MOVEMT;
  DEFINE BRANCH  /         FORMAT=6. 'BRANCH';
  DEFINE BRCH    /         FORMAT=$6.  '/CODE ';
  DEFINE NAME    / FORMAT=$25. 'NAME OF CUSTOMER';
  DEFINE ACCTNO  / DISPLAY FORMAT=20. 'ACCOUNT NUMBER';
  DEFINE PREBAL  / DISPLAY FORMAT=COMMA15.2 'PREVIOUS BALANCE';
  DEFINE CURBAL  / DISPLAY FORMAT=COMMA15.2 'CURRENT BALANCE';
  DEFINE MOVEMENT/ NOPRINT;
  DEFINE MOVEMT  / COMPUTED FORMAT=10.2
                   'INCREASE/*DECREASE*(RM MILLION)';
  COMPUTE MOVEMT;
    MOVEMT=MOVEMENT.SUM*0.000001;
  ENDCOMP;
  RBREAK AFTER / PAGE DUL OL SUMMARIZE;
RUN;
%END;
TITLE4 "SPTF CREDIT MOVEMENT IN BANK'S DEMAND DEPOSITS AS AT " &RDATE;
%IF &CRMVSPTF EQ 0 %THEN %DO;
 DATA _NULL_;
 FILE PRINT;
 PUT;PUT;
 PUT @5 '************************************************************';
 PUT @5 'NO CUSTOMER WITH SPTF CREDIT MOVEMENT OF 1 MILLION AND ABOVE';
 PUT @5 "AT &RDATE";
 PUT @5 '************************************************************';
 RUN;
%END;
%ELSE %DO;
PROC REPORT DATA=CRMVSPTF NOWD HEADLINE SPLIT='*';
  COLUMN BRANCH BRCH NAME ACCTNO PREBAL CURBAL MOVEMENT MOVEMT;
  DEFINE BRANCH  /         FORMAT=6. 'BRANCH';
  DEFINE BRCH    /         FORMAT=$6.  '/CODE ';
  DEFINE NAME    / FORMAT=$25. 'NAME OF CUSTOMER';
  DEFINE ACCTNO  / DISPLAY FORMAT=20. 'ACCOUNT NUMBER';
  DEFINE PREBAL  / DISPLAY FORMAT=COMMA15.2 'PREVIOUS BALANCE';
  DEFINE CURBAL  / DISPLAY FORMAT=COMMA15.2 'CURRENT BALANCE';
  DEFINE MOVEMENT/ NOPRINT;
  DEFINE MOVEMT  / COMPUTED FORMAT=10.2
                   'INCREASE/*DECREASE*(RM MILLION)';
  COMPUTE MOVEMT;
    MOVEMT=MOVEMENT.SUM*0.000001;
  ENDCOMP;
  RBREAK AFTER / PAGE DUL OL SUMMARIZE;
RUN;
%END;
%MEND CURRENT;
%CURRENT;

TITLE4 "SUMMARY OF MOVEMENT IN BANK'S DEMAND DEPOSIT AS AT " &RDATE;
TITLE5 '(EXCLUDING ACE & AL-WADIAH CURRENT ACCOUNTS)';
TITLE6 'NET INCREASE/DECREASE BY RANGE';
TITLE7;
*;
PROC FORMAT;
   VALUE MOVERANG
     300000  = '1)  BELOW RM300K              '
     500000  = '2)  RM300K TO BELOW RM500K    '
    1000000  = '3)  RM500K TO BELOW RM1 MIL   '
    1500000  = '4)  RM1 MIL TO BELOW RM1.5 MIL'
    2000000  = '5)  RM1.5 MIL TO BELOW RM2 MIL'
    3000000  = '6)  RM2 MIL TO BELOW RM3 MIL  '
    4000000  = '7)  RM3 MIL TO BELOW RM4 MIL  '
    5000000  = '8)  RM4 MIL TO BELOW RM5 MIL  '
    10000000 = '9)  RM5 MIL TO BELOW RM10 MIL '
    10000001 = '10) ABOVE RM10 MIL            ';

   PICTURE MOVETHOU
     LOW - <0 = '000,000,009.99' (PREFIX='-' MULT=.01)
     0-HIGH   = '000,000,009.99' (MULT=.01);
RUN;
*;
PROC PRINT DATA=MIS.DYDDCR&REPTMON SPLIT='*';
  ID  RANGE;
  VAR MOVEMENT;
  SUM MOVEMENT;
  FORMAT MOVEMENT COMMA15.2 RANGE MOVERANG.;
  LABEL RANGE    = 'NET INCREASE/DECREASE'
        MOVEMENT = "TOTAL MOVEMENT";
  WHERE REPTDATE EQ &ZDATE;
RUN;
