OPTIONS SORTDEV=3390 YEARCUTOFF=1950;
*;
DATA REPTDATE;
   SET DEPOSIT.REPTDATE;
   SELECT(DAY(REPTDATE));
      WHEN (8)  CALL SYMPUT('NOWK',PUT('1',$1.));
      WHEN (15) CALL SYMPUT('NOWK',PUT('2',$1.));
      WHEN (22) CALL SYMPUT('NOWK',PUT('3',$1.));
      OTHERWISE CALL SYMPUT('NOWK',PUT('4',$1.));
   END;
   CALL SYMPUT('REPTYEAR',PUT(REPTDATE,YEAR4.));
   CALL SYMPUT('REPTMON',PUT(MONTH(REPTDATE),Z2.));
   CALL SYMPUT('REPTDAY',PUT(DAY(REPTDATE),Z2.));
   CALL SYMPUT('RDATE',PUT(REPTDATE,DDMMYY8.));
RUN;
%INC PGM(PBBDPFMT);
*;
*----------------------------------------------------------------*
*  PRODUCE REPORTS TOP 50 DEPOSITOR - DAILY RM & FCY             *
*----------------------------------------------------------------*;
*;
DATA CISCA(KEEP=CUSTNO ACCTNO CUSTNAME ICNO NEWIC OLDIC INDORG);
   SET CISDP.DEPOSIT;
   IF  SECCUST='901';
   IF (3000000000<=ACCTNO<=3999999999);
   IF NEWIC NE '' THEN ICNO = NEWIC;
   ELSE ICNO = CUSTNO;
RUN;

DATA CISFD(KEEP=CUSTNO ACCTNO CUSTNAME ICNO NEWIC OLDIC INDORG);
   SET CISFD.DEPOSIT;
   IF  SECCUST='901';
   IF (1000000000<=ACCTNO<=1999999999) OR
      (7000000000<=ACCTNO<=7999999999) OR
      (4000000000<=ACCTNO<=6999999999);
   IF NEWIC NE '' THEN ICNO = NEWIC;
   ELSE ICNO = CUSTNO;
RUN;

DATA CA;
   SET DEPOSIT.CURRENT;
   PRODCD=PUT(PRODUCT, CAPROD.);
   IF CURBAL > 0 AND PRODCD NE 'N';
RUN;

DATA FD;
   SET DEPOSIT.FD;
   IF CURBAL > 0;
RUN;

DATA SA;
   SET DEPOSIT.SAVING;
   PRODCD=PUT(PRODUCT, SAPROD.);
   IF CURBAL > 0 AND PRODCD NE 'N';
RUN;

PROC SORT DATA=CISCA; BY ACCTNO; RUN;
PROC SORT DATA=CA; BY ACCTNO; RUN;
DATA CA;
   MERGE CA(IN=A) CISCA; BY ACCTNO;
   IF CUSTNAME='   '            THEN CUSTNAME=NAME;
   IF A;
   /*
   IF A AND PURPOSE NE '2' AND
   PRODUCT NOT IN (400,401,402,403,404,405,406,407,408,409,410);
   */
*;
DATA CAIND
     CAORG;
   SET CA;
   CABAL = CURBAL;
   IF CUSTCODE IN (77,78,95,96) THEN OUTPUT CAIND;
   ELSE DO;
        IF PURPOSE='2'  THEN DELETE;
        IF INDORG = 'O' THEN OUTPUT CAORG;
   END;
RUN;

DATA CAIND;
   SET CAIND;
   IF PURPOSE='2' THEN DO;
      ICNO='JOINT';
      CUSTNAME=NAME;
   END;
PROC SORT DATA=CAIND OUT=CAIND NODUPKEY; BY ACCTNO ICNO CUSTNAME;
*;
PROC SORT DATA=CISFD; BY ACCTNO; RUN;
PROC SORT DATA=FD; BY ACCTNO; RUN;
DATA FD;
   MERGE CISFD FD(IN=A); BY ACCTNO;
   IF CUSTNAME='   '            THEN CUSTNAME=NAME;
   IF A;
   /*
   IF A AND PURPOSE NE '2' AND
   PRODUCT NOT IN (400,401,402,403,404,405,406,407,408,409,410);
   */
*;
DATA FDIND
     FDORG;
   SET FD;
   FDBAL = CURBAL;
   IF CUSTCODE IN (77,78,95,96) THEN OUTPUT FDIND;
   ELSE DO;
        IF PURPOSE='2'  THEN DELETE;
        IF INDORG = 'O' THEN OUTPUT FDORG;
   END;
RUN;
*;
DATA FDIND;
   SET FDIND;
   IF PURPOSE='2' THEN DO;
      ICNO='JOINT';
      CUSTNAME=NAME;
   END;
PROC SORT DATA=FDIND OUT=FDIND NODUPKEY; BY ACCTNO ICNO CUSTNAME;
*;
PROC SORT DATA=SA; BY ACCTNO; RUN;
DATA SA;
   MERGE CISFD SA(IN=A); BY ACCTNO;
   IF CUSTNAME='   '            THEN CUSTNAME=NAME;
   IF A;
   /*
   IF A AND PURPOSE NE '2' AND
   PRODUCT NOT IN (400,401,402,403,404,405,406,407,408,409,410);
   */
*;
DATA SAIND
     SAORG;
   SET SA;
   SABAL = CURBAL;
   IF CUSTCODE IN (77,78,95,96) THEN OUTPUT SAIND;
   ELSE DO;
        IF PURPOSE='2'  THEN DELETE;
        IF INDORG = 'O' THEN OUTPUT SAORG;
   END;
RUN;
*;
DATA SAIND;
   SET SAIND;
   IF PURPOSE='2' THEN DO;
      ICNO='JOINT';
      CUSTNAME=NAME;
   END;
PROC SORT DATA=SAIND OUT=SAIND NODUPKEY; BY ACCTNO ICNO CUSTNAME;
*;
%MACRO PRNREC;
   PROC SORT DATA=DATA1; WHERE ICNO NE ''; BY ICNO CUSTNAME; RUN;
   PROC SUMMARY DATA=DATA1;
      BY ICNO CUSTNAME;
      VAR CURBAL FDBAL CABAL SABAL;
      OUTPUT OUT=DATA2 (DROP=_TYPE_ _FREQ_) SUM=;
   RUN;

   PROC SORT DATA=DATA2; BY DESCENDING CURBAL; RUN;
   DATA DATA2;
      SET DATA2(OBS=100);
   RUN;

   PROC PRINT DATA=DATA2 SPLIT='*';
      FORMAT  CURBAL FDBAL CABAL SABAL COMMA16.2;
      LABEL CUSTNAME   = 'DEPOSITOR'
            CURBAL     = 'TOTAL BALANCE'
            FDBAL      = 'FD BALANCE'
            CABAL      = 'CA BALANCE'
            SABAL      = 'SA BALANCE';
      VAR CUSTNAME CURBAL FDBAL CABAL SABAL;
   RUN;

   PROC SORT DATA=DATA2 OUT=DATA2(KEEP=ICNO CUSTNAME);
      BY ICNO CUSTNAME; RUN;
   DATA DATA3;
      MERGE DATA1(IN=A) DATA2(IN=B); BY ICNO CUSTNAME;
      IF A AND B;
   RUN;
   PROC SORT DATA=DATA3; BY ICNO CUSTNAME; RUN;
   PROC PRINT DATA=DATA3 SPLIT='*';
      FORMAT  CURBAL COMMA16.2;
      BY ICNO CUSTNAME;
      LABEL BRANCH     = 'BRANCH*CODE'
            ACCTNO     = 'MNI NO'
            CUSTCODE   = 'CUSTCD'
            CUSTNAME   = 'DEPOSITOR'
            CUSTNO     = 'CIS NO'
            NEWIC      = 'NEW IC'
            OLDIC      = 'OLD IC'
            CURBAL     = 'CURRENT*BALANCE'
            PRODUCT    = 'PRODUCT';
      VAR BRANCH ACCTNO CUSTCODE CUSTNAME CUSTNO NEWIC OLDIC CURBAL
          PRODUCT;
      SUM CURBAL;
   RUN;
%MEND;

*** FD+CA INDIVIDUAL CUSTOMERS ***;
DATA DATA1;
   SET FDIND CAIND SAIND;
   IF ICNO='  ' THEN ICNO='XX';
RUN;
PROC PRINTTO PRINT=FD11TEXT NEW;
TITLE1 'PUBLIC BANK BERHAD      PROGRAM-ID: EIBDTP50';
TITLE2 'TOP 100 LARGEST FD/CA/SA INDIVIDUAL CUSTOMERS AS AT ' "&RDATE";
%PRNREC;

*** FD+CA CORPORATE CUSTOMERS ***;
DATA DATA1;
   SET FDORG CAORG SAORG;
   IF ICNO='  ' THEN ICNO='XX';
RUN;
PROC PRINTTO PRINT=FD12TEXT NEW;
TITLE1 'PUBLIC BANK BERHAD      PROGRAM-ID: EIBDTP50';
TITLE2 'TOP 100 LARGEST FD/CA/SA CORPORATE CUSTOMERS AS AT ' "&RDATE";
%PRNREC;

*** FD/CA/SA SUBSIDIARIES CUSTOMERS ***;
DATA SUBS_ALL;
   SET FDORG CAORG SAORG;
   FORMAT DEPID 8.
          DEPGRP $100.;
   IF NEWIC NE '' THEN ICNO = NEWIC;
   ELSE ICNO = OLDIC;
   IF CURCODE EQ 'MYR' THEN RMAMT = CURBAL;
   ELSE FCYAMT = CURBAL;
RUN;
PROC SORT DATA=SUBS_ALL; BY NEWIC; RUN;

PROC SORT DATA=LIST.COF_MNI_DEPOSITOR_LIST
           OUT=COF_MNI_IDNO(KEEP=DEPID DEPGRP BUSSREG) NODUPKEY;
   BY BUSSREG;
RUN;

DATA MNI_IC MNI_ICX(DROP=DEPID DEPGRP);
   MERGE SUBS_ALL(IN=A) COF_MNI_IDNO(RENAME=(BUSSREG=NEWIC));
   BY NEWIC;
   IF A;
   IF DEPID > 0 THEN OUTPUT MNI_IC;
   ELSE              OUTPUT MNI_ICX;
RUN;

PROC SORT DATA=MNI_ICX; BY CUSTNO; RUN;
PROC SORT DATA=LIST.COF_MNI_DEPOSITOR_LIST
           OUT=COF_MNI_CUST(KEEP=DEPID DEPGRP CUSTNO) NODUPKEY;
   BY CUSTNO;
RUN;

DATA MNI_CUST MNI_CUSTX(DROP=DEPID);
   MERGE MNI_ICX(IN=A) COF_MNI_CUST;
   BY CUSTNO;
   IF A;
   IF DEPID > 0 THEN OUTPUT MNI_CUST;
   ELSE              OUTPUT MNI_CUSTX;
RUN;

DATA MNI_ALL;
   SET MNI_IC MNI_CUST;
RUN;
PROC SORT; BY CUSTNO; RUN;
PROC SORT DATA=LIST.KEEP_TOP_DEP_EXCL_PBB OUT=TOPDEP; BY CUSTNO; RUN;

DATA SUBS_ALL;
    MERGE MNI_ALL(IN=A) TOPDEP(IN=B);
    BY CUSTNO;
    IF A AND NOT B;
RUN;

PROC SORT DATA=SUBS_ALL; BY DEPID; RUN;
PROC MEANS DATA=SUBS_ALL NWAY NOPRINT;
   VAR DEPID;
   OUTPUT OUT=MAX_ID MIN=S_ID MAX=L_ID;
RUN;

DATA _NULL_;
   SET MAX_ID;
   CALL SYMPUT('MIN',PUT(S_ID,8.));
   CALL SYMPUT('MAX',PUT(L_ID,8.));
RUN;

%PUT &MIN;
%PUT &MAX;

PROC SORT DATA=SUBS_ALL; BY CUSTNO ACCTNO; RUN;
PROC PRINTTO PRINT=FD2TEXT;

%MACRO PRNSUB;
 %DO I=0 %TO &MAX %BY 1;
   DATA SUBS;
      SET SUBS_ALL;
      WHERE DEPID=&I;
      GROUP=DEPGRP;
      CALL SYMPUT('GROUP',GROUP);
   RUN;
   TITLE1 'PUBLIC BANK BERHAD      PROGRAM-ID: EIBDTOP5';
   TITLE2 'GROUP OF COMPANIES UNDER TOP 100 CORP DEPOSITORS @' "&RDATE";
   TITLE3 '***** ' "&GROUP" ' *****';

   PROC PRINT DATA=SUBS SPLIT='*';
      FORMAT  CURBAL COMMA16.2;
      BY CUSTNO;
      LABEL BRANCH     = 'BRANCH*CODE'
            ACCTNO     = 'MNI NO'
            CUSTNAME   = 'DEPOSITOR'
            CUSTNO     = 'CIS NO'
            CUSTCODE   = 'CUSTCD'
            CURBAL     = 'CURRENT*BALANCE'
            PRODUCT    = 'PRODUCT';
      VAR BRANCH ACCTNO CUSTNAME CUSTNO CUSTCODE CURBAL PRODUCT;
      SUM CURBAL;
   RUN;
%END;
%MEND PRNSUB;
%PRNSUB;
