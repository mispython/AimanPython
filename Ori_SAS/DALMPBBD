/* PROGRAM NAME  : DALMPBBD                                     */
/* INVOKE BY JOB : PBBMTH1/PBBQTR1                              */
/* DATE MODIFIED : 10-7-2001                                    */
/* SMR/OTHERS    : JS DATED 11/7/01                             */
/* CHANGES MADE  : ADDITIONAL FIELDS TO BE KEPT                 */
/*                 &ACE IS IN PBBDPFMT - ADDED 177 PRODUCT INTO */
/*                 MAPPING                                      */
/* OBJECTIVE     : PBB DEPOSIT MANIPULATION EXTRACTED FROM      */
/*                 SAP.PBB.MNITB                                */

%INC PGM(PBBDPFMT);

/***********************************************************/
/** MANIPULATE THE EXTRACTED THE SAVING AND CURRENT       **/
/** ACCOUNTS DATABASE                                     **/
/***********************************************************/

%LET SAVG1=(KEEP=BRANCH DEPTYPE PRODUCT OPENIND CUSTCODE INTPAYBL
                 RACE CURBAL OPENMH CLOSEMH BDATE NAME ACCTNO LASTTRAN
                 ACCYTD LEDGBAL SCHIND BANKNO OPENDT COSTCTR);

%LET SAVG2=(KEEP=BRANCH DEPTYPE PRODUCT PRODCD CUSTCD STATECD AGE
                 RACE INTPAYBL CURBAL OPENMH CLOSEMH RANGE BDATE NAME
                 ACCTNO LASTTRAN ACCYTD AMTIND LEDGBAL SCHIND BANKNO
                 OPENDATE COSTCTR R2NGE);

%LET CURN1=(KEEP=BRANCH DEPTYPE PRODUCT OPENIND CUSTCODE INTPAYBL
                 RACE CURBAL OPENMH CLOSEMH AVGAMT PURPOSE NAME ACCTNO
                 LASTTRAN ACCYTD LEDGBAL ODINTACC COSTCTR SECTOR);

%LET CURN2=(KEEP=BRANCH DEPTYPE PRODUCT PRODCD CUSTCD STATECD CUSTNO
                 RACE INTPAYBL CURBAL OPENMH CLOSEMH RANGE AVGAMT
                 AVGRNGE PURPOSE SABAL CABAL AGE NAME ACCTNO AMTIND
                 LASTTRAN ACCYTD LEDGBAL ODINTACC COSTCTR SECTOR);

%LET AGELIMIT = 12;
%LET MAXAGE   = 18;
%LET AGEBELOW = 11;

OPTIONS YEARCUTOFF=1930;

DATA BNM.SAVG&REPTMON&NOWK &SAVG2;
  LENGTH CUSTCD $2. PRODCD $5. STATECD $1. AMTIND $1.;
  SET DEPOSIT.SAVING &SAVG1;
  IF OPENIND NOT IN ('B','C','P') AND CURBAL GE 0;
  /****************************************************/
  /** APPLY THE FORMAT                               **/
  /****************************************************/
  CUSTCD=PUT(CUSTCODE, SACUSTCD.);
  STATECD=PUT(BRANCH, STATECD.);
  PRODCD=PUT(PRODUCT, SAPROD.);
  AMTIND=PUT(PRODUCT, SADENOM.);
  RANGE=INPUT(CURBAL, SDRANGE.);
  R2NGE=INPUT(CURBAL, S2RANGE.);
  RACE=PUT(RACE, $RACE.);

  IF OPENDT NE 0 THEN
     OPENDATE=INPUT(SUBSTR(PUT(OPENDT,Z11.),1,8), MMDDYY8.);
  ELSE OPENDATE=0;

  /******************************************/
  /*  CALCULATION OF AGE RANGE              */
  /******************************************/
  AGE = 0;
  IF (BDATE NE 0) AND (BDATE NE .) THEN
    DO;
     BDATE  =INPUT(SUBSTR(PUT(BDATE, Z11.),1, 8), MMDDYY8.);
     BDAY   =DAY(BDATE);
     BMONTH =MONTH(BDATE);
     BYEAR  =YEAR(BDATE);

     AGE=(&REPTYEAR - BYEAR);
     SELECT;
      WHEN (AGE = &AGELIMIT)
        DO;
         IF (BMONTH = &REPTMON) AND
            (BDAY > &REPTDAY) THEN AGE=&AGEBELOW;
         ELSE IF BMONTH > &REPTMON THEN AGE=&AGEBELOW;
        END;
      WHEN (AGE = &MAXAGE)
        DO;
         IF (BMONTH = &REPTMON) AND
            (BDAY > &REPTDAY) THEN AGE=&AGELIMIT;
         ELSE IF BMONTH > &REPTMON THEN AGE=&AGELIMIT;
        END;
      WHEN (AGE > &MAXAGE) AGE = &MAXAGE;
      WHEN (AGE < &AGELIMIT) AGE = &AGEBELOW;
      OTHERWISE AGE = &AGELIMIT;
     END;
    END;
RUN;

DATA BNM.CURN&REPTMON&NOWK &CURN2
     BNM.FCY&REPTMON&NOWK &CURN2;
  LENGTH CUSTCD $2. PRODCD $5. STATECD $1. AMTIND $1.;
  SET DEPOSIT.CURRENT &CURN1;
  IF OPENIND NOT IN ('B','C','P') AND CURBAL GE 0;
  STATECD=PUT(BRANCH, STATECD.);
  PRODCD=PUT(PRODUCT, CAPROD.);
  AMTIND=PUT(PRODUCT, CADENOM.);
  RACE=PUT(RACE, $RACE.);
  RANGE=INPUT(CURBAL, DDRANGE.);
  AVGRNGE=INPUT(AVGAMT, DDRANGE.);
  CABAL = 0; SABAL = 0;
  /****************************************************/
  /** IF VOSTRO ACCOUNT THEN THE COUNTER PARTY CODE  **/
  /** WILL BE TAKEN AS EITHER CB '02' OR '81' BASED  **/
  /** ON THE PRODUCT CODE :                          **/
  /**              104 = VOSTRO LOCAL                **/
  /**              105 = VOSTRO FOREIGN              **/
  /****************************************************/
  SELECT(PRODUCT);
    WHEN(104) CUSTCD='02';
    WHEN(105) CUSTCD='81';
    OTHERWISE CUSTCD=PUT(CUSTCODE, DDCUSTCD.);
  END;

  /*****************************************************/
  /** IF ACE PRODUCTS THEN                            **/
  /**        TREAT LIKE OTHER CURRENT ACCOUNT PRODUCT **/
  /*****************************************************/
  IF PRODUCT IN &ACE THEN DO;
        INTPAYBL = 0;
        PRODCD=PUT(PRODUCT, CAPROD.);
        AMTIND=PUT(PRODUCT, CADENOM.);
        RANGE=INPUT(CURBAL, DDRANGE.);
        AVGRNGE=INPUT(AVGAMT, DDRANGE.);
        OUTPUT BNM.CURN&REPTMON&NOWK;
  END;
  ELSE IF 400 <= PRODUCT <= 444 OR PRODUCT IN (450:454) THEN DO;
     IF CUSTCD IN ('77','78','95') THEN DO;
        IF SECTOR IN (4,5) THEN SECTOR = 1;
        ELSE IF SECTOR NOT IN (1,2,3,4,5) THEN SECTOR = 1;
     END;
     ELSE DO;
        IF SECTOR IN (1,2,3) THEN SECTOR = 4;
        ELSE IF SECTOR NOT IN (1,2,3,4,5) THEN SECTOR = 4;
     END;
     OUTPUT BNM.FCY&REPTMON&NOWK;
  END;
  ELSE DO;
     OUTPUT BNM.CURN&REPTMON&NOWK;
  END;
RUN;
DATA CISDP;
   INFILE CISDP MISSOVER;
   INPUT @1 ACCTNO 11. @13 CUSTNO 11.;
RUN;
PROC SORT DATA=CISDP OUT=CISDP(KEEP=ACCTNO CUSTNO);
   BY ACCTNO;
*;
DATA BNM.FCY&REPTMON&NOWK;
   MERGE BNM.FCY&REPTMON&NOWK(IN=A) CISDP;
   BY ACCTNO;
   IF A;
*;
PROC APPEND DATA=BNM.FCY&REPTMON&NOWK(DROP=CUSTNO)
            BASE=BNM.CURN&REPTMON&NOWK; RUN;

/***********************************************************/
/** SUMMARIZE THE DATASET AT BRANCH LEVEL - FOR RDAL      **/
/***********************************************************/
PROC DATASETS LIB=BNM NOLIST; DELETE DEPT&REPTMON&NOWK; RUN;
PROC SUMMARY DATA=BNM.SAVG&REPTMON&NOWK NWAY;
CLASS BRANCH STATECD PRODCD CUSTCD AMTIND;
VAR CURBAL INTPAYBL;
OUTPUT OUT=DEPT
       SUM=CURBAL INTPAYBL;
RUN;

PROC APPEND DATA=DEPT BASE=BNM.DEPT&REPTMON&NOWK; RUN;
PROC DATASETS LIB=WORK NOLIST; DELETE DEPT; RUN;

PROC SUMMARY DATA=BNM.CURN&REPTMON&NOWK NWAY MISSING;
CLASS BRANCH STATECD PRODCD CUSTCD SECTOR AMTIND;
VAR CURBAL INTPAYBL;
OUTPUT OUT=DEPT
       SUM=CURBAL INTPAYBL;
RUN;

PROC APPEND DATA=DEPT BASE=BNM.DEPT&REPTMON&NOWK FORCE; RUN;
PROC DATASETS LIB=WORK NOLIST; DELETE DEPT; RUN;

DATA SAVE293 &SAVG2;
  LENGTH CUSTCD $2. PRODCD $5. STATECD $1. AMTIND $1.;
  SET DEPOSIT.SAVING &SAVG1;
  IF OPENIND NOT IN ('B','C','P');
  /****************************************************/
  /** APPLY THE FORMAT                               **/
  /****************************************************/
  CUSTCD =PUT(CUSTCODE, SACUSTCD.);
  STATECD=PUT(BRANCH, STATECD.);
  PRODCD =PUT(PRODUCT, SAPROD.);
  AMTIND =PUT(PRODUCT, SADENOM.);
  IF PRODCD='N' THEN DELETE;
  OPENMH=1;
  OUTPUT SAVE293;
*;
PROC SUMMARY DATA=SAVE293 NWAY;
CLASS BRANCH STATECD PRODCD CUSTCD AMTIND;
VAR OPENMH;
OUTPUT OUT=SAVE293 SUM=;
*;
DATA CURR293 &CURN2;
  LENGTH CUSTCD $2. PRODCD $5. STATECD $1. AMTIND $1.;
  SET DEPOSIT.CURRENT &CURN1;
  IF  OPENIND NOT IN ('B','C','P');
  IF  BRANCH > 900 THEN DELETE;
  STATECD=PUT(BRANCH, STATECD.);
  PRODCD =PUT(PRODUCT, CAPROD.);
  AMTIND =PUT(PRODUCT, CADENOM.);
  CUSTCD ='00';
  IF PRODCD='N' THEN DELETE;
  /*  IF PRODUCT IN (160,161,162,163,164,165,182) THEN DO; */
  IF PRODUCT IN (160,161,162,163,164,165,166,182) THEN DO;
     PRODCD='42310'; AMTIND='I';
  END;
  ELSE DO;
     PRODCD='42110'; AMTIND='D';
  END;
  OPENMH=1;
  OUTPUT CURR293;
*;
PROC SUMMARY DATA=CURR293 NWAY;
CLASS BRANCH STATECD PRODCD CUSTCD AMTIND;
VAR OPENMH;
OUTPUT OUT=CURR293 SUM=;
*;
