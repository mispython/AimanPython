//EIBWDIRT JOB ACCOUNT-CODE,'DIRWPBBA',                                 JOB19305
//         MSGCLASS=X,MSGLEVEL=(1,1),CLASS=A,
//         REGION=4096K,NOTIFY=&SYSUID
/*JOBPARM S=S1M2
//*
//DIRWPBBA EXEC SAS609,REGION=8M,WORK='320000,300000'
//PGM      DD DSN=SAP.BNM.PROGRAM,DISP=SHR
//DEPOSIT  DD DSN=SAP.PBB.MNITB(0),DISP=SHR
//RATE     DD DSN=RBP2.B033.WADIAH.RATE.SORT,DISP=SHR
//SASLIST  DD SYSOUT=A
//SYSIN    DD *
*+--------------------------------------------------------------+
 |  PROGRAM : EIBWDIRT                                          |
 |  DATE    : 18TH.MAR.2002                                     |
 |  FUNCTION: TO CONVERT THE EXTRACT RATE FROM CIS AND COMPUTE  |
 |            THE DOMESTIC INTEREST RATE.                       |
 +--------------------------------------------------------------+;
OPTIONS SORTDEV=3390 YEARCUTOFF=1950 NOCENTER;
*;
%LET ACELIMIT = 5000;
*------------------------------------------------*
*  GET REPTDATE                                  *
*------------------------------------------------*;
DATA _NULL_;
   SET DEPOSIT.REPTDATE;
   SELECT(DAY(REPTDATE));
      WHEN  (8) CALL SYMPUT('NOWK',PUT('1',$1.));
      WHEN (15) CALL SYMPUT('NOWK',PUT('2',$1.));
      WHEN (22) CALL SYMPUT('NOWK',PUT('3',$1.));
      OTHERWISE CALL SYMPUT('NOWK',PUT('4',$1.));
   END;
   CALL SYMPUT('REPTYEAR',PUT(REPTDATE,YEAR4.));
   CALL SYMPUT('REPTMON',PUT(MONTH(REPTDATE),Z2.));
   CALL SYMPUT('REPTDAY',PUT(DAY(REPTDATE),Z2.));
   CALL SYMPUT('RDATE',PUT(REPTDATE,DDMMYY8.));
   CALL SYMPUT('REPTDATE',REPTDATE);
RUN;
*;
LIBNAME BNM "SAP.PBB.D&REPTYEAR" DISP=OLD;
*;
DATA RATES;
   INFILE RATE;
   INPUT @01  TRANCD    $3.
         @04  PRODUCT  ZD3.
         @07  TIER     ZD9.
         @16  RATE       5.3
         @21  YYYY      $4.
         @21  CC        $2.
         @23  YY        $2.
         @25  MMMM      $2.
         @27  DDDD      $2.;
   IF (TRANCD IN ('215') AND PRODUCT IN (200,201,202,203,
      204,212,213,214,215,150,177))
      THEN OUTPUT;
*;
PROC PRINT DATA=RATES;
*;
DATA EFFECT
     SARATE;
   SET RATES;
   INTEREST = RATE / 100;
   IF PRODUCT IN (200,201)  THEN DO;
      PAYMENT = 2;
      EFFECTR = ((1 + INTEREST/2)**2 - 1) * 100;
      ERATE   = ROUND(EFFECTR,.01);
   END;
   ELSE IF PRODUCT IN (202,203,213,150,177)  THEN DO;
      PAYMENT = 12;
      EFFECTR = ((1 + INTEREST/12)**12 - 1) * 100;
      ERATE   = ROUND(EFFECTR,.01);
   END;
   ELSE IF PRODUCT IN (212)  THEN DO;
      PAYMENT = 12;
      EFFECTR = ((1 + (INTEREST*1.05)/12)**12 - 1) * 100;
      ERATE   = ROUND(EFFECTR,.01);
   END;
   ELSE IF PRODUCT IN (204,214,215)  THEN DO;
      ERATE   = RATE;
   END;
   IF PRODUCT IN (212,213) THEN DO;
      EFFDATE = DDDD || '/' || MMMM || '/' || YY;
      OUTPUT SARATE;
   END;
   OUTPUT EFFECT;
*;
PROC SORT DATA=EFFECT; BY PRODUCT TIER;
PROC PRINT DATA=EFFECT;
*;
DATA ACCOUNTS;
   SET DEPOSIT.SAVING
       DEPOSIT.CURRENT;
   IF  OPENIND NOT IN ('B','C','P') AND CURBAL GE 0 AND PRODUCT IN
       (150,151,152,181,177,200,201,202,203,204,212,213,214,215);
       SELECT (PRODUCT);
          WHEN (150,151,152,181) DO;
             PRODUCT = 150;
             IF CURBAL > &ACELIMIT  THEN DO;
                CURBAL = CURBAL - &ACELIMIT;
                TIER = 999999999;
             END;
             ELSE DO;
                TIER = 5000;
                CURBAL = 0;
             END;
          END;
          WHEN (177) DO;
             IF CURBAL <  5000          THEN TIER = 5000;
             ELSE IF CURBAL >= 5000     THEN TIER = 999999999;
          END;
          WHEN (200,201) DO;
             IF CURBAL < 2500          THEN TIER = 2500;
             ELSE IF CURBAL <  5000    THEN TIER = 5000;
             ELSE IF CURBAL <  10000   THEN TIER = 10000;
             ELSE IF CURBAL <  30000   THEN TIER = 30000;
             ELSE IF CURBAL <  50000   THEN TIER = 50000;
             ELSE IF CURBAL <  75000   THEN TIER = 75000;
             ELSE IF CURBAL >= 75000   THEN TIER = 999999999;
          END;
          WHEN (202) DO;
             IF CURBAL <    500        THEN TIER =  500;
             ELSE IF CURBAL <  2000    THEN TIER = 2000;
             ELSE IF CURBAL <  5000    THEN TIER = 5000;
             ELSE IF CURBAL <  10000   THEN TIER = 10000;
             ELSE IF CURBAL <  30000   THEN TIER = 30000;
             ELSE IF CURBAL <  50000   THEN TIER = 50000;
             ELSE IF CURBAL <  75000   THEN TIER = 75000;
             ELSE IF CURBAL >= 75000   THEN TIER = 999999999;
          END;
          WHEN (203) DO;
             IF CURBAL <  2000         THEN TIER = 2000;
             ELSE IF CURBAL <  5000    THEN TIER = 5000;
             ELSE IF CURBAL <  10000   THEN TIER = 10000;
             ELSE IF CURBAL <  30000   THEN TIER = 30000;
             ELSE IF CURBAL <  50000   THEN TIER = 50000;
             ELSE IF CURBAL <  75000   THEN TIER = 75000;
             ELSE IF CURBAL >= 75000   THEN TIER = 999999999;
          END;
          WHEN (204) DO;
             TIER = 999999999;
          END;
          WHEN (214) DO;
             IF CURBAL < 1000          THEN TIER = 1000;
             ELSE IF CURBAL <  5000    THEN TIER = 5000;
             ELSE IF CURBAL <  25000   THEN TIER = 25000;
             ELSE IF CURBAL <  50000   THEN TIER = 50000;
             ELSE IF CURBAL >= 50000   THEN TIER = 999999999;
          END;
          WHEN (212) DO;
             IF CURBAL <  5000         THEN TIER = 5000;
             ELSE IF CURBAL <  10000   THEN TIER = 10000;
             ELSE IF CURBAL <  20000   THEN TIER = 20000;
             ELSE IF CURBAL <  30000   THEN TIER = 30000;
             ELSE IF CURBAL <  50000   THEN TIER = 50000;
             ELSE IF CURBAL >= 50000   THEN TIER = 999999999;
          END;
          WHEN (213) DO;
             IF CURBAL <  5000         THEN TIER = 5000;
             ELSE IF CURBAL <  10000   THEN TIER = 10000;
             ELSE IF CURBAL <  30000   THEN TIER = 30000;
             ELSE IF CURBAL <  50000   THEN TIER = 50000;
             ELSE IF CURBAL <  75000   THEN TIER = 75000;
             ELSE IF CURBAL >= 50000   THEN TIER = 999999999;
          END;
          WHEN (215) DO;
             IF CURBAL < 50000         THEN TIER = 50000;
             ELSE IF CURBAL >= 50000   THEN TIER = 999999999;
          END;
          OTHERWISE DO; CURBAL=0; TIER=0;    END;
       END; /* END FOR SELECT   */
RUN;
*;
PROC SORT DATA=ACCOUNTS; BY PRODUCT TIER;
*;
DATA RATEACCT;
   MERGE ACCOUNTS (IN=A) EFFECT(IN=B); BY PRODUCT TIER;
   IF A;
*;
PROC DATASETS LIB=WORK;
DELETE ACCOUNTS;
RUN;
*;
DATA BNM.IRWTT&REPTMON&NOWK(KEEP=BNMCODE AMOUNT EFFDATE);
   SET RATEACCT  END=LAST;
   TOTINT+(ERATE*CURBAL);
   TOTAMT+CURBAL;
   IF LAST THEN DO;
      BNMCODE='8420300000000Y';
      AMOUNT=TOTINT/TOTAMT;
      EFFDATE=.;
      OUTPUT;
   END;
RUN;
*;
PROC SORT DATA=SARATE;
  BY PRODUCT RATE EFFDATE;
RUN;
*;
DATA SARATE;
  SET SARATE; BY PRODUCT RATE EFFDATE;
  IF FIRST.PRODUCT   THEN OUTPUT;
*;
PROC SORT DATA=SARATE;
  BY ERATE EFFDATE;
RUN;
*;
DATA DIRW(KEEP=BNMCODE AMOUNT EFFDATE);
  SET SARATE;
  IF _N_=1 THEN
    DO;
      BNMCODE='8420100000000Y';
      AMOUNT=ERATE;
      OUTPUT;
    END;
RUN;
*;
PROC APPEND DATA=DIRW BASE=BNM.IRWTT&REPTMON&NOWK;RUN;
*;
PROC SORT DATA=SARATE;
  BY DESCENDING ERATE EFFDATE;
RUN;
*;
DATA DIRW(KEEP=BNMCODE AMOUNT EFFDATE);
  SET SARATE;
  IF _N_=1 THEN
    DO;
      BNMCODE='8420200000000Y';
      AMOUNT=ERATE;
      OUTPUT;
    END;
RUN;
*;
PROC APPEND DATA=DIRW BASE=BNM.IRWTT&REPTMON&NOWK;RUN;

PROC SORT DATA=BNM.IRWTT&REPTMON&NOWK;
  BY BNMCODE;
RUN;
*;
DATA BNM.IRWTT&REPTMON&NOWK(RENAME=(BNMCODE=ITCODE));
  SET BNM.IRWTT&REPTMON&NOWK;
  FLAG='E';
RUN;
*;
OPTIONS NOCENTER NONUMBER NODATE;
TITLE1 'PUBLIC BANK BERHAD';
TITLE2 'REPORT ON DOMESTIC INTEREST RATE - PART I';
TITLE3 'REPORTING DATE :' &RDATE;

PROC PRINT DATA=BNM.IRWTT&REPTMON&NOWK;
   VAR ITCODE AMOUNT EFFDATE FLAG;
RUN;
*;
