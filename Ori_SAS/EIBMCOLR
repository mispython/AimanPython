//EIBMCOLR JOB MSGCLASS=X,MSGLEVEL=(1,1),REGION=8M,USER=OPCC            JOB53755
/*JOBPARM S=S1M1
//*
//* RUN AFTER EIBMIISA.
//**********************************************************
//* 270308 RHA  SMR 2008-318 GENERATE SOFTCOPY TEXT RPT &
//*                          TRANSFERRED TO 'FIN' FOLDER
//*                          (FTPBCOLR)
//**********************************************************
//*
//DELETE   EXEC PGM=IEFBR14
//DEL01    DD DSN=SAP.PBB.EIBMCOLR.TEXT,
//            DISP=(MOD,DELETE,DELETE),
//            SPACE=(CYL,(0,0))
//*
//CREATE   EXEC PGM=IEFBR14
//CRT01    DD DSN=SAP.PBB.EIBMCOLR.TEXT,
//             DISP=(NEW,CATLG,DELETE),
//             UNIT=SYSDA,SPACE=(CYL,(1,1),RLSE),
//             DCB=(RECFM=FB,LRECL=150,BLKSIZE=27000)
//*
//*PRINT7   OUTPUT CLASS=R,
//*         NAME='MS CHUA YEU HWA / MS KELLY CHOY',
//*         ROOM='26TH FLOOR',
//*         BUILDING='MENARA PBB',
//*         DEPT='FINANCE DIVISION',
//*         ADDRESS=('FINANCIAL ACCOUNTING','MENARA PUBLIC BANK',
//*         '146 JALAN AMPANG','50450 KUALA LUMPUR'),
//*         DEST=S1.RMT5
//*
//EIBMCOLR EXEC SAS609
//BRANCH   DD DSN=RBP2.B033.PBB.BRANCH,DISP=SHR
//BNM      DD DSN=SAP.PBB.SASDATA,DISP=SHR
//NPL      DD DSN=SAP.PBB.NPL.SASDATA,DISP=SHR
//RCAT     DD DSN=SAP.PBB.EIBMCOLR.RCAT(+1),
//            DISP=(NEW,CATLG,DELETE),
//            DCB=(RECFM=FB,LRECL=80,BLKSIZE=0),
//            SPACE=(TRK,(50,50),RLSE),UNIT=SYSDA
//RCAT1    DD DSN=SAP.PBB.EIBMCOLR.RCAT(0),DISP=SHR
//*SASLIST  DD SYSOUT=(,),OUTPUT=(*.PRINT7)
//SASLIST   DD DSN=SAP.PBB.EIBMCOLR.TEXT,DISP=MOD
//SYSIN    DD *

OPTIONS YEARCUTOFF=1950 SORTDEV=3390 NODATE PS=65 NOCENTER;

*+--------------------------------------------------------------+
 |  PROGRAM : EIBMCOLR                                          |
 |  DATE    : 23.03.01                                          |
 |  REPORT  : BANK'S TOTAL LOANS AND ADVANCES BY COLLATERALS    |
 |            REQUESTED BY FINANCIAL ACCOUNTING, FINANCE DIV.   |
 +--------------------------------------------------------------+
 |  SMR/OTHERS    : A277                                        |
 |  DATE MODIFIED : 08-09-01 (WBL)                              |
 |  CHANGES MADE  : OUTPUT CURRENT MONTH EXTRACTION BY BRANCH   |
 |                  INTO A FLAT FILE FOR VARIANCE REPORT BETWEEN|
 |                  PREVIOUS & CURRENT MONTH BALANCE.           |
 +--------------------------------------------------------------+;

%LET NPRODUCT=(500,520);
*;
DATA REPTDATE;
  SET BNM.REPTDATE;
  SELECT(DAY(REPTDATE));
    WHEN (8)  DO; SDD = 1;  WK = '1'; WK1 = '4'; END;
    WHEN(15)  DO; SDD = 9;  WK = '2'; WK1 = '1'; END;
    WHEN(22)  DO; SDD = 16; WK = '3'; WK1 = '2'; END;
    OTHERWISE DO; SDD = 23; WK = '4'; WK1 = '3'; END;
  END;
  MM = MONTH(REPTDATE);
  IF WK = '1' THEN DO;
     MM1 = MM - 1;
     IF MM1 = 0 THEN MM1 = 12;
  END;
  ELSE MM1 = MM;
  SDATE = MDY(MM,SDD,YEAR(REPTDATE));
  CALL SYMPUT('NOWK',PUT(WK,$1.));
  CALL SYMPUT('NOWK1',PUT(WK1,$1.));
  CALL SYMPUT('REPTMON',PUT(MM,Z2.));
  CALL SYMPUT('REPTMON1',PUT(MM1,Z2.));
  CALL SYMPUT('REPTYEAR',PUT(REPTDATE,YEAR4.));
  CALL SYMPUT('REPTDAY',PUT(DAY(REPTDATE),Z2.));
  CALL SYMPUT('RDATE',PUT(REPTDATE,DDMMYY8.));
  CALL SYMPUT('SDATE',PUT(SDATE,DDMMYY8.));
RUN;
*;
PROC FORMAT;
   VALUE RISK
      0='  0%'
     10=' 10%'
     20=' 20%'
     50=' 50%'
    100='100%';
RUN;
*;
DATA LOAN;
   KEEP ACCTNO NOTENO CUSTCODE RISKCD RISKRTE NAME PRODUCT
        RISKCAT BRANCH CURBAL INTAMT NETPROC BNMCODE LIABCOD1
        LOANSTAT APPVALUE FEEAMT BALANCE CLOSEDTE APPRLIMT
        ISSDTE RLEASAMT APPRLIM2 ACCTYPE COL1 AMTIND VBAL
        COL2 LIMIT1 LIMIT2 FISSPURP;
   LENGTH BNMCODE   $10.  RISKCAT  3.;
   SET BNM.LOAN&REPTMON&NOWK;
   IF PRODCD NOT IN  ('N','54120') & PRODUCT ^IN &NPRODUCT;
   /* IF AMTIND='I' & ACCTYPE='LN' THEN BALANCE=CURBAL+FEEAMT; */
   IF ACCTYPE='OD' THEN DO;
      LIAB1=SUBSTR(COL1,1,2);
      LIAB2=SUBSTR(COL1,4,2);
      LIAB3=SUBSTR(COL2,1,2);
      LIAB4=SUBSTR(COL2,4,2);
      IF LIAB1 NE ' '  THEN LIABCODE=LIAB1; ELSE
      IF LIAB2 NE ' '  THEN LIABCODE=LIAB2; ELSE
      IF LIAB3 NE ' '  THEN LIABCODE=LIAB3; ELSE
      IF LIAB4 NE ' '  THEN LIABCODE=LIAB4; ELSE
         LIABCODE = ' ';
   END;
   LIABCOD1 = '0' || LIABCODE;
   SELECT (LIABCOD1);
      WHEN('007','012','013','014','024','048','049','117') DO;
         BNMCODE = '30307';
         RISKCAT = 0;
      END;
      WHEN('021') DO;
         BNMCODE = '30309';
         RISKCAT = 0;
      END;
      WHEN('017','026','029') DO;
         BNMCODE = '30009/17';
         RISKCAT = 10;
      END;
      WHEN('006','016') DO;
         BNMCODE = '30323';
         RISKCAT = 20;
      END;
      WHEN('011','030') DO;
         BNMCODE = '30325';
         RISKCAT = 20;
      END;
      WHEN('018','027') DO;
         BNMCODE = '30327';
         RISKCAT = 20;
      END;
      WHEN('003') DO;
         BNMCODE = '30009/10';
         RISKCAT = 20;
      END;
      WHEN('025') DO;
         BNMCODE = '30335';
         RISKCAT = 20;
      END;
      WHEN('050','118') DO;
        BNMCODE = '30341';
        IF FISSPURP IN ('0311','0312','0313','0314','0315','0316') THEN
          RISKCAT = 50;
        ELSE
          RISKCAT = 100;
      END;
      WHEN('019','028','031') DO;
         BNMCODE = '30351';
         RISKCAT = 100;
      END;
      OTHERWISE DO;
         BNMCODE = '30359';
         RISKCAT = 100;
      END;
   END;
   IF ACCTYPE='LN' THEN DO;  /* NO COLLATERAL VALUE FOR OD */
      IF LIABCOD1 NE '050' THEN DO;
         VBAL=0;
         IF (APPVALUE > 0) & (APPVALUE < BALANCE) THEN DO;
            VBAL=BALANCE;       /* SCENARIO 3 FOR 1 COLLATERAL ONLY */
            BALANCE=APPVALUE; OUTPUT;
            BALANCE=VBAL-APPVALUE; BNMCODE='30359'; RISKCAT=100;
            OUTPUT;
         END;
         ELSE OUTPUT;
      END;
      ELSE OUTPUT;
   END;
   ELSE OUTPUT;
RUN;
*;
DATA ODAC; SET LOAN; IF ACCTYPE='OD'; TOTIIS=0; NOTENO=0;
DATA LNAC; SET LOAN; IF ACCTYPE='LN';
PROC SORT  DATA=LNAC; BY ACCTNO NOTENO;
*;
PROC SORT  DATA=NPL.TOTIIS&REPTMON (KEEP=ACCTNO NOTENO TOTIIS)
     OUT=NPL NODUPKEY; BY ACCTNO NOTENO;
*;
DATA LNAC;
     MERGE LNAC(IN=A) NPL; BY ACCTNO NOTENO;
     IF A;
     IF TOTIIS=.  THEN TOTIIS=0;
*;
DATA LOAN;
     SET ODAC LNAC;
     NETBAL=BALANCE-TOTIIS;
*;
PROC SORT DATA=LOAN; BY BRANCH;
*;
DATA BRHDATA;
  INFILE BRANCH  LRECL=80;
  INPUT @2 BRANCH  3.
        @6 BRHCODE $3.;
  BRBOTH = PUT(BRANCH,Z3.)||'-'||BRHCODE;
*;
PROC SORT DATA=BRHDATA; BY BRANCH;
*;
DATA LOAN;
   MERGE LOAN (IN=A) BRHDATA;
   BY BRANCH;
   IF A THEN OUTPUT;
*;
TITLE1 'REPORT ID: EIBMCOLR';
TITLE2 'PUBLIC BANK BERHAD';
TITLE3 'TOTAL LOANS AND ADVANCES BY COLLATERAL AS AT : ' "&RDATE";
TITLE4 '(CONVENTIONAL + ISLAMIC) BY BRANCHES';
TITLE5;
*;
PROC SUMMARY DATA=LOAN NWAY;
CLASS BRANCH RISKCAT BNMCODE;
VAR BALANCE TOTIIS NETBAL;
OUTPUT OUT=LNBR SUM=;
RUN;
*;
PROC PRINT DATA=LNBR SPLIT='*';
VAR BALANCE TOTIIS NETBAL;
ID BNMCODE;
BY BRANCH RISKCAT;
SUMBY RISKCAT;
PAGEBY BRANCH;
SUM BALANCE TOTIIS NETBAL;
LABEL RISKCAT='RISK CATEGORY'
      BALANCE='BALANCE (RM)'
      TOTIIS ='IIS AMT (RM)'
      NETBAL ='NET AMT (RM)';
FORMAT RISKCAT RISK. BALANCE TOTIIS NETBAL COMMA20.2;
RUN;
*;
PROC SUMMARY DATA=LOAN;
CLASS RISKCAT BNMCODE AMTIND;
VAR BALANCE TOTIIS NETBAL;
OUTPUT OUT=LN
       SUM=;
RUN;
*;
TITLE4 'SUMMARY REPORT FOR ALL LOANS';
TITLE5;
*;
PROC PRINT DATA=LN SPLIT='*';
VAR BNMCODE BALANCE TOTIIS NETBAL;
ID RISKCAT;
BY RISKCAT;
SUMBY RISKCAT;
SUM BALANCE TOTIIS NETBAL;
LABEL RISKCAT='RISK*CATEGORY'
      BALANCE='BALANCE (RM)'
      TOTIIS ='IIS AMT (RM)'
      NETBAL ='NET AMT (RM)';
FORMAT RISKCAT RISK. BALANCE TOTIIS NETBAL COMMA20.2;
WHERE _TYPE_=6;
RUN;
*;
TITLE4 'SUMMARY REPORT FOR CONVENTIONAL LOANS ONLY';
TITLE5;
*;
PROC PRINT DATA=LN SPLIT='*';
VAR BNMCODE BALANCE TOTIIS NETBAL;
ID RISKCAT;
BY RISKCAT;
SUMBY RISKCAT;
SUM BALANCE TOTIIS NETBAL;
WHERE _TYPE_=7 & AMTIND='D';
LABEL RISKCAT='RISK*CATEGORY'
      BALANCE='BALANCE (RM)'
      TOTIIS ='IIS AMT (RM)'
      NETBAL ='NET AMT (RM)';
FORMAT RISKCAT RISK. BALANCE TOTIIS NETBAL COMMA20.2;
RUN;
*;
TITLE4 'SUMMARY REPORT FOR ISLAMIC LOANS ONLY';
TITLE5;
*;
PROC PRINT DATA=LN SPLIT='*';
VAR BNMCODE BALANCE TOTIIS NETBAL;
ID RISKCAT;
BY RISKCAT;
SUMBY RISKCAT;
SUM BALANCE TOTIIS NETBAL;
WHERE _TYPE_=7 & AMTIND='I';
LABEL RISKCAT='RISK*CATEGORY'
      BALANCE='BALANCE (RM)'
      TOTIIS ='IIS AMT (RM)'
      NETBAL ='NET AMT (RM)';
FORMAT RISKCAT RISK. BALANCE TOTIIS NETBAL COMMA20.2;
RUN;
*;
*+--------------------------------------------------------------+
 |  OUTPUT TO FLAT FILE                                         |
 +--------------------------------------------------------------+;
*;
DATA _NULL_;
FILE RCAT;
SET LNBR END=EOF;
IF _N_ = 1 THEN
   PUT @1  "&REPTYEAR" "&REPTMON" "&REPTDAY";
   PUT @1  BRANCH  Z3.
       @5  RISKCAT Z3.
       @9  BNMCODE $8.
       @18 BALANCE Z15.2
       @35 TOTIIS  Z15.2
       @52 NETBAL  Z15.2;
IF EOF THEN PUT @1 'EOF';
RUN;
*;
*+--------------------------------------------------------------+
 |  VARIANCE REPORT                                             |
 +--------------------------------------------------------------+;
*;
DATA LNPV;

  INFILE RCAT1 MISSOVER;

  INPUT @1   STRING   $10. @;

  IF _N_=1 THEN DO;
    PDTEDMY=SUBSTR(STRING,7,2)||'/'||SUBSTR(STRING,5,2)||'/'||
            SUBSTR(STRING,3,2);
    CALL SYMPUT('PDTEDMY',PUT(PDTEDMY,$8.));
  END;
  ELSE DO;
    IF SUBSTR(STRING,1,3) NE 'EOF' THEN DO;
       INPUT @1  BRANCH    3.      /*SUBSTR(STRING,1,3);*/
             @5  RISKCAT   3.      /*SUBSTR(STRING,5,3); */
             @9  BNMCODE   $8.     /* SUBSTR(STRING,9,8); */
             @18 PREVBAL   15.2    /* SUBSTR(STRING,18,15); */
             @52 PREVNETB  15.2;   /* SUBSTR(STRING,18,15); */
       OUTPUT;
    END;
  END;

RUN;

PROC SORT DATA=LNPV; BY BRANCH RISKCAT BNMCODE;
*;
PROC SORT DATA=LNBR; BY BRANCH RISKCAT BNMCODE;
*;
DATA LNMRG;
   KEEP BRANCH RISKCAT BNMCODE BALANCE TOTIIS NETBAL VARBAL PREVNETB;
   FORMAT VARBAL 15.2;
   MERGE LNBR(IN=A) LNPV(IN=B);
   BY BRANCH RISKCAT BNMCODE;
   IF BALANCE=.   THEN BALANCE =0;
   IF TOTIIS =.   THEN TOTIIS  =0;
   IF NETBAL  =.  THEN NETBAL  =0;
   IF PREVBAL =.  THEN PREVBAL =0;
   IF PREVNETB=.  THEN PREVNETB=0;
   VARBAL=BALANCE-PREVBAL;
RUN;
*;
PROC SORT DATA=LNMRG; BY RISKCAT BNMCODE BRANCH;
*;
PROC SUMMARY DATA=LNMRG NWAY;
CLASS RISKCAT BNMCODE BRANCH;
VAR BALANCE TOTIIS NETBAL VARBAL PREVNETB;
OUTPUT OUT=LNVAR SUM=;
RUN;
*;
TITLE3 'VARIANCE REPORT ON TOTAL LOANS AND ADVANCES AS AT : ' "&RDATE";
TITLE4 'BY RISK CATEGORY';
TITLE5;
*;
PROC PRINT DATA=LNVAR;
VAR BALANCE TOTIIS NETBAL VARBAL PREVNETB;
ID BRANCH;
BY RISKCAT BNMCODE;
SUMBY BNMCODE;
PAGEBY BNMCODE;
SUM BALANCE TOTIIS NETBAL VARBAL PREVNETB;
LABEL RISKCAT ='RISK CATEGORY'
      BALANCE ='CURR MTH BAL'
      TOTIIS  ='CURR MTH IIS'
      NETBAL  ='CURR MTH NET'
      VARBAL  ='INCREASE/(DECREASE)'
      PREVNETB='PREV MTH NET';
FORMAT RISKCAT RISK. BALANCE TOTIIS NETBAL VARBAL PREVNETB COMMA20.2;
RUN;
*;
