*;
OPTIONS NODATE NONUMBER NOCENTER MISSING=0;

%INC PGM(PBBDPFMT);

DATA REPTDATE;
    SET DEPOSIT.REPTDATE;
    CALL SYMPUT('REPTMON',PUT(MONTH(REPTDATE),Z2.));
    CALL SYMPUT('REPTYEAR',PUT(REPTDATE,YEAR4.));
    CALL SYMPUT('RDATE',PUT(REPTDATE,DDMMYY8.));
    CALL SYMPUT('ZDATE',PUT(REPTDATE,Z5.));
    CALL SYMPUT('REPTDT6',PUT(REPTDATE,YYMMDDN6.));
RUN;

LIBNAME CIS  "SAP.PBB.CIDATAWH.DAILY" DISP=SHR;

DATA BRHDATA;
  INFILE BRHFILE LRECL=80;
  INPUT @2 BRANCH  3.
        @6 BRCH $3.;
RUN;
PROC SORT DATA=BRHDATA; BY BRANCH;RUN;

DATA PREV;
  KEEP ACCTNO PREBAL;
  SET DEP0.CURRENT;
  PREBAL=CURBAL;
  IF PREBAL < 0 THEN PREBAL = 0;
PROC SORT; BY ACCTNO;
*;
DATA CRMOVE;
  SET DEPOSIT.CURRENT;
  PRODCD=PUT(PRODUCT,CAPROD.);
  IF OPENIND NOT IN ('B','C','P');
  IF PRODCD IN ('N','42610') OR PRODUCT IN (30,31,34,79,068,069,72,
                                            104,105) THEN DELETE;
  IF CURBAL < 0 THEN CURBAL = 0;
  IF NOT (3590000000<=ACCTNO<=3599999999);
*;
PROC SORT DATA=CRMOVE; BY BRANCH;RUN;

DATA CRMOVE;
  MERGE CRMOVE(IN=A) BRHDATA;
  BY BRANCH;
  IF A;
RUN;
PROC SORT DATA=CRMOVE;BY ACCTNO;RUN;
DATA CRMOVE;
  MERGE CRMOVE(IN=A) PREV; BY ACCTNO;
  IF A;
  IF PREBAL=. THEN PREBAL=0.00;
  MOVEMENT=CURBAL-PREBAL;
  IF ABS(MOVEMENT) >= 999999.99;
RUN;
PROC SORT; BY ACCTNO;
DATA CNAME;
  SET CIS.CISR1CA&REPTDT6;
  IF  SECCUST='901';
  KEEP ACCTNO CUSTNAME CUSTNO MNIADDL1 MNIADDL2;
PROC SORT DATA=CNAME OUT=CNAME NODUPKEY; BY ACCTNO;
DATA CRMOVE;
  MERGE CNAME CRMOVE(IN=A); BY ACCTNO;
  IF A;
  *IF CUSTNAM1=' ' THEN CUSTNAM1=NAME;
*;
PROC SORT DATA=CRMOVE;
 BY DESCENDING MOVEMENT ACCTNO;
*;
DATA CRMOVE;
   RETAIN COUNT 0;
   SET CRMOVE;
   COUNT=COUNT+1;
   MOVEMT=ROUND((MOVEMENT*0.000001), .01);
RUN;

%MACRO CURRENT;
OPTIONS NODATE NONUMBER NOCENTER MISSING=0;
DATA _NULL_;
  CALL SYMPUT('CRMOVE', N);
  SET CRMOVE   NOBS=N;
RUN;

TITLE1 'REPORT ID : DMMISR42';
TITLE2 "CREDIT MOVEMENT IN BANK'S DEMAND DEPOSITS AS AT" &RDATE;
TITLE3 'DAILY NET INCREASE/DECREASE OF RM 1 MILLION & ABOVE '
       'PER ACCOUNT (CONVENTIONAL)';
TITLE6;

%IF &CRMOVE EQ 0 %THEN %DO;
 DATA _NULL_;
   FILE PRINT;
   PUT;PUT;
   PUT @5 '*******************************************************';
   PUT @5 'NO CUSTOMER WITH CREDIT MOVEMENT OF 1 MILLION AND ABOVE';
   PUT @5 "AT &RDATE";
   PUT @5 '*******************************************************';
 RUN;
%END;
%ELSE %DO;
PROC REPORT DATA=CRMOVE NOWD HEADLINE SPLIT='*' LS=256;
  COLUMN COUNT BRANCH ACCTNO BRCH MNIADDL1
         MOVEMT CUSTNAME MNIADDL2 CUSTNO CURBAL PREBAL PRODUCT
         CUSTCODE SECOND;
  DEFINE COUNT   / DISPLAY FORMAT=5. 'NO.';
  DEFINE BRANCH  / DISPLAY FORMAT=6. 'BRANCH CODE';
  DEFINE ACCTNO  / DISPLAY FORMAT=10. 'ACCOUNT' 'NO.';
  DEFINE BRCH    /         FORMAT=$6.  'BRANCH ABBR';
  DEFINE MNIADDL1 / FORMAT=$40. 'NAME1 OF CUSTOMER (FR. A/C LVL)';
  DEFINE MOVEMT  / FORMAT=COMMA11.2
                   "DAILY NET" "INC./(DEC.)" "(RM'MIL)";
  DEFINE CUSTNAME / FORMAT=$50. 'NAME OF CUSTOMER (FR. CIS LVL)';
  DEFINE MNIADDL2 / FORMAT=$40. 'NAME2 OF CUSTOMER (FR. A/C LVL)';
  DEFINE CUSTNO  / DISPLAY FORMAT=8. 'CUSTOMER CIS NO.';
  DEFINE CURBAL  / FORMAT=COMMA14.2 'CURRENT DAY' 'BALANCE (RM)';
  DEFINE PREBAL  / FORMAT=COMMA14.2 'PREVIOUS DAY' 'BALANCE (RM)';
  DEFINE PRODUCT / DISPLAY FORMAT=7. 'PRODUCT CODE';
  DEFINE CUSTCODE / DISPLAY FORMAT=8. 'CUSTOMER CODE';
  DEFINE SECOND  / DISPLAY FORMAT=9. 'SECONDARY OFFICER NO.';

  RBREAK AFTER / PAGE DUL OL SUMMARIZE;

RUN;
%END;
%MEND CURRENT;
%CURRENT;

