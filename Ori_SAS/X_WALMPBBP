/** PROGRAM : WALMPBBP                       **/
/** DATE    : 1/12/96                        **/
/** REPORT  : RDAL PART II (WALKER ITEMS )   **/

/** INCLUDE THE FORMAT FILE **/
/** AND CREATE THE FORMAT   **/
%INC PGM(WALMPBBF);

PROC FORMAT CNTLIN=WALM;
RUN;

/** JOINING THE DATA SETS **/

PROC SQL;
  CREATE TABLE R2R913 AS
  SELECT T1.SET_ID, T1.USERCD, T2.ACCT_NO FROM
  ALM.R2R115 T1, ALM.R2R913 T2
  WHERE T1.SET_ID = T2.SET_ID;
QUIT;

PROC SQL;
  CREATE TABLE BNM.ALMGL&REPTMON&NOWK AS
  SELECT T1.SET_ID, T1.USERCD,
         T2.ACCT_NO, T2.GLAMT,
         SUBSTR(T2.ACCT_NO, 1, 4) AS BRNO FROM
  R2R913 T1, ALM.R2GL4000 T2
  WHERE T1.ACCT_NO = T2.ACCT_NO
    AND T2.ACKIND = 'N';
QUIT;

PROC SORT DATA=BNM.ALMGL&REPTMON&NOWK
          OUT=ALMGL&REPTMON&NOWK;
BY BRNO SET_ID;
RUN;

DATA BNM.W2AL&REPTMON&NOWK(KEEP=BRANCH SET_ID BNMCODE AMOUNT);
  SET ALMGL&REPTMON&NOWK;
  BY BRNO SET_ID;
  IF FIRST.SET_ID THEN TGLAMT=0;
  SELECT(USERCD);
    WHEN('6666') IF GLAMT < 0 THEN GLAMT=0;
    WHEN('7777') IF GLAMT > 0 THEN GLAMT=0;
    WHEN('8888') DO;
                 IF GLAMT > 0 THEN GLAMT=0;
                 ELSE GLAMT=(-1)*GLAMT;
                 END;
    WHEN('9999') GLAMT=(-1)*GLAMT;
    OTHERWISE;
  END;
  TGLAMT+GLAMT;
  IF LAST.SET_ID THEN DO;
     BRANCH=INPUT(BRNO, 4.);
     BNMCODE=PUT(SET_ID, $BNMCODE.);
     RENAME TGLAMT=AMOUNT;
     OUTPUT;
  END;
RUN;

PROC SUMMARY DATA=BNM.W2AL&REPTMON&NOWK NWAY;
CLASS BNMCODE;
VAR AMOUNT;
OUTPUT OUT=BNM.WALM&REPTMON&NOWK (DROP=_TYPE_ _FREQ_)
       SUM=AMOUNT;
RUN;

PROC SORT DATA=BNM.WALM&REPTMON&NOWK OUT=BNM.WALM&REPTMON&NOWK;
BY BNMCODE;RUN;
PROC SORT DATA=WALM(RENAME=(LABEL=BNMCODE))
          OUT=WALM(KEEP=BNMCODE) NODUPKEYS;
BY BNMCODE;RUN;

/*PROC FORMAT;
  PICTURE RIBU
    LOW-HIGH = '000,000,000,009'
               (MULT=.001);
RUN;*/

DATA BNM.WALM&REPTMON&NOWK;
  MERGE WALM(IN=A)
        BNM.WALM&REPTMON&NOWK(IN=B);
  BY BNMCODE;
  IF A AND NOT B THEN AMOUNT=0;
  /*AMOUNT=ROUND(AMOUNT,1000);
  FORMAT AMOUNT RIBU.;*/
RUN;

OPTIONS NOCENTER NODATE NONUMBER;
TITLE1 &SDESC;
TITLE2 'REPORT ON DOMESTIC ASSETS AND LIABILITIES PART II - WALKER';
TITLE3 'REPORT DATE : ' &RDATE;
TITLE4;
PROC PRINT DATA=BNM.WALM&REPTMON&NOWK;
FORMAT AMOUNT COMMA25.2;
RUN;
