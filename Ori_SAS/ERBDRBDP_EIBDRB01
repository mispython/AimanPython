*;
OPTIONS NOCENTER NODATE NONUMBER MISSING=0;

DATA REPTDATE;
   SET DEPO.REPTDATE;
   CALL SYMPUT('REPTYEAR',PUT(REPTDATE,YEAR4.));
   CALL SYMPUT('REPTYY',PUT(REPTDATE,YEAR2.));
   CALL SYMPUT('REPTMON',PUT(MONTH(REPTDATE),Z2.));
   CALL SYMPUT('REPTDAY',PUT(DAY(REPTDATE),Z2.));
   CALL SYMPUT('REPTDATE',PUT(REPTDATE,8.));
   CALL SYMPUT('RDATE',PUT(REPTDATE,DDMMYY8.));
RUN;

DATA FCY;
   SET DEPO.FD;
   WHERE CURCODE NE 'MYR' AND
         CURBAL GE 0;

   ID=3;
   REPTDATE=&REPTDATE;
   CURBAL=CURBAL/1000;
   IF OPENDT NOT IN (0,.) THEN
      ODATES = INPUT(SUBSTR(PUT(OPENDT,Z11.),1,8),MMDDYY8.);
   ELSE ODATES = 0;
   OPENDT = ODATES;

   IF CLOSEDT NOT IN (0,.) THEN
      CDATES = INPUT(SUBSTR(PUT(CLOSEDT,Z11.),1,8),MMDDYY8.);
   ELSE CDATES = 0;
   CLOSEDT = CDATES;

   IF (YEAR(OPENDT) = &REPTYEAR) & (MONTH(OPENDT) = &REPTMON) THEN DO;
      IF (YEAR(CLOSEDT)=YEAR(OPENDT)) & (MONTH(CLOSEDT)=MONTH(OPENDT))
         THEN OPCLMH=1;
   END;

   IF OPENIND NOT IN ('B','C','P') THEN OSACCT=1;
   IF OPCLMH  NE 1 AND CURBAL GT 0 THEN NOACCT=1;
RUN;

DATA WK;
   SET WALK.WKDTL&REPTYY&REPTMON&REPTDAY;
   ID=2;
   REPTDATE=&REPTDATE;
   CURBAL=(-1)*(CURBAL/1000);
   CURCODE=SUBSTR(CURR,4,3);
RUN;

DATA FCY;
   SET FCY WK;
RUN;

PROC SORT DATA=FCY; BY REPTDATE ID CURCODE; RUN;

PROC SUMMARY DATA=FCY NWAY;
   BY  REPTDATE ID CURCODE;
   VAR CURBAL NOACCT OSACCT;
   OUTPUT OUT=FCY (DROP=_TYPE_ _FREQ_) SUM=;
RUN;

DATA FCY;
   SET FCY;
   CURBAL=ROUND(CURBAL/1000,.001);
   OUTPUT;
   ID=1;
   OUTPUT;
RUN;

PROC SORT DATA=FCY; BY REPTDATE ID CURCODE; RUN;
PROC SUMMARY DATA=FCY NWAY;
   BY  REPTDATE ID;
   VAR CURBAL NOACCT OSACCT;
   OUTPUT OUT=TOTAL (DROP=_TYPE_ _FREQ_) SUM=TOTFCYFD NOACCT OSACCT;
RUN;

PROC SUMMARY DATA=FCY NWAY;
   BY  REPTDATE ID CURCODE;
   VAR CURBAL;
   OUTPUT OUT=FCY (DROP=_TYPE_ _FREQ_) SUM=;
RUN;

PROC TRANSPOSE DATA=FCY OUT=FD(DROP=_NAME_);
   BY REPTDATE ID;
   ID CURCODE;
RUN;

DATA RBDP;
   MERGE FD(IN=A) TOTAL(IN=B);
   BY REPTDATE ID;
   IF A & B;
   IF USD = . THEN USD = 0;
   IF NZD = . THEN NZD = 0;
   IF AUD = . THEN AUD = 0;
   IF GBP = . THEN GBP = 0;
   IF HKD = . THEN HKD = 0;
   IF SGD = . THEN SGD = 0;
   IF EUR = . THEN EUR = 0;
   IF JPY = . THEN JPY = 0;
   IF CAD = . THEN CAD = 0;
   IF CNY = . THEN CNY = 0;
   IF CHF = . THEN CHF = 0;
   IF THB = . THEN THB = 0;
RUN;

%MACRO APPEND;
%IF "&REPTDAY" EQ "01" %THEN
   %DO;
      DATA STORE.RB01DP&REPTMON;
         SET RBDP;
      RUN;
   %END;
   %ELSE %DO;
      DATA STORE.RB01DP&REPTMON;
         SET RBDP STORE.RB01DP&REPTMON;
      RUN;
      PROC SORT DATA=STORE.RB01DP&REPTMON NODUPKEY;
         BY REPTDATE ID;
      RUN;
   %END;
%MEND APPEND;
%APPEND;

DATA _NULL_;
  SET STORE.RB01DP&REPTMON;
  FORMAT RPDATE $8.
         NOACCT OSACCT COMMA10.
         TOTFCYFD AUD CAD CNY EUR GBP HKD JPY NZD SGD USD CHF THB
         COMMA16.3;
  IF ID = 1 THEN RPDATE = PUT(REPTDATE,DDMMYY8.);
  FILE SASLIST;
  IF _N_ = 1 THEN DO;
     PUT @1   'REPORT ID : EIBDRB01';
     PUT @1   'DAILY TOTAL OUTSTANDING BALANCE/ACCOUNT ON FCY FD '
              '& FOREIGN COMPANIES';
     PUT @1   'AS AT ' "&RDATE";
     PUT @1   ' ';
     PUT @2   'DATE'  @60  'OUTSTANDING AMOUNT (RM''MIL) (3-decimal)';
     PUT @213 'TOTAL NO OF O/S ACCT';
     PUT @15  'USD'   @30  'NZD'   @45  'AUD'   @60  'GBP'   @75  'HKD'
         @90  'SGD'   @105 'EUR'   @120 'JPY'   @135 'CAD'   @150 'CNY'
         @165 'CHF'   @180 'THB'   @195 'TOT AMT O/S RM'
         @213 'NO OF A/C'          @227 'NO OF A/C'
         ;
     PUT @2   '(A)+(B)'
         @15  'TOTAL' @30  'TOTAL' @45  'TOTAL' @60  'TOTAL'
         @75  'TOTAL' @90  'TOTAL' @105 'TOTAL' @120 'TOTAL'
         @135 'TOTAL' @150 'TOTAL' @165 'TOTAL' @180 'TOTAL'
         @195 'TOTAL' @213 '(EXCL' @227 '(INCL'
         ;
     PUT @4   '(A)'
         @15  'FRGN CO.'   @30  'FRGN CO.'   @45  'FRGN CO.'
         @60  'FRGN CO.'   @75  'FRGN CO.'   @90  'FRGN CO.'
         @105 'FRGN CO.'   @120 'FRGN CO.'   @135 'FRGN CO.'
         @150 'FRGN CO.'   @165 'FRGN CO.'   @180 'FRGN CO.'
         @195 'FRGN CO.'   @213 'ZERO'       @227 'ZERO'
         ;
     PUT @4   '(B)'
         @15  'FCY FD'     @30  'FCY FD'     @45  'FCY FD'
         @60  'FCY FD'     @75  'FCY FD'     @90  'FCY FD'
         @105 'FCY FD'     @120 'FCY FD'     @135 'FCY FD'
         @150 'FCY FD'     @165 'FCY FD'     @180 'FCY FD'
         @195 'FCY FD'     @213 'BALANCE)'   @227 'BALANCE)'
         ;
     PUT @1   275*'-'
         ;
  END;
     PUT @2   RPDATE
         @15  USD     @30  NZD     @45  AUD     @60  GBP     @75  HKD
         @90  SGD     @105 EUR     @120 JPY     @135 CAD     @150 CNY
         @165 CHF     @180 THB     @195 TOTFCYFD
         @213 NOACCT  @227 OSACCT
         ;
  IF ID=3 THEN
     PUT @01  275*'-'
         ;
RUN;
