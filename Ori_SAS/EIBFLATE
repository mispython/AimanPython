//EIBFLATE JOB MSGCLASS=X,MSGLEVEL=(1,1),REGION=8M
/*JOBPARM S=S1M1
//SAS609    EXEC SAS609
//MISFILE   DD DSN=RBP2.B033.LNINTR.NPL.MISFILE,DISP=SHR
//HPFILE    DD DSN=RBP2.B033.INTRADAY.NPL.FILE.HP,DISP=SHR
//LNSTATFL  DD DSN=RBP2.B033.INTRADAY.NPL.BTCARD,DISP=SHR
//RNRFILE   DD DSN=RBP2.B033.LNRNRMIS.RECLASS(0),DISP=SHR
//RNRHPFL   DD DSN=RBP2.B033.LNRNRMIS.RECLASS.HP(0),DISP=SHR
//AKPKFL    DD DSN=RBP2.B033.LNLNNRID.MTHEND(0),DISP=SHR
//          DD DSN=RBP2.B033.LNHPNRID.MTHEND(0),DISP=SHR
//MORLNFL   DD DSN=RBP2.B033.LNSASMOR.OUTMIS,DISP=SHR
//MORCOV    DD DSN=RBP2.B033.LNMORCOV.OUTMIS,DISP=SHR
//MORHPFL   DD DSN=RBP2.B033.LNSASMOR.OUTMIS.HP,DISP=SHR
//FIXLOAN   DD DSN=SAP.LOAN.FIX.LATE,DISP=OLD
//LOAN      DD DSN=SAP.PBB.MNILN(0),DISP=OLD
//ILOAN     DD DSN=SAP.PIBB.MNILN(0),DISP=OLD

DATA _NULL_;
   SET LOAN.REPTDATE;
  SELECT;
     WHEN(1 <= DAY(REPTDATE) <= 8) CALL SYMPUT('NOWK', PUT('1', $1.));
     WHEN(9 <= DAY(REPTDATE) <= 15) CALL SYMPUT('NOWK', PUT('2', $1.));
     WHEN(16 <= DAY(REPTDATE) <= 22) CALL SYMPUT('NOWK', PUT('3', $1.));
     OTHERWISE CALL SYMPUT('NOWK', PUT('4', $1.));
  END;
   CALL SYMPUT('REPTMON',PUT(MONTH(REPTDATE),Z2.));
   CALL SYMPUT('REPTYR',PUT(YEAR(REPTDATE),Z4.));
RUN;
*;
%MACRO MONTHLY;
   %IF "&NOWK" EQ "4" %THEN %DO;
PROC SORT DATA=LOAN.LNNOTE;  BY ACCTNO NOTENO;
PROC SORT DATA=ILOAN.LNNOTE; BY ACCTNO NOTENO;
*;
  /* LN  */
DATA LNSTAT;
   INFILE LNSTATFL;
   INPUT @8  ACCTNO     11.
         @19 NOTENO      5.
         @80 LOANSTAT    1.
         ;
RUN;

DATA MISFILE;
   INFILE MISFILE;
   INPUT @1  ACCTNO     11.
         @12 NOTENO      5.
         @17 RISKRATE   $1.
         @18 BILDUE1    11.;
  IF BILDUE1 GT 0 THEN
     BLDATE = INPUT(SUBSTR(PUT(BILDUE1,Z11.),1,8),YYMMDD8.);
RUN;
*;
DATA LOAN.LNNOTE;
   MERGE LOAN.LNNOTE(IN=A) MISFILE(IN=B) LNSTAT;
   BY ACCTNO NOTENO;
   IF A;
RUN;
DATA ILOAN.LNNOTE;
   MERGE ILOAN.LNNOTE(IN=A) MISFILE(IN=B) LNSTAT;
   BY ACCTNO NOTENO;
   IF A;
RUN;
*;
DATA FIXLOAN.MISFILE&REPTMON&REPTYR;
   SET MISFILE;
RUN;

DATA FIXLOAN.LNSTAT&REPTMON&REPTYR;
   SET LNSTAT;
RUN;

  /* HP  */
DATA HPFILE;
   INFILE HPFILE;
   INPUT @1  ACCTNO     11.
         @13 NOTENO      5.
         @19 BILDUE1    11.;
  IF BILDUE1 GT 0 THEN
     BLDATE = INPUT(SUBSTR(PUT(BILDUE1,Z11.),1,8),YYMMDD8.);
RUN;
*;
DATA LOAN.LNNOTE;
   MERGE LOAN.LNNOTE(IN=A) HPFILE(IN=B);
   BY ACCTNO NOTENO;
   IF A;
RUN;
DATA ILOAN.LNNOTE;
   MERGE ILOAN.LNNOTE(IN=A) HPFILE(IN=B);
   BY ACCTNO NOTENO;
   IF A;
RUN;
DATA FIXLOAN.HPFILE&REPTMON&REPTYR;
   SET HPFILE;
RUN;

  /* RNR  */
DATA RNR;
   INFILE RNRFILE;
   INPUT @01 RRCYCLE     $3.
         @05 ACCTNO      11.
         @17 NOTENO       5.
         @23 BILDUE1     11.
         @35 LOANSTAT     1.
         @36 NUMCPNS      4.
         @41 FCLOSUREDT  11.
         @53 BILLCNT      5.
         @58 ASSMDATE    11.
         @80 RSN         $1.
         @81 RR_IL_RECLASS_DT DDMMYY8.
         ;
   IF BILDUE1 GT 0 THEN
      BLDATE = INPUT(SUBSTR(PUT(BILDUE1,Z11.),1,8),YYMMDD8.);
RUN;
PROC SORT DATA=RNR DUPOUT=FIXLOAN.RNR NODUPKEY; BY ACCTNO NOTENO; RUN;

DATA RNRMOR;
   INFILE MORLNFL;
   INPUT @001 ACCTNO     11.
         @013 NOTENO      5.
         @019 MORDAYARR   5.
         @025 MO_INSTL_ARR 3.
         ;
RUN;
PROC SORT DATA=RNRMOR DUPOUT=FIXLOAN.RNRMOR NODUPKEY;
   BY ACCTNO NOTENO;
RUN;

DATA RNRCOV;
   INFILE MORCOV;
   INPUT @001 ACCTNO      11.
         @013 NOTENO       5.
         @019 MORDAYARR    5.
         @025 MO_INSTL_ARR 3.
         @029 LOANSTAT     1.
         ;
RUN;
PROC SORT DATA=RNRCOV DUPOUT=FIXLOAN.RNRCOV NODUPKEY;
   BY ACCTNO NOTENO;
RUN;

  /* RNR-HP */
DATA RNRHP;
   INFILE RNRHPFL;
   INPUT @001 RRCYCLE    $3.
         @005 ACCTNO     11.
         @017 NOTENO      5.
     /*  @023 BILDUE1    11.  EXCLUDE UPDATE FOR HP (REQ BY HPCC) */
         @035 USER5      $1.
         @037 NUMCPNS     3.
         @041 FCLOSUREDT 11.
         @069 CPNSTDTE   11.
         ;
RUN;
PROC SORT DATA=RNRHP DUPOUT=FIXLOAN.RNRHP NODUPKEY;
   BY ACCTNO NOTENO;
RUN;

DATA RNRHPMOR;
   INFILE MORHPFL;
   INPUT @001 ACCTNO     11.
         @013 NOTENO      5.
         @019 MORDAYARR   5.
         ;
RUN;
PROC SORT DATA=RNRHPMOR DUPOUT=FIXLOAN.RNRHPMOR NODUPKEY;
   BY ACCTNO NOTENO;
RUN;

DATA AKPK;
   INFILE AKPKFL;
   INPUT @001 ACCTNO           PD6.
         @007 NOTENO           PD3.
         @010 NURS_TAG          $6.
         @016 NUR_STARTDT  DDMMYY8.
         @024 NURS_TAGDT   DDMMYY8.
         @032 NURS_ENDDT   DDMMYY8.
         @040 NURS_COUNTER     PD3.
         ;
RUN;
PROC SORT DATA=AKPK DUPOUT=FIXLOAN.AKPK NODUPKEY; BY ACCTNO NOTENO; RUN;

DATA LOAN.LNNOTE;
   MERGE LOAN.LNNOTE(IN=A) RNR RNRHP RNRMOR RNRHPMOR AKPK RNRCOV;
   BY ACCTNO NOTENO;
   IF A;
   IF USER5 NE 'N' THEN OLDNOTEDAYARR = 0;
RUN;

DATA ILOAN.LNNOTE;
   MERGE ILOAN.LNNOTE(IN=A) RNR RNRHP RNRMOR RNRHPMOR AKPK RNRCOV;
   BY ACCTNO NOTENO;
   IF A;
   IF USER5 NE 'N' THEN OLDNOTEDAYARR = 0;
RUN;

DATA FIXLOAN.RNRFILE&REPTMON&REPTYR;
   SET RNR;
RUN;

DATA FIXLOAN.MORLNFL&REPTMON&REPTYR;
   SET RNRMOR;
RUN;

DATA FIXLOAN.RNRCOV&REPTMON&REPTYR;
   SET RNRCOV;
RUN;

DATA FIXLOAN.RNRHPFL&REPTMON&REPTYR;
   SET RNRHP;
RUN;

DATA FIXLOAN.MORHPFL&REPTMON&REPTYR;
   SET RNRHPMOR;
RUN;

DATA FIXLOAN.AKPKFL&REPTMON&REPTYR;
   SET AKPK;
RUN;

   %END;
%MEND MONTHLY;
*;
%MONTHLY;
*;
