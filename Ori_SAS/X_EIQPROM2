//EIQPROM2 JOB MISEIS,CLASS=A,MSGCLASS=A,NOTIFY=&SYSUID
/*JOBPARM S=S1M2
//PRINT1   OUTPUT CLASS=R,
//         NAME='MR.TAN KONG CHON',
//         ROOM='14TH FLOOR',
//         DEPT='PBB - CREDIT ADMIN',
//         BUILDING='MENARA PBB',
//         ADDRESS=('MENARA PUBLIC BANK',
//         '146 JALAN AMPANG','50450 KUALA LUMPUR'),
//         DEST=S1.LOCAL
//*
//DELETE   EXEC PGM=IEFBR14
//DD1      DD DSN=SAP.PBB.PROMOTE.RPS,
//            DISP=(MOD,DELETE,DELETE),
//            SPACE=(TRK,(0))
//DD2      DD DSN=SAP.PBB.PROMOTE.SUMMARY.TEXT,
//            DISP=(MOD,DELETE,DELETE),
//            SPACE=(TRK,(0))
//DD3      DD DSN=SAP.PBB.PROMOTE.TEXT,
//            DISP=(MOD,DELETE,DELETE),
//            SPACE=(TRK,(0))
//DD4      DD DSN=SAP.PIBB.PROMOTE.TEXT,
//            DISP=(MOD,DELETE,DELETE),
//            SPACE=(TRK,(0))
//*
//EIQPROM2 EXEC SAS609,REGION=8M,WORK='240000,240000'
//LNBILL   DD DSN=SAP.PBB.PROMOTE,DISP=OLD
//PBBSAS   DD DSN=SAP.PBB.SASDATA,DISP=SHR
//PIBSAS   DD DSN=SAP.PIBB.SASDATA,DISP=SHR
//PBBLN    DD DSN=SAP.PBB.MNILN(0),DISP=SHR
//PIBLN    DD DSN=SAP.PIBB.MNILN(0),DISP=SHR
//COLL     DD DSN=SAP.PBB.MNICOL(0),DISP=SHR
//COLLI    DD DSN=SAP.PIBB.MNICOL(0),DISP=SHR
//ELDS     DD DSN=SAP.ELDS.ELAA(0),DISP=SHR
//CIS      DD DSN=SAP.PBB.CISBEXT.LN,DISP=SHR
//BRRPS    DD DSN=SAP.PBB.PROMOTE.TEXT,
//            DISP=(NEW,CATLG,DELETE),
//            SPACE=(CYL,(30,30)),
//            DCB=(RECFM=FBA,LRECL=136,BLKSIZE=13600)
//IBRRPS   DD DSN=SAP.PIBB.PROMOTE.TEXT,
//            DISP=(NEW,CATLG,DELETE),
//            SPACE=(CYL,(30,30)),
//            DCB=(RECFM=FBA,LRECL=136,BLKSIZE=13600)
//PGM      DD DSN=SAP.BNM.PROGRAM,DISP=SHR
//SASLIST  DD DSN=SAP.PBB.PROMOTE.SUMMARY.TEXT,
//            DISP=(NEW,CATLG,DELETE),
//            SPACE=(CYL,(30,30)),
//            DCB=(RECFM=FBA,LRECL=133,BLKSIZE=13300)
//SYSIN    DD *

OPTIONS LINESIZE=135 NOCENTER NONUMBER NODATE YEARCUTOFF=1950;

%INC PGM(PBBELF);
%LET PBBPROD=(200,201,204,205,209,210,211,212,214,215,219,220,
              225,226,227,228,230,233,234,235,236,237,238,239,
              240,241,242,243,300,301,304,305,359,361,363,213,
              216,217,218,231,232,244,245,246,247,315,568,248,
              249,250,348,349,368);
%LET PIBPROD=(152,153,154,155,423,423,424,425,426,175,176,177,
              178,400,401,402,406,407,408,409,410,411,412,413,
              414,415,416,419,420,422,429,430,464);

DATA REPTDATE (KEEP=REPTDATE);
   SET PBBLN.REPTDATE;
   SELECT;
     WHEN(1 <= DAY(REPTDATE) <= 8) CALL SYMPUT('NOWK', PUT('1', $1.));
     WHEN(9 <= DAY(REPTDATE) <= 15) CALL SYMPUT('NOWK', PUT('2', $1.));
     WHEN(16 <= DAY(REPTDATE) <= 22) CALL SYMPUT('NOWK', PUT('3', $1.));
     OTHERWISE CALL SYMPUT('NOWK', PUT('4', $1.));
   END;
   PREVDATE = REPTDATE-DAY(REPTDATE);
   IF MONTH(REPTDATE) GE 6 THEN DO;
      PMTH = MONTH(REPTDATE) - 5;
      PYEAR = YEAR(REPTDATE) - 2;
   END;
   ELSE DO;
      PMTH = MONTH(REPTDATE) + 7;
      PYEAR = YEAR(REPTDATE) - 3;
   END;
   PDATE = MDY(PMTH,1,PYEAR);
   CALL SYMPUT('PRDATE',PDATE);
   CALL SYMPUT('REPTMON', PUT(MONTH(REPTDATE), Z2.));
   CALL SYMPUT('PREVMON', PUT(MONTH(PREVDATE), Z2.));
   CALL SYMPUT('RDATE',PUT(REPTDATE,DDMMYY8.));
   CALL SYMPUT('REPTDT',REPTDATE);
RUN;

DATA LNNOTE(KEEP=ACCTNO NOTENO NAME FLAG1 ORGBAL NETPROC AANO
                 COLLMAKE COLLYEAR NTINDEX SPREAD MODELDES
                 SCORE1 IA_LRU BORSTAT DELQCD GUAREND MAILCODE);
   SET PBBLN.LNNOTE PIBLN.LNNOTE;
   WHERE (LOANTYPE IN &PBBPROD OR LOANTYPE IN &PIBPROD) AND
         LOANSTAT NE 3        AND
         CURBAL GT 0          AND
         LSTTRNCD NE 661;
   FORMAT AANO $13.;
   AANO1 = SUBSTR(LEFT(VINNO),1,13);
   COMP = COMPRESS(AANO1,'@*(#)-');
   COMP = COMPRESS(COMP);
   IF LENGTH(COMP) = 13 THEN AANO = COMP;
   ELSE                      AANO = ' ';
   IF COLLMAKE IN ('.','0','') AND COLLYEAR IN (.,0);
RUN;

DATA LOAN (KEEP=ACCTNO NOTENO COMMNO ISSDTE DAYDIFF PRODUCT
           NOTE1 NOTE2 BRANCH APPRLIMT BALANCE EXPRDATE REMTERM
           PRODUCT);
   SET PBBSAS.LOAN&REPTMON&NOWK
       PIBSAS.LOAN&REPTMON&NOWK;
   WHERE (PRODUCT IN &PBBPROD OR PRODUCT IN &PIBPROD) AND
         LOANSTAT NE 3       AND
         CURBAL GT 0         ;
   THISDATE = INPUT("&RDATE", DDMMYY8.);
   IF BLDATE > 0 THEN DAYDIFF = THISDATE - BLDATE;
   ELSE DAYDIFF = 0;
   IF EXPRDATE > 0 THEN REMTERM = YEAR(EXPRDATE)-YEAR(&REPTDT);
   NOTE1 = SUBSTR(PUT(NOTENO,5.),1,1);
   NOTE2 = SUBSTR(PUT(NOTENO,5.),2,1);
RUN;

PROC SORT DATA=LNBILL.BILL OUT=BILLBAD NODUPKEYS; BY ACCTNO NOTENO;
RUN;
PROC SORT DATA=LOAN; BY ACCTNO NOTENO; RUN;
PROC SORT DATA=LNNOTE; BY ACCTNO NOTENO; RUN;
DATA LOAN;
     MERGE LNNOTE LOAN BILLBAD(IN=A);
     BY ACCTNO NOTENO;
     IF A THEN DELETE;
RUN;

PROC SORT DATA=LOAN; BY ACCTNO COMMNO; RUN;
DATA LNCOMM;
   SET PBBLN.LNCOMM PIBLN.LNCOMM;
   KEEP ACCTNO COMMNO CORGAMT;
RUN;
PROC SORT DATA=LNCOMM NODUPKEY; BY ACCTNO COMMNO; RUN;

DATA LOAN;
   MERGE LOAN(IN=A) LNCOMM;
   BY ACCTNO COMMNO;
   IF A;
RUN;

DATA LOAN;
   SET LOAN;
   WHERE BALANCE > 30000      AND
         ISSDTE < &PRDATE     AND
         FLAG1='F'            AND
         DAYDIFF <= 0         AND
        (PRODUCT IN &PBBPROD  OR PRODUCT IN &PIBPROD) AND
         (NOTE1 NE '1' AND NOTE2 NE '2');
   LMTAPPR = MAX(CORGAMT, ORGBAL, NETPROC, APPRLIMT);
   REPAID = LMTAPPR- BALANCE;
   BRCH = PUT(BRANCH,BRCHCD.);
   *18-2927 AUTO REPRICE/RNR/WATCH LIST/SM/AKPK/LEGALCD;
   *19-1923 SCORE1;
   IF ((NTINDEX  = 1  AND SPREAD = 0.00) OR
       (NTINDEX  = 1  AND SPREAD = 3.50) OR
       (NTINDEX  = 38 AND SPREAD = 3.20) OR
       (NTINDEX  = 38 AND SPREAD = 6.70) OR
       (SUBSTR(MODELDES,1,1) IN ('S','T','R','C')) OR
       (MODELDES = 'Z') OR
       (SUBSTR(MODELDES,5,1) IN ('F')) OR
       (SUBSTR(SCORE1,1,1) IN ('D','E','F','G','H','I')) OR
       (IA_LRU   = 'I') OR
       (BORSTAT  = 'K') OR
       (DELQCD IN ( '9','09','10','11','12','13','14',
                   '15','16','17','18','19','20'))
      ) THEN DELETE;
RUN;
PROC SORT; BY ACCTNO NOTENO;
DATA COLL(KEEP=ACCTNO NOTENO CPRPROPD MRESERVE CPRLANDU HOLDEXPD EXPDT);
   SET COLL.COLLATER COLLI.COLLATER;
   IF    (CPRPROPD IN ('10','11','32','33','34','35') AND
          CPRLANDU IN ('10','11','32','33','34','35')) OR
          MRESERVE = 'Y' THEN EXCL = 'Y';
   IF EXPDATE > 0 THEN
      EXPDT = INPUT(SUBSTR(PUT(EXPDATE,$8.),1,8),DDMMYY8.);
   IF EXCL = 'Y' OR HOLDEXPD = 'L' THEN OUTPUT; *19-1923;
RUN;
PROC SORT; BY ACCTNO NOTENO;
DATA LOAN;
   MERGE LOAN(IN=A) COLL(IN=B);
   BY ACCTNO NOTENO;
   IF A;
   IF EXCL='Y' OR
     (HOLDEXPD='L' AND YRDIF(EXPRDATE,EXPDT,'ACT/ACT')<30) THEN DELETE;
RUN;

PROC SORT DATA=CIS.LOAN(KEEP=ACCTNO CUSTNO EMAILADD SECCUST NEWIC)
          OUT=CIS; BY CUSTNO;
RUN;
DATA CISLN CISBILL(KEEP=CUSTNO);
   SET CIS;
   IF (2500000000<=ACCTNO<=2599999999) OR
      (2850000000<=ACCTNO<=2859999999) THEN OUTPUT CISBILL;
   ELSE                                     OUTPUT CISLN;
RUN;
PROC SORT DATA=CISBILL NODUPKEY; BY CUSTNO; RUN;
DATA CISLN(KEEP=ACCTNO);
   MERGE CISLN(IN=A) CISBILL(IN=B);
   BY CUSTNO;
   IF A AND B;
RUN;
PROC SORT DATA=CISLN NODUPKEY; BY ACCTNO; RUN;
PROC SORT DATA=CIS; WHERE SECCUST = '901'; BY ACCTNO; RUN;

DATA LOAN;
   MERGE LOAN(IN=A) CISLN(IN=B) CIS;
   BY ACCTNO;
   IF A;
   IF A AND B THEN DELETE;
RUN;
PROC SORT; BY AANO; RUN;

PROC SORT DATA=ELDS.ELBNMAX(KEEP=AANO REINPROD) OUT=ELDS; BY AANO; RUN;
*;
DATA LOAN;
    MERGE LOAN(IN=A) ELDS(IN=B);
    BY AANO;
    IF A;
    IF REINPROD = 'Y' THEN DELETE;
RUN;
PROC SUMMARY DATA=LOAN NWAY;
     CLASS BRCH;
     VAR LMTAPPR BALANCE REPAID;
     OUTPUT OUT=BILLSUM (RENAME=(_FREQ_=NOACCT)) SUM=;

PROC REPORT DATA=BILLSUM
     HEADLINE HEADSKIP NOWD SPLIT='*' MISSING;
     COLUMN BRCH NOACCT LMTAPPR BALANCE REPAID;
     DEFINE BRCH      / ORDER FORMAT=$6.  'BRANCH' ID ;
     DEFINE NOACCT    / FORMAT=COMMA9. 'TOTAL NO.*OF A/C';
     DEFINE LMTAPPR   / SUM FORMAT=COMMA15.2
                        'FACILITY/*APPROVED LIMIT';
     DEFINE BALANCE   / SUM FORMAT=COMMA15.2
                        'OUTSTANDING*BALANCE';
     DEFINE REPAID    / SUM FORMAT=COMMA15.2
                        'AMOUNT*REPAID ';
     RBREAK AFTER  / DUL DOL SUMMARIZE;
     COMPUTE AFTER BRCH;
     ENDCOMP;
     TITLE1 'PBB HL/FL/TL CUSTOMER WITH MINIMUM 2 1/2 YEARS';
     TITLE2 'PROMPT REPAYMENT RECORD AS AT ' &RDATE;
     TITLE3 'REPORT ID : EIQPROM2 ';
RUN;

********************************************************************
* PRINTING FOR RPS                                                 *
********************************************************************;
PROC SORT DATA=LOAN; BY ACCTNO NOTENO; RUN;
DATA LOAN LNBILL.LOAN&REPTMON;
   MERGE LOAN(IN=A) LNBILL.LOAN&PREVMON(IN=B KEEP=ACCTNO NOTENO);
   BY ACCTNO NOTENO;
   IF A;
   NEW = '';
   IF A AND NOT B THEN NEW = 'Y';
RUN;
PROC SORT DATA=LOAN; BY BRANCH ACCTNO; RUN;

DATA LNPBB LNPIB;
   SET LOAN;
   IF PRODUCT IN &PBBPROD THEN OUTPUT LNPBB;
   ELSE                        OUTPUT LNPIB;
RUN;

%MACRO PRINTER(DSN,FLNAME);
DATA _NULL_;
  SET &DSN;
  BY BRANCH;
  FILE &FLNAME NOTITLES;
  RETAIN LINECNT PAGECNT;

  IF FIRST.BRANCH THEN DO;
     PAGECNT=0;
     ACCNT=0;
     ACLIMT=0;
     ACBAL=0;
     ACPAID=0;
     LINK NEWPAGE;
  END;

  ACCNT+1;
  ACLIMT+LMTAPPR;
  ACBAL+BALANCE;
  ACPAID+REPAID;

  PUT @4  BRANCH  3. @9  ACCTNO 10. @20 NOTENO 5.
      @26 AANO       @42 PRODUCT 3. @48 NAME
      @72 ISSDTE DATE8. @80  LMTAPPR 12.2
      @93 BALANCE 12.2  @106 REPAID  12.2
      @123 REMTERM 3.   @130 NEW     $1.;
  LINECNT +1;

  IF LINECNT > 55 THEN DO;
     LINK NEWPAGE;
  END;

  IF LAST.BRANCH THEN DO;
     PUT @1   132*'-';
     PUT @1 'NO OF A/C : ' @17 ACCNT COMMA8.0
         @80 ACLIMT 12.2
         @93 ACBAL 12.2   @106 ACPAID 12.2 ;
     PUT @1   132*'=';
     LINECNT +4;
  END;
  RETURN;

  NEWPAGE:
    PAGECNT+1;
    IF "&DSN" = "LNPBB" THEN
       PUT @1   'BRANCH    : ' BRANCH   3.
           @50  'P U B L I C   B A N K   B E R H A D'
           @120 'PAGE NO : ' PAGECNT;
    ELSE
       PUT @1   'BRANCH    : ' BRANCH   3.
           @43  'P U B L I C   I S L A M I C   B A N K   B E R H A D'
           @120 'PAGE NO : ' PAGECNT;
    PUT @1   'REPORT NO : EIQPROM2'
        @47  'HL/FL/TL CUSTOMER WITH MINIMUM 2 1/2 YEARS';
    PUT @49  'PROMPT REPAYMENT RECORD AS AT ' "&RDATE";
    PUT @1   ' ';
    PUT @21  'NOTE'
        @40  'PRODUCT'
        @73  'RELEASED'
        @84  'APPROVED'
        @100 'O/S'
        @111 'AMT'
        @119 'REMAINING';
    PUT @1   'BRANCH'
        @11  'A/C NO'
        @22  'NO'
        @29  'A/A NO'
        @42  'CODE'
        @48  'NAME OF CUSTOMER'
        @75  'DATE'
        @86  'LIMIT'
        @98  'BALANCE'
        @109 'REPAID'
        @121 'TENURE'
        @129 'NEW';
    PUT @1   132*'-';
    PUT @1   ' ';
    LINECNT = 10;
  RETURN;
RUN;
%MEND PRINTER;
%PRINTER(LNPBB,BRRPS);
%PRINTER(LNPIB,IBRRPS);
//**********************************************************************
//*--> CONCATENATE DAILY FILES.
//**********************************************************************
//COPY     EXEC PGM=ICEGENER
//SYSPRINT DD SYSOUT=*
//SYSUT1   DD DISP=SHR,DSN=SAP.PBB.PROMOTE.TEXT
//         DD DISP=SHR,DSN=SAP.PIBB.PROMOTE.TEXT
//SYSUT2   DD DSN=SAP.PBB.PROMOTE.RPS,
//            DISP=(NEW,CATLG,DELETE),
//            SPACE=(CYL,(30,30)),
//            DCB=(RECFM=FBA,LRECL=136,BLKSIZE=13600)
//SYSIN    DD DUMMY
//**********************************************************************
//*--> COLDER FILES
//**********************************************************************
//DELETE   EXEC PGM=IEFBR14
//DD3      DD DSN=SAP.PBB.PROMOTE.TEXT.COLD,
//            DISP=(MOD,DELETE,DELETE),
//            SPACE=(TRK,(0))
//DD4      DD DSN=SAP.PIBB.PROMOTE.TEXT.COLD,
//            DISP=(MOD,DELETE,DELETE),
//            SPACE=(TRK,(0))
//*
//EIQPROM2 EXEC SAS609,REGION=8M,WORK='240000,240000'
//LNBILL   DD DSN=SAP.PBB.PROMOTE,DISP=OLD
//PBBSAS   DD DSN=SAP.PBB.SASDATA,DISP=SHR
//PIBSAS   DD DSN=SAP.PIBB.SASDATA,DISP=SHR
//PBBLN    DD DSN=SAP.PBB.MNILN(0),DISP=SHR
//PIBLN    DD DSN=SAP.PIBB.MNILN(0),DISP=SHR
//COLL     DD DSN=SAP.PBB.MNICOL(0),DISP=SHR
//COLLI    DD DSN=SAP.PIBB.MNICOL(0),DISP=SHR
//ELDS     DD DSN=SAP.ELDS.ELAA(0),DISP=SHR
//CIS      DD DSN=SAP.PBB.CISBEXT.LN,DISP=SHR
//BRRPS    DD DSN=SAP.PBB.PROMOTE.TEXT.COLD,
//            DISP=(NEW,CATLG,DELETE),
//            SPACE=(CYL,(30,30)),
//            DCB=(RECFM=FBA,LRECL=136,BLKSIZE=13600)
//IBRRPS   DD DSN=SAP.PIBB.PROMOTE.TEXT.COLD,
//            DISP=(NEW,CATLG,DELETE),
//            SPACE=(CYL,(30,30)),
//            DCB=(RECFM=FBA,LRECL=136,BLKSIZE=13600)
//PGM      DD DSN=SAP.BNM.PROGRAM,DISP=SHR
//SYSIN    DD *

OPTIONS LINESIZE=135 NOCENTER NONUMBER NODATE YEARCUTOFF=1950
        PAGESIZE=57;

%INC PGM(PBBELF);
%LET PBBPROD=(200,201,204,205,209,210,211,212,214,215,219,220,
              225,226,227,228,230,233,234,235,236,237,238,239,
              240,241,242,243,300,301,304,305,359,361,363,213,
              216,217,218,231,232,244,245,246,247,315,568,248,
              249,250,348,349,368);
%LET PIBPROD=(152,153,154,155,423,423,424,425,426,175,176,177,
              178,400,401,402,406,407,408,409,410,411,412,413,
              414,415,416,419,420,422,429,430,464);

DATA REPTDATE (KEEP=REPTDATE);
   SET PBBLN.REPTDATE;
   SELECT;
     WHEN(1 <= DAY(REPTDATE) <= 8) CALL SYMPUT('NOWK', PUT('1', $1.));
     WHEN(9 <= DAY(REPTDATE) <= 15) CALL SYMPUT('NOWK', PUT('2', $1.));
     WHEN(16 <= DAY(REPTDATE) <= 22) CALL SYMPUT('NOWK', PUT('3', $1.));
     OTHERWISE CALL SYMPUT('NOWK', PUT('4', $1.));
   END;
   PREVDATE = REPTDATE-DAY(REPTDATE);
   IF MONTH(REPTDATE) GE 6 THEN DO;
      PMTH = MONTH(REPTDATE) - 5;
      PYEAR = YEAR(REPTDATE) - 2;
   END;
   ELSE DO;
      PMTH = MONTH(REPTDATE) + 7;
      PYEAR = YEAR(REPTDATE) - 3;
   END;
   PDATE = MDY(PMTH,1,PYEAR);
   CALL SYMPUT('PRDATE',PDATE);
   CALL SYMPUT('REPTMON', PUT(MONTH(REPTDATE), Z2.));
   CALL SYMPUT('PREVMON', PUT(MONTH(PREVDATE), Z2.));
   CALL SYMPUT('RDATE',PUT(REPTDATE,DDMMYY8.));
   CALL SYMPUT('REPTDT',REPTDATE);
RUN;

PROC SORT DATA=LNBILL.LOAN&REPTMON OUT=LOAN; BY BRANCH ACCTNO; RUN;

DATA LNPBB LNPIB;
   SET LOAN;
   NOACCT = 1;
   IF PRODUCT IN &PBBPROD THEN OUTPUT LNPBB;
   ELSE                        OUTPUT LNPIB;
RUN;

%MACRO PRINTER(DSN,FLNAME);
PROC PRINTTO PRINT=&FLNAME NEW;
%IF "&DSN" = "LNPBB" %THEN %DO;
    TITLE  'PUBLIC BANK BERHAD';
    TITLE2 ' ';
%END;
%ELSE %DO;
    TITLE  'PUBLIC ISLAMIC BANK BERHAD';
    TITLE2 ' ';
%END;
    TITLE3 'REPORT NO : EIQPROM2';
    TITLE4 'HL/FL/TL CUSTOMER WITH MINIMUM 2 1/2 YEARS ';
    TITLE5 'PROMPT REPAYMENT RECORD AS AT ' "&RDATE";
PROC   REPORT DATA=&DSN NOWD HEADLINE SPLIT='*'; BY BRANCH;
COLUMN BRANCH ACCTNO NOTENO AANO PRODUCT NAME ISSDTE LMTAPPR
       BALANCE REPAID REMTERM NEW NOACCT;
DEFINE BRANCH   /ORDER FORMAT=3. 'BRANCH';
DEFINE ACCTNO   /DISPLAY FORMAT=10.  'A/C NO';
DEFINE NOTENO   /DISPLAY FORMAT=5.   'NOTE NO';
DEFINE AANO     /DISPLAY FORMAT=$13. 'A/A NO';
DEFINE PRODUCT  /DISPLAY FORMAT=3.   'PRODUCT CODE';
DEFINE NAME     /DISPLAY FORMAT=$27. 'NAME OF CUSTOMER';
DEFINE ISSDTE   /DISPLAY FORMAT=DATE8.  'RELEASED DATE';
DEFINE LMTAPPR  /SUM     FORMAT=12.2 'APPROVED LIMIT';
DEFINE BALANCE  /SUM     FORMAT=12.2 'O/S BALANCE';
DEFINE REPAID   /SUM     FORMAT=12.2 'AMT REPAID';
DEFINE REMTERM  /DISPLAY FORMAT=3.   'REMAINING TENURE';
DEFINE NEW      /DISPLAY FORMAT=$1.  'NEW';
DEFINE NOACCT   /SUM NOPRINT;
*;
COMPUTE AFTER BRANCH;
   LINE @1 132*'-';
   LINE @1 'NO OF A/C :' @17 NOACCT.SUM COMMA8.0 @86 LMTAPPR.SUM 12.2
        @100 BALANCE.SUM 12.2 @114 REPAID.SUM 12.2 ;
   LINE @1 132*'=';
ENDCOMP;
%MEND PRINTER;
%PRINTER(LNPBB,BRRPS);
%PRINTER(LNPIB,IBRRPS);
//*--------------------------------------------------------------------
//* SMR 2024-4882
//* SEND REPORT TO COLDER UAT
//*--------------------------------------------------------------------
//*%OPC SCAN
//RUNSFTP  EXEC COZBATCH
//OUTPUT   DD SYSOUT=*
//SYSPRINT DD SYSOUT=*
//CMD.SYSUT1 DD DISP=SHR,DSN=OPER.PBB.PARMLIB(COZ#NCLD)
//           DD *
lzopts servercp=$servercp,notrim,overflow=trunc,mode=text
lzopts linerule=$lr
cd RPS5
PUT //SAP.PBB.PROMOTE.TEXT.COLD  EIQPROM2_PBB_%OYYYY.%OMM.%ODD..TXT
PUT //SAP.PIBB.PROMOTE.TEXT.COLD EIQPROM2_PIBB_%OYYYY.%OMM.%ODD..TXT
EOB
/*
