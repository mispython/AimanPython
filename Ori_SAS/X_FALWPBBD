/*********************************************************************/
/**  PROGRAM  : FALWPBBD                                            **/
/**  FUNCTION : TO FILTER OUT INVALID CUSTOMER CODE & PRODUCT CODE  **/
/**  DATE     : 15.05.97                                            **/
/**  AMENDED  : 03.05.99 BY CGP2                                    **/
/*********************************************************************/
OPTIONS YEARCUTOFF=1950;
*;
%INC PGM(PBBDPFMT);
*;
DATA BNM.FDWKLY
     (KEEP=BRANCH ACCTNO CUSTCODE NAME AMTIND ACCTTYPE
           OPENIND CURBAL BIC INTPAY STATE TERM INTPLAN MATDATE);
     LENGTH CUSTCODE $2. BIC $5.;
     SET FD.FD;
     BIC = PUT(INTPLAN, FDPROD.);
     AMTIND=PUT(INTPLAN, FDDENOM.);
     STATE = PUT(BRANCH, STATECD.);
     IF BIC IN ('42130','42630') THEN CUSTCODE = PUT(CUSTCD, FDCUSTCD.);
        ELSE CUSTCODE = PUT(CUSTCD, IFDCUSCD.);
     IF BIC = '42630' THEN DO;
        IF CUSTCODE IN ('77','78','95') THEN DO;
           IF PURPOSE NOT IN ('1','2','3') THEN PURPOSE=1;
        END;
        ELSE DO;
           IF PURPOSE NOT IN ('4','5') THEN PURPOSE=4;
        END;
     END;
     IF ACCTTYPE IN (315,394) THEN BIC='42132'; ELSE
     IF ACCTTYPE IN (397,398) THEN BIC='42199';
     IF OPENIND = 'O' OR OPENIND = 'D' THEN OUTPUT;
RUN;
DATA BNM.UMA
     (KEEP=BRANCH ACCTNO CUSTCODE NAME AMTIND
           OPENIND CURBAL BIC STATE);
     LENGTH STATE $1. CUSTCODE $2. BIC $5.;
     SET DEPOSIT.UMA(RENAME=(CUSTCODE=CUSTCD));
     IF PRODUCT = 297 THEN DO;
        CUSTCODE = PUT(CUSTCD, FDCUSTCD.);
        AMTIND = 'D';
     END;
     ELSE DO;
        CUSTCODE = PUT(CUSTCD, IFDCUSCD.);
        AMTIND = 'I';
     END;
     STATE = PUT(BRANCH, STATECD.);
     BIC='42199';
     IF OPENIND = 'O' OR OPENIND = 'D' THEN OUTPUT;
RUN;
