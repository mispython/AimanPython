/*-------------------------------------------------------------*/
/* REPORT OWNER : PRODUCT DEVELOPMENT (RETAIL BANKING DIV)     */
/* DAILY LOANS SUMMARISED BY BRANCH                            */
/*-------------------------------------------------------------*/
OPTIONS NOCENTER YEARCUTOFF=1950 LS=132 MISSING=0 NODATE NONUMBER;

DATA REPTDATE;
     INFILE DATEFILE LRECL=80 OBS=1;
     INPUT @01  EXTDATE   11.;
     REPTDATE = INPUT(SUBSTR(PUT(EXTDATE, Z11.), 1, 8), MMDDYY8.);
     PREVDATE = REPTDATE -1;
     DLETDATE = REPTDATE -3;
     REPTDAY  = DAY(REPTDATE);
     PREVDAY  = DAY(PREVDATE);
     DLETDAY  = DAY(DLETDATE);
     IF MONTH(REPTDATE) = 1 AND DAY(REPTDATE) = 1 THEN
        YY = YEAR(REPTDATE) - 1;
     ELSE YY = YEAR(REPTDATE);
     CALL SYMPUT('REPTYEAR', PUT(REPTDATE, YEAR4.));
     CALL SYMPUT('PREVYEAR', PUT(YY,Z4.));
     CALL SYMPUT('REPTMON', PUT(MONTH(REPTDATE), Z2.));
     CALL SYMPUT('REPTDAY', PUT(DAY(REPTDATE), Z2.));
     CALL SYMPUT('RDATE', PUT(REPTDATE, DDMMYY8.));
     CALL SYMPUT('REPTDATE',PUT(REPTDATE,Z5.));
     CALL SYMPUT('PREVDAY',PUT(PREVDAY,Z2.));
     CALL SYMPUT('DLETDAY',PUT(DLETDAY,Z2.));
RUN;
TITLE1 'REPORT ID : EIBDLOAN';
/* PROC PRINT DATA=REPTDATE; */

LIBNAME MIS "SAP.PBB.MIS.D&REPTYEAR" DISP=OLD;
LIBNAME MIS1"SAP.PBB.MIS.D&PREVYEAR" DISP=SHR;

DATA FEPLAN;
   INFILE FEEFILE;
   INPUT @001 ACCTNO    PD6.
         @007 NOTENO    PD3.
         @010 LOANTYPE  PD2.
         @022 FEEPLN    $2.
         @035 FEEAMTA   PD8.2 /*  AMT-ASSESSED-CURRENT */
         @067 FEEAMTC   PD8.2 /*  AMT-DUE CURRENT - FOR THE DAY */
         @075 FEEAMTB   PD8.2 /*  AMT-DUE-TOTAL      */
         ;
   IF ACCTNO < 3000000000 THEN DO;
   IF FEEPLN IN ('PA')  OR
      (LOANTYPE IN (241,242,243,362) AND
         FEEPLN IN ('LA','LB','LW','PO','RP')) OR
      (LOANTYPE IN (138) AND
         FEEPLN IN ('PO','RP','MI','PM','IH')) OR
      (LOANTYPE IN (182) AND
         FEEPLN IN ('PO','RP','MI','PM','IH','LA','LV','LW')) OR
      (LOANTYPE IN (244,245) AND
         FEEPLN IN ('LA','LB','LW','PO','RP')) OR
      (LOANTYPE IN (363) AND
         FEEPLN IN ('LA','LB','LV','LW','PO','RP','CR')) OR
      (LOANTYPE IN (569) AND
         FEEPLN IN ('LA','LW','PO','RP','CR'))
      THEN OUTPUT;
   END;

   IF ACCTNO > 8000000000 THEN DO;
      IF (LOANTYPE IN (117,118,119,126) AND
         FEEPLN IN ('BC','LG','LX','PL','LF','IP','MC','99')) OR
         (LOANTYPE IN (113,115,116,127) AND
         FEEPLN IN ('BC','LE','LX','PL','LF','IP','MC','99')) OR
         (LOANTYPE IN (227,228,230,231,234,235,236,237,238,239,
                    240,241,242,359) AND
         FEEPLN IN ('LA','PL','RP','99','LD'))
         THEN OUTPUT;
      IF (LOANTYPE IN (128,130,380,381,700,705,983,993,996) AND
         FEEPLN IN ('LD'))
         THEN OUTPUT;
      IF (LOANTYPE IN (720,725)  AND
         FEEPLN IN ('AD','AC','DC','IP','LF','MC','PC','RE','BA','LD',
                    'RI','SC','SE','SF','TC','99'))
         THEN OUTPUT;
   END;

 ;
PROC SUMMARY DATA=FEPLAN NWAY;
CLASS ACCTNO NOTENO;
VAR   FEEAMTA FEEAMTB FEEAMTC;
OUTPUT OUT=FEEPO (DROP=_TYPE_ _FREQ_) SUM=;
*;
DATA FEEPO; SET FEEPO;
*;
PROC SORT DATA=FEEPO; BY ACCTNO NOTENO;
*;
DATA LOAN;
  INFILE ACCTFILE;
  INPUT @001 ACCTNO    PD6.
        @007 NAME      $24.
        @063 BANKNO    PD2.
        @066 ACCBRCH   PD4.
        @081 NOTENO    PD3.
        @084 REVERSED   $1.
        @085 LOANTYPE  PD2.
        @087 NTBRCH    PD4.
        @091 PENDBRH   PD4.
        @113 LASTTRAN  PD6.
        @121 CURBAL    PD8.2
        @129 INTAMT    PD8.2
        @261 PAIDIND    $1.
        @296 NTINT      $1.
        @311 INTEARN   PD8.2
        @319 ACCRUAL   PD8.7
        @400 INTEARN2  PD8.2
        @408 INTEARN3  PD8.2
        @416 INTEARN4  PD8.2
        @457 FEEAMT    PD8.2
        @465 FEEAMT2   PD8.2
        @554 FEEAMT4   PD8.2
        @764 NFEEAMT5  PD8.2
        @772 NFEEAMT6  PD8.2
        @780 NFEEAMT7  PD8.2
        @861 FEEAMT8   PD8.2
        @869 FEEAMT9   PD8.2
        @885 FEEAMT13  PD8.2
        @904 FEEAMT10  PD8.2
        @912 FEEAMT11  PD8.2
        @920 FEEAMT12  PD8.2
        @928 FEEAMT14  PD8.2
        @936 FEEAMT15  PD8.2
        @944 FEEAMT16  PD8.2;
  IF _N_ = 1  THEN SET REPTDATE;
  BRANCH=PENDBRH;
  IF PENDBRH = 0  THEN BRANCH=NTBRCH;
  IF BRANCH  = 0  THEN BRANCH=ACCBRCH;
  IF ((REVERSED NE 'Y') AND (NOTENO NE .) AND (PAIDIND NE 'P')) OR
     ((PAIDIND  EQ 'P') AND (LASTTRAN EQ REPTDATE))  THEN OUTPUT;
*;

DATA LOAN;
  MERGE LOAN(IN=A) FEEPO; BY ACCTNO NOTENO;
  IF A AND LOANTYPE LE 980;
  IF ACCTNO < 3000000000 THEN DO;

  IF (100<=LOANTYPE<=103) OR
     (110<=LOANTYPE<=118) OR
     (120<=LOANTYPE<=125) OR
     (135<=LOANTYPE<=136) OR
     (180<=LOANTYPE<=182) OR
     (193<=LOANTYPE<=196) OR
     (241<=LOANTYPE<=245) OR
     LOANTYPE IN (127,129,138,270,362,363,569)
     THEN FEEAMT=SUM(FEEAMT,FEEAMTA);

  IF LOANTYPE IN (300,301,309,310,345,900,901,904,905,
                  914,915,919,920)
     THEN FEEAMT=SUM(FEEAMT,NFEEAMT5,NFEEAMT6,NFEEAMT7);

  IF (225<=LOANTYPE<=228) OR
     (230<=LOANTYPE<=234) OR
     (355<=LOANTYPE<=359) OR
     (515<=LOANTYPE<=520) OR
     (524<=LOANTYPE<=527) OR
     (559<=LOANTYPE<=561) OR
     (564<=LOANTYPE<=568) OR
     LOANTYPE IN (200,201,204,205,211,212,214,215,219,220,
                  304,305,315,320,325,330,
                  335,340,350,361,391,500,504,505,509,
                  510,530,555,556,570,910,925)
     THEN FEEAMT=SUM(FEEAMT,NFEEAMT5);

  IF NTINT = 'A' THEN DO;
     IF LOANTYPE = 196 THEN
        BALANCE=SUM(CURBAL,INTEARN,(-1)*INTAMT,
                    (-1)*INTEARN2,INTEARN3,FEEAMT);
     ELSE
        BALANCE=SUM(CURBAL,INTEARN,(-1)*INTAMT,FEEAMT);
  END;
  ELSE
     BALANCE=SUM(CURBAL,ACCRUAL,FEEAMT);

  END;

  IF ACCTNO > 8000000000 THEN DO;

     FEEAMT5=FEEAMT13;
     NFEEAMT8=FEEAMT9;
     NFEEAMT5=FEEAMT10;
     NFEEAMT6=FEEAMT11;
     NFEEAMT7=FEEAMT12;
     NFEEAMT10=FEEAMT14;
     NFEEAMT11=FEEAMT15;
     NFEEAMT12=FEEAMT16;

     IF LOANTYPE IN (227,228,230,231,234,235,236,
                     237,238,239,240,241,242,359)
                     THEN FEEAMT4=FEEAMTA;
     ELSE
     IF LOANTYPE IN (991,992,994,995)
                     THEN FEEAMT =FEEAMT8;

     IF LOANTYPE IN (128,130,380,381,700,705,720,725,983,993,996)
                     THEN DO;
        IF LOANTYPE IN (720,725) THEN
           FEEAMT8 = SUM(FEEAMT8,FEEAMTA,FEEAMTB);
        ELSE FEEAMT8 = SUM(FEEAMT8,FEEAMTA);
        FEEAMT  = FEEAMT8;
        FEETOT2 = SUM(FEETOT2,FEEAMTA);
     END;

     IF NTINT = 'A' THEN
        BALANCE=SUM(CURBAL,INTEARN,(-1)*INTAMT,
                    (-1)*INTEARN2,INTEARN3,FEEAMT);
     ELSE
        BALANCE=SUM(CURBAL,ACCRUAL,FEEAMT,FEEAMT4,FEEAMT5);

  END;
*;
PROC DATASETS LIB=MIS NOLIST;
     DELETE LNDLY&REPTDAY;
     DELETE LNDLY&DLETDAY;
RUN;
*;
DATA MIS.LNDLY&REPTDAY (KEEP=ACCTNO NOTENO NAME BALANCE BRANCH
                             LOANTYPE CURBAL REPTDATE EXTDATE);
    SET LOAN;
RUN;
*;
   /**********************************************************/
   /** START PROCESSING REPORT AFTER THE MIS BEING UPDATED. **/
   /***    PART 1.  FOR BRANCH SUMMARY REPORT               **/
   /**********************************************************/
*;
DATA LNNOTE
     HPLOAN
     REVCRE;
  SET MIS.LNDLY&REPTDAY;
  IF LOANTYPE IN (302,350,364,365,506,902,903,910,
                  925,951)  THEN OUTPUT REVCRE;
  ELSE IF LOANTYPE IN (128,130,380,381,700,705)
                       THEN OUTPUT HPLOAN;
  ELSE OUTPUT LNNOTE;
*;
PROC SUMMARY DATA=LNNOTE NWAY;
   CLASS BRANCH REPTDATE EXTDATE;
   VAR BALANCE;
   OUTPUT OUT=LOANSUM
              (RENAME=(_FREQ_=NOACCT) DROP=_TYPE_)
          SUM=BRLNAMT;
RUN;
*;
PROC SUMMARY DATA=REVCRE NWAY;
   CLASS BRANCH REPTDATE EXTDATE;
   VAR BALANCE;
   OUTPUT OUT=REVSUMM
              (RENAME=(_FREQ_=REVACC) DROP=_TYPE_)
          SUM=BRRVAMT;
RUN;
*;
PROC SUMMARY DATA=HPLOAN NWAY;
   CLASS BRANCH REPTDATE EXTDATE;
   VAR BALANCE;
   OUTPUT OUT=HPSUMM
              (RENAME=(_FREQ_=HPACC) DROP=_TYPE_)
          SUM=BRHPAMT;
RUN;
*;
DATA PLNNOTE
     PHPLOAN
     PREVCRE;
  SET MIS1.LNDLY&PREVDAY;
  IF LOANTYPE IN (302,350,364,365,506,902,903,910,
                  925,951)  THEN OUTPUT PREVCRE;
  ELSE IF LOANTYPE IN (128,130,380,381,700,705)
                       THEN OUTPUT PHPLOAN;
  ELSE OUTPUT PLNNOTE;
*;
PROC SUMMARY DATA=PLNNOTE NWAY;
   CLASS BRANCH REPTDATE EXTDATE;
   VAR BALANCE;
   OUTPUT OUT=PLOANSUM
              (RENAME=(_FREQ_=PNOACCT) DROP=_TYPE_)
          SUM=PBRLNAMT;
RUN;
*;
PROC SUMMARY DATA=PREVCRE NWAY;
   CLASS BRANCH REPTDATE EXTDATE;
   VAR BALANCE;
   OUTPUT OUT=PREVSUMM
              (RENAME=(_FREQ_=PREVACC) DROP=_TYPE_)
          SUM=PBRRVAMT;
RUN;
*;
PROC SUMMARY DATA=PHPLOAN NWAY;
   CLASS BRANCH REPTDATE EXTDATE;
   VAR BALANCE;
   OUTPUT OUT=PHPSUMM
              (RENAME=(_FREQ_=PHPACC) DROP=_TYPE_)
          SUM=PBRHPAMT;
RUN;
*;
DATA HPLOANS;
   MERGE HPSUMM PHPSUMM; BY BRANCH;
*;
DATA REVCRED;
   MERGE REVSUMM PREVSUMM; BY BRANCH;
*;
DATA LOANS;
   MERGE LOANSUM PLOANSUM; BY BRANCH;
*;
PROC SORT DATA=LOANS; BY BRANCH;
PROC SORT DATA=REVCRED; BY BRANCH;
PROC SORT DATA=HPLOANS; BY BRANCH;
*;
DATA BRANCH;
  INFILE BRANCHF;
  INPUT @001 BANK       $1.
        @002 BRANCH      3.
        @006 ABBREV     $3.
        @012 BRCHNAME  $30.;
*;
PROC SORT DATA=BRANCH; BY BRANCH;
*;
DATA MLOAN;
   MERGE LOANS(IN=A) BRANCH; BY BRANCH;
   IF A;
   VARIANLN = BRLNAMT - PBRLNAMT;
*;
DATA MCRED;
   MERGE REVCRED(IN=A) BRANCH; BY BRANCH;
   IF A;
   VARIANRV = BRRVAMT - PBRRVAMT;
*;
DATA MHP;
   MERGE HPLOANS(IN=A) BRANCH; BY BRANCH;
   IF A;
   VARIANHP = BRHPAMT - PBRHPAMT;
*;
TITLE2 'PUBLIC BANK BERHAD';
TITLE3 'BRANCH SUMMARY REPORT ON DAILY TERM LOAN OUTSTANDING(RM)';
TITLE4 'AS AT ' &RDATE;
*;
PROC TABULATE DATA=MLOAN MISSING NOSEPS;
   CLASS BRANCH ABBREV BRCHNAME;
   VAR PBRLNAMT BRLNAMT NOACCT VARIANLN;
   TABLE BRANCH='CODE'*
         ABBREV='ABBREV'*
         BRCHNAME='NAME' ALL='TOTAL',
         SUM=' '*NOACCT='NO OF ACCOUNTS'*F=10.
         SUM='OUSTANDING TERM LOAN (RM)'*(PBRLNAMT='PREVIOUS AMT'
         BRLNAMT='CURRENT AMT')*F=COMMA18.2
         SUM=' '*VARIANLN='VARIANCE'*F=COMMA20.2
         /RTS=60 BOX='BRANCH';
*;
PROC TABULATE DATA=MCRED MISSING NOSEPS;
   CLASS BRANCH ABBREV BRCHNAME;
   VAR PBRRVAMT BRRVAMT REVACC VARIANRV;
   TABLE BRANCH='CODE'*
         ABBREV='ABBREV'*
         BRCHNAME='NAME' ALL='TOTAL',
         SUM=' '*REVACC='NO OF ACCOUNTS'*F=10.
         SUM='OUSTANDING REVOLVING CREDIT (RM)'*(PBRRVAMT='PREVIOUS AMT'
         BRRVAMT='CURRENT AMT')*F=COMMA18.2
         SUM=' '*VARIANRV='VARIANCE'*F=COMMA20.2
         /RTS=60 BOX='BRANCH';
*;
TITLE3 'BRANCH SUMMARY REPORT ON DAILY REVOLVING CREDIT OUTSTANDING(RM)';
*;
PROC TABULATE DATA=MHP MISSING NOSEPS;
   CLASS BRANCH ABBREV BRCHNAME;
   VAR PBRHPAMT BRHPAMT HPACC VARIANHP;
   TABLE BRANCH='CODE'*
         ABBREV='ABBREV'*
         BRCHNAME='NAME' ALL='TOTAL',
         SUM=' '*HPACC='NO OF ACCOUNTS'*F=10.
         SUM='OUSTANDING HP (RM)'*(PBRHPAMT='PREVIOUS AMT'
         BRHPAMT='CURRENT AMT')*F=COMMA18.2
         SUM=' '*VARIANHP='VARIANCE'*F=COMMA20.2
         /RTS=60 BOX='BRANCH';
*;
TITLE3 'BRANCH SUMMARY REPORT ON DAILY HP OUTSTANDING(RM)'              ;
*;
   /**********************************************************/
   /** START PROCESSING REPORT II.                          **/
   /***       CUSTOMER'S MOVEMENT ON DAILY BASIS FOR BANK   **/
   /**********************************************************/
*;
DATA DLOAN
     DHP
     DCRED;
  SET MIS.LNDLY&REPTDAY;
  IF LOANTYPE IN (302,350,364,365,506,902,903,910,
                  925,951)  THEN OUTPUT DCRED;
  ELSE IF LOANTYPE IN (128,130,380,381,700,705)
                       THEN OUTPUT DHP;
  ELSE OUTPUT DLOAN;
*;
DATA PDLOAN
     PDHP
     PDCRED;
  SET MIS1.LNDLY&PREVDAY (RENAME=(CURBAL=PCURBAL BALANCE=PBALANCE));
  IF LOANTYPE IN (302,350,364,365,506,902,903,910,
                  925,951)  THEN OUTPUT PDCRED;
  ELSE IF LOANTYPE IN (128,130,380,381,700,705)
                       THEN OUTPUT PDHP;
  ELSE OUTPUT PDLOAN;
*;
PROC SUMMARY DATA=DLOAN;
   BY    ACCTNO REPTDATE;
   VAR BALANCE;
   OUTPUT OUT=DLOAN1
              (DROP=_FREQ_ _TYPE_) SUM=DLTOTOL;
RUN;
*;
PROC SUMMARY DATA=DCRED;
   BY    ACCTNO REPTDATE;
   VAR BALANCE;
   OUTPUT OUT=DCRED1
              (DROP=_FREQ_ _TYPE_) SUM=DRTOTOL;
RUN;
*;
PROC SUMMARY DATA=DHP;
   BY    ACCTNO REPTDATE;
   VAR BALANCE;
   OUTPUT OUT=DHP1
              (DROP=_FREQ_ _TYPE_) SUM=DHPTOTOL;
RUN;
*;
PROC SUMMARY DATA=PDLOAN;
   BY    ACCTNO REPTDATE;
   VAR PBALANCE;
   OUTPUT OUT=PDLOAN1
              (DROP=_FREQ_ _TYPE_) SUM=PDLTOTOL;
RUN;
*;
PROC SUMMARY DATA=PDCRED;
   BY    ACCTNO REPTDATE;
   VAR PBALANCE;
   OUTPUT OUT=PDCRED1
              (DROP=_FREQ_ _TYPE_) SUM=PDRTOTOL;
RUN;
*;
PROC SUMMARY DATA=PDHP;
   BY    ACCTNO REPTDATE;
   VAR PBALANCE;
   OUTPUT OUT=PDHP1
              (DROP=_FREQ_ _TYPE_) SUM=PDHPTOTO;
RUN;
*;
PROC SORT DATA=DLOAN1;  BY ACCTNO;
PROC SORT DATA=DCRED1;  BY ACCTNO;
PROC SORT DATA=DHP1;    BY ACCTNO;
PROC SORT DATA=PDLOAN1; BY ACCTNO;
PROC SORT DATA=PDCRED1; BY ACCTNO;
PROC SORT DATA=PDHP1;   BY ACCTNO;
*;
DATA LOAN;
   MERGE DLOAN1 PDLOAN1; BY ACCTNO;
*;
DATA CRED;
   MERGE DCRED1 PDCRED1; BY ACCTNO;
*;
DATA HP;
   MERGE DHP1 PDHP1; BY ACCTNO;
*;
DATA DMLOAN;
   SET LOAN;
   IF DLTOTOL = .    THEN DLTOTOL = 0.00;
   IF PDLTOTOL = .   THEN PDLTOTOL = 0.00;
   IF ABS(DLTOTOL-PDLTOTOL) GE 1000000   THEN OUTPUT;
*;
DATA DMCRED;
   SET CRED;
   IF DRTOTOL = .    THEN DRTOTOL = 0.00;
   IF PDRTOTOL = .   THEN PDRTOTOL = 0.00;
   IF ABS(DRTOTOL-PDRTOTOL) GE 1000000   THEN OUTPUT;
*;
DATA DMHP;
   SET HP;
   IF DHPTOTOL = .    THEN DHPTOTOL = 0.00;
   IF PDHPTOTO = .   THEN PDHPTOTO = 0.00;
   IF ABS(DHPTOTOL-PDHPTOTO) GE 1000000   THEN OUTPUT;
*;
DATA DMLOAN;
   MERGE DLOAN PDLOAN DMLOAN(IN=A); BY ACCTNO;
   IF A   THEN OUTPUT;

DATA DMCRED;
   MERGE DCRED PDCRED DMCRED(IN=A); BY ACCTNO;
   IF A   THEN OUTPUT;
*;
DATA DMHP;
   MERGE DHP PDHP DMHP(IN=A); BY ACCTNO;
   IF A   THEN OUTPUT;
*;
DATA DMLOAN;
  SET DMLOAN; BY ACCTNO;
  IF FIRST.ACCTNO  THEN OUTPUT;

DATA DMCRED;
  SET DMCRED; BY ACCTNO;
  IF FIRST.ACCTNO  THEN OUTPUT;
*;
DATA DMHP;
  SET DMHP; BY ACCTNO;
  IF FIRST.ACCTNO  THEN OUTPUT;
*;
PROC SORT DATA=DMLOAN; BY BRANCH;
PROC SORT DATA=DMCRED; BY BRANCH;
*;
DATA DMLOAN;
   MERGE DMLOAN(IN=A) BRANCH; BY BRANCH;
   IF A   THEN OUTPUT;
*;
DATA DMCRED;
   MERGE DMCRED(IN=A) BRANCH; BY BRANCH;
   IF A   THEN OUTPUT;
*;
DATA DMHP;
   MERGE DMHP(IN=A) BRANCH; BY BRANCH;
   IF A   THEN OUTPUT;
*;
TITLE2 'PUBLIC BANK BERHAD - RETAIL BANKING DIVISION';
TITLE3 'REPORT TITLE : EIBDLOAN';
TITLE4 "DAILY MOVEMENT IN BANK'S TERM LOAN ACCOUNTS AS AT : " &RDATE;
TITLE5 'NET INCREASED/(DECREASE) OF RM1 MILLION & ABOVE PER CUSTOMER';
*;
PROC REPORT DATA=DMLOAN NOWD HEADLINE SPLIT='*';
  COLUMN ABBREV NAME ACCTNO DLTOTOL PDLTOTOL MOVEMENT;
  DEFINE ABBREV / DISPLAY FORMAT=$6. 'BRANCH';
  DEFINE NAME    / DISPLAY FORMAT=$25. 'NAME OF CUSTOMER';
  DEFINE ACCTNO  / DISPLAY FORMAT=20. 'ACCOUNT NO';
  DEFINE DLTOTOL / ANALYSIS SUM FORMAT=COMMA18.2 'CURRENT BALANCE';
  DEFINE PDLTOTOL / ANALYSIS SUM FORMAT=COMMA18.2 'PREVIOUS BALANCE';
  DEFINE MOVEMENT/ COMPUTED FORMAT=COMMA18.2
                   'NET INCREASE/*(DECREASE)';

  COMPUTE MOVEMENT;
     MOVEMENT=SUM(DLTOTOL.SUM,(-1)*PDLTOTOL.SUM);
  ENDCOMP;

  RBREAK AFTER / PAGE DUL OL SUMMARIZE;
RUN;
*;
TITLE2 'PUBLIC BANK BERHAD - RETAIL BANKING DIVISION';
TITLE3 'REPORT TITLE : EIBDLOAN';
TITLE4 "DAILY MOVEMENT IN BANK S REVOLVING CREDIT ACCOUNTS AS AT : " &RDATE;
TITLE5 'NET INCREASED/(DECREASE) OF RM1 MILLION & ABOVE PER CUSTOMER';
*;
PROC REPORT DATA=DMCRED NOWD HEADLINE SPLIT='*';
  COLUMN ABBREV NAME ACCTNO DRTOTOL PDRTOTOL MOVEMENT;
  DEFINE ABBREV / DISPLAY  FORMAT=$6. 'BRANCH';
  DEFINE NAME    / DISPLAY FORMAT=$25. 'NAME OF CUSTOMER';
  DEFINE ACCTNO  / DISPLAY FORMAT=20.  'ACCOUNT NO';
  DEFINE DRTOTOL / ANALYSIS SUM FORMAT=COMMA18.2 'CURRENT BALANCE';
  DEFINE PDRTOTOL / ANALYSIS SUM FORMAT=COMMA18.2 'PREVIOUS BALANCE';
  DEFINE MOVEMENT/ COMPUTED FORMAT=COMMA18.2
                   'NET INCREASE/*(DECREASE)';

  COMPUTE MOVEMENT;
     MOVEMENT=SUM(DRTOTOL.SUM,(-1)*PDRTOTOL.SUM);
  ENDCOMP;

  RBREAK AFTER / PAGE DUL OL SUMMARIZE;
RUN;

TITLE2 'PUBLIC BANK BERHAD - RETAIL BANKING DIVISION';
TITLE3 'REPORT TITLE : EIBDLOAN';
TITLE4 "DAILY MOVEMENT IN BANK S HP ACCOUNTS AS AT : " &RD              ATE;
TITLE5 'NET INCREASED/(DECREASE) OF RM1 MILLION & ABOVE PER CUSTOMER';
*;
PROC REPORT DATA=DMHP NOWD HEADLINE SPLIT='*';
  COLUMN ABBREV NAME ACCTNO DHPTOTOL PDHPTOTO MOVEMENT;
  DEFINE ABBREV / DISPLAY  FORMAT=$6. 'BRANCH';
  DEFINE NAME    / DISPLAY FORMAT=$25. 'NAME OF CUSTOMER';
  DEFINE ACCTNO  / DISPLAY FORMAT=20.  'ACCOUNT NO';
  DEFINE DHPTOTOL / ANALYSIS SUM FORMAT=COMMA18.2 'CURRENT BALANCE';
  DEFINE PDHPTOTO / ANALYSIS SUM FORMAT=COMMA18.2 'PREVIOUS BALANCE';
  DEFINE MOVEMENT/ COMPUTED FORMAT=COMMA18.2
                   'NET INCREASE/*(DECREASE)';

  COMPUTE MOVEMENT;
     MOVEMENT=SUM(DHPTOTOL.SUM,(-1)*PDHPTOTO.SUM);
  ENDCOMP;

  RBREAK AFTER / PAGE DUL OL SUMMARIZE;
RUN;

