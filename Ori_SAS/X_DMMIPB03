OPTIONS NOCENTER NODATE NONUMBER MISSING=0;

%INC PGM(PBBDPFMT,PBMISFMT);

DATA REPTDATE;
    SET DEPOSIT.REPTDATE;
    CALL SYMPUT('REPTMON',PUT(MONTH(REPTDATE),Z2.));
    CALL SYMPUT('REPTYEAR',PUT(REPTDATE,YEAR4.));
    CALL SYMPUT('RDATE',PUT(REPTDATE,DDMMYY8.));
    CALL SYMPUT('ZDATE',PUT(REPTDATE,Z5.));
RUN;

DATA SAVG;
  LENGTH CUSTCD $2. PRODCD $5. STATECD $1. AMTIND $1.;
  SET DEPOSIT.SAVING;
  IF  PRODUCT=208;
  IF  OPENIND NOT IN ('B','C','P','Z') AND CURBAL NE 0;
  CUSTCD=PUT(CUSTCODE, SACUSTCD.);
  STATECD=PUT(BRANCH, STATECD.);
  PRODCD=PUT(PRODUCT, SAPROD.);
  AMTIND='D';
  RANGE=INPUT(CURBAL,SDRANGE.);

  IF OPENDT NE 0 THEN
     OPENDATE=INPUT(SUBSTR(PUT(OPENDT,Z11.),1,8), MMDDYY8.);
  ELSE OPENDATE=0;

  AGE=0;

  IF (BDATE NE 0) AND (BDATE NE .) THEN
    DO;
     BDATE  =INPUT(SUBSTR(PUT(BDATE, Z11.),1, 8), MMDDYY8.);
     BDAY   =DAY(BDATE);
     BMONTH =MONTH(BDATE);
     BYEAR  =YEAR(BDATE);
     AGE=(&REPTYEAR - BYEAR);
  END;
RUN;
*;
TITLE1 'PUBLIC BANK BERHAD   REPORT=DMMIPB03';
TITLE2 'FOR PRODUCT DEVELOPMENT & MARKETING';
TITLE3 'PROFILE ON PB BRIGHT STAR SAVINGS ACCOUNTS AS AT ' &RDATE;
*;
DATA PB03;
  LENGTH DEPRANGE $33;
  SET SAVG;
  DEPRANGE=PUT(RANGE,SADPRG.);
  NOACCT=1;
RUN;
*;
PROC TABULATE DATA=PB03 NOSEPS MISSING;
CLASS DEPRANGE PRODUCT;
VAR   NOACCT CURBAL;
TABLE PRODUCT ALL='BANK TOTAL',
      DEPRANGE='DEPOSIT RANGE' ALL='TOTAL',
      NOACCT=''*SUM='NO OF A/C'*F=COMMA12.
      CURBAL=''*SUM='AMOUNT'*F=DOLLAR20.2
    / RTS=36 BOX='PB BRIGHT STAR' PRINTMISS;
RUN;
