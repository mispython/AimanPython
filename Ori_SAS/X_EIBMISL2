***************************************************
* MONTHLY EXTRACTION FOR ISLAMIC PRODUCTS.        *
***************************************************;
OPTIONS YEARCUTOFF=1930 NOCENTER;
%INC PGM(PBMISFMT);
%INC PGM(PBBDPFMT);

DATA REPTDATE;
  SET DEPOSIT.REPTDATE;
  SELECT;
     WHEN(1 <= DAY(REPTDATE) <= 8) CALL SYMPUT('NOWK', PUT('1', $1.));
     WHEN(9 <= DAY(REPTDATE) <= 15) CALL SYMPUT('NOWK', PUT('2', $1.));
     WHEN(16 <= DAY(REPTDATE) <= 22) CALL SYMPUT('NOWK', PUT('3', $1.));
     OTHERWISE CALL SYMPUT('NOWK', PUT('4', $1.));
  END;
  CALL SYMPUT('REPTYEAR', PUT(REPTDATE, YEAR4.));
  CALL SYMPUT('REPTMON', PUT(MONTH(REPTDATE), Z2.));
  CALL SYMPUT('REPTDAY', PUT(DAY(REPTDATE), Z2.));
  CALL SYMPUT('RDATE', PUT(REPTDATE, DDMMYY8.));
RUN;
*;
DATA ALWSA;
   SET DEPOSIT.SAVING;
   WHERE PRODUCT IN (204,215);
   IF OPENIND NOT IN ('B','C','P') THEN DO;

      ACCYTD = 0;
      IF OPENDT NE 0  AND CLOSEDT EQ 0 THEN DO;
      IF YEAR(INPUT(SUBSTR(PUT(OPENDT, Z11.),1, 8), MMDDYY8.)) EQ
         YEAR(INPUT("&RDATE", DDMMYY8.)) THEN
         ACCYTD = 1;
      END;

      CUSTCD=PUT(CUSTCODE, SACUSTCD.);
      STATECD=PUT(BRANCH, STATECD.);
      PRODCD=PUT(PRODUCT, SAPROD.);
      AMTIND=PUT(PRODUCT, SADENOM.);
      AVGRNGE=INPUT(AVGAMT, DDRANGE.);
      IF PRODUCT IN (214)  THEN
         RANGE=INPUT(CURBAL, ISARANGE.);
      ELSE RANGE=INPUT(CURBAL, CARANGE.);
   END;
*;
OPTIONS NOCENTER NODATE NONUMBER MISSING=0;
TITLE1 'REPORT ID : EIBMISL2';
TITLE2 'PUBLIC ISLAMIC BANK BERHAD';
TITLE3 'FOR ISLAMIC BANKING DEPARTMENT';
TITLE4 'PROFILE ON ISLAMIC(PERSONAL/JOINT) AL-WADIAH SAVINGS ACCOUNT';
TITLE5 'PRODUCT (204,215), REPORT AS AT ' &RDATE;
*;
DATA ALWSA1;
  SET ALWSA;
  LENGTH DEPRANGE $ 30;
  DEPRANGE = PUT(CURBAL,IWSRNGE.);
  IF PURPOSE=' ' THEN PURPOSE='0';
  IF RACE   =' ' THEN RACE   ='0';
RUN;
*;
PROC SUMMARY DATA=ALWSA1 NWAY;
   CLASS PURPOSE RACE CUSTCD AVGRNGE DEPRANGE PRODUCT;
   VAR AVGAMT CURBAL ACCYTD;
   OUTPUT OUT=ALWSA1 (RENAME=(_FREQ_=NOACCT) DROP=_TYPE_)
          N(AVGAMT)=AVGACCT SUM(AVGAMT)=AVGAMT
                            SUM(ACCYTD)=ACCYTD
                            SUM(CURBAL)=CURBAL;
RUN;
*;
DATA ALWSA1;
   SET ALWSA1;
   AVGAMT=AVGAMT/&REPTDAY;
PROC TABULATE DATA=ALWSA1 MISSING;
  CLASS DEPRANGE RACE PURPOSE;
  VAR NOACCT ACCYTD CURBAL AVGAMT;
  TABLE
        DEPRANGE='DEPOSIT RANGE' ALL='TOTAL',
        NOACCT=''*(SUM='NO OF A/C'*F=COMMA10. PCTSUM*F=COMMA6.2)
        ACCYTD=''*(SUM='OPEN YTD '*F=COMMA10. PCTSUM*F=COMMA6.2)
        CURBAL=''*(SUM='O/S BALANCE'*F=COMMA15.2 PCTSUM*F=COMMA6.2
                   PCTSUM<NOACCT>='AVG AMT/ACCOUNT'*F=HUNDRED.)
        AVGAMT=''*(SUM='AVERAGE BAL'*F=COMMA15.2 PCTSUM*F=COMMA6.2
                   PCTSUM<NOACCT>='AVG BAL/ACCOUNT'*F=HUNDRED.)
        PURPOSE='A/C TYPE'*NOACCT=''*SUM=''*F=COMMA8.
        RACE*NOACCT=''*SUM=''*F=COMMA8.
        / RTS=30 PRINTMISS;
  KEYLABEL PCTSUM='%';
  FORMAT RACE $RACE. PURPOSE $PURPOSE.;
RUN;
