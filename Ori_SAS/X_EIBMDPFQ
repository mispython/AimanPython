OPTIONS YEARCUTOFF=1950 SORTDEV=3390 NONUMBER NODATE NOCENTER;
*;
DATA REPTDATE;
   SET DEPOSIT.REPTDATE;
   MM=MONTH(REPTDATE);
   MM1=MM-1;
   IF MM1=0 THEN MM1=12;
   CALL SYMPUT('RDATE',PUT(REPTDATE,DDMMYY8.));
   CALL SYMPUT('RYEAR',PUT(REPTDATE,YEAR4.));
   CALL SYMPUT('RMONTH',PUT(REPTDATE,MONTH2.));
   CALL SYMPUT('REPTMON',PUT(MM,Z2.));
   CALL SYMPUT('REPTMON1',PUT(MM1,Z2.));
   CALL SYMPUT('RDAY',PUT(REPTDATE,DAY2.));
RUN;
*;
*------------------------------------------------*
*  AMOUNT OUTSTANDING (EXCLUDE -IVE BALANCES)    *
*------------------------------------------------*;
PROC SORT DATA=FD.FD OUT=FDC; BY ACCTNO;
WHERE OPENIND IN ('O','D');
*;
DATA FDC;
  SET FDC; BY ACCTNO;
  IF  FIRST.ACCTNO THEN NOCD=0;
  NOCD+1;
  IF LAST.ACCTNO THEN  OUTPUT;
PROC SORT; BY ACCTNO;
*;
DATA FD;
  SET DEPOSIT.FD;

  IF  BRANCH=227 THEN DELETE;
  IF  BRANCH=250 THEN BRANCH=092;

  IF  OPENMH=1 AND CLOSEMH=1 THEN DELETE;

  IF  (300<=PRODUCT<=303);
  IF  OPENIND='Z'  THEN OPENIND='O';
  IF  OPENIND='O'  OR
      OPENIND IN ('B','C','P') AND CLOSEMH=1;

  NOACCT=0;
  IF  OPENIND IN ('B','C','P') THEN OPENMH=0;
  ELSE DO;
      CLOSEMH=0; NOACCT=1;
  END;
*;
PROC SORT; BY ACCTNO;
*;
DATA FDO; SET FD; IF OPENMH=1;
*;
PROC SUMMARY DATA=FDO NWAY;
CLASS BRANCH;
VAR   OPENMH CURBAL;
OUTPUT OUT=MIS.FDO&REPTMON (DROP=_TYPE_ _FREQ_) SUM=;
*;
DATA FD;
  MERGE FDC FD(IN=A); BY ACCTNO;
  IF A;
  IF NOCD=.   THEN NOCD=0;
PROC SUMMARY DATA=FD NWAY;
CLASS BRANCH PRODUCT;
VAR   OPENMH CLOSEMH NOACCT NOCD CURBAL;
OUTPUT OUT=FD (DROP=_TYPE_ _FREQ_) SUM=;
*;
%MACRO PROCESS;
   %IF &REPTMON > 01  %THEN %DO;

       DATA FDP;
         SET MIS.FD1&REPTMON1;
         OPENCUX =OPENCUM;
         CLOSECUX=CLOSECUM;
         IF  BRANCH=250 THEN BRANCH=092;

       PROC SUMMARY DATA=FDP NWAY;
       CLASS BRANCH PRODUCT;
       VAR   OPENCUX CLOSECUX;
       OUTPUT OUT=FDP (DROP=_TYPE_ _FREQ_) SUM=;

       DATA FD;
         MERGE FDP FD; BY BRANCH PRODUCT;
         IF CLOSECUX=.   THEN CLOSECUX=0;
         IF OPENCUX =.   THEN  OPENCUX=0;
         IF OPENMH  =.   THEN  OPENMH =0;
         IF CLOSEMH =.   THEN CLOSEMH =0;
         IF NOACCT  =.   THEN  NOACCT =0;
         IF CURBAL  =.   THEN  CURBAL =0;
         IF NOCD    =.   THEN  NOCD   =0;

         OPENCUM =OPENMH  +OPENCUX;
         CLOSECUM=CLOSEMH +CLOSECUX;
         NETCHGMH=OPENMH  -CLOSEMH;
         NETCHGYR=OPENCUM -CLOSECUM;
   %END;
   %ELSE %DO;
       DATA FD;
         SET FD;
         OPENCUM =OPENMH;
         CLOSECUM=CLOSEMH;
         NETCHGMH=OPENMH  -CLOSEMH;
         NETCHGYR=OPENCUM -CLOSECUM;
         IF BRANCH=250 THEN BRANCH=092;
   %END;
*;
%MEND PROCESS;

%PROCESS;

DATA MIS.FD1&REPTMON;
  SET FD;
*;
*------------------------------------------------*
*  PRINT REPORT TO TEXT FILE                     *
*------------------------------------------------*;
TITLE1 'PUBLIC BANK BERHAD';
TITLE2 'FD ACCOUNT OPENED/CLOSED FOR THE MONTH AS AT' &RDATE;
PROC TABULATE DATA=FD FORMAT=COMMA10. MISSING NOSEPS
   FORMCHAR='           ';
   CLASS BRANCH;
   VAR   OPENMH OPENCUM CLOSEMH CLOSECUM
         NOACCT NOCD  CURBAL NETCHGMH NETCHGYR;
   TABLE BRANCH  ='  ' ALL='TOTAL',SUM=' '*(
         OPENMH  ='CURRENT MONTH OPENED'
         OPENCUM ='CUMULATIVE OPENED'
         CLOSEMH ='CURRENT MONTH CLOSED'
         CLOSECUM='CUMULATIVE CLOSED'
         NOACCT  ='NO.OF ACCTS'
         NOCD    ='NO.OF CDS'
         CURBAL  ='TOTAL (RM) O/S'*F=COMMA18.2
         NETCHGMH='NET CHANGE FOR THE MONTH'
         NETCHGYR='NET CHANGE YEAR TO DATE')
       / BOX='BRANCH' RTS=10;
