//EIBMNLF0 JOB MIS,'MIS',COND=(4,LT),CLASS=A,
//         MSGCLASS=X,NOTIFY=&SYSUID
/*JOBPARM S=S1M1
//*
//* TO RUN NLF REPORT USING MTH-END FORMAT (EIBMRLFM)
//*
//EIBMNLF0 EXEC SAS609,REGION=6M,WORK='120000,8000'
//PGM      DD DSN=SAP.BNM.PROGRAM,DISP=SHR
//LOAN     DD DSN=SAP.PBB.MNILN(0),DISP=SHR
//BNM1     DD DSN=SAP.PBB.SASDATA,DISP=SHR
//BNM      DD DSN=SAP.PBB.NLF.LN.DAILY,DISP=OLD
//SASLIST  DD SYSOUT=X
//SYSIN    DD *

OPTIONS SORTDEV=3390 YEARCUTOFF=1950 NOCENTER;
*;
%INC PGM(PBBLNFMT,PBBELF,PBBDPFMT);
%LET FCY=(800,801,802,803,804,805,806,851,852,853,854,855,856,857,858,
          859,860,807,808,809,810,811,812,813,814);
*;
PROC FORMAT;
  VALUE REMFMT
     LOW-0.1 = '01'   /*  UP TO 1 WK       */
     0.1-1   = '02'   /*  >1 WK - 1 MTH    */
     1-3     = '03'   /*  >1 MTH - 3 MTHS  */
     3-6     = '04'   /*  >3 - 6 MTHS      */
     6-12    = '05'   /*  >6 MTHS - 1 YR   */
     OTHER   = '06';  /*  > 1 YEAR         */
*;
*------------------------------------------------*
*  MACRO TO DECLARE VARIABLES                    *
*------------------------------------------------*;
%MACRO DCLVAR;
   RETAIN D1-D12 31 D4 D6 D9 D11 30
          RD1-RD12 MD1-MD12 31 RD2 MD2 28 RD4 RD6 RD9 RD11
          MD4 MD6 MD9 MD11 30 RPYR RPMTH RPDAY;
   ARRAY LDAY D1-D12;
   ARRAY RPDAYS RD1-RD12;
   ARRAY MDDAYS MD1-MD12;
%MEND DCLVAR;
*;
*------------------------------------------------*
*  MACRO TO CALCULATE NEXT BLDATE                *
*------------------------------------------------*;
%MACRO NXTBLDT;
   IF PAYFREQ = '6' THEN DO;
      DD = DAY(BLDATE) + 14;
      MM = MONTH(BLDATE);
      YY = YEAR(BLDATE);
      IF MM = 2 THEN
         IF MOD(YY,4) = 0 THEN D2 = 29;
         ELSE D2 = 28;
      IF DD > LDAY(MM) THEN DO;
         DD = DD - LDAY(MM);
         MM + 1;
         IF MM > 12 THEN DO;
            MM = MM - 12; YY + 1;
         END;
      END;
   END;
   ELSE DO;
      DD = DAY(ISSDTE);
      MM = MONTH(BLDATE) + FREQ;
      YY = YEAR(BLDATE);
      IF MM > 12 THEN DO;
         MM = MM - 12; YY + 1;
      END;
   END;
   IF MM = 2 THEN
      IF MOD(YY,4) = 0 THEN D2 = 29;
      ELSE D2 = 28;
   IF DD > LDAY(MM) THEN DD = LDAY(MM);
   BLDATE = MDY(MM,DD,YY);
%MEND NXTBLDT;
*;
*------------------------------------------------*
*  MACRO TO CALCULATE REMAIN MONTH               *
*------------------------------------------------*;
%MACRO REMMTH;
   MDYR  = YEAR(MATDT);
   MDMTH = MONTH(MATDT);
   MDDAY = DAY(MATDT);
   IF MDMTH = 2 THEN
      IF MOD(MDYR,4) = 0 THEN MD2 = 29;
      ELSE MD2 = 28;
   IF MDDAY > RPDAYS(RPMTH) THEN MDDAY = RPDAYS(RPMTH);
   REMY = MDYR - RPYR;
   REMM = MDMTH - RPMTH;
   REMD = MDDAY - RPDAY;
   REMMTH = REMY*12 + REMM + REMD/RPDAYS(RPMTH);
%MEND REMMTH;
*;
*------------------------------------------------*
*  GET REPTDATE                                  *
*------------------------------------------------*;
DATA BNM.REPTDATE;
   SET LOAN.REPTDATE;
   SELECT(DAY(REPTDATE));
      WHEN (8)  CALL SYMPUT('NOWK',PUT('1',$1.));
      WHEN (15) CALL SYMPUT('NOWK',PUT('2',$1.));
      WHEN (22) CALL SYMPUT('NOWK',PUT('3',$1.));
      OTHERWISE CALL SYMPUT('NOWK',PUT('4',$1.));
   END;
   CALL SYMPUT('REPTYEAR',PUT(REPTDATE,YEAR4.));
   CALL SYMPUT('REPTMON',PUT(MONTH(REPTDATE),Z2.));
   CALL SYMPUT('REPTDAY',PUT(DAY(REPTDATE),Z2.));
   CALL SYMPUT('RDATE',PUT(REPTDATE,DDMMYY8.));
RUN;
*;
*----------------------------------------------------------------*
*  BREAKDOWN BY MATURITY PROFILE (PART 1 & 2 - RM)               *
*----------------------------------------------------------------*;
*------------------------------------------------*
*  LOANS - FL/HL USE REPAYMENT DATE              *
*        - OD/RC USE EXPIRY DATE                 *
*------------------------------------------------*;
DATA NOTE (KEEP=BNMCODE AMOUNT);
   %DCLVAR
   SET BNM1.LOAN&REPTMON&NOWK;
   IF _N_ = 1 THEN DO;
      SET BNM.REPTDATE;
      RPYR  = YEAR(REPTDATE);
      RPMTH = MONTH(REPTDATE);
      RPDAY = DAY(REPTDATE);
      IF MOD(RPYR,4) = 0 THEN RD2 = 29;
   END;
   IF PAIDIND NOT IN ('P','C') OR EIR_ADJ NE . ;
   IF SUBSTR(PRODCD,1,2) = '34' OR PRODUCT IN (225,226);
   IF CUSTCD IN ('77','78','95','96') THEN CUST = '08';
   ELSE CUST = '09';
   IF ACCTYPE = 'OD' THEN DO;
      REMMTH = 0.1;
      AMOUNT = BALANCE;
      BNMCODE = '95213'||CUST||PUT(REMMTH,REMFMT.)||'0000Y';
      IF PRODCD='34240' THEN
         BNMCODE = '95219'||CUST||PUT(REMMTH,REMFMT.)||'0000Y';
      OUTPUT;
   END;
*;
   IF ACCTYPE = 'LN';
   PROD = PUT(PRODUCT,LIQPFMT.);
   IF CUSTCD IN ('77','78','95','96') THEN
      SELECT (PROD);
         WHEN ('HL') ITEM = '214';
         OTHERWISE   ITEM = '219';
      END;
   ELSE SELECT (PROD);
      WHEN ('FL','HL') ITEM = '211';
      WHEN ('RC') ITEM = '212';
      OTHERWISE   ITEM = '219';
   END;
   IF BLDATE > 0 THEN DAYS = REPTDATE - BLDATE;
   IF EXPRDATE - REPTDATE < 8 THEN REMMTH = 0.1;
   ELSE DO;
      /*  CONVERT PAYFREQ CODE TO MONTHS  */
      SELECT (PAYFREQ);
         WHEN ('1') FREQ = 1;
         WHEN ('2') FREQ = 3;
         WHEN ('3') FREQ = 6;
         WHEN ('4') FREQ = 12;
         OTHERWISE;
      END;
      IF PAYFREQ IN ('5','9',' ') OR PRODUCT IN (350,910,925) THEN
         BLDATE = EXPRDATE;
      ELSE IF BLDATE <= 0 THEN DO;
         BLDATE = ISSDTE;
         DO WHILE (BLDATE <= REPTDATE);
            %NXTBLDT
         END;
      END;
      IF PAYAMT < 0 THEN PAYAMT = 0;
      IF BLDATE > EXPRDATE | BALANCE <= PAYAMT THEN BLDATE = EXPRDATE;
      DO WHILE (BLDATE <= EXPRDATE);
         MATDT = BLDATE;
         %REMMTH
         IF REMMTH > 12 OR BLDATE = EXPRDATE THEN LEAVE;
         IF REMMTH > 0.1 AND (BLDATE-REPTDATE) < 8 THEN REMMTH=0.1;
         AMOUNT = PAYAMT;
         BALANCE = BALANCE - PAYAMT;
         IF PRODUCT NOT IN &FCY THEN DO;
            BNMCODE = '95'||ITEM||CUST||PUT(REMMTH,REMFMT.)||'0000Y';
            OUTPUT;
            END;
     /*  IF PRODUCT IN &FCY THEN DO;
            BNMCODE = '94'||ITEM||CUST||PUT(REMMTH,REMFMT.)||'0000Y';
         OUTPUT;
         END;*/
         IF DAYS > 89 OR LOANSTAT ^= 1 THEN REMMTH = 13;
         IF PRODUCT NOT IN &FCY THEN DO;
            BNMCODE = '93'||ITEM||CUST||PUT(REMMTH,REMFMT.)||'0000Y';
            OUTPUT;
            END;
     /*  IF PRODUCT IN &FCY THEN DO;
            BNMCODE = '96'||ITEM||CUST||PUT(REMMTH,REMFMT.)||'0000Y';
         OUTPUT;
         END;*/
         %NXTBLDT
         IF BLDATE > EXPRDATE | BALANCE <= PAYAMT THEN
            BLDATE = EXPRDATE;
      END;
   END;
   AMOUNT = BALANCE;

   IF PRODUCT NOT IN &FCY THEN DO;
      BNMCODE = '95'||ITEM||CUST||PUT(REMMTH,REMFMT.)||'0000Y';
      OUTPUT;
      END; /*
   IF PRODUCT IN &FCY THEN DO;
      BNMCODE = '94'||ITEM||CUST||PUT(REMMTH,REMFMT.)||'0000Y';
   OUTPUT;
   END; */
   IF DAYS > 89 OR LOANSTAT ^= 1 THEN REMMTH = 13;
   IF PRODUCT NOT IN &FCY THEN DO;
      BNMCODE = '93'||ITEM||CUST||PUT(REMMTH,REMFMT.)||'0000Y';
      OUTPUT;
      END; /*
   IF PRODUCT IN &FCY THEN DO;
      BNMCODE = '96'||ITEM||CUST||PUT(REMMTH,REMFMT.)||'0000Y';
   OUTPUT;
   END;*/
   IF EIR_ADJ NE . THEN DO;
      AMOUNT = EIR_ADJ;
      BNMCODE = '95'||ITEM||CUST||'060000Y';
      OUTPUT;
      AMOUNT = EIR_ADJ;
      BNMCODE = '93'||ITEM||CUST||'060000Y';
      OUTPUT;
   END;
*;
PROC PRINT DATA=NOTE;
WHERE SUBSTR(BNMCODE,1,2) IN('94','96');
RUN;
*------------------------------------------------*
*  REVOLVING CREDITS                             *
*------------------------------------------------*;
DATA NOTE;
   SET NOTE;
   IF SUBSTR(BNMCODE,1,5)='93212' THEN DELETE;
RUN;

DATA RCCORP(RENAME=(BNMCODE=BNMCODE1));
   SET NOTE;
   IF SUBSTR(BNMCODE,1,5)='95212' THEN OUTPUT;
RUN;

DATA RCCORP1(DROP=BNMCODE1);
   SET RCCORP;
   BNMCODE=COMPRESS('93212'||SUBSTR(BNMCODE1,6,9));
RUN;

DATA NOTE;
   SET NOTE RCCORP1;
RUN;

*------------------------------------------------*
*  DEFAULTING                                    *
*------------------------------------------------*;
DATA DEFAULT(DROP=N);
  N=1;
  DO WHILE(N<7);
     AMOUNT = 0;
   /* RM */
     BNMCODE = COMPRESS('93211090'||N||'0000Y');OUTPUT;
     BNMCODE = COMPRESS('95211090'||N||'0000Y');OUTPUT;
     BNMCODE = COMPRESS('93212090'||N||'0000Y');OUTPUT;
     BNMCODE = COMPRESS('95212090'||N||'0000Y');OUTPUT;
     BNMCODE = COMPRESS('93213080'||N||'0000Y');OUTPUT;
     BNMCODE = COMPRESS('95213080'||N||'0000Y');OUTPUT;
     BNMCODE = COMPRESS('93213090'||N||'0000Y');OUTPUT;
     BNMCODE = COMPRESS('95213090'||N||'0000Y');OUTPUT;
     BNMCODE = COMPRESS('93214080'||N||'0000Y');OUTPUT;
     BNMCODE = COMPRESS('95214080'||N||'0000Y');OUTPUT;
     BNMCODE = COMPRESS('93215080'||N||'0000Y');OUTPUT;
     BNMCODE = COMPRESS('95215080'||N||'0000Y');OUTPUT;
     BNMCODE = COMPRESS('93219080'||N||'0000Y');OUTPUT;
     BNMCODE = COMPRESS('95219080'||N||'0000Y');OUTPUT;
     BNMCODE = COMPRESS('93219090'||N||'0000Y');OUTPUT;
     BNMCODE = COMPRESS('95219090'||N||'0000Y');OUTPUT;
   /* FCY */
     BNMCODE = COMPRESS('94211090'||N||'0000Y');OUTPUT;
     BNMCODE = COMPRESS('96211090'||N||'0000Y');OUTPUT;
     BNMCODE = COMPRESS('94212090'||N||'0000Y');OUTPUT;
     BNMCODE = COMPRESS('96212090'||N||'0000Y');OUTPUT;
     BNMCODE = COMPRESS('94213080'||N||'0000Y');OUTPUT;
     BNMCODE = COMPRESS('96213080'||N||'0000Y');OUTPUT;
     BNMCODE = COMPRESS('94213090'||N||'0000Y');OUTPUT;
     BNMCODE = COMPRESS('96213090'||N||'0000Y');OUTPUT;
     BNMCODE = COMPRESS('94214080'||N||'0000Y');OUTPUT;
     BNMCODE = COMPRESS('96214080'||N||'0000Y');OUTPUT;
     BNMCODE = COMPRESS('94215080'||N||'0000Y');OUTPUT;
     BNMCODE = COMPRESS('96215080'||N||'0000Y');OUTPUT;
     BNMCODE = COMPRESS('94219080'||N||'0000Y');OUTPUT;
     BNMCODE = COMPRESS('96219080'||N||'0000Y');OUTPUT;
     BNMCODE = COMPRESS('94219090'||N||'0000Y');OUTPUT;
     BNMCODE = COMPRESS('96219090'||N||'0000Y');OUTPUT;
     N+1;
END;
PROC SORT DATA=DEFAULT;BY BNMCODE;RUN;
PROC SORT DATA=NOTE;BY BNMCODE;RUN;
*;
DATA NOTE;
   MERGE DEFAULT NOTE;
   BY BNMCODE;
RUN;
*;
  /* ADD IN */
PROC SUMMARY DATA=NOTE NWAY;
   CLASS BNMCODE;
   VAR AMOUNT;
   OUTPUT OUT=BNM.NOTE (DROP=_TYPE_ _FREQ_) SUM=;
*;
PROC PRINT DATA=BNM.NOTE;RUN;
