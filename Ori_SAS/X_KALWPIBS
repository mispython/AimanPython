/** PROGRAM : KALWPBBS                       **/
/** DATE    : 18/11/2002                     **/
/** REPORT  : RDIR PART I  (KAPITI ITEMS )   **/
/**         : QUOTED RATES ON NIDS, OWN BAS  **/
/**         : AND REPOS (BNM TABLES 1,3      **/

%INC PGM(PBBDPFMT);
%LET AMTIND='D';
*;
*------------------------------------------------*
*  MACRO TO DECLARE VARIABLES                    *
*------------------------------------------------*;
%MACRO DCLVAR;
   RETAIN D1-D12 31 D4 D6 D9 D11 30
          RD1-RD12 MD1-MD12 31 RD2 MD2 28 RD4 RD6 RD9 RD11
          MD4 MD6 MD9 MD11 30 RPYR RPMTH RPDAY 0;
   ARRAY LDAY D1-D12;
   ARRAY RPDAYS RD1-RD12;
   ARRAY MDDAYS MD1-MD12;
%MEND DCLVAR;
*;
*------------------------------------------------*
*  MACRO TO CALCULATE REMAIN MONTH               *
*------------------------------------------------*;
%MACRO REMMTH;
   MDYR  = YEAR(MATDTE);
   MDMTH = MONTH(MATDTE);
   MDDAY = DAY(MATDTE);
   REMY = MDYR - RPYR;
   REMM = MDMTH - RPMTH;
   REMD = MDDAY - RPDAY;
   REMMTH = REMY*12 + REMM + REMD/RPDAYS(RPMTH);
%MEND REMMTH;
   /* QUOTED RATES ON REPOS */
DATA K1TBLQ (KEEP=BNMCODE AMOUNT BNMC WAMT AMTIND);
  LENGTH BNMCODE $14. BNMC $2. AMTIND $1.;
  SET BNMK.KWTBL&REPTMON&NOWK (RENAME=(GWBALC=AMOUNT));
  IF  GWSDT > &PDATE;
  AMTIND=&AMTIND;
  DAYS=(GWMDT-GWSDT);
  IF SUBSTR(GWDLP,2,2) IN ('MI','MT') AND DAYS > 0;
     WAMT=(AMOUNT*GWRATC)/100;
     IF      DAYS=1    THEN BNMC='11'; ELSE
     IF (2<= DAYS<=7 ) THEN BNMC='18'; ELSE
     IF (7<  DAYS<=14) THEN BNMC='19'; ELSE
     IF (14< DAYS<=21) THEN BNMC='27'; ELSE
     IF (21< DAYS<=28) THEN BNMC='28';
     IF WAMT NE . AND BNMC IN ('11','18','19','27','28')
        THEN DO;
        BNMCODE='8430800'||BNMC||'0000Y';
        OUTPUT K1TBLQ;
     END;
*;
   /* QUOTED RATES ON BA (BANKERS ACCEPTANCE) AND NIDS */
DATA K3TBLA (KEEP=BNMCODE AMOUNT OMTH WAMT AMTIND)
     K3TBLB (KEEP=BNMCODE AMOUNT OMTH WAMT AMTIND);
  LENGTH BNMCODE $14. OMTH $2. AMTIND $1.;
  %DCLVAR
  SET BNMK.K3TBL&REPTMON&NOWK;
  IF  ISSDT > &PDATE;
  IF  UTDLP NE 'MOS';
  MATDTE = INPUT(UTMDT,DDMMYY10.);
  ISSDTE = INPUT(UTOSD,DDMMYY10.);
  RPYR  = YEAR(ISSDTE);
  RPMTH = MONTH(ISSDTE);
  RPDAY = DAY(ISSDTE);
  AMTIND=&AMTIND;
  IF UTSTY='ILD' THEN UTQDS=UTCPR;
  IF UTSTY IN ('IFD','ILD','ISD','IZD','PBA');
     AMOUNT=UTFCV;
     WAMT=(UTQDS*AMOUNT)/100;
     SELECT (UTSTY);
     WHEN  ('IFD','ILD','ISD','IZD') DO;
     %REMMTH
     OMTH=PUT(REMMTH,FDORGMT.);
        IF WAMT NE . AND OMTH IN ('14','15','16','17')
           THEN DO;
           BNMCODE='8430600'||OMTH||'0000Y';
           OUTPUT K3TBLA;
        END;
     END;
     WHEN  ('PBA') DO;
     MATDD = DAY(MATDTE);
     MATMM = MONTH(MATDTE);
     MATYY = YEAR(MATDTE);
     ORIDD = MATDD - RPDAY;
     ORIMM = MATMM - RPMTH;
     ORIYY = MATYY - RPYR;
      IF ORIYY >= 1 THEN ORIMM = ORIMM + 12;
        IF (ORIMM = 1 AND ORIDD LE 0) OR (ORIMM = 0 AND ORIDD > 0)
                               THEN OMTH='12'; ELSE
        IF (ORIMM = 2 AND ORIDD LE 0) OR (ORIMM = 1 AND ORIDD > 0)
                               THEN OMTH='13'; ELSE
        IF (ORIMM = 3 AND ORIDD LE 0) OR (ORIMM = 2 AND ORIDD > 0)
                               THEN OMTH='14'; ELSE
        IF (ORIMM = 4 AND ORIDD LE 0) OR (ORIMM = 3 AND ORIDD > 0)
                               THEN OMTH='35'; ELSE
        IF (ORIMM = 5 AND ORIDD LE 0) OR (ORIMM = 4 AND ORIDD > 0)
                               THEN OMTH='36'; ELSE
        IF (ORIMM = 6 AND ORIDD LE 0) OR (ORIMM = 5 AND ORIDD > 0)
                               THEN OMTH='37';
        IF WAMT NE . AND  OMTH IN ('12','13','14','35','36','37')
           THEN DO;
           BNMCODE='8430700'||OMTH||'0000Y';
           OUTPUT K3TBLB;
        END;
     END;
     OTHERWISE;
     END;
RUN;
*;
DATA K3TBL3;
  SET K3TBLA K3TBLB K1TBLQ;
RUN;

PROC SUMMARY DATA=K3TBL3 NWAY;
   CLASS BNMCODE AMTIND;
   VAR   AMOUNT WAMT;
   OUTPUT OUT=K3TBL3 SUM=BALANCE WAMT;
RUN;
*;
DATA K3TBL3 (KEEP=BNMCODE AMOUNT AMTIND);
   SET K3TBL3;
   AMOUNT=(WAMT/BALANCE)*100;
*;
PROC SORT DATA=K3TBL3; BY BNMCODE;
PROC SORT DATA=BNMK.K3TBD&REPTMON1&NOWK1 OUT=K3TBLD; BY BNMCODE;
*;
DATA K3TBL3;
  MERGE K3TBLD K3TBL3; BY BNMCODE;
*;
OPTIONS NOCENTER NONUMBER NODATE;
TITLE1 &SDESC;
TITLE2 'REPORT ON DOMESTIC INTEREST RATE - PART I (KAPITI)';
TITLE3 'REPORTING DATE :' &RDATE;
TITLE4;
*;
*;
PROC PRINT DATA=K3TBL3; RUN;
DATA BNMK.K3TBD&REPTMON&NOWK;
  SET K3TBL3;
