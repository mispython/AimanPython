/** PROGRAM : EIBRDL1B                             **/
/** DATE    : 30/9/01                              **/
/** REPORT  : RDAL PART I (OUTPUT FOR WALKER ONLY) **/
OPTIONS YEARCUTOFF=1950;
*;
%LET AL100 = ('42110','42120','42130','42131','42132','42150','42160',
              '42170','42180','42199','42510','42520','42599','42190');
%LET AL500 = ('42510','42520','42599');
%LET AL600 = ('42610','42620','42630','42631','42632','42660','42699');

*;
DATA OTHER;
  SET BNM.DALW&REPTMON&NOWK
      BNM.FALW&REPTMON&NOWK
      BNM.LALW&REPTMON&NOWK
      BNM.KALW&REPTMON&NOWK;
  AMOUNT = 0;
RUN;
*;
PROC SUMMARY DATA=OTHER NWAY;
CLASS BNMCODE AMTIND;
VAR AMOUNT;
OUTPUT OUT=OTHER (DROP=_TYPE_ _FREQ_)
       SUM=AMOUNT;
RUN;
*;
DATA OTHER;
  SET OTHER;
  OUTPUT;
  IF SUBSTR(BNMCODE,1,5) IN &AL100 & SUBSTR(BNMCODE,6,2) IN ('57','75')
     THEN DO;
     IF SUBSTR(BNMCODE,6,2) = '75' THEN DO;
        BNMCODE = '4210075000000Y'; OUTPUT;
     END;
     IF SUBSTR(BNMCODE,6,2) = '57' THEN DO;
        BNMCODE = '4210057000000Y'; OUTPUT;
     END;
  END;
  IF SUBSTR(BNMCODE,1,5) IN &AL500 & SUBSTR(BNMCODE,6,2) IN ('01','71')
     THEN DO;
     IF SUBSTR(BNMCODE,6,2) = '75' THEN DO;
        BNMCODE = '4250001000000Y'; OUTPUT;
     END;
     IF SUBSTR(BNMCODE,6,2) = '57' THEN DO;
        BNMCODE = '4250071000000Y'; OUTPUT;
     END;
  END;
  IF SUBSTR(BNMCODE,1,5) IN &AL600 & SUBSTR(BNMCODE,6,2) IN ('57','75')
     THEN DO;
     IF SUBSTR(BNMCODE,6,2) = '75' THEN DO;
        BNMCODE = '4260075000000Y'; OUTPUT;
     END;
     IF SUBSTR(BNMCODE,6,2) = '57' THEN DO;
        BNMCODE = '4260057000000Y'; OUTPUT;
     END;
  END;
RUN;

PROC SORT DATA=BNM.WALW&REPTMON&NOWK OUT=WALW;
BY BNMCODE AMTIND;
RUN;

PROC SORT DATA=OTHER OUT=OTHER;
BY BNMCODE AMTIND;
RUN;

DATA BNM.ALWWK&REPTMON&NOWK (KEEP=ITCODE AMOUNT AMTIND);
  LENGTH BNMCODE $14.;
  MERGE WALW (RENAME=(AMOUNT=WISAMT) IN=A)
        OTHER (RENAME=(AMOUNT=OTHAMT) IN=B);
  BY BNMCODE AMTIND;
  RENAME BNMCODE=ITCODE;
  WEEKLY='Y';

  IF SUBSTR(BNMCODE,1,2) ^ IN ('35','36','37','59') AND
     SUBSTR(BNMCODE,1,3) ^ IN ('821') AND
     SUBSTR(BNMCODE,1,3) ^ IN ('411','391','392','491','492') THEN DO;
  /*  FILTER OUT MONTHLY ITEMS  */
     IF SUBSTR(BNMCODE,1,2) = '30' THEN AMTIND=' ';
     IF A AND B THEN DO;
        AMOUNT=OTHAMT;
        OUTPUT BNM.ALWWK&REPTMON&NOWK;
     END;
     IF A AND NOT B THEN DO;
        AMOUNT=WISAMT;
        OUTPUT BNM.ALWWK&REPTMON&NOWK;
     END;
     IF B AND NOT A THEN DO;
        AMOUNT=OTHAMT;
        OUTPUT BNM.ALWWK&REPTMON&NOWK;
     END;
  END;
  ELSE DO;
     IF "&NOWK"='4' THEN WEEKLY='N';
     IF A AND B THEN DO;
        AMOUNT=OTHAMT;
        IF SUBSTR(BNMCODE,1,7) IN ('4911050','4911080','4929950')
        THEN AMOUNT=WISAMT;
     END;
     IF A AND NOT B THEN DO;
        AMOUNT=WISAMT;
     END;
     IF B AND NOT A THEN DO;
        AMOUNT=OTHAMT;
     END;

     IF SUBSTR(BNMCODE,1,2) IN ('37') AND WEEKLY='Y'
     THEN OUTPUT BNM.ALWWK&REPTMON&NOWK;
     IF SUBSTR(BNMCODE,1,3) IN ('391','392') THEN DO;
        IF SUBSTR(BNMCODE,1,4)='3911' THEN OUTPUT
           BNM.ALWWK&REPTMON&NOWK;
        BNMCODE='3910000000000Y';
        IF WEEKLY='Y' THEN OUTPUT BNM.ALWWK&REPTMON&NOWK;
     END;
     IF SUBSTR(BNMCODE,1,4) IN ('4110','4111') THEN DO;
        BNMCODE='4100000000000Y';
        IF WEEKLY='Y' THEN OUTPUT BNM.ALWWK&REPTMON&NOWK;
     END;
     IF SUBSTR(BNMCODE,1,3) IN ('491','492') THEN DO;
        IF SUBSTR(BNMCODE,1,7) IN
        ('4911050','4911080','4912050','4912080','4929950','4929980',
         '4929000') THEN OUTPUT BNM.ALWWK&REPTMON&NOWK;
        BNMCODE='4910000000000Y';
        IF WEEKLY='Y' THEN OUTPUT BNM.ALWWK&REPTMON&NOWK;
     END;
  END;
RUN;
